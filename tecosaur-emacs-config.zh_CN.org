# SPDX-FileCopyrightText:
# SPDX-License-Identifier: MIT
# Title: Doom Emacs é…ç½®æ–‡ä»¶
#+title: Doom Emacs Configuration
# Subtitle: ç–¯ç‹‚çš„æ–¹æ³•ã€ç®¡ç†ä¸åŠ¨ç‰©å›­ --- ç»†è‡´å…¥å¾®
#+subtitle: The Methods, Management, and Menagerie@@latex:\\@@ of Madness@@latex: --- in meticulous detail@@
#+date: @@html:<!--@@{{{git-rev}}}@@html:-->@@@@latex:\\\Large\bfseries@@ {{{modification-time(%Y-%m-%d, t)}}} @@latex:\\\normalsize\mdseries@@{{{modification-time(%H:%M, t)}}} @@latex:\acr{\lowercase{@@{{{timezone}}}@@latex:}}\iffalse@@, {{{git-rev}}}@@latex:\fi@@
#+macro: timezone (eval (substring (shell-command-to-string "date +%Z") 0 -1))
#+macro: git-rev (eval (format "@@html:<a href=\"https://github.com/tecosaur/emacs-config/commit/%1$s\" style=\"text-decoration: none\"><code style=\"padding: 0; color: var(--text-light); font-size: inherit; opacity: 0.7\">%1$s</code></a>@@@@latex:\\href{https://github.com/tecosaur/emacs-config/commit/%1$s}{\\normalsize\\texttt{%1$s}}@@" (substring (shell-command-to-string "git rev-parse --short HEAD") 0 -1)))
#+html_head: <link rel='shortcut icon' type='image/png' href='https://www.gnu.org/software/emacs/favicon.png'>
#+property: header-args:emacs-lisp :tangle yes :comments link
#+property: header-args:elisp :exports code
#+property: header-args:shell :tangle "setup.sh"
#+property: header-args :tangle no :results silent :eval no-export
#+embed: LICENCE :description MIT licence file
#+options: coverpage:yes
#+startup: fold

#+latex_class: book
#+latex_header: \usepackage[autooneside=false,automark,headsepline]{scrlayer-scrpage}
#+latex_header: \clearpairofpagestyles \renewcommand*{\chaptermarkformat}{} \renewcommand*{\sectionmarkformat}{}
#+latex_header: \ihead{\upshape\scshape\leftmark} \chead{\ifstr{\leftmark}{\rightmark}{}{\rightmark}} \ohead[\pagemark]{\pagemark}

#+begin_export html
<a href="https://github.com/tecosaur/emacs-config/"
   style="font-family: 'Open Sans'; background-image: none; color: inherit;
          text-decoration: none; position: relative; top: clamp(-26px, calc(1280px - 100vw), 0px); opacity: 0.7;">
  <img src="https://upload.wikimedia.org/wikipedia/commons/9/91/Octicons-mark-github.svg"
       class="invertible" alt="GitHub Octicon"
       style="height: 1em; position: relative; top: 0.1em;">
  View on GitHub</a>&ensp;
<a href="https://www.buymeacoffee.com/tecosaur"
   style="position: relative;top: clamp(-22px, calc(1280px - 100vw), 0px);">
  <img src="https://img.shields.io/badge/Buy_me_a%C2%A0coffee-FFDD00?style=flat-square&amp;logo=buy-me-a-coffee&amp;logoColor=black"
       class="invertible" alt="Buy me a coffee"
       style="border-radius: 4px; opacity: 0.85;"></a>
#+end_export

#+begin_quote
è®©æˆ‘ä»¬æ”¹å˜å¯¹ç¨‹åºæ„å»ºçš„ä¼ ç»Ÿæ€åº¦ï¼š
ä¸å…¶æƒ³è±¡æˆ‘ä»¬çš„ä¸»è¦ä»»åŠ¡æ˜¯æŒ‡å¯¼è®¡ç®—æœºåšä»€ä¹ˆï¼Œ
ä¸å¦‚ä¸“æ³¨äºå‘äººä»¬è§£é‡Šæˆ‘ä»¬æƒ³è®©è®¡ç®—æœºåšä»€ä¹ˆã€‚
@@latex:\mbox{@@--- é«˜å¾·çº³@@latex:}@@
#+end_quote

* å¼•è¨€
å®šåˆ¶ç¼–è¾‘å™¨æ˜¯ååˆ†å€¼å¾—çš„ä¸€ä»¶äº‹â€¦â€¦ç›´åˆ°ä½ éœ€è¦ç¦»å¼€å®ƒã€‚å¤šå¹´æ¥æˆ‘ä¸€åªåœ¨å¯»æ‰¾é¿å…è¿™ç§ç—›è‹¦çš„
æ–¹æ³•ã€‚æˆ‘å‘ç°äº† [[https://github.com/cknadler/vim-anywhere][vim-anywhere]]ï¼Œéšåå‘ç°äº† Emacs ç‰ˆæœ¬çš„ [[https://github.com/zachcurry/emacs-anywhere][emacs-anywhere]]ã€‚å¯¹æˆ‘æ¥è¯´è¿™å®åœ¨æ˜¯å¤ªå¸å¼•äººäº†ã€‚

å¦å¤–ï¼Œåœ¨çº¿ä¸Šæˆ‘å·²ç»çœ‹åˆ°å¾ˆå¤šè¿™æ ·çš„æ®µå­äº†ï¼Œæˆ‘è§‰å¾—è¿™æ˜¯ä¸ªæ¢—
#+begin_quote
Redditor 1: æˆ‘åˆšåˆšå‘ç°äº†è¿™ä¸ªä¸œè¥¿ï¼Œæ˜¯ä¸æ˜¯å¾ˆé…·ã€‚ \\
Redditor 2: å“¦ï¼Œè¿™æœ‰ä¸€ä¸ª Emacs æ¨¡å¼ã€‚
#+end_quote

å¯¹æˆ‘æ¥è¯´ï¼Œè¿™è¶³ä»¥è®©æˆ‘å®‰è£… Emacs äº†ï¼Œä½†æˆ‘å¾ˆå¿«äº†è§£äº† [[https://github.com/remacs/remacs#why-emacs][æ›´æœ‰è¯´æœåŠ›çš„ç†ç”±]] å¹¶å¼€å§‹æŒç»­ä½¿
ç”¨å®ƒã€‚

æˆ‘å°è¯•ç€ä½¿ç”¨ =spacemacs= ä½†å¹¶ä¸å–œæ¬¢å®ƒï¼Œä¹‹åå¬è¯´äº† =doom emacs= å¹¶è§‰å¾—ä¸å¦¨ä¸€è¯•ã€‚TLDR
å®ƒå¤ªæ£’äº†ï¼

å¦‚ä»Šæˆ‘å‘ç°äº†æ–‡å­¦ç¼–ç¨‹çš„ç¥å¥‡ä¹‹å¤„ï¼ŒEmacs é€æ¸æˆä¸ºäº†æˆ‘ç”Ÿæ´»çš„ä¸€éƒ¨åˆ†ã€‚è¿™å³æ˜¯æˆ‘çš„é…ç½®ï¼Œ
ä¹Ÿæ˜¯ä¸€ä¸ªå€¼å¾—è¯´é“çš„æ•…äº‹ã€‚(å°†ä¸‹é¢å›¾ç‰‡ä¸­çš„ Linux æ¢æˆ Emacs å°±å¥½)

[[xkcd:456]]

** ä¸ºä»€ä¹ˆæ˜¯ Emacs?

Emacs [[https://www.eigenbahn.com/2020/01/12/emacs-is-no-editor][ä¸æ˜¯ä¸€ä¸ªæ–‡æœ¬ç¼–è¾‘å™¨]]ï¼Œè¿™æ˜¯ä¸€ä¸ªå¸¸è§çš„è¯¯ä¼šã€‚å°† Emacs æè¿° *ä¸ºæä¾›ä»¥ç”¨æˆ·ä¸ºä¸­å¿ƒçš„
é€šç”¨æ–‡æœ¬æ“ä½œç¯å¢ƒçš„ Lisp è™šæ‹Ÿæœº* åˆ™æ›´ä¸ºå‡†ç¡®ã€‚çœŸæ˜¯å¤ªç»•å£äº†ã€‚
ç®€å•æ¥è¯´ï¼Œå¯ä»¥å°† Emacs è§†ä¸ºæ–‡æœ¬å¤„ç†ç›¸å…³ç¨‹åºçš„å¹³å°ã€‚è¿™æ˜¯ä¸ªæ¨¡ç³Šä¸”é€šç”¨çš„å®šä¹‰ï¼Œå› ä¸º
Emacs æœ¬èº«å°±æ˜¯é€šç”¨çš„ã€‚

ä¸æ–‡å­—ç›¸å¾—ç›Šå½°ï¼Œé‚£è¿˜æœ‰ä»€ä¹ˆå‘¢ï¼Ÿå½“ç„¶æ¯”æœ€åˆè®¤ä¸ºçš„è¦å¤šå¾—å¤šï¼š
+ [[https://orgmode.org/][ä»»åŠ¡è®¡åˆ’]]
+ [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Dired.html][æ–‡ä»¶ç®¡ç†]]
+ [[https://github.com/akermu/emacs-libvterm][ç»ˆç«¯æ¨¡æ‹Ÿ]]
+ [[https://www.djcbsoftware.nl/code/mu/mu4e.html][é‚®ç®±å®¢æˆ·ç«¯]]
+ [[https://www.gnu.org/software/tramp/][è¿œç¨‹æœåŠ¡å™¨å·¥å…·]]
+ [[https://magit.vc/][Git å‰ç«¯]]
+ Web [[https://github.com/pashky/restclient.el][å®¢æˆ·ç«¯]] / [[https://github.com/skeeto/emacs-web-server][æœåŠ¡å™¨]]
+ è¿˜æœ‰æ›´å¤šâ€¦â€¦

ç†æƒ³æƒ…å†µä¸‹ï¼Œå¯ä»¥ä½¿ç”¨ Emacs ä½œä¸ºå¤„ç† =è¾“å…¥ â†’ è½¬æ¢ â†’ è¾“å‡º= å¾ªç¯çš„æ¥å£ï¼Œå³æ¶èµ·ä¸€åº§è¿
æ¥äººè„‘ä¸ä¿¡æ¯æ“ä½œçš„æ¡¥æ¢ã€‚

*** åŒ…ç½—ä¸‡è±¡çš„ç¼–è¾‘å™¨
Emacs é¼“åŠ±æ•´åˆå…¶ä»–åº”ç”¨çš„åŠŸèƒ½ï¼Œè®©å®ƒä»¬éƒ½èƒ½åœ¨ Emacs é‡Œä½¿ç”¨ã€‚ä¸ºä»€ä¹ˆè¿™æ ·å¥½ï¼Ÿ
+ ä½¿ç”¨ä¸€è‡´çš„ã€æ ‡å‡†çš„ä¸€ç»„å¿«æ·é”®ã€GUI ä¸ç¼–è¾‘æ–¹å¼ï¼Œæ¥å®Œæˆä»»åŠ¡ --- ä¸€æ¬¡å­¦ä¹ éšå¤„ä½¿ç”¨
+ å‡å°‘ä¸Šä¸‹æ–‡åˆ‡æ¢
+ å‹ç¼©é¡¹ç›®æµç¨‹ --- æ›´è½»æ¾åœ°è¿›è¡Œé›†ä¸­çš„é¡¹ç›®æµç¨‹
+ é›†æˆä¸åŒåº”ç”¨ä½†å…·æœ‰ç›¸åŒä¸»é¢˜çš„ä»»åŠ¡ --- ä¾‹å¦‚ï¼Œé“¾æ¥åˆ°ä»£åŠäº‹é¡¹çš„ç”µå­é‚®ä»¶

Emacs å¯ä»¥è¢«è®¤ä¸ºæ˜¯ä¸€ä¸ªå¹³å°ï¼Œä½ çš„å·¥ä½œæµç¨‹ä¸­çš„å„ç§å…ƒç´ éƒ½èƒ½åœ¨è¿™é‡Œå¾—åˆ°è§£å†³ï¼Œå¹¶ä¸”å…·æœ‰
æé«˜çš„é›†æˆæ½œåŠ› --- å¦‚æœä½ æ„¿æ„ï¼Œå¯ä»¥æ‰“é€ æˆ *ç”Ÿæ´»çš„* IDEã€‚

ä»Šå¤©ï¼Œæ—¥å¸¸ä½¿ç”¨è®¡ç®—æœºçš„è®¸å¤šæ–¹é¢ï¼Œè¢«åˆ†å‰²åœ¨äº†åƒä¸€ç‰‡ç‰‡å­¤å²›ä¸€æ ·çš„åº”ç”¨ä¸­ï¼Œä½†è¿™å¾€å¾€ä¸èƒ½
åæ˜ å‡ºæˆ‘ä»¬ä½¿ç”¨è®¡ç®—æœºçš„ *çœŸå®* æƒ…å†µã€‚å¦‚æœå¯ä»¥è¿›å…¥è¿™å¥‡å¹»èˆ¬çš„ Emacs ä¸–ç•Œï¼Œå®ƒèƒ½ä¸ºæˆ‘ä»¬
å¼¥è¡¥è¿™ä¸€å·®è·ã€‚

#+name: emacs-platform
#+begin_src dot :cmd circo :file misc/emacs-platform.svg :exports none
digraph {
    graph[bgcolor="transparent"];
    node[shape="underline" penwidth="2" style="rounded,filled" fillcolor="#efefef"
          color="#c9c9c9" fontcolor="#000000" fontname="overpass"];
    edge[arrowhead=none color="#aaaaaa" penwidth="1.2"];
    // nodes
    "ä»»åŠ¡ç®¡ç†"[color="#2ec27e"]
    "ç”µå­é‚®ç®±"[color="#1c71d8"]
    "åŠå…¬å¥—ä»¶"[color="#813d9c"]
    "ä»£ç ç¼–è¾‘"[color="#f5c211"]
    "Gitå‰ç«¯"[color="#e66100"]
    // "æ–°é—»æè¦"[color="#c01c28"]
    // "ä¸ªäººçŸ¥è¯†åº“"[color="#986a44"]

    "ä»»åŠ¡ç®¡ç†" -> "ç”µå­é‚®ç®±"
    "ä»»åŠ¡ç®¡ç†" -> "åŠå…¬å¥—ä»¶"
    "ä»»åŠ¡ç®¡ç†" -> "ä»£ç ç¼–è¾‘"
    "ä»»åŠ¡ç®¡ç†" -> "Gitå‰ç«¯"
    // "ä»»åŠ¡ç®¡ç†" -> "æ–°é—»æè¦"
    // "ä»»åŠ¡ç®¡ç†" -> "ä¸ªäººçŸ¥è¯†åº“"

    "ç”µå­é‚®ç®±" -> "åŠå…¬å¥—ä»¶"
    "ç”µå­é‚®ç®±" -> "ä»£ç ç¼–è¾‘"
    "ç”µå­é‚®ç®±" -> "Gitå‰ç«¯"
    // "ç”µå­é‚®ç®±" -> "ä¸ªäººçŸ¥è¯†åº“"

    "åŠå…¬å¥—ä»¶" -> "ä»£ç ç¼–è¾‘"
    "åŠå…¬å¥—ä»¶" -> "Gitå‰ç«¯"
    // "åŠå…¬å¥—ä»¶" -> "æ–°é—»æ‘˜è¦"
    // "åŠå…¬å¥—ä»¶" -> "ä¸ªäººçŸ¥è¯†åº“"

    "ä»£ç ç¼–è¾‘" -> "Gitå‰ç«¯"

    // "æ–°é—»æ‘˜è¦" -> "ä¸ªäººçŸ¥è¯†åº“"
}
#+end_src

#+caption: åœ¨ Emacs ä¸Šå¯ä»¥ç®€å•é›†æˆçš„å·¥ä½œæµ
#+attr_html: :class invertible :alt Graph of possible Emacs task integrations :style max-width:min(24em,100%)
#+attr_latex: :width 0.5linewidth
[[file:misc/emacs-platform.svg]]

*** ä¸€äº›ç‹¬ä¸€æ— äºŒçš„åŠŸèƒ½

+ é€’å½’ç¼–è¾‘
+ æ™®éçš„æ–‡æ¡£å­—ç¬¦ä¸²ï¼Œä»¥åŠå®Œå…¨çš„å†…çœæ€§
+ åœ¨å¯å˜ç¯å¢ƒä¸­çš„å¢—é‡ä¿®æ”¹
+ æ— éœ€ç‰¹å®šåº”ç”¨å¯ç”¨åŠŸèƒ½
+ å…è®¸å®¢æˆ·ç«¯ã€æœåŠ¡å™¨åˆ†ç¦»ï¼Œå¯åŠ¨å®ˆæŠ¤è¿›ç¨‹ï¼Œæä¾›å‡ ä¹æ— æ„ŸçŸ¥çš„å¯åŠ¨æ—¶é—´

*** é—®é¢˜

+ Emacs æœ‰ä¸€äº›ä»¤äººåŒçƒ¦çš„å¥‡æ€ªæ“ä½œ
+ æŸäº›æ–¹é¢å¾ˆæš´éœ²å¹´é¾„ (åç§°çº¦å®šã€API ç­‰)
+ Emacs [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Threads.html][å‡ ä¹]] æ˜¯å•çº¿ç¨‹çš„ï¼Œè¿™æ„å‘³ç€ä¸€äº›ä¸å½“çš„æ“ä½œå°†é˜»å¡æ•´ä¸ªåº”ç”¨
+ ä¸€äº›å…¶ä»–æ–¹é¢çš„å¹²æ‰°â€¦â€¦

*** æˆäººä»¥é±¼ä¸å¦‚æˆäººä»¥æ¸”
#+begin_quote
Give a man a fish, and you feed him for a day. Teach a man to fish, and you feed
him for a lifetime. --- Anne Isabella\\
\\
æˆäººä»¥é±¼ï¼Œåªä¾›ä¸€é¤ï¼Œæˆäººä»¥æ¸”ï¼Œå¯äº«ä¸€ç”Ÿã€‚
#+end_quote

è®¸å¤šä¸»æµçš„ç¼–è¾‘å™¨æœ‰ç€ç®€å•ä¸”ç¾è§‚çš„ [[https://code.visualstudio.com/docs/getstarted/settings][è®¾ç½®ç•Œé¢]]ï¼Œå…¶ä¸­å……æ»¡äº†å¤é€‰æ¡†ã€é€‰æ‹©ä»¥åŠå¶å°”çš„æ–‡æœ¬ã€‚è¿™ä½¿å¾—
ç”¨æˆ·å¯ä»¥æ›´è½»æ¾åœ°åœ¨è¿™äº›é€šç”¨æè¿°è¡Œä¸ºä¹‹é—´è¿›è¡Œé€‰æ‹©ã€‚å¯¹æˆ‘æ¥è¯´è¿™å°±åƒ /æˆäººä»¥é±¼/ ã€‚

å¦‚æœä½ å¸Œæœ›è¿™äº›å¤é€‰æ¡†åªåœ¨æŸäº›ç‰¹å®šçš„æ¡ä»¶ä¸‹å¯ç”¨æ€ä¹ˆåŠï¼ŸæŸäº›ç¼–è¾‘å™¨æœ‰å·¥ä½œåŒºè®¾ç½®ï¼Œä½†è¿™å¯èƒ½éœ€
è¦ä½ æ‰‹åŠ¨è®¾ç½® /æ¯ä¸ªå®ä¾‹/ çš„å€¼ã€‚Emmm... [[https://github.com/microsoft/vscode/issues/93153][å¤ª]][[https://github.com/microsoft/vscode/issues/93628][ç—›è‹¦]][[https://github.com/microsoft/vscode/issues/8962][äº†]]ï¼

å¦‚æœä½ æƒ³å°†å¤é€‰æ¡†çš„å€¼è®¾ç½®ä¸ºï¼Œæ¯ä¸ªæ–‡ä»¶æ‰§è¡Œè¡¨è¾¾å¼çš„ç»“æœï¼Œé‚£éœ€è¦æ€ä¹ˆåšï¼Ÿè¿™å°±æ˜¯ Emacs ç­‰
ç¼–è¾‘å™¨çš„ç”¨æ­¦ä¹‹åœ°äº†ã€‚
é…ç½® Emacs å¹¶ä¸æ˜¯ç”¨ JSON ç­‰æ–‡ä»¶åˆ—å‡ºè®¾ç½®ï¼Œè€Œæ˜¯ä¸€ä»½ *é€‚åº”ä½ çš„å¯ä»¥ä¿®æ”¹ç¼–è¾‘å™¨è¡Œä¸ºçš„å¯æ‰§
è¡Œç¨‹åº* ã€‚è¿™å°±æ˜¯ "æˆäººä»¥æ¸”"ã€‚

Emacs æ„å»ºåœ¨ä¸é…ç½®ç›¸åŒçš„ç¼–ç¨‹è¯­è¨€ä¹‹ä¸Š (Emacs [[https://en.wikipedia.org/wiki/Lisp_(programming_language)][Lisp]], [[https://en.wikipedia.org/wiki/Emacs_Lisp][elisp]])ã€‚å®ƒæœ‰éå¸¸å¤šçš„æœ‰ç”¨çš„æ–‡æœ¬ç¼–
è¾‘åŠŸèƒ½ï¼ŒDoom æ·»åŠ äº†ä¸€äº›å°å·§ä¾¿åˆ©çš„åŠŸèƒ½ã€‚

å¦‚ä½•ä¸ºåˆ é™¤ä¸Šä¸€è¡Œæ·»åŠ ä¸€ä¸ªå¿«æ·é”®ï¼Ÿå°±åƒä¸‹é¢è¿™ä¹ˆç®€å•
#+name: ç»‘å®šåˆ é™¤ä¸Šä¸€è¡Œçš„å¿«æ·é”®
#+begin_src emacs-lisp :tangle-mode
(map! "C-d"
      (cmd! (previous-line)
            (kill-line)
            (forward-line)))
#+end_src

å†ä¸¾ä¸ªä¾‹å­ï¼Œå½“ä½ æ‹†åˆ†çª—å£æ—¶ï¼Œä½ å¸Œæœ›åˆ—å‡ºå½“å‰æ‰“å¼€çš„æ‰€æœ‰ /ç¼“å†²åŒº/ (é€šå¸¸æ˜¯æ–‡ä»¶)ï¼Œå°±å¯ä»¥åƒè¿™
æ ·
#+name: æ‹†åˆ†æ—¶æç¤ºç¼“å†²åŒº
#+begin_src emacs-lisp :tangle no
(defadvice! prompt-for-buffer (&rest _)
  :after 'window-split (switch-to-buffer))
#+end_src

å¦‚æœæƒ³æµ‹è¯•å®ƒï¼Œä¸éœ€è¦ä¿å­˜é‡å¯ï¼Œä½ åªéœ€è¦åœ¨å½“å‰çš„ Emacs ç¼“å†²åŒºä¸­ /æ‰§è¡Œè¡¨è¾¾å¼/ (
/evaluate the expression/) å°±å¯ä»¥ç«‹å³å°è¯•å®ƒï¼æ¯•ç«Ÿè¿™ç¼–è¾‘æ˜¯ä¸ª lisp è§£é‡Šå™¨ã€‚

å¦‚æœæƒ³è¦è°ƒæ•´è¡Œä¸ºï¼Ÿè¿™æ˜¯ä¸€ä¸ªè¶…ç´§å¯†çš„è¿­ä»£è¿‡ç¨‹ --- é‡æ–°æ‰§è¡Œä½ çš„æ–°ç‰ˆæœ¬é…ç½®ã€‚

** ç¼–è¾‘å™¨æ¯”è¾ƒ

[[xkcd:378]]

è¿‘å¹´æˆ‘è¯•è¿‡äº† (è‡³å°‘èŠ±è´¹ä¸€å¹´æ—¶é—´ä½œä¸ºæˆ‘çš„ä¸»ç¼–è¾‘å™¨) ä»¥ä¸‹åº”ç”¨
- Python IDLE
- Komodo Edit
- Brackets
- VSCode
- å’Œç°åœ¨ä½¿ç”¨çš„ Emacs

#+plot: transpose:yes type:radar min:0 max:4 ticks:4 file:"misc/editor-comparison.svg"
| Editor      | Extensibility | Ecosystem | Ease of Use | Comfort | Completion | Performance |
|-------------+---------------+-----------+-------------+---------+------------+-------------|
| IDLE        |             1 |         1 |           3 |       1 |          1 |           2 |
| VSCode      |             3 |         3 |           4 |     3.5 |          4 |           3 |
| Brackets    |           2.5 |         2 |           3 |       3 |        2.5 |           2 |
| Emacs       |             4 |         4 |           2 |       4 |        3.5 |           3 |
| Komodo Edit |             2 |         1 |           3 |       2 |          2 |           2 |

#+attr_html: :class invertible :alt Radar chart comparing my thoughts on a few editors.
#+attr_latex: :options inkscapelatex=false
[[file:misc/editor-comparison.svg]]

** è‡´ç²—å¿ƒçš„å†’é™©è€…
å¦‚æœä½ è§‰å¾—è¿™çœ‹èµ·æ¥ä¸é”™ï¼Œè¿™çœŸçš„å¤ªæ£’äº†ï¼Œæˆ‘å¾ˆé«˜å…´ä½ å¯¹æˆ‘æ‰€è®²çš„æ„Ÿå…´è¶£ï¼Œä½†æ˜¯ï¼š
#+begin_warning
è¿™äº›é…ç½®å¯èƒ½æœ‰äº›é—®é¢˜ï¼Œç›²ç›®çš„æ‹·è´å…¨éƒ¨çš„ä»£ç å¯èƒ½ä¼šå‡ºç°é—®é¢˜ã€‚æˆ‘å»ºè®®ä¸€å—ä¸€å—å¤åˆ¶ã€‚
#+end_warning

å¦‚æœä½ å¤§èƒ†åœ°è¿›è¡Œæ‹·è´ (æˆ–æœ‰ä¸€éƒ¨åˆ†åœ¨å‡çº§åä¸å·¥ä½œ)ï¼Œæœ¬èŠ‚åˆ—å‡ºäº†ä¸€ä»½ä¾èµ–å¤–éƒ¨é…ç½®çš„åˆ—è¡¨
(æ­¤é…ç½®ä¹‹å¤–)ã€‚

+ å­—å…¸ :: åœ¨ä½¿ç”¨ [[*Ispell][ispell]] æ—¶ï¼Œæˆ‘ä¼šä¸‹è½½ä¸€ä»½è‡ªå®šä¹‰çš„ SCOWL å­—å…¸ã€‚å¦‚æœå› ä¸ºè¿™ä¸ªé—®é¢˜ï¼Œè¯·åˆ é™¤
  ä»£ç  src_elisp{(setq ispell-dictionary ...)}

æˆ‘æœ‰æ²¡æœ‰æè¿‡è¿™ä»½é…ç½®åœ¨æˆ‘å¯¹ =elisp= æ²¡å¤šå°‘äº†è§£æ—¶å°±å¼€å§‹äº†ï¼Œè€Œè¿™æ•´ä¸ªå·¥ä½œååˆ†æå®¢ã€‚å¦‚æœä½ å¯¹
æ”¹è¿›æå‡ºä»»ä½•å»ºè®®ï¼Œæ— è®ºæœ‰å¤šå°‘æ‰¹è¯„ï¼Œæˆ‘çœŸå¿ƒæ„Ÿè°¢ã€‚

[[xkcd:1513]]

*** å¤–éƒ¨ä¾èµ–

å¯çˆ±çš„ ~doom docker~ å¯ä»¥å¾ˆå¥½çš„è¯Šæ–­å‡ºç¼ºå°‘ä»€ä¹ˆï¼Œä½†è¿™äº›å¹¶ä¸å¯ä»¥ã€‚
+ [[https://www.tug.org/texlive/][LaTeX ç¼–è¯‘å™¨]] æ˜¯ [[#org][org]] ä¸ [[*CalcTeX][CalcTeX]] æ‰€ä¾èµ–çš„æ•°å­¦å…¬å¼æ¸²æŸ“å™¨
+ ä½¿ç”¨æ— è¡¬çº¿å­—ä½“ [[https://overpassfont.org][Overpass]]ï¼Œå®ƒè¢«ç”¨åœ¨ ~doomo-variableo-pitch-font~ å’Œ [[https://tecosaur.github.io/emacs-config/config.html#roam][Roam]] ç”Ÿæˆ
  çš„å›¾è¡¨ä¸­ã€‚
  æˆ‘é€‰æ‹©å®ƒä¸»è¦æ˜¯å› ä¸ºå…¶å…·æœ‰æˆ‘å¸Œæœ›çš„ä¸€äº›ç‰¹å¾ï¼Œå³ï¼š
  - ç®€çº¦ã€æ˜“è¯»çš„é£æ ¼ï¼Œå— /[[https://en.wikipedia.org/wiki/Highway_Gothic][Highway Gothic]]/ çš„å½±å“ï¼Œè¿™ç§ Highway é£æ ¼çš„å­—ä½“è®¾è®¡åå‘
    äºä¸€ç›®äº†ç„¶ï¼Œå¹¶ä¸”åœ¨ç²—å­—é‡ä¸‹ä¹Ÿå¾ˆå¥½è¾¨è®¤ã€‚
  - å®ƒæœ‰ä¸€ç‚¹å¤æ€ªï¼Œæ¯”å¦‚è¯´å­—ä½“ä¸»å¹²ä¸Šçš„æ–œåˆ‡ç¼ºå£ã€‚æ¯«æ— ç–‘é—® [[https://en.wikipedia.org/wiki/Helvetica][Helvetica]] æ˜¯ç²¾æ¹›çš„è®¾è®¡ï¼Œä½†æœ‰
    æ—¶æˆ‘ä¼šæ›´å–œæ¬¢æ—¶é«¦ä¸€ç‚¹çš„ã€‚
+ ä¸€äº› LSP ç¨‹åºã€‚è¿™äº›ç¨‹åºå¯ä»¥åœ¨ [[file:init.el][init.el]] ä¸­æŸ¥çœ‹é‚£äº›æœ‰ ~+lsp~ æ ‡è®°çš„æ¨¡ç»„ã€‚

** å½“å‰å­˜åœ¨çš„é—®é¢˜
*** åœ¨å®ˆæŠ¤è¿›ç¨‹ä¸­è¿›è¡Œ push æ“ä½œ
åœ¨å®ˆæŠ¤è¿›ç¨‹ä¸­ç»å¸¸å°è¯• push åˆ°è¿œç«¯å°†å¯èƒ½å‘ç”Ÿä»¥ä¸‹é”™è¯¯
#+begin_src fundamental
128 git â€¦ push -v origin refs/heads/master\:refs/heads/master
Pushing to git@github.com:tecosaur/emacs-config.git

fatal: Could not read from remote repository.

Please make sure you have the correct access rights
and the repository exists.
#+end_src

*** æœªè¯»ç”µå­é‚®ä»¶ä¸é€‚ç”¨äº Emacs å®ä¾‹
æˆ‘å¸Œæœ› Emacs å®ˆæŠ¤è¿›ç¨‹å¯ä»¥ä¿æŒ mu4e ä¼šè¯ï¼Œå¹¶ä¾ç„¶å¯ä»¥è·å–æ¶ˆæ¯ï¼Œå¦‚æœå¯ä»¥é‚£å°±å¤ªå¥½äº†ã€‚è¿™ç§
æƒ…å†µä¸‹æˆ‘æ›´æƒ³é€šè¿‡ Emacs å®ˆæŠ¤è¿›ç¨‹è¿›è¡Œæ“ä½œï¼Œè¿™ç†åº”æ˜¯å¯ä»¥çš„ã€‚

è¿™å¯èƒ½æ¶‰åŠåˆ° hook è¿›å®ˆæŠ¤è¿›ç¨‹çš„æ¨¡å¼è¡Œå‡çº§å‡½æ•°ï¼Œç”¨æ¥å†™å…¥ä¸´æ—¶æ–‡ä»¶ï¼Œå¹¶åœ¨å…¶ä»– Emacs å®ä¾‹ä¸­
å¯åŠ¨ä¸€ä¸ªæ–‡ä»¶è§‚å¯Ÿå™¨ï¼Œç±»ä¼¼äºä½¿ç”¨ mu4e æ—¶é‡å»ºé‚®ä»¶ç´¢å¼•ã€‚

* åŸºç¡€é…ç½®
åˆ›å»ºè¯æ³•ç»‘å®šå¯ä»¥ (ç¨å¾®) åŠ é€Ÿé…ç½®æ–‡ä»¶çš„è¿è¡Œã€‚ (æ›´å¤šå†…å®¹å¯ä»¥æŸ¥çœ‹ [[https://nullprogram.com/blog/2016/12/22/][è¿™ç¯‡åšå®¢]])
#+begin_src emacs-lisp :comments no
;;; config.el -*- lexical-binding: t; -*-
#+end_src

#+begin_src shell :exports none :comments no :tangle-mode (identity #o755)
#!/usr/bin/env bash
#+end_src
** ä¸ªäººä¿¡æ¯
é…ç½®æœ‰ç”¨ä¸”åŸºç¡€çš„ä¸ªäººä¿¡æ¯
#+begin_src emacs-lisp
(setq user-full-name "TEC"
      user-mail-address "tec@tecosaur.com")
#+end_src
æ˜¾ç„¶è¿™å¯ä»¥è¢« GPG æˆ–å…¶ä»–ç¨‹åºä½¿ç”¨ã€‚

å¯¹äº ~GPG~ æˆ‘ä½¿ç”¨çš„æ˜¯ =~/.authinfo.gpg= æ¥æ›¿æ¢é»˜è®¤çš„ =~/.emacs.d= ã€‚ä¸ºä»€ä¹ˆï¼Ÿ
å› ä¸ºæˆ‘çš„ home ç›®å½•å·²ç»å¾ˆä¹±äº†ï¼Œè¿™å·²ç»æ— æ‰€è°“äº†ï¼Œå†µä¸”æˆ‘æ›¾ä¸å°å¿ƒç”¨
src_shell{rm -rf ~/.emacs.d} åˆ é™¤è¿‡å®ƒã€‚æˆ‘è¿˜æƒ³å°½å¯èƒ½åœ°ç¼“å­˜è¿™äº›æ–‡ä»¶ï¼Œå› ä¸ºæˆ‘å®¶ä¸­çš„ç”µè„‘
ååˆ†å®‰å…¨ï¼Œè€Œæˆ‘çš„ç¬”è®°æœ¬ç»å¸¸å…³æœºã€‚
#+begin_src emacs-lisp
(setq auth-sources '("~/.authinfo.gpg")
      auth-source-cache-expiry nil) ; default is 7200s (2h)
#+end_src
** æ›´å¥½çš„é»˜è®¤å€¼
*** ç®€å•è®¾ç½®
æµè§ˆç½‘é¡µæ—¶çœ‹åˆ°äº† [[https://github.com/angrybacon/dotemacs/blob/master/dotemacs.org#use-better-defaults][angrybacon/dotemacs]]ï¼Œå¯¹æ¯”è¯¥è®¾ç½®ä¸ =SPC h v= çš„å€¼ï¼Œé€‰æ‹©ä¸€äº›å¯¹æˆ‘æœ‰
ç”¨çš„å€¼ï¼Œæœ€ç»ˆæˆ‘é€‰æ‹©æ·»åŠ è¿™äº›å€¼
#+begin_src emacs-lisp
(setq-default
 delete-by-moving-to-trash t        ; å°†æ–‡ä»¶åˆ é™¤åˆ°å›æ”¶ç«™
 window-combination-resize t        ; ä»å…¶ä»–çª—å£è·å–æ–°çª—å£çš„å¤§å°
 x-stretch-cursor t                 ; å°†å…‰æ ‡æ‹‰ä¼¸åˆ°å­—å½¢å®½åº¦
 )

(setq undo-limit 104857600          ; é‡ç½®æ’¤é”€é™åˆ¶åˆ° 100 MiB
      evil-want-fine-undo t         ; åœ¨é»˜è®¤æƒ…å†µä¸‹æ’å…¥æ—¶æ˜¯ä¸€å—ï¼Œé‡‡ç”¨æ›´ç»†ç²’åº¦
      auto-save-default t           ; æ²¡æœ‰äººå–œæ¬¢ä¸¢å¤±å·¥ä½œï¼Œæˆ‘ä¹Ÿæ˜¯å¦‚æ­¤
      truncate-string-ellipsis "â€¦"  ; Unicode çœç•¥å·ç›¸æ¯” ascii æ›´å¥½
                                    ; åŒæ—¶èŠ‚çœ /å®è´µçš„/ ç©ºé—´
      password-cache-expiry nil     ; æˆ‘èƒ½ä¿¡ä»»æˆ‘çš„ç”µè„‘ ... æˆ–ä¸èƒ½?
      ;scroll-preserve-screen-position 'always
                                    ; ä¸è¦è®© `ç‚¹' (å…‰æ ‡) è·³æ¥è·³å»
      scroll-margin 2               ; é€‚å½“ä¿æŒä¸€ç‚¹ç‚¹è¾¹è·
      )

(display-time-mode 1)               ; å¼€å¯æ—¶é—´çŠ¶æ€æ 

(unless (string-match-p "^Power N/A" (battery))  ; ç¬”è®°æœ¬ç”µè„‘ä¸Š
  (display-battery-mode 1)                       ; çŸ¥é“è¿˜å‰©å¤šå°‘ âš¡ï¸ å¾ˆé‡è¦
  )

(global-subword-mode 1)                          ; è¯†åˆ«é©¼å³°ï¼Œè€Œä¸æ˜¯å‚»ç“œå‰è¿›
#+end_src
*** æ¡†å¤§å°
åœ¨ Emacs å¯åŠ¨æ—¶å¯ä»¥é€šè¿‡ src_shell{emacs -geometry 160x48} è¿™æ ·çš„å‘½ä»¤è¡Œå‚æ•°æ¥å¾ˆ
å¥½çš„æ§åˆ¶ Emacs çš„æ¡†å¤§å°ã€‚åœ¨åˆå§‹åŒ–æœŸé—´è°ƒæ•´å­—ä½“å¤§å°åï¼Œå¤§æ¦‚ä¸º ~102x31~ ã€‚

æ„Ÿè°¢æœ‰å¿«æ·é”®å¯ä»¥è®©æˆ‘æ–¹ä¾¿çš„å°†çª—å£ç¼©æ”¾åˆ°åŠå±ã€å…¨å±ï¼Œå› æ­¤å¯ä»¥é€‰æ‹©ä¿å®ˆçš„å¤§å°å¯åŠ¨æ–°çª—å£ã€‚

åœ¨ä¸€ä¸ª Emacs å®ä¾‹ä¸­åˆ›å»ºæ–°çª—å£ï¼Œæˆ‘ä»¬åªéœ€è¦å°†å…¶è®¾ç½®ä¸ºåŸæ¥çš„ 80%ã€‚

#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(height . 24))
(add-to-list 'default-frame-alist '(width . 80))
#+end_src
*** è‡ªåŠ¨å®¢åˆ¶åŒ–
é»˜è®¤æƒ…å†µä¸‹é€šè¿‡è‡ªå®šä¹‰ç•Œé¢æ‰€åšçš„ä¿®æ”¹ä¼šè¢«æ·»åŠ åˆ° =init.el= ä¸­ã€‚æˆ‘æ›´å–œæ¬¢ä¸ºå®ƒä»¬æ·»åŠ ä¸€ä¸ªå•ç‹¬
çš„æ–‡ä»¶ã€‚åªéœ€è¦ä¸€ç‚¹æ”¹åŠ¨å°±å¯ä»¥è®¾ç½®å¹¶è‡ªåŠ¨åŠ è½½ã€‚
#+begin_src emacs-lisp
(setq-default custom-file (expand-file-name ".custom.el" doom-private-dir))
(when (file-exists-p custom-file) (load custom-file))
#+end_src
*** Windows
æˆ‘å‘ç°åœ¨æ‹†åˆ†çª—å£åè¯¢é—®æˆ‘æƒ³çœ‹å“ªä¸ªç¼“å†²åŒºæ˜¯ååˆ†æ–¹ä¾¿çš„ï¼Œè®©æˆ‘ä»¬å®ç°å®ƒã€‚

é¦–å…ˆï¼Œæˆ‘ä»¬è¦è¿›å…¥æ–°çª—å£
#+begin_src emacs-lisp
(setq evil-vsplit-window-right t
      evil-split-window-below t)
#+end_src

ä¹‹åéœ€è¦å¼¹å‡ºä¸€ä¸ªæç¤ºç¼“å†²åŒº
#+begin_src emacs-lisp
(defadvice! prompt-for-buffer (&rest _)
  :after '(evil-window-split evil-window-vsplit)
  (consult-buffer))
#+end_src

çª—å£ä¸ /å¸ƒå±€/ å¯ä»¥é€šè¿‡ =SPC w r= å’Œ =SPC w R= å¾ˆæ–¹ä¾¿çš„è¿›è¡Œåˆ‡æ¢ã€‚åœ¨ Tmux ä¸­ä½¿ç”¨å¿«
æ·é”® =C-b SPC= æ¥åˆ‡æ¢çª—å£ï¼Œæˆ‘ä»¬ä¹Ÿå¯ä»¥åŒæ ·å°†åˆ‡æ¢çª—å£çš„å¿«æ·é”®æ”¾åœ¨ =SPC w SPC= ä¸‹ã€‚

ç”¨æ–¹å‘é”®æ¥åšçª—å£åˆ‡æ¢å’Œäº¤æ¢çš„å¯¼èˆªé”®æ˜¯ä¸€ä¸ªä¸é”™çš„é€‰æ‹©ã€‚
#+begin_src emacs-lisp
(map! :map evil-window-map
      "SPC" #'rotate-layout
      ;; æ–¹å‘
      "<left>"   #'evil-window-left
      "<down>"   #'evil-window-down
      "<up>"     #'evil-window-up
      "<right>"  #'evil-window-right
      ;; äº¤æ¢çª—å£
      "C-<left>"   #'+evil/window-move-left
      "C-<down>"   #'+evil/window-move-down
      "C-<up>"     #'+evil/window-move-up
      "C-<right>"  #'+evil/window-move-right
      )
#+end_src
*** ç¼“å†²åŒºé»˜è®¤è®¾ç½®
æˆ‘éå¸¸å–œæ¬¢å°†æ–°ç¼“å†²åŒºè®¾ç½®ä¸º ~org-mode~ è€Œé ~fundamental-mode~
#+begin_src emacs-lisp
;; (setq-default major-mode 'org-mode)
#+end_src

ç”±äºæŸäº›åŸå› ï¼Œæ·»åŠ è¿™ä¸ª hook ä¼šå¯¼è‡´ hydra å‡ºç°é—®é¢˜ï¼Œæ‰€ä»¥æˆ‘ç°åœ¨åªå¥½é‡‡ç”¨ =SPC b o= ã€‚

** Doom é…ç½®
*** æ¨¡ç»„
:PROPERTIES:
:header-args:emacs-lisp: :tangle no
:END:
ç”±äº Doom çš„ /æ¨¡å—åŒ–é…ç½®/ ï¼Œæˆ‘ä»¬å¯ä»¥åœ¨é…ç½® Emacs æ—¶å¯ä»¥åšå¾ˆå¤šå·¥ä½œã€‚æ¯ä¸ªæ¨¡å— (å¼€å¯æ—¶
) å¯ä»¥æä¾›ä¸€ç³»åˆ—åŒ…ï¼Œå¹¶è¿›è¡Œå®‰è£… (~doom sync~) é…ç½®åº”ç”¨ã€‚å½“ç„¶ä¹Ÿå¯ä»¥å¢åŠ ä¸€äº›æ ‡å¿—æ¥æ”¹å˜
å…¶è¡Œä¸ºã€‚

#+name: init.el
#+attr_html: :collapsed t
#+begin_src emacs-lisp :tangle "init.el" :noweb no-export :comments no
;;; init.el -*- lexical-binding: t; -*-

;; This file controls what Doom modules are enabled and what order they load
;; in. Remember to run 'doom sync' after modifying it!

;; NOTE Press 'SPC h d h' (or 'C-h d h' for non-vim users) to access Doom's
;;      documentation. There you'll find a link to Doom's Module Index where all
;;      of our modules are listed, including what flags they support.

;; NOTE Move your cursor over a module's name (or its flags) and press 'K' (or
;;      'C-c c k' for non-vim users) to view its documentation. This works on
;;      flags as well (those symbols that start with a plus).
;;
;;      Alternatively, press 'gd' (or 'C-c c d') on a module to browse its
;;      directory (for easy access to its source code).

(doom! :completion
       <<doom-completion>>

       :ui
       <<doom-ui>>

       :editor
       <<doom-editor>>

       :emacs
       <<doom-emacs>>

       :term
       <<doom-term>>

       :checkers
       <<doom-checkers>>

       :tools
       <<doom-tools>>

       :os
       <<doom-os>>

       :lang
       <<doom-lang>>

       :email
       <<doom-email>>

       :app
       <<doom-app>>

       :config
       <<doom-config>>
       )
#+end_src

**** ç»“æ„
æ­£å¦‚ä½ å¯èƒ½å·²ç»æ³¨æ„åˆ°çš„é‚£æ ·ï¼Œè¿™æ˜¯ä¸€ç¯‡æ–‡å­¦ç¼–ç¨‹é…ç½®ã€‚Doom å¯¹å…¶æ”¯æŒè‰¯å¥½ï¼Œæ›´å¤šè¯¦æƒ…æˆ‘ä»¬å¯
ä»¥é€šè¿‡ ~literate~ (æ–‡å­¦) æ¨¡å—äº†è§£ã€‚

åœ¨ src_elisp{:config} ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨ Doom ä¸­éå¸¸æ£’çš„é»˜è®¤å€¼ï¼Œå¹¶ç»‘å®šæ›´æ™ºèƒ½çš„è¡Œä¸ºã€‚(
è™½ç„¶è¿™äº›æ ‡è®°æ²¡æœ‰æ–‡æ¡£ï¼Œä½†å®ƒä»¬ç¡®å®å¯ç”¨)
#+name: doom-config
#+begin_src emacs-lisp
literate
(default +bindings +smartparens)
#+end_src

**** æ¥å£
å¯ä»¥åšå¾ˆå¤šäº‹æ¥å¢å¼º Emacs çš„åŠŸèƒ½ï¼Œæˆ‘çŒœå¤§æ¦‚éœ€è¦å¼€å¯ Doom ä¸€åŠçš„æ¨¡ç»„ã€‚

#+name: doom-completion
#+begin_src emacs-lisp
(company                     ; the ultimate code completion backend
 +childframe)                ; ... when your children are better than you
;;helm                       ; the *other* search engine for love and life
;;ido                        ; the other *other* search engine...
;; (ivy                      ; a search engine for love and life
;;  +icons                   ; ... icons are nice
;;  +prescient)              ; ... I know what I want(ed)
(vertico +icons)             ; the search engine of the future
#+end_src

#+name: doom-ui
#+begin_src emacs-lisp
;;deft                       ; notational velocity for Emacs
doom                         ; what makes DOOM look the way it does
doom-dashboard               ; a nifty splash screen for Emacs
doom-quit                    ; DOOM quit-message prompts when you quit Emacs
(emoji +unicode)             ; ğŸ™‚
;;fill-column                ; a `fill-column' indicator
hl-todo                      ; highlight TODO/FIXME/NOTE/DEPRECATED/HACK/REVIEW
;;hydra                      ; quick documentation for related commands
;;indent-guides              ; highlighted indent columns, notoriously slow
(ligatures +extra)           ; ligatures and symbols to make your code pretty again
;;minimap                    ; show a map of the code on the side
modeline                     ; snazzy, Atom-inspired modeline, plus API
nav-flash                    ; blink the current line after jumping
;;neotree                    ; a project drawer, like NERDTree for vim
ophints                      ; highlight the region an operation acts on
(popup                       ; tame sudden yet inevitable temporary windows
 +all                        ; catch all popups that start with an asterix
 +defaults)                  ; default popup rules
;;(tabs                      ; an tab bar for Emacs
;;  +centaur-tabs)           ; ... with prettier tabs
treemacs                     ; a project drawer, like neotree but cooler
;;unicode                    ; extended unicode support for various languages
vc-gutter                    ; vcs diff in the fringe
vi-tilde-fringe              ; fringe tildes to mark beyond EOB
(window-select +numbers)     ; visually switch windows
workspaces                   ; tab emulation, persistence & separate workspaces
zen                          ; distraction-free coding or writing
#+end_src

#+name: doom-editor
#+begin_src emacs-lisp
(evil +everywhere)           ; come to the dark side, we have cookies
file-templates               ; auto-snippets for empty files
fold                         ; (nigh) universal code folding
(format)                     ; automated prettiness
;;god                        ; run Emacs commands without modifier keys
;;lispy                      ; vim for lisp, for people who don't like vim
multiple-cursors             ; editing in many places at once
;;objed                      ; text object editing for the innocent
;;parinfer                   ; turn lisp into python, sort of
rotate-text                  ; cycle region at point between text candidates
snippets                     ; my elves. They type so I don't have to
;;word-wrap                  ; soft wrapping with language-aware indent
#+end_src

#+name: doom-emacs
#+begin_src emacs-lisp
(dired +icons)               ; making dired pretty [functional]
electric                     ; smarter, keyword-based electric-indent
(ibuffer +icons)             ; interactive buffer management
undo                         ; persistent, smarter undo for your inevitable mistakes
vc                           ; version-control and Emacs, sitting in a tree
#+end_src

#+name: doom-term
#+begin_src emacs-lisp
;;eshell                     ; the elisp shell that works everywhere
;;shell                      ; simple shell REPL for Emacs
;;term                       ; basic terminal emulator for Emacs
vterm                        ; the best terminal emulation in Emacs
#+end_src

#+name: doom-checkers
#+begin_src emacs-lisp
syntax                       ; tasing you for every semicolon you forget
(:if (executable-find "aspell") spell) ; tasing you for misspelling mispelling
grammar                      ; tasing grammar mistake every you make
#+end_src

#+name: doom-tools
#+begin_src emacs-lisp
ansible                      ; a crucible for infrastructure as code
;;debugger                   ; FIXME stepping through code, to help you add bugs
;;direnv                     ; be direct about your environment
docker                       ; port everything to containers
;;editorconfig               ; let someone else argue about tabs vs spaces
;;ein                        ; tame Jupyter notebooks with emacs
(eval +overlay)              ; run code, run (also, repls)
;;gist                       ; interacting with github gists
(lookup                      ; helps you navigate your code and documentation
 +dictionary                 ; dictionary/thesaurus is nice
 +docsets)                   ; ...or in Dash docsets locally
lsp                          ; Language Server Protocol
;;macos                      ; MacOS-specific commands
(magit                       ; a git porcelain for Emacs
 +forge)                     ; interface with git forges
make                         ; run make tasks from Emacs
;;pass                       ; password manager for nerds
pdf                          ; pdf enhancements
;;prodigy                    ; FIXME managing external services & code builders
rgb                          ; creating color strings
;;taskrunner                 ; taskrunner for all your projects
;;terraform                  ; infrastructure as code
;;tmux                       ; an API for interacting with tmux
upload                       ; map local to remote projects via ssh/ftp
#+end_src

#+name: doom-os
#+begin_src emacs-lisp
tty                          ; improve the terminal Emacs experience
#+end_src

**** ç¼–ç¨‹è¯­è¨€æ”¯æŒ
åŒæ ·ä½¿ç”¨æ–‡å­¦ç¼–ç¨‹æ¥å¼€å¯ç›¸å…³ç¼–ç¨‹è¯­è¨€çš„åŒ…å’Œé…ç½®ï¼Œå½“ç„¶è¿™äº›é…ç½®ä»…åœ¨ç¬¬ä¸€æ¬¡æ‰“å¼€ç›¸å…³æ–‡ä»¶æ—¶å¯
ç”¨ã€‚

#+name: doom-lang
#+begin_src emacs-lisp
;;agda                       ; types of types of types of types...
;;beancount                  ; mind the GAAP
;;cc                         ; C/C++/Obj-C madness
;;clojure                    ; java with a lisp
;;common-lisp                ; if you've seen one lisp, you've seen them all
;;coq                        ; proofs-as-programs
;;crystal                    ; ruby at the speed of c
;;csharp                     ; unity, .NET, and mono shenanigans
data                         ; config/data formats
;;(dart +flutter)            ; paint ui and not much else
;;dhall                      ; JSON with FP sprinkles
;;elixir                     ; erlang done right
;;elm                        ; care for a cup of TEA?
emacs-lisp                   ; drown in parentheses
;;erlang                     ; an elegant language for a more civilized age
ess                          ; emacs speaks statistics
;;faust                      ; dsp, but you get to keep your soul
;;fsharp                     ; ML stands for Microsoft's Language
;;fstar                      ; (dependent) types and (monadic) effects and Z3
;;gdscript                   ; the language you waited for
;;(go +lsp)                  ; the hipster dialect
;;(haskell +lsp)             ; a language that's lazier than I am
;;hy                         ; readability of scheme w/ speed of python
;;idris                      ;
json                         ; At least it ain't XML
;;(java +meghanada)          ; the poster child for carpal tunnel syndrome
(javascript +lsp)            ; all(hope(abandon(ye(who(enter(here))))))
(julia +lsp)                 ; Python, R, and MATLAB in a blender
;;kotlin                     ; a better, slicker Java(Script)
(latex                       ; writing papers in Emacs has never been so fun
 +latexmk                    ; what else would you use?
 +cdlatex                    ; quick maths symbols
 +fold)                      ; fold the clutter away nicities
;;lean                       ; proof that mathematicians need help
;;factor                     ; for when scripts are stacked against you
;;ledger                     ; an accounting system in Emacs
lua                          ; one-based indices? one-based indices
markdown                     ; writing docs for people to ignore
;;nim                        ; python + lisp at the speed of c
;;nix                        ; I hereby declare "nix geht mehr!"
;;ocaml                      ; an objective camel
(org                         ; organize your plain life in plain text
 +pretty                     ; yessss my pretties! (nice unicode symbols)
 +dragndrop                  ; drag & drop files/images into org buffers
 ;;+hugo                     ; use Emacs for hugo blogging
 +noter                      ; enhanced PDF notetaking
 +jupyter                    ; ipython/jupyter support for babel
 +pandoc                     ; export-with-pandoc support
 +gnuplot                    ; who doesn't like pretty pictures
 ;;+pomodoro                 ; be fruitful with the tomato technique
 +present                    ; using org-mode for presentations
 +roam2)                     ; wander around notes
;;php                        ; perl's insecure younger brother
;;plantuml                   ; diagrams for confusing people more
;;purescript                 ; javascript, but functional
(python +lsp +pyright)       ; beautiful is better than ugly
;;qt                         ; the 'cutest' gui framework ever
;;racket                     ; a DSL for DSLs
;;raku                       ; the artist formerly known as perl6
;;rest                       ; Emacs as a REST client
;;rst                        ; ReST in peace
;;(ruby +rails)              ; 1.step {|i| p "Ruby is #{i.even? ? 'love' : 'life'}"}
(rust +lsp)                  ; Fe2O3.unwrap().unwrap().unwrap().unwrap()
;;scala                      ; java, but good
scheme                       ; a fully conniving family of lisps
sh                           ; she sells {ba,z,fi}sh shells on the C xor
;;sml                        ; no, the /other/ ML
;;solidity                   ; do you need a blockchain? No.
;;swift                      ; who asked for emoji variables?
;;terra                      ; Earth and Moon in alignment for performance.
web                          ; the tubes
yaml                         ; JSON, but readable
;;zig                        ; C, but simpler
#+end_src

**** Everything in Emacs
Everything in Emacs å®åœ¨å¤ªæ–¹ä¾¿äº†ï¼Œæˆ‘æ— æ³•æ‹’ç»åœ¨ Emacs ä¸­å¼€å¯ Email å’Œè®¢é˜…æ¨¡å—ã€‚

#+name: doom-email
#+begin_src emacs-lisp
(:if (executable-find "mu") (mu4e +org +gmail))
;;notmuch
;;(wanderlust +gmail)
#+end_src

#+name: doom-app
#+begin_src emacs-lisp
;;calendar                   ; A dated approach to timetabling
;;emms                       ; Multimedia in Emacs is music to my ears
everywhere                   ; *leave* Emacs!? You must be joking.
irc                          ; how neckbeards socialize
(rss +org)                   ; emacs as an RSS reader
;;twitter                    ; twitter client https://twitter.com/vnought
#+end_src

*** è§†è§‰è®¾ç½®
'Fira Code' çš„æ•ˆæœå¾ˆä¸é”™ï¼Œè€Œ 'Overpass' åˆ™æ˜¯å…¶å¾ˆæ£’çš„æ— è¡¬çº¿å­—ä½“ã€‚æˆ‘ä»¬åªéœ€è°ƒæ•´ä¸‹å­—å·
å°±å¯ä»¥åœ¨è§†è§‰ä¸Šæ¯”è¾ƒèˆ’æœäº†ã€‚ä¸ºäº†å¥½ç©æˆ‘å°è¯•äº†ä¸‹ JetBrains Monoï¼Œä¸è¿‡ç›®å‰ä¸ºæ­¢æˆ‘å¯¹å…¶çš„æ„Ÿ
è§‰å¾ˆå¤æ‚ï¼ŒæŸäº›æ–¹é¢å®ƒå¾ˆä¸é”™ï¼Œä½†å…¶ä»–æ–¹é¢æˆ‘æ›´å–œæ¬¢ Firaã€‚
#+begin_src emacs-lisp
(setq doom-font (font-spec :family "JetBrains Mono" :size 24)
      doom-big-font (font-spec :family "JetBrains Mono" :size 36)
      doom-variable-pitch-font (font-spec :family "Overpass" :size 24)
      doom-unicode-font (font-spec :family "JuliaMono")
      doom-serif-font (font-spec :family "IBM Plex Mono" :weight 'light)
      )
#+end_src

#+attr_html: :class invertible :alt Screenshot of the fonts within Emacs.
[[https://tecosaur.com/lfs/emacs-config/screenshots/font-face.png]]

é™¤äº†è¿™äº›å­—ä½“å¤–ï¼Œå­—ä½“ [[https://github.com/SorkinType/Merriweather/][Merriweather]] è¿˜è¢«ç”¨äº =nov.el= ä¸­ï¼Œå­—ä½“ [[https://github.com/huertatipografica/Alegreya][Alegreya]] ä½œä¸ºè¡¬çº¿æ¯”
ä¾‹å­—ä½“è¢«ç”¨äº Org æ–‡ä»¶çš„ =writeroom-mode= ä¸­çš„ =mixed-pitch-mode= ã€‚

æ¯”è¾ƒå…³å¿ƒå¤–è§‚çš„è®¾ç½®ï¼Œå› æ­¤æˆ‘ä»¬è®¾ç½®ä¸€ä¸ªæ£€æµ‹å‡½æ•°ï¼Œç¡®ä¿åœ¨ç³»ç»Ÿä¸­æ²¡æœ‰è¿™äº›å­—ä½“æ—¶å¯ä»¥å‘Šè¯‰æˆ‘ä»¬ã€‚

#+name: detect-missing-fonts
#+begin_src emacs-lisp
(defvar required-fonts '("JetBrainsMono.*" "Overpass" "JuliaMono"
                         "IBM Plex Mono" "Merriweather" "Alegreya"))
(defvar available-fonts
  (delete-dups (or (font-family-list)
                   (split-string
                    (shell-command-to-string "fc-list : family")
                    "[,\n]"))))

(defvar missing-fonts
  (delq nil (mapcar
             (lambda (font)
               (unless
                   (delq nil (mapcar (lambda (f)
                                       (string-match-p
                                        (format "^%s$" font) f))
                                     available-fonts))
                 font))
             required-fonts)))

(if missing-fonts
    (pp-to-string
     `(unless noninteractive
        (add-hook! 'doom-init-ui-hook
          (run-at-time nil nil
                       (lambda ()
                         (message "%s missing the following fonts: %s"
                                  (propertize "Warning!" 'face '(bold warning))
                                  (mapconcat (lambda (font)
                                               (propertize font 'face 'font-lock-variable-name-face))
                                  ',missing-fonts
                                  ", "))
                       (sleep-for 0.5))))))
  ";; No missing fonts detected")
#+end_src

#+begin_src emacs-lisp :no-export
<<detect-missing-fonts()>>
#+end_src

åœ¨ Doom çš„ UI åˆå§‹åŒ–å®Œæˆåï¼Œæœ‰å­—ä½“ç¼ºå¤±æ—¶ï¼Œä¼šçˆ†å‡ºä¸€ä¸ªè­¦å‘Šï¼Œå¹¶åˆ—å‡ºç¼ºå¤±çš„å­—ä½“è‡³å°‘åŠç§’é’Ÿã€‚

**** ä¸»é¢˜å’Œæ¨¡å¼æ 

~doom-one~ æ˜¯ Doom è‡ªå¸¦çš„å¤§è€Œå…¨çš„ä¸»é¢˜ï¼Œæˆ‘å‘ç°å…¶ä¸­è¿™ä¸ª ~vibrant~ æ•ˆæœæ›´å¥½ã€‚Doom
æä¾›äº†å¦‚æ­¤ä¸°å¯Œä¸”ä¸é”™çš„é€‰æ‹©ï¼Œæˆ‘æ²¡æœ‰ä»€ä¹ˆç†ç”±å»ç”¨é»˜è®¤çš„ä¸»é¢˜ã€‚

#+begin_src emacs-lisp
(setq doom-theme 'doom-vibrant)
(remove-hook 'window-setup-hook #'doom-init-theme-h)
(add-hook 'after-init-hook #'doom-init-theme-h 'append)
(delq! t custom-theme-load-path)
#+end_src

ç„¶è€Œï¼Œé»˜è®¤æƒ…å†µä¸‹æ¨¡å¼æ ä½¿ç”¨äº†çº¢è‰²å­—ä½“ï¼Œæˆ‘å¯ä¸æƒ³åœ¨ç¼–è¾‘æ–‡ä»¶æ—¶æ„Ÿè§‰è¿™æœ‰ä¸€äº›é”™è¯¯ï¼Œå› æ­¤è®©æˆ‘
ä»¬å°†å…¶æ”¹ä¸ºæ©™è‰²ã€‚

#+begin_src emacs-lisp
(custom-set-faces!
  '(doom-modeline-buffer-modified :foreground "orange"))
#+end_src

=LF UTF-8= æ˜¯é»˜è®¤çš„æ–‡ä»¶ç¼–ç æ–¹å¼ï¼Œå› æ­¤å®ƒå‡ºç°åœ¨è¿™é‡Œçš„æ„ä¹‰ä¸å¤§ã€‚è®©æˆ‘ä»¬ç»§ç»­ä¿®æ”¹æ¨¡å¼æ ï¼Œ
è®©ç¼–ç æ–¹å¼é€‰æ‹©æ€§éšè—ã€‚

#+begin_src emacs-lisp
(defun doom-modeline-conditional-buffer-encoding ()
  "We expect the encoding to be LF UTF-8, so only show the modeline when this is not the case"
  (setq-local doom-modeline-buffer-encoding
              (unless (and (memq (plist-get (coding-system-plist buffer-file-coding-system) :category)
                                 '(coding-category-undecided coding-category-utf-8))
                           (not (memq (coding-system-eol-type buffer-file-coding-system) '(1 2))))
                t)))
(add-hook 'after-change-major-mode-hook
          #'doom-modeline-conditional-buffer-encoding)
#+end_src

**** æ‚é¡¹
ç›¸å¯¹è¡Œå·å¯ä»¥å¾ˆå¥½çš„çŸ¥é“è·ç¦»ç›®æ ‡è¡Œæœ‰å¤šè¿œï¼Œç„¶åç”¨å¿«æ·é”® =C-u num <UP>= æˆ–
=ESC num <UP>= åˆ°è¾¾ä½ æƒ³å»çš„è¡Œã€‚
#+begin_src emacs-lisp
(setq display-line-numbers-type 'relative)
#+end_src

æˆ‘æƒ³è®¾ç½®ä¸€ä¸‹æ›´å¥½çœ‹çš„é»˜è®¤ç¼“å†²åŒºåç§°
#+begin_src emacs-lisp
(setq doom-fallback-buffer-name "â–º Doom"
      +doom-dashboard-name "â–º Doom")
#+end_src

*** ä¸€äº›è¾…åŠ©å®
è¿™äº›æ˜¯ doom æ·»åŠ çš„ä¸€äº›éå¸¸æœ‰ç”¨çš„å®
- ~load!~ å¯ä»¥ç›¸å¯¹äºæœ¬æ–‡ä»¶è¿›è¡Œå¤–éƒ¨ ~.el~ æ–‡ä»¶çš„åŠ è½½
- ~use-package!~ ç”¨äºé…ç½®åŒ…
- ~add-load-path!~ å°†æŒ‡å®šç›®å½•æ·»åŠ åˆ° ~load-path~ ä¸­ï¼Œå¯ä»¥è®© Emacs åœ¨ä½¿ç”¨ ~require~ å’Œ ~use-package~ æ—¶åœ¨ ~load-path~ ä¸­è¿›è¡ŒæŸ¥æ‰¾
- ~map!~ ç”¨äºç»‘å®šæ–°çš„å¿«æ·é”®

*** å…è®¸ CLI è¿è¡Œ org-babel ç¨‹åº

æœ‰æ—¶æˆ‘ä¼šç”Ÿæˆä¸€äº›ä»£ç åŒ…å«åœ¨é…ç½®æ–‡ä»¶å½“ä¸­ã€‚è¿™å¾ˆå¥½ç”¨ï¼Œå¹¶ä¸”å¯ä»¥å’Œ =doom sync= ä¸€èµ·å·¥ä½œã€‚æˆ‘
éœ€è¦ç¡®ä¿ Org ä¸ä¼šå°è¯•ç¡®è®¤æˆ‘æƒ³è¿è¡Œçš„å†…å®¹ã€‚

ä¸‡å¹¸çš„æ˜¯ Doom æ”¯æŒåœ¨ =$DOOMDIR/cli.el= æ–‡ä»¶ä¸­å†™å…¥å‘½ä»¤ï¼Œå¹¶ä¸”æ¯æ¬¡è¿è¡Œ CLI å‘½ä»¤æ—¶éƒ½ä¼š
è¯»å–è¯¥æ–‡ä»¶ã€‚å› æ­¤æˆ‘ä»¬å¯ä»¥å°† ~org-confirm-babel-evaluate~ è®¾ç½®ä¸º ~nil~ ã€‚å½“ç„¶è¿˜è¦
æ²‰é»˜ ~org-babel-execute-src-block~ æ¥é¿å…å®ƒæ±¡æŸ“è¾“å‡ºã€‚

#+begin_src emacs-lisp :tangle cli.el :comments no
;;; cli.el -*- lexical-binding: t; -*-
(setq org-confirm-babel-evaluate nil)
(defun doom-shut-up-a (orig-fn &rest args)
  (quiet! (apply orig-fn args)))
(advice-add 'org-babel-execute-src-block :aroud #'doom-shut-up-a)
#+end_src

*** Elisp REPL

æˆ‘è§‰å¾— elisp REPL (Read Eval Print Loop, äº¤äº’å¼è§£é‡Šå™¨) æ˜¯ä¸€ä¸ªä¸é”™çš„ä¸»æ„ï¼Œå³ä½¿å®ƒ
å¹¶ä¸æ˜¯å¾ˆæœ‰ç”¨ ğŸ˜›ã€‚æˆ‘ä»¬å¯ä»¥åœ¨ =cli.el= ä¸­æ·»åŠ ä¸€æ¡æ–°å‘½ä»¤æ¥å¯ç”¨ REPLã€‚

#+begin_src emacs-lisp :tangle cli.el
(defcli! repl ((in-rlwrap-p ["--rl"] "For internal use only."))
  "Start an elisp REPL."
  (when (and (executable-find "rlwrap") (not in-rlwrap-p))
    ;; For autocomplete
    (setq autocomplete-file "/tmp/doom_elisp_repl_symbols")
    (unless (file-exists-p autocomplete-file)
      (princ "\e[0;33mInitialising autocomplete list...\e[0m\n")
      (with-temp-buffer
        (cl-do-all-symbols (s)
          (let ((sym (symbol-name s)))
            (when (string-match-p "\\`[[:ascii:]][[:ascii:]]+\\'" sym)
              (insert sym "\n"))))
        (write-region nil nil autocomplete-file)))
    (princ "\e[F")
    (throw 'exit (list "rlwrap" "-f" autocomplete-file
                       (concat doom-emacs-dir "bin/doom") "repl" "--rl")))

  (doom-initialize-packages)
  (require 'engrave-faces-ansi)
  (setq engrave-faces-ansi-color-mode '3-bit)

  ;; For some reason (require 'parent-mode) doesn't work :(
  (defun parent-mode-list (mode)
    "Return a list of MODE and all its parent modes.

The returned list starts with the parent-most mode and ends with MODE."
    (let ((result ()))
      (parent-mode--worker mode (lambda (mode)
                                  (push mode result)))
      result))
  (defun parent-mode--worker (mode func)
    "For MODE and all its parent modes, call FUNC.

FUNC is first called for MODE, then for its parent, then for the parent's
parent, and so on.

MODE shall be a symbol referring to a function.
FUNC shall be a function taking one argument."
    (funcall func mode)
    (when (not (fboundp mode))
      (signal 'void-function (list mode)))
    (let ((modefunc (symbol-function mode)))
      (if (symbolp modefunc)
          ;; Hande all the modes that use (defalias 'foo-parent-mode (stuff)) as
          ;; their parent
          (parent-mode--worker modefunc func)
        (let ((parentmode (get mode 'derived-mode-parent)))
          (when parentmode
            (parent-mode--worker parentmode func))))))
  (provide 'parent-mode)
  ;; Some extra highlighting (needs parent-mode)
  (require 'rainbow-delimiters)
  (require 'highlight-quoted)
  (require 'highlight-numbers)
  (setq emacs-lisp-mode-hook '(rainbow-delimiters-mode
                               highlight-quoted-mode
                               highlight-numbers-mode))
  ;; Pretty print
  (defun pp-sexp (sexp)
    (with-temp-buffer
      (cl-prettyprint sexp)
      (emacs-lisp-mode)
      (font-lock-ensure)
      (with-current-buffer (engrave-faces-ansi-buffer)
        (princ (string-trim (buffer-string)))
        (kill-buffer (current-buffer)))))
  ;; Now do the REPL
  (defvar accumulated-input nil)
  (while t
    (condition-case nil
        (let ((input (if accumulated-input
                         (read-string "\e[31m .\e[0m  ")
                       (read-string "\e[31mÎ»:\e[0m "))))
          (setq input (concat accumulated-input
                              (when accumulated-input "\n")
                              input))
          (cond
           ((string-match-p "\\`[[:space:]]*\\'" input)
            nil)
           ((string= input "exit")
            (princ "\n") (kill-emacs 0))
           (t
            (condition-case err
                (let ((input-sexp (car (read-from-string input))))
                  (setq accumulated-input nil)
                  (pp-sexp (eval input-sexp))
                  (princ "\n"))
              ;; Caused when sexp in unbalanced
              (end-of-file (setq accumulated-input input))
              (error
               (cl-destructuring-bind (backtrace &optional type data . _)
                   (cons (doom-cli--backtrace) err)
                 (princ (concat "\e[1;31mERROR:\e[0m " (get type 'error-message)))
                 (princ "\n       ")
                 (pp-sexp (cons type data))
                 (when backtrace
                   (print! (bold "Backtrace:"))
                   (print-group!
                    (dolist (frame (seq-take backtrace 10))
                      (print!
                       "%0.74s" (replace-regexp-in-string
                                 "[\n\r]" "\\\\n"
                                 (format "%S" frame))))))
                 (princ "\n")))))))
      ;; C-d causes an end-of-file error
      (end-of-file (princ "exit\n") (kill-emacs 0)))
    (unless accumulated-input (princ "\n"))))
#+end_src

*** å¼‚æ­¥é…ç½®
Doom ä¸º =org-mode= æ·»åŠ äº† hook ~+literate-enable-recompile-h~ ã€‚è¿™æ˜¯ä¸€ä¸ªå¾ˆæ£’
çš„æƒ³æ³•ï¼Œä½†ä¸å¤ªç¬¦åˆæˆ‘çš„å£å‘³ã€‚å› ä¸ºæˆ‘ç›¸ä¿¡æˆ‘çš„éœ€æ±‚ç›¸å½“ç®€å•ï¼Œæ‰€ä»¥æˆ‘æŠŠå®ƒé‡å®šä¹‰ä¸ºäº†ä¸€ä¸ªç®€å•ã€
å¼‚æ­¥çš„å‡½æ•°ã€‚
#+begin_src emacs-lisp
(defvar +literate-tangle--proc nil)
(defvar +literate-tangle--proc-start-time nil)

(defadvice! +literate-tangle-async-h ()
  "A very simplified version of `+literate-tangle-h', but async."
  :override #'+literate-tangle-h
  (unless (getenv "__NOTANGLE")
    (let ((default-directory doom-private-dir))
      (when +literate-tangle--proc
        (message "Killing outdated tangle process...")
        (set-process-sentinel +literate-tangle--proc #'ignore)
        (kill-process +literate-tangle--proc)
        (sit-for 0.3)) ; ensure the message is seen for a bit
      (setq +literate-tangle--proc-start-time (float-time)
            +literate-tangle--proc
            (start-process "tangle-config"
                           (get-buffer-create " *tangle config*")
                           "emacs" "--batch" "--eval"
                           (format "(progn \
(require 'ox) \
(require 'ob-tangle) \
(setq org-confirm-babel-evaluate nil \
      org-inhibit-startup t \
      org-mode-hook nil \
      write-file-functions nil \
      before-save-hook nil \
      after-save-hook nil \
      vc-handled-backends nil \
      org-startup-folded nil \
      org-startup-indented nil) \
(org-babel-tangle-file \"%s\" \"%s\"))"
                                   +literate-config-file
                                   (expand-file-name (concat doom-module-config-file ".el")))))
      (set-process-sentinel +literate-tangle--proc #'+literate-tangle--sentinel)
      (run-at-time nil nil (lambda () (message "Tangling config.org"))) ; ensure shown after a save message
      "Tangling config.org...")))

(defun +literate-tangle--sentinel (process signal)
  (cond
   ((and (eq 'exit (process-status process))
         (= 0 (process-exit-status process)))
    (message "Tangled config.org sucessfully (took %.1fs)"
             (- (float-time) +literate-tangle--proc-start-time))
    (setq +literate-tangle--proc nil))
   ((memq (process-status process) (list 'exit 'signal))
    (pop-to-buffer (get-buffer " *tangle config*"))
    (message "Failed to tangle config.org (after %.1fs)"
             (- (float-time) +literate-tangle--proc-start-time))
    (setq +literate-tangle--proc nil))))

(defun +literate-tangle-check-finished ()
  (when (and (process-live-p +literate-tangle--proc)
             (yes-or-no-p "Config is currently retangling, would you please wait a few seconds?"))
    (switch-to-buffer " *tangle config*")
    (signal 'quit nil)))
(add-hook! 'kill-emacs-hook #'+literate-tangle-check-finished)
#+end_src

*** dashboard å¿«é€Ÿæ“ä½œ

å½“ä½¿ç”¨ dashboard æ—¶ï¼Œæˆ‘é€šå¸¸ä¸ä¼šåšä»€ä¹ˆæ“ä½œã€‚dashboard æœ‰è‡ªå·±çš„è¡Œä¸ºæ¨¡å¼ï¼Œå› æ­¤ä¸å¿…é­
å—å¤šä½™çš„æŒ‰é”®æ‘§æ®‹ --- æˆ‘ä»¬å°†ä¸€äº›ç®€å•ã€å¸¸ç”¨çš„å‘½ä»¤ç»‘å®šåˆ°å•é”®ä¸Šã€‚

#+begin_src emacs-lisp
(defun +doom-dashboard-setup-modified-keymap ()
  (setq +doom-dashboard-mode-map (make-sparse-keymap))
  (map! :map +doom-dashboard-mode-map
        :desc "Find file" :ne "f" #'find-file
        :desc "Recent files" :ne "r" #'consult-recent-file
        :desc "Config dir" :ne "C" #'doom/open-private-config
        :desc "Open config.org" :ne "c" (cmd! (find-file (expand-file-name "config.org" doom-private-dir)))
        :desc "Open dotfile" :ne "." (cmd! (doom-project-find-file "~/.config/"))
        :desc "Notes (roam)" :ne "n" #'org-roam-node-find
        :desc "Switch buffer" :ne "b" #'+vertico/switch-workspace-buffer
        :desc "Switch buffers (all)" :ne "B" #'consult-buffer
        :desc "IBuffer" :ne "i" #'ibuffer
        :desc "Previous buffer" :ne "p" #'previous-buffer
        :desc "Set theme" :ne "t" #'consult-theme
        :desc "Quit" :ne "Q" #'save-buffers-kill-terminal
        :desc "Show keybindings" :ne "h" (cmd! (which-key-show-keymap '+doom-dashboard-mode-map))))

(add-transient-hook! #'+doom-dashboard-mode (+doom-dashboard-setup-modified-keymap))
(add-transient-hook! #'+doom-dashboard-mode :append (+doom-dashboard-setup-modified-keymap))
(add-hook! 'doom-init-ui-hook :append (+doom-dashboard-setup-modified-keymap))
#+end_src

ä¸å¹¸çš„æ˜¯ï¼Œå¿«æ·é”®å¸®åŠ©å¹¶æ²¡æœ‰æŒ‰ç…§é¢„æœŸå·¥ä½œï¼Œä¸è¿‡æ€»ä½“ä¸Šè¿˜æ˜¯ç›¸å½“ä¸é”™çš„ã€‚

ç°åœ¨ dashboard å·²ç»å¾ˆæ–¹ä¾¿äº†ï¼Œé‚£æˆ‘ç°åœ¨éœ€è¦å¿«é€Ÿè°ƒå‡º dashboardã€‚
#+begin_src emacs-lisp
(map! :leader :desc "Dashboard" "d" #'+doom-dashboard/open)
#+end_src

** å…¶ä»–è®¾ç½®
*** ç¼–è¾‘ç›¸äº’ä½œç”¨
**** é¼ æ ‡æŒ‰é’®
#+begin_src emacs-lisp
(map! :n [mouse-8] #'better-jumper-jump-backward
      :n [mouse-9] #'better-jumper-jump-forward)
#+end_src
*** çª—å£æ ‡é¢˜
æˆ‘æ›´å–œæ¬¢çª—å£å±•ç¤ºç¼“å†²åŒºçš„åå­—ï¼Œç„¶åæ˜¯é¡¹ç›®æ–‡ä»¶å¤¹ (å¦‚æœå¯ç”¨)ã€‚
#+begin_src emacs-lisp
(setq frame-title-format
      '(""
        (:eval
         (if (s-contains-p org-roam-directory (or buffer-file-name ""))
             (replace-regexp-in-string
              ".*/[0-9]*-?" "â˜° "
              (subst-char-in-string ?_ ? buffer-file-name))
           "%b"))
        (:eval
         (let ((project-name (projectile-project-name)))
           (unless (string= "-" project-name)
             (format (if (buffer-modified-p) " â—‰ %s" " â€†â—â€† %s")
                     project-name))))))
#+end_src

æ¯”å¦‚è¯´æˆ‘æ‰“å¼€äº†ä¸€ä¸ªé…ç½®æ–‡ä»¶ï¼Œæ ‡é¢˜æ å°†æ˜¾ç¤º =config.org â— doom= ï¼Œå½“æˆ‘ä¿®æ”¹åæ ‡é¢˜æ å°±
å˜æˆäº† =config.org â—‰ doom= ã€‚
*** å¯åŠ¨ç”»é¢
Emacs å¯ä»¥åœ¨å¯åŠ¨ç”»é¢æ¸²æŸ“å›¾ç‰‡ï¼Œ[[https://github.com/MarioRicalde][MarioRicalde]] æƒ³å‡ºäº†ä¸€ä¸ª cracker (ç‚¹å­?)ï¼Œå¹¶å‘æˆ‘æ
ä¾›äº†ä¸€ä¸ª Emacs åˆ†å‰²çš„å›¾ç‰‡ /E/ ã€‚æˆ‘ä¹‹å‰ä½¿ç”¨çš„æ˜¯é»‘æ´çš„å›¾ç‰‡ï¼Œæˆ‘åœ¨å…³æ‰å¯åŠ¨ç”»é¢åå°±åªç”¨è¿™
ä¸ª /E/ äº†ã€‚

#+attr_latex: :width 0.2\linewidth
#+attr_html: :style width:20% :alt Fancy Emacs "E"
[[file:misc/splash-images/emacs-e.svg]]

ç°åœ¨æˆ‘ä»¬è°ƒæ•´å®ƒä½¿å…¶æ›´æ¥è¿‘ä¸»é¢˜ï¼Œå¹¶éšæ¡†æ¶è°ƒæ•´å¤§å°ã€‚

#+begin_src emacs-lisp
(defvar fancy-splash-image-template
  (expand-file-name "misc/splash-images/emacs-e-template.svg" doom-private-dir)
  "Default template svg used for the splash image, with substitutions from ")

(defvar fancy-splash-sizes
  `((:height 300 :min-height 50 :padding (0 . 2))
    (:height 250 :min-height 42 :padding (2 . 4))
    (:height 200 :min-height 35 :padding (3 . 3))
    (:height 150 :min-height 28 :padding (3 . 3))
    (:height 100 :min-height 20 :padding (2 . 2))
    (:height 75  :min-height 15 :padding (2 . 1))
    (:height 50  :min-height 10 :padding (1 . 0))
    (:height 1   :min-height 0  :padding (0 . 0)))
  "list of plists with the following properties
  :height the height of the image
  :min-height minimum `frame-height' for image
  :padding `+doom-dashboard-banner-padding' (top . bottom) to apply
  :template non-default template file
  :file file to use instead of template")

(defvar fancy-splash-template-colours
  '(("$colour1" . keywords) ("$colour2" . type) ("$colour3" . base5) ("$colour4" . base8))
  "list of colour-replacement alists of the form (\"$placeholder\" . 'theme-colour) which applied the template")

(unless (file-exists-p (expand-file-name "theme-splashes" doom-cache-dir))
  (make-directory (expand-file-name "theme-splashes" doom-cache-dir) t))

(defun fancy-splash-filename (theme-name height)
  (expand-file-name (concat (file-name-as-directory "theme-splashes")
                            theme-name
                            "-" (number-to-string height) ".svg")
                    doom-cache-dir))

(defun fancy-splash-clear-cache ()
  "Delete all cached fancy splash images"
  (interactive)
  (delete-directory (expand-file-name "theme-splashes" doom-cache-dir) t)
  (message "Cache cleared!"))

(defun fancy-splash-generate-image (template height)
  "Read TEMPLATE and create an image if HEIGHT with colour substitutions as
   described by `fancy-splash-template-colours' for the current theme"
  (with-temp-buffer
    (insert-file-contents template)
    (re-search-forward "$height" nil t)
    (replace-match (number-to-string height) nil nil)
    (dolist (substitution fancy-splash-template-colours)
      (goto-char (point-min))
      (while (re-search-forward (car substitution) nil t)
        (replace-match (doom-color (cdr substitution)) nil nil)))
    (write-region nil nil
                  (fancy-splash-filename (symbol-name doom-theme) height) nil nil)))

(defun fancy-splash-generate-images ()
  "Perform `fancy-splash-generate-image' in bulk"
  (dolist (size fancy-splash-sizes)
    (unless (plist-get size :file)
      (fancy-splash-generate-image (or (plist-get size :template)
                                       fancy-splash-image-template)
                                   (plist-get size :height)))))

(defun ensure-theme-splash-images-exist (&optional height)
  (unless (file-exists-p (fancy-splash-filename
                          (symbol-name doom-theme)
                          (or height
                              (plist-get (car fancy-splash-sizes) :height))))
    (fancy-splash-generate-images)))

(defun get-appropriate-splash ()
  (let ((height (frame-height)))
    (cl-some (lambda (size) (when (>= height (plist-get size :min-height)) size))
             fancy-splash-sizes)))

(setq fancy-splash-last-size nil)
(setq fancy-splash-last-theme nil)
(defun set-appropriate-splash (&rest _)
  (let ((appropriate-image (get-appropriate-splash)))
    (unless (and (equal appropriate-image fancy-splash-last-size)
                 (equal doom-theme fancy-splash-last-theme)))
    (unless (plist-get appropriate-image :file)
      (ensure-theme-splash-images-exist (plist-get appropriate-image :height)))
    (setq fancy-splash-image
          (or (plist-get appropriate-image :file)
              (fancy-splash-filename (symbol-name doom-theme) (plist-get appropriate-image :height))))
    (setq +doom-dashboard-banner-padding (plist-get appropriate-image :padding))
    (setq fancy-splash-last-size appropriate-image)
    (setq fancy-splash-last-theme doom-theme)
    (+doom-dashboard-reload)))

(add-hook 'window-size-change-functions #'set-appropriate-splash)
(add-hook 'doom-load-theme-hook #'set-appropriate-splash)
#+end_src

ç°åœ¨å”¯ä¸€çš„ç¼ºé™·æ˜¯å°‘äº†ä¸€æ¡æœ‰è¶£çš„çº¿ï¼Œæ— è®ºæ˜¯ä¸€äº›å…¬å¸çš„å±è¯ã€å¼€å‘äººå‘˜çš„å€Ÿå£ï¼Œè¿˜æ˜¯ä¸€ä¸ªæœ‰è¶£ (
æ— ç”¨) çš„äº‹å®ã€‚

ä»¥ä¸‹çš„å†…å®¹ç›¸å½“é•¿ï¼Œä½†æœ¬è´¨ä¸Šæ˜¯ï¼š
+ ä» API ä¸­è·å–è¯­å¥
+ å°†å…¶å¼‚æ­¥æ’å…¥ dashboard
+ å°† ~å…‰æ ‡~ ç§»è‡³è¯­å¥
+ åœ¨å®ƒæ‹‰å–è¯·æ±‚çš„å‡ ç§’é’Ÿå†…é‡ç”¨æœ€åä¸€ä¸ªè¯­å¥

#+begin_src emacs-lisp
(defvar splash-phrase-source-folder
  (expand-file-name "misc/splash-phrases" doom-private-dir)
  "A folder of text files with a fun phrase on each line.")

(defvar splash-phrase-sources
  (let* ((files (directory-files splash-phrase-source-folder nil "\\.txt\\'"))
         (sets (delete-dups (mapcar
                             (lambda (file)
                               (replace-regexp-in-string "\\(?:-[0-9]+-\\w+\\)?\\.txt" "" file))
                             files))))
    (mapcar (lambda (sset)
              (cons sset
                    (delq nil (mapcar
                               (lambda (file)
                                 (when (string-match-p (regexp-quote sset) file)
                                   file))
                               files))))
            sets))
  "A list of cons giving the phrase set name, and a list of files which contain phrase components.")

(defvar splash-phrase-set
  (nth (random (length splash-phrase-sources)) (mapcar #'car splash-phrase-sources))
  "The default phrase set. See `splash-phrase-sources'.")

(defun splase-phrase-set-random-set ()
  "Set a new random splash phrase set."
  (interactive)
  (setq splash-phrase-set
        (nth (random (1- (length splash-phrase-sources)))
             (cl-set-difference (mapcar #'car splash-phrase-sources) (list splash-phrase-set))))
  (+doom-dashboard-reload t))

(defvar splase-phrase--cache nil)

(defun splash-phrase-get-from-file (file)
  "Fetch a random line from FILE."
  (let ((lines (or (cdr (assoc file splase-phrase--cache))
                   (cdar (push (cons file
                                     (with-temp-buffer
                                       (insert-file-contents (expand-file-name file splash-phrase-source-folder))
                                       (split-string (string-trim (buffer-string)) "\n")))
                               splase-phrase--cache)))))
    (nth (random (length lines)) lines)))

(defun splash-phrase (&optional set)
  "Construct a splash phrase from SET. See `splash-phrase-sources'."
  (mapconcat
   #'splash-phrase-get-from-file
   (cdr (assoc (or set splash-phrase-set) splash-phrase-sources))
   " "))

(defun doom-dashboard-phrase ()
  "Get a splash phrase, flow it over multiple lines as needed, and make fontify it."
  (mapconcat
   (lambda (line)
     (+doom-dashboard--center
      +doom-dashboard--width
      (with-temp-buffer
        (insert-text-button
         line
         'action
         (lambda (_) (+doom-dashboard-reload t))
         'face 'doom-dashboard-menu-title
         'mouse-face 'doom-dashboard-menu-title
         'help-echo "Random phrase"
         'follow-link t)
        (buffer-string))))
   (split-string
    (with-temp-buffer
      (insert (splash-phrase))
      (setq fill-column (min 70 (/ (* 2 (window-width)) 3)))
      (fill-region (point-min) (point-max))
      (buffer-string))
    "\n")
   "\n"))

(defadvice! doom-dashboard-widget-loaded-with-phrase ()
  :override #'doom-dashboard-widget-loaded
  (setq line-spacing 0.2)
  (insert
   "\n\n"
   (propertize
    (+doom-dashboard--center
     +doom-dashboard--width
     (doom-display-benchmark-h 'return))
    'face 'doom-dashboard-loaded)
   "\n"
   (doom-dashboard-phrase)
   "\n"))
#+end_src

æœ€ç»ˆ doom dashboard è¿™äº›æœ‰ç”¨çš„å‘½ä»¤å¯¹æˆ‘ä¸å†æœ‰ç”¨ã€‚æ‰€ä»¥æˆ‘å…³é—­äº†è¿™äº›è®¾ç½®ï¼Œå¹¶å…³é—­äº†
modeline å’Œ ~hl-line-mode~ ï¼Œéšè—äº†å…‰æ ‡ï¼Œè¿™è®© dashboard æ˜¾å¾—æ ¼å¤– /æ¸…çˆ½/ ã€‚

#+begin_src emacs-lisp
(remove-hook '+doom-dashboard-functions
             #'doom-dashboard-widget-shortmenu)
(add-hook! '+doom-dashboard-mode-hook (hide-mode-line-mode 1)
           (hl-line-mode 1))
(setq-hook! '+doom-dashboard-mode-hook evil-normal-state-cursor
            (list nil))
#+end_src

æœ€åæˆ‘ä»¬å¾—åˆ°äº†ä¸€ä¸ªæœ€å°åŒ–çœ‹èµ·æ¥éå¸¸æ£’çš„å¯åŠ¨ç•Œé¢ã€‚

#+attr_html: :class invertible :alt The splash screen, just loaded.
[[https://tecosaur.com/lfs/emacs-config/screenshots/splash-screen.png]]

æˆ‘å½“ç„¶ä¸ä¼šå¿˜è®° ASCII æ¨ªå¹…ï¼å½“ç„¶ä¹Ÿæ˜¯ç®€åŒ–ï¼
#+begin_src emacs-lisp
(defun doom-dashboard-draw-ascii-emacs-banner-fn ()
  (let* ((banner
          '(",---.,-.-.,---.,---.,---."
            "|---'| | |,---||    `---."
            "`---'` ' '`---^`---'`---'"))
         (longest-line (apply #'max (mapcar #'length banner))))
    (put-text-property
     (point)
     (dolist (line banner (point))
       (insert (+doom-dashboard--center
                +doom-dashboard--width
                (concat
                 line (make-string (max 0 (- longest-line (length line)))
                                   32)))
               "\n"))
     'face 'doom-dashboard-banner)))

(unless (display-graphic-p)
  ; for some reason this messes up the graphical splash screen atm
  (setq +doom-dashboard-ascii-banner-fn  #'doom-dashboard-draw-ascii-emacs-banner-fn))
#+end_src

*** systemd å®ˆæŠ¤è¿›ç¨‹
æˆ‘é€šå¸¸ä¼šä½¿ç”¨ systemd å¯åŠ¨ä¸€ä¸ª Emacs æœåŠ¡å™¨
#+name: emacsclient service
#+begin_src systemd :tangle ~/.config/systemd/user/emacs.service :mkdirp yes
[Unit]
Description=Emacs server daemon
Documentation=info:emacs man:emacs(1) https://gnu.org/software/emacs/

[Service]
Type=forking
ExecStart=sh -c 'emacs --daemon && emacsclient -c --eval "(delete-frame)"'
ExecStop=/usr/bin/emacsclient --no-wait --eval "(progn (setq kill-emacs-hook nil) (kill emacs))"
Restart=on-failure

[Install]
WantedBy=default.target
#+end_src

è®©å…¶å¼€æœºè‡ªå¯åŠ¨
#+begin_src shell :tangle (if (string= "enabled\n" (shell-command-to-string "systemctl --user is-enabled emacs.service")) "no" "setup.sh")
systemctl --user enable emacs.service
#+end_src

å‡ºäºä¸€äº›åŸå› ï¼Œå¦‚æœåœ¨åˆå§‹åŒ–è¿‡ç¨‹å¼€å§‹æ—¶æ²¡æœ‰å¼€å¯æ¡†æ¶ï¼Œå®ˆæŠ¤è¿›ç¨‹ä¼¼ä¹å¹¶ä¸å–œæ¬¢åœ¨ä¹‹å
æ‰“å¼€æ¡†æ¶ --- å› æ­¤ ~&& emacsclient~ éœ€è¦ä½œä¸º =ExecStart= å€¼çš„ä¸€éƒ¨åˆ†ã€‚

ç°åœ¨æˆ‘ä»¬å¯ä»¥è®¾ç½®å¥½ç›¸åº”æ¡Œé¢ç¯å¢ƒçš„å…¥å£ï¼Œæ·»åŠ æ‰“å¼€æ–‡ä»¶çš„é»˜è®¤åº”ç”¨ã€‚

#+begin_src conf :tangle ~/.local/share/applications/emacs-client.desktop :mkdirp yes
[Desktop Entry]
Name=Emacs client
GenericName=Text Editor
Comment=A flexible platform for end-user applications
MimeType=text/english;text/plain;text/x-makefile;text/x-c++hdr;text/x-c++src;text/x-chdr;text/x-csrc;text/x-java;text/x-moc;text/x-pascal;text/x-tcl;text/x-tex;application/x-shellscript;text/x-c;text/x-c++;
Exec=emacsclient -create-frame --alternate-editor="" --no-wait %F
Icon=emacs
Type=Application
Terminal=false
Categories=TextEditor;Utility;
StartupWMClass=Emacs
Keywords=Text;Editor;
X-KDE-StartupNotify=false
#+end_src

ä¸è¿‡ä½¿ç”¨å®ˆæŠ¤è¿›ç¨‹å¯åŠ¨æ—¶ï¼Œæˆ‘æ€»æƒ³ç”¨å…¶è¿›è¡Œä¸€äº›ç‰¹æ®Šçš„æ“ä½œï¼Œæ‰€ä»¥æˆ‘ä¸åœ¨ä¹å®ƒä¼šåœ¨å¯åŠ¨æ—¶æ¶ˆè€—ä¸€äº›
æ—¶é—´ã€‚åŒæ—¶ç”¨å®ƒæŒç»­è¿è¡Œ =mu4e= (email client)ã€‚

å½“ç„¶ä¹Ÿå¯ä»¥ä½œä¸º IRC å®¢æˆ·ç«¯ (=circe=) å¯ç”¨ï¼Œä½†å¥½åƒåœ¨éå›¾å½¢ä¼šè¯å¯åŠ¨æ—¶æœ‰äº›é—®é¢˜ã€‚

æœ€åï¼Œè¿‡ä¸€ä¼šä¼¼ä¹æ–°çš„ Emacsclient æ¡†æ¶å¼€å§‹äº =*scratch*= ç¼“å†²åŒºè€Œä¸æ˜¯
dashboardï¼Œæˆ‘ä¸å¤ªç¡®å®šä¸ºä»€ä¹ˆä¼šå‘ç”Ÿè¿™ç§æƒ…å†µã€‚ä½†æˆ‘æ›´å–œæ¬¢ dashboardï¼Œæ‰€ä»¥è®©æˆ‘ä»¬ç¡®ä¿å®ƒæ€»
æ˜¯åœ¨æ–°æ¡†æ¶ä¸­å±•ç°ã€‚

#+name: daemon initialisation
#+begin_src emacs-lisp
(defun greedily-do-daemon-setup ()
  (require 'org)
  (when (require 'mu4e nil t)
    (setq mu4e-confirm-quit t)
    (setq +mu4e-lock-greedy t)
    (setq +mu4e-lock-relaxed t)
    (+mu4e-lock-add-watcher)
    (when (+mu4e-lock-available t)
      (mu4e~start)))
  (when (require 'elfeed nil t)
    (run-at-time nil (* 8 60 60) #'elfeed-update)))

(when (daemonp)
  (add-hook 'emacs-startup-hook #'greedily-do-daemon-setup)
  (add-hook! 'server-after-make-frame-hook
    (unless (string-match-p "\\*draft\\|\\*stdin\\|emacs-everywhere" (buffer-name))
      (switch-to-buffer +doom-dashboard-name))))
#+end_src
*** emacs å®¢æˆ·ç«¯åˆ«å
æˆ‘ç»å¸¸æƒ³åœ¨ç»ˆç«¯ä»¿çœŸå™¨ä¸­ä½¿ç”¨ Emacsã€‚ä¸ºäº†è®©ä½¿ç”¨æ›´åŠ æ–¹ä¾¿ï¼Œæˆ‘éœ€è¦å£°æ˜ä¸€äº›åˆ«åã€‚

ç„¶è€Œï¼Œå°†ç®€çŸ­æ–¹ä¾¿çš„è„šæœ¬æ”¾å…¥ =~/.local/bin= æ˜¯ä¸€ç§æœ‰æ•ˆçš„æ–¹æ³•ï¼Œé™¤äº†å…è®¸æˆ‘å°†åˆ«åæ”¾å…¥åˆ«
çš„ shell å¤–ï¼Œè¿˜å¯ä»¥åšä¸€äº›åˆ«çš„èŠ±é‡Œèƒ¡å“¨çš„æ“ä½œ --- æ¯”å¦‚ï¼š
+ æ¥å—æ ‡å‡†è¾“å…¥çš„æ•°æ®ï¼Œå°†å…¶æ”¾å…¥ä¸´æ—¶æ–‡ä»¶å¹¶ç«‹å³æ‰“å¼€å®ƒ
+ å‡è®¾ ~$DISPLAY~ æ²¡æœ‰è¢«è®¾ç½®è€Œä½¿ç”¨ =tty= æ˜¯ä¸€ä¸ªç›¸å½“å¥½çš„ä¸»æ„ (SSH ç›¸å…³ä¼šè¯)
+ åœ¨æ”¯æŒ 24-bit è‰²å½©çš„ç»ˆç«¯æ—¶ï¼Œå°† ~TERM~ è®¾ç½®ä¸º =terminfo= æ¥æ”¯æŒ 24-bit è‰²å½©
+ å°† GUI =emacsclient= å®ä¾‹é»˜è®¤è¡Œä¸ºæ›´æ”¹ä¸ºéé˜»å¡ (~--no-wait~)ï¼Œè€Œéä½¿ç”¨æ ‡
  å¿— ~-w~

æˆ‘ä¼šä½¿ç”¨ =sh= ï¼Œä½†ä½¿ç”¨æ•°ç»„éšå½¢å‚æ•°æ“ä½œå®åœ¨å¤ªæ–¹ä¾¿äº†ï¼Œå› æ­¤æˆ‘å°†ä½¿ç”¨ =bash= ã€‚ç›¸æ¯”äº
=sh= ä»…ä»…ä½¿ç”¨äº†æ•°ç»„ï¼Œå› æ­¤å¯¹äº =ksh= ç­‰ä¹Ÿåº”è¯¥å¯ä»¥å·¥ä½œã€‚

#+name: e
#+begin_src shell :tangle ~/.local/bin/e :mkdirp yes :tangle-mode (identity #o755) :comments no
#!/usr/bin/env bash
force_tty=false
force_wait=false
stdin_mode=""

args=()

while :; do
    case "$1" in
        -t | -nw | --tty)
            force_tty=true
            shift ;;
        -w | --wait)
            force_wait=true
            shift ;;
        -m | --mode)
            stdin_mode=" ($2-mode)"
            shift 2 ;;
        -h | --help)
            echo -e "\033[1mUsage: e [-t] [-m MODE] [OPTIONS] FILE [-]\033[0m

Emacs client convenience wrapper.

\033[1mOptions:\033[0m
\033[0;34m-h, --help\033[0m            Show this message
\033[0;34m-t, -nw, --tty\033[0m        Force terminal mode
\033[0;34m-w, --wait\033[0m            Don't supply \033[0;34m--no-wait\033[0m to graphical emacsclient
\033[0;34m-\033[0m                     Take \033[0;33mstdin\033[0m (when last argument)
\033[0;34m-m MODE, --mode MODE\033[0m  Mode to open \033[0;33mstdin\033[0m with

Run \033[0;32memacsclient --help\033[0m to see help for the emacsclient."
            exit 0 ;;
        --*=*)
            set -- "$@" "${1%%=*}" "${1#*=}"
            shift ;;
        ,*)
            if [ "$#" = 0 ]; then
                break; fi
            args+=("$1")
            shift ;;
    esac
done

if [ ! "${#args[*]}" = 0 ] && [ "${args[-1]}" = "-" ]; then
    unset 'args[-1]'
    TMP="$(mktemp /tmp/emacsstdin-XXX)"
    cat > "$TMP"
    args+=(--eval "(let ((b (generate-new-buffer \"*stdin*\"))) (switch-to-buffer b) (insert-file-contents \"$TMP\") (delete-file \"$TMP\")${stdin_mode})")
fi

if [ -z "$DISPLAY" ] || $force_tty; then
    # detect terminals with sneaky 24-bit support
    if { [ "$COLORTERM" = truecolor ] || [ "$COLORTERM" = 24bit ]; } \
        && [ "$(tput colors 2>/dev/null)" -lt 257 ]; then
        if echo "$TERM" | grep -q "^\w\+-[0-9]"; then
            termstub="${TERM%%-*}"; else
            termstub="${TERM#*-}"; fi
        if infocmp "$termstub-direct" >/dev/null 2>&1; then
            TERM="$termstub-direct"; else
            TERM="xterm-direct"; fi # should be fairly safe
    fi
    emacsclient --tty -create-frame --alternate-editor="" "${args[@]}"
else
    if ! $force_wait; then
        args+=(--no-wait); fi
    emacsclient -create-frame --alternate-editor="" "${args[@]}"
fi
#+end_src

ç°åœ¨ï¼Œå¯ä»¥å°† =e= ä¸ magit ä¸€èµ·ä½¿ç”¨ï¼Œä½†ä¸ºäº†æœ€å¤§ç¨‹åº¦çš„æ‡’æƒ°ï¼Œå¯ä»¥åœ¨ç»ˆç«¯ä¸­åœ¨è®¾ç½®ä¸€äº›åˆ«
åã€‚
#+begin_src shell :tangle no
alias m='e --eval "(progn (magit-status) (delete-other-windows))"'
alias mt="m -t"
alias et="e -t"
#+end_src
*** æç¤ºè¿è¡Œå¯åŠ¨è„šæœ¬
åœ¨é…ç½®æ—¶ï¼Œå†…å®¹å¤šå¤šå°‘å°‘éƒ½ä¼šå­˜åœ¨äº =./setup.sh= ä¸­ï¼Œå¦‚æœä¸è¿è¡Œç»ˆå½’æ˜¯ä¸å¥½çš„ã€‚ä¸ºäº†æç¤ºæˆ‘
åœ¨å¿…è¦æ—¶è¿è¡Œå®ƒï¼Œè®©æˆ‘ä»¬ä¸ºéœ€è¦è¿è¡Œå®ƒçš„æ—¶å€™ï¼Œæ·»åŠ ä¸€æ¡å°æç¤ºã€‚

#+name: run-setup
#+begin_src emacs-lisp :tangle no
(if (file-exists-p "setup.sh")
    (if (string-empty-p (string-trim (with-temp-buffer (insert-file-contents "setup.sh") (buffer-string)) "#!/usr/bin/env bash"))
        (message ";; Setup script is empty")
      (message ";; Detected content in the setup script")
      (pp-to-string
       `(unless noninteractive
          (defun +config-run-setup ()
            (when (yes-or-no-p (format "%s The setup script has content. Check and run the script?"
                                       (propertize "Warning!" 'face '(bold warning))))
              (find-file (expand-file-name "setup.sh" doom-private-dir))
              (when (yes-or-no-p "Would you like to run this script?")
                (async-shell-command "./setup.sh"))))
          (add-hook! 'doom-init-ui-hook
            (run-at-time nil nil #'+config-run-setup)))))
  (message ";; setup.sh did not exist during tangle. Tangle again.")
  (pp-to-string
   `(unless noninteractive
      (add-hook! 'doom-init-ui-hook #'+literate-tangle-async-h))))
#+end_src

#+begin_src emacs-lisp :noweb no-export
<<run-setup()>>
#+end_src

* åŒ…
** åŠ è½½ç»“æ„
:PROPERTIES:
:header-args:emacs-lisp: :tangle no
:END:
è¿™æ˜¯å®‰è£…åŒ…çš„åœ°æ–¹ï¼Œé€šè¿‡åœ¨ =package.el= ä¸­ä½¿ç”¨ ~package!~ å®æ¥å£°æ˜å®ƒä»¬ã€‚ä¹‹ååœ¨å‘½ä»¤è¡Œä¸­
è¿è¡Œ ~doom refresh~ ã€‚
éœ€è¦æ³¨æ„ï¼Œä¸åº”è¯¥å°†è¯¥æ–‡ä»¶ç¼–è¯‘ä¸ºå­—èŠ‚ç ã€‚
#+begin_src emacs-lisp :tangle "package.el" :comments no
;; -*- no-byte-compile: t; -*-
#+end_src

ä¹‹åä½ éœ€è¦é‡å¯ Emacs ç¡®ä¿æ”¹å˜ç”Ÿæ•ˆï¼æˆ–è€…åœ¨æœ€æ–°ç‰ˆ doom ä¸­è¿è¡ŒæŒ‡ä»¤ =M-x doom/reload= ã€‚

*è­¦å‘Š*: ä¸è¦ç¦ç”¨ =~/.emacs.d/core/package.el= ä¸­åˆ—å‡ºçš„åŒ…ã€‚Doom ä¾èµ–è¿™äº›ï¼Œç¦ç”¨å®ƒä»¬
å¯èƒ½å‡ºç°ä¸¥é‡é—®é¢˜ã€‚

*** MELPA / ELPA / emacsmirror ä¸­çš„åŒ…
æƒ³å®‰è£… MELPA / ELPA / emacsmirror ä¸­çš„åŒ…å¯ä»¥é‡‡ç”¨ä»¥ä¸‹è¯­å¥
#+begin_src emacs-lisp
(package! some-package)
#+end_src

*** git ä»“åº“ä¸­çš„åŒ…
å¦‚æœä½ æƒ³ä»æŒ‡å®šä»“åº“ä¸­å®‰è£…æŸä¸ªåŒ…ï¼Œéœ€è¦æŒ‡å®šå‚æ•° ~:recipe~ ã€‚ä½ å¯ä»¥ [[https://github.com/raxod502/straight.el#the-recipe-format][åœ¨æ­¤]] æ‰¾åˆ°æ›´å¤šå…³äº
~:recipe~ çš„ä¿¡æ¯ã€‚
#+begin_src emacs-lisp
(package! another-package
  :recipe (:host github :repo "username/repo"))
#+end_src

å¦‚æœä½ å®‰è£…çš„åŒ…ä¸­å¹¶ä¸åŒ…å« ~PACKAGENAME.el~ æ–‡ä»¶ï¼Œæˆ–è€…ä½äº repo çš„å­ç›®å½•ä¸­ï¼Œåˆ™éœ€è¦æŒ‡
å®š ~:recipe~ çš„ ~:files~ å‚æ•°
#+begin_src emacs-lisp
(package! this-package
  :recipe (:hoet github :repo "username/repo"
           :file ("some-file.el" "src/lisp/*.el")))
#+end_src

*** å…³é—­å†…ç½®çš„åŒ…
å‡ºäºæŸäº›åŸå› ï¼Œä½ æƒ³å…³é—­ Doom å†…ç½®çš„åŒ…ï¼Œå¯ä»¥ä½¿ç”¨ ~:disable~ å±æ€§
#+begin_src emacs-lisp
(package! builtin-package :disable t)
#+end_src

ä½ å¯ä»¥ä½¿ç”¨ ~:recipe~ å†ä¸æŒ‡å®šæ‰€æœ‰å±æ€§çš„æƒ…å†µä¸‹è¦†ç›–æ‰å†…å»ºåŒ…ã€‚å®ƒå°†ä» Doom æˆ– MELPA /
ELPA / Emacsmirror å¯»æ‰¾  ~:recipe~ å…¶ä»–éƒ¨åˆ†ã€‚
#+begin_src emacs-lisp
(package! builtin-package :recipe (:nonrecursive t))
(package! builtin-package-2 :recipe (:repo "myfork/package"))
#+end_src

æŒ‡å®š ~:branch~ å¯ä»¥ä¸ºåŒ…æŒ‡å®šåˆ†æ”¯æˆ– tagã€‚
#+begin_src emacs-lisp
(package! builtin-package :recipe (:branch "develop"))
#+end_src

** ç®€ä¾¿
*** Avy
#+begin_quote
æ¥è‡ª =:config default= æ¨¡å—
#+end_quote

ä½¿ç”¨ QWERTY å¸ƒå±€çš„ä¸»è¡Œæ˜¯è·³åˆ°ç¼“å†²åŒºæŒ‡å®šä½ç½®çš„å¥½æ–¹æ³•ï¼Œéå¸¸æ–¹ä¾¿ â€¦â€¦ åªä¸è¿‡æˆ‘ç”¨çš„æ˜¯
Colemak å¸ƒå±€ã€‚

#+begin_src emacs-lisp :tangle (if (= 0 (call-process "sh" nil nil nil "-c" "dmesg |grep -q 'ErgoDox'")) "yes" "no")
(after! avy
  ;; home row priorities: 8 6 4 5 - - 1 2 3 7
  (setq avy-keys '(?n ?e ?i ?s ?t ?r ?i ?a))
  )
#+end_src

*** æ—‹è½¬ (çª—å£ç®¡ç†)
=rotate= åŒ…åªæ˜¯å¢åŠ äº†æ—‹è½¬çª—å£å¸ƒå±€çš„èƒ½åŠ›ï¼Œä½†è¿™å¯¹æˆ‘æ¥è¯´å¬èµ·æ¥ä¸é”™ã€‚

#+begin_src emacs-lisp
(package! rotate :pin "4e9ac3ff800880bd9b705794ef0f7c99d72900a6")
#+end_src

*** Emacs Everywhere
è¿™ä¸ªåå­—å°±è¯´åäº†ä¸€åˆ‡ã€‚ å®ƒç”± =:app everywhere= åŠ è½½å’Œè®¾ç½®ï¼Œä½†æ˜¯å½“æˆ‘å¼€å‘æ—¶ï¼Œæˆ‘æƒ³å°†å…¶
ä½œä¸ºå­æ¨¡å—çš„ unpin ç‰ˆæœ¬ä½¿ç”¨ã€‚

#+begin_src emacs-lisp :tangle packages.el
(package! emacs-everywhere
  :recipe (:local-repo "lisp/emacs-everywhere"))
(unpin! emacs-everywhere)
#+end_src

æ­¤å¤–ï¼Œæˆ‘å°†åšå‡ºä¸€äº›åœ¨ Doom æ¨¡å—ä¸­æ²¡æœ‰åšå‡ºçš„ä¸ªäººé€‰æ‹©ã€‚
#+begin_src emacs-lisp
(use-package! emacs-everywhere
  :if (daemonp)
  :config
  (require 'spell-fu)
  (setq emacs-everywhere-major-mode-function #'org-mode
        emacs-everywhere-frame-name-format "Edit âˆ· %s â€” %s")
  (defadvice! emacs-everywhere-raise-frame ()
    :after #'emacs-everywhere-set-frame-name
    (setq emacs-everywhere-frame-name (format emacs-everywhere-frame-name-format
                                (emacs-everywhere-app-class emacs-everywhere-current-app)
                                (truncate-string-to-width
                                 (emacs-everywhere-app-title emacs-everywhere-current-app)
                                 45 nil nil "â€¦")))
    ;; need to wait till frame refresh happen before really set
    (run-with-timer 0.1 nil #'emacs-everywhere-raise-frame-1))
  (defun emacs-everywhere-raise-frame-1 ()
    (call-process "wmctrl" nil nil nil "-a" emacs-everywhere-frame-name)))
#+end_src

*** Which-key
#+begin_quote
æ¥è‡ª =:config default= æ¨¡å—
#+end_quote

å°†è¿™ä¸ªå¼¹å‡ºçª—å£å¼¹å‡ºå¾—æ›´å¿«äº›
#+begin_src emacs-lisp
(setq which-key-idle-delay 0.5) ;; I need the help, I really do
#+end_src

æˆ‘è®¤ä¸ºè¿™ä¹ˆå¤š =evil-= å‡ºç°åœ¨å¼¹å‡ºçª—å£ä¸­æœ‰ç‚¹è¿‡äºå†—é•¿ï¼Œè®©æˆ‘ä»¬æ”¹å˜å®ƒï¼Œå¹¶åœ¨æˆ‘ä»¬å¤„ç†å®ƒçš„åŒæ—¶
åšä¸€äº›å…¶ä»–ç±»ä¼¼çš„è°ƒæ•´ã€‚
#+begin_src emacs-lisp
(setq which-key-allow-multiple-replacements t)
(after! which-key
  (pushnew!
   which-key-replacement-alist
   '(("" . "\\`+?evil[-:]?\\(?:a-\\)?\\(.*\\)") . (nil . "â—‚\\1"))
   '(("\\`g s" . "\\`evilem--?motion-\\(.*\\)") . (nil . "â—ƒ\\1"))
   ))
#+end_src

#+attr_html: :class invertible :alt Whichkey triggered on an evil motion
[[https://tecosaur.com/lfs/emacs-config/screenshots/whichkey-evil.png]]

** å·¥å…·
*** ç¼©å†™
æˆ‘ä»¥ä¸‹é…ç½®éœ€è¦æ„Ÿè°¢ [[https://emacs.stackexchange.com/questions/45462/use-a-single-abbrev-table-for-multiple-modes/45476#45476][Emacs Stack Exchange ä¸Šçš„ã€Œåœ¨å¤šç§æ¨¡å¼ä¸‹ä½¿ç”¨ç»Ÿä¸€çš„ç¼©å†™è¡¨ã€]]ã€‚
#+begin_src emacs-lisp
(add-hook 'doom-first-buffer-hook
          (defun +abbrev-file-name ()
            (setq-default abbrev-mode t)
            (setq abbrev-file-name (expand-file-name "abbrev.el" doom-private-dir))))
#+end_src

*** è¶…å¤§æ–‡ä»¶
/è¶…å¤§æ–‡ä»¶/ æ¨¡å¼å…è®¸ä»¥å— (chunk) çš„å½¢å¼åŠ è½½å¤§æ–‡ä»¶ï¼Œä¼˜åŒ–é‚£äº›å¤§çš„ç¦»è°±çš„æ–‡ä»¶çš„æ‰“å¼€ä½“éªŒã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! vlf :recipe (:host github :repo "m00natic/vlfi" :files ("*.el"))
  :pin "cc02f2533782d6b9b628cec7e2dcf25b2d05a27c" :disable t)
#+end_src

ä¸è¦å»¶è¿ŸåŠ è½½ VLFï¼Œå¯åŠ¨ä¸ä¹…å°±ç›´æ¥åŠ è½½å®ƒã€‚
#+begin_src emacs-lisp
(use-package! vlf-setup
  :defer-incrementally vlf-tune vlf-base vlf-write vlf-search vlf-occur vlf-follow vlf-ediff vlf)
#+end_src

*** Eros
#+begin_quote
æ¥è‡ª =:tools eval= æ¨¡å—
#+end_quote

è¿™ä¸ªåŒ…å¯ä»¥ä¿®æ”¹ emacs lisp å†…è”æ‰§è¡Œçš„è§†è§‰æ•ˆæœï¼Œè®©è¿™ä¸ªç»“æœçš„å‰ç¼€æ›´å¥½çœ‹ä¸€ç‚¹ã€‚
#+begin_src emacs-lisp
(setq eros-eval-result-prefix "âŸ¹ ") ; default =>
#+end_src

*** EVIL
#+begin_quote
æ¥è‡ª =:editor evil= æ¨¡å—
#+end_quote

æˆ‘æƒ³æ›¿æ¢æ—¶ï¼Œæˆ‘å¸Œæœ›è¿™æ˜¯å…¨å±€çš„ --- å°†è¿™ä¸€è¡Œä¸ºä¿®æ”¹ä¸ºé»˜è®¤å€¼ã€‚

ç°åœ¨ EVIL ç›¸å½“å…³å¿ƒä¸ VIM é»˜è®¤å€¼çš„è¡Œä¸ºçš„å…¼å®¹æ€§ã€‚ä¸è¿‡æˆ‘æ‰ä¸å…³å¿ƒã€‚æœ‰ä¸€äº›ç‰¹æ®Šçš„è®¾å®šï¼Œæˆ‘
æ›´å¸Œæœ›æ˜¯åˆ«çš„è¡Œä¸ºï¼Œæ‰€ä»¥èµ¶ç´§æŠŠå®ƒä¿®æ”¹æ‰ã€‚
#+begin_src emacs-lisp
(after! evil
  (setq evil-ex-substitute-global t    ; æˆ‘å–œæ¬¢å°† s/../.. è®¾ç½®ä¸ºå…¨å±€é»˜è®¤è¡Œä¸º
        evil-move-cursor-back nil      ; åœ¨åˆ‡æ¢æ’å…¥æ¨¡å¼æ—¶ä¸è¦å°†é¼ æ ‡åç§»
        evil-kill-on-visual-paste nil  ; ä¸è¦æŠŠè¦†ç›–çš„æ–‡æœ¬æ”¾åœ¨å‰ªåˆ‡æ¿ä¸­
        ))
#+end_src

æˆ‘ä¸ä½¿ç”¨ ~evil-escape-mode~ æ‰€ä»¥æˆ‘æŠŠå®ƒå…³é—­äº†ï¼Œè€Œä¸”æˆ‘å¬è¯´å®ƒä¼šäº§ç”Ÿæ‰“å­—å»¶è¿Ÿã€‚æˆ‘ä¸ç¡®å®š
è¿™ä¹ˆåšæ˜¯å¦æ­£ç¡®ï¼Œä½†å®ƒé¢å¤–å¸¦å…¥çš„ ~pre-command-hook~ å¯¹æˆ‘æ¥è¯´æ²¡ä»€ä¹ˆç”¨ï¼Œæ‰€ä»¥â€¦â€¦
å®ƒä¼¼ä¹è¿˜æœ‰ä¸€ä¸ªä¸“ç”¨åŒ…ï¼Œè®©æˆ‘ä»¬é˜»æ­¢è¿™ä¸ªå®‰è£…åŒ…ï¼Œåœ¨å¯åŠ¨æ—¶ä¸å¯ç”¨å®ƒã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! evil-escape :disable t)
#+end_src

*** Consult
#+begin_quote
æ¥è‡ª =:completion vertico= æ¨¡å—
#+end_quote

è‡ªä»ç”¨äº† [[Marginalia]], ä¸éœ€è¦å†ç”¨é¢å¤–çš„æ ‡è®°ä¹Ÿèƒ½å¾ˆæ¸…æ™°çš„åŒºåˆ†ç¼“å†²åŒºä¸æ–‡ä»¶ã€‚

#+begin_src emacs-lisp
(after! consult
  (set-face-attribute 'consult-file nil :inherit 'consult-buffer)
  (setf (plist-get (alist-get 'perl consult-async-split-styles-alist)
                   :initial) ";"))
#+end_src

*** Magit
#+begin_quote
æ¥è‡ª =:tools magit= æ¨¡å—
#+end_quote

[[xkcd:1597]]

Magit è¶…æ£’ï¼Œæ„Ÿè°¢åˆ¶ä½œå¦‚æ­¤æ£’çš„åŒ…çš„ [[https://github.com/tarsius][Jonas]]!

**** æäº¤æ¶ˆæ¯æ¨¡æ¿
ä¸ºæ¯ä¸ªé¡¹ç›®æ·»åŠ æäº¤æ¶ˆæ¯æ¨¡æ¿æ˜¯ä¸€ä»¶ç®€å•çš„äº‹ã€‚
#+begin_src emacs-lisp
(defvar +magit-project-commit-templates-alist nil
  "Alist of toplevel dirs and template strings/functions.")
(after! magit
  (defun +magit-fill-in-commit-template ()
    "Insert template from `+magit-fill-in-commit-template' if applicable."
    (when-let ((template (and (save-excursion (goto-char (point-min)) (string-match-p "\\`\\s-*$" (thing-at-point 'line)))
                              (cdr (assoc (file-name-base (directory-file-name (magit-toplevel)))
                                          +magit-project-commit-templates-alist)))))
      (goto-char (point-min))
      (insert (if (stringp template) template (funcall template)))
      (goto-char (point-min))
      (end-of-line)))
  (add-hook 'git-commit-setup-hook #'+magit-fill-in-commit-template 90))
#+end_src

åœ¨ä¸º Org åˆ›å»ºæäº¤æ—¶è¿™ç‰¹åˆ«æœ‰ç”¨ï¼Œå®ƒéµå¾ªä¸€ç§ç‰¹å®šçš„æ ¼å¼ï¼Œä¸è¿‡æœ‰æ—¶æˆ‘è¿˜æ˜¯ä¼šå¿˜è®° (ç³Ÿç³•)ã€‚
#+begin_src emacs-lisp
(after! magit
  (defun +org-commit-message-template ()
    "Create a skeleton for an Org commit message based on the staged diff."
    (let (change-data last-file file-changes temp-point)
      (with-temp-buffer
        (apply #'call-process magit-git-executable
               nil t nil
               (append
                magit-git-global-arguments
                (list "diff" "--cached")))
        (goto-char (point-min))
        (while (re-search-forward "^@@\\|^\\+\\+\\+ b/" nil t)
          (if (looking-back "^\\+\\+\\+ b/" (line-beginning-position))
              (progn
                (push (list last-file file-changes) change-data)
                (setq last-file (buffer-substring-no-properties (point) (line-end-position))
                      file-changes nil))
            (setq temp-point (line-beginning-position))
            (re-search-forward "^\\+\\|^-" nil t)
            (end-of-line)
            (cond
             ((string-match-p "\\.el$" last-file)
              (when (re-search-backward "^\\(?:[+-]? *\\|@@[ +-\\d,]+@@ \\)(\\(?:cl-\\)?\\(?:defun\\|defvar\\|defmacro\\|defcustom\\)" temp-point t)
                (re-search-forward "\\(?:cl-\\)?\\(?:defun\\|defvar\\|defmacro\\|defcustom\\) " nil t)
                (add-to-list 'file-changes (buffer-substring-no-properties (point) (forward-symbol 1)))))
             ((string-match-p "\\.org$" last-file)
              (when (re-search-backward "^[+-]\\*+ \\|^@@[ +-\\d,]+@@ \\*+ " temp-point t)
                (re-search-forward "@@ \\*+ " nil t)
                (add-to-list 'file-changes (buffer-substring-no-properties (point) (line-end-position)))))))))
      (push (list last-file file-changes) change-data)
      (setq change-data (delete '(nil nil) change-data))
      (concat
       (if (= 1 (length change-data))
           (replace-regexp-in-string "^.*/\\|.[a-z]+$" "" (caar change-data))
         "?")
       ": \n\n"
       (mapconcat
        (lambda (file-changes)
          (if (cadr file-changes)
              (format "* %s (%s): "
                      (car file-changes)
                      (mapconcat #'identity (cadr file-changes) ", "))
            (format "* %s: " (car file-changes))))
        change-data
        "\n\n"))))

  (add-to-list '+magit-project-commit-templates-alist (cons "org-mode" #'+org-commit-message-template)))
#+end_src

è¿™ä¾èµ–äº git è®¾ç½®æ–‡ä»¶ä¸­çš„ä¸¤ä¸ªå°é¡¹ï¼Œè¿™ä¸¤é¡¹æä¾›äº†åœ¨ elisp å’Œ Org æ–‡ä»¶ä¸­å¤§å—çš„
headline ä¸­é€‰æ‹©çš„èƒ½åŠ›ã€‚
#+begin_src gitconfig
[diff "lisp"]
  xfuncname = "^(((;;;+ )|\\(|([ \t]+\\(((cl-|el-patch-)?def(un|var|macro|method|custom)|gb/))).*)$"

[diff "org"]
  xfuncname = "^(\\*+ +.*)$"
#+end_src

*** Magit delta

[[https://github.com/dandavison/delta/][Delta]] æ˜¯ç”¨ rust å®ç°çš„ git diff è¯­æ³•é«˜äº®çš„å·¥å…·ã€‚è¯¥ä½œè€…è¿˜å†™äº†ä¸€ä¸ªå°†å®ƒæŒ‚æ¥åˆ° magit
çš„ diff è§†å›¾ä¸Š (é»˜è®¤æƒ…å†µä¸‹å¹¶ä¸ä¼šæœ‰ä»»ä½•è¯­æ³•é«˜äº®)ã€‚å½“ç„¶è¿™éœ€è¦ ~delta~ äºŒè¿›åˆ¶æ–‡ä»¶ã€‚å®ƒ
è¢«æ‰“åŒ…åœ¨æŸäº›å‘è¡Œç‰ˆä¸Šï¼Œä¸è¿‡æœ€å¯é çš„å®‰è£…æ–¹å¼è¿˜æ˜¯é€šè¿‡ Rust çš„åŒ…ç®¡ç†å™¨ Cargo å®‰è£…ã€‚

#+begin_src shell :eval no :tangle (if (or (not (executable-find "cargo")) (executable-find "delta")) "no" "setup.sh")
cargo install git-delta
#+end_src

ç°åœ¨æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ delta äº†
#+begin_src emacs-lisp :tangle packages.el
;; (package! magit-delta :recipe (:host github :repo "dandavison/magit-delta") :pin "56cdffd377279589aa0cb1df99455c098f1848cf")
#+end_src

é¡ºä¾¿è®°å¾—æ·»åŠ ä¸‹ hook
#+begin_src emacs-lisp
;; (after! magit (magit-delta-mode +1))
#+end_src

æœ‰ç‚¹ç³Ÿç³•çš„æ˜¯ç°åœ¨å¥½åƒä¸èƒ½ç”¨ï¼Œæˆ‘ä¹‹åå†å¥½å¥½ç ”ç©¶ç ”ç©¶ã€‚

*** Smerge

å¯¹äºé‡å¤æ“ä½œï¼ŒHydra éå¸¸æœ‰å¸®åŠ©ï¼Œä½†æˆ‘æ›´å–œæ¬¢ teansientã€‚
#+begin_src emacs-lisp
(defun smerge-repeatedly ()
  "Perform smerge actions again and again"
  (interactive)
  (smerge-mode 1)
  (smerge-transient))
(after! transient
  (transient-define-prefix smerge-transient ()
    [["Move"
      ("n" "next" (lambda () (interactive) (ignore-errors (smerge-next)) (smerge-repeatedly)))
      ("p" "previous" (lambda () (interactive) (ignore-errors (smerge-prev)) (smerge-repeatedly)))]
     ["Keep"
      ("b" "base" (lambda () (interactive) (ignore-errors (smerge-keep-base)) (smerge-repeatedly)))
      ("u" "upper" (lambda () (interactive) (ignore-errors (smerge-keep-upper)) (smerge-repeatedly)))
      ("l" "lower" (lambda () (interactive) (ignore-errors (smerge-keep-lower)) (smerge-repeatedly)))
      ("a" "all" (lambda () (interactive) (ignore-errors (smerge-keep-all)) (smerge-repeatedly)))
      ("RET" "current" (lambda () (interactive) (ignore-errors (smerge-keep-current)) (smerge-repeatedly)))]
     ["Diff"
      ("<" "upper/base" (lambda () (interactive) (ignore-errors (smerge-diff-base-upper)) (smerge-repeatedly)))
      ("=" "upper/lower" (lambda () (interactive) (ignore-errors (smerge-diff-upper-lower)) (smerge-repeatedly)))
      (">" "base/lower" (lambda () (interactive) (ignore-errors (smerge-diff-base-lower)) (smerge-repeatedly)))
      ("R" "refine" (lambda () (interactive) (ignore-errors (smerge-refine)) (smerge-repeatedly)))
      ("E" "ediff" (lambda () (interactive) (ignore-errors (smerge-ediff)) (smerge-repeatedly)))]
     ["Other"
      ("c" "combine" (lambda () (interactive) (ignore-errors (smerge-combine-with-next)) (smerge-repeatedly)))
      ("r" "resolve" (lambda () (interactive) (ignore-errors (smerge-resolve)) (smerge-repeatedly)))
      ("k" "kill current" (lambda () (interactive) (ignore-errors (smerge-kill-current)) (smerge-repeatedly)))
      ("q" "quit" (lambda () (interactive) (smerge-auto-leave)))]]))
#+end_src

*** Company
#+begin_quote
æ¥è‡ª =:completion company= æ¨¡å—
#+end_quote

æˆ‘è®¤ä¸ºåœ¨å¤§å¤šæ•°æ—¶å€™è¡¥å…¨éƒ½å¾ˆæœ‰ç”¨ã€‚ä¸è¿‡åœ¨æŒ‰é”®æ—¶éœ€è¦ç­‰å¾…ä¿å­˜ã€‚
#+begin_src emacs-lisp
(after! company
  (setq company-idle-delay 0.5
        company-minimum-prefix-length 2)
  (setq company-show-numbers t)
  (add-hook 'evil-normal-state-entry-hook #'company-abort)
  ) ;; make aborting less annoying.
#+end_src

ç°åœ¨æ”¹è¿›å¤§å¤šæ¥è‡ª ~å…ˆå‰é€‰é¡¹~ çš„å†å²è®°å½•ï¼Œæ‰€ä»¥æˆ‘ä»¬æ”¹è¿›ä»¥ä¸‹å†å²è®°å½•ã€‚
#+begin_src emacs-lisp
(setq-default history-length 1000)
(setq-default prescient-history-length 1000)
#+end_src
**** çº¯æ–‡æœ¬æ–‡ä»¶
~Ispell~ æ˜¯è¶…çº§æ£’çš„æ£€æŸ¥åŠŸèƒ½ï¼Œå¯ä»¥ç”¨åœ¨ ~text~ ã€ ~markdown~ å’Œ ~GFM~ ä¸­ã€‚
#+begin_src emacs-lisp
(set-company-backend!
  '(text-mode
    markdown-mode
    gfm-mode)
  '(:seperate
    company-ispell
    company-files
    company-yasnippet))
#+end_src

å½“ç„¶éœ€è¦å•ç‹¬é…ç½®ä¸€ä¸‹ä¸‹ [[*Ispell][Ispell]]ã€‚
**** ESS
~company-dabbrev-code~ å¥½ï¼å¿«å»é…ç½®ï¼
#+begin_src emacs-lisp
(set-company-backend!
  'ess-r-mode
  '(company-R-args company-R-objects company-dabbrev-code :separate))
#+end_src

*** Projectile
#+begin_quote
æ¥è‡ª =:core packages= æ¨¡å—
#+end_quote

é€šè¿‡æŸ¥çœ‹æ–‡æ¡£ =SPC h f= å’Œ =SPC h v= å’Œæºç å¯ä»¥å¾—çŸ¥ï¼Œå¯ä»¥ä¸º Projectileæ·»åŠ é¡¹ç›®ä»£
ç ç›®å½•ã€‚åœ¨æˆ‘çœ‹æ¥æœ‰äº›ä¸åˆé€‚ã€‚
#+begin_src emacs-lisp
(setq projectile-ignored-projects
      '("~/" "/tmp" "~/.emacs.d/.local/straight/repos/"))
(defun projectile-ignored-project-function (filepath)
  "Return t if FILEPATH is within any of `projectile-ignored-projects'"
  (or (mapcar (lambda (p) (s-starts-with-p p filepath))
              projectile-ignored-projects)))
#+end_src

*** Ispell
**** ä¸‹è½½å­—å…¸
è®©æˆ‘ä»¬ä» [[http://app.aspell.net/create][SCOWL Custom List / Dicionary Creator]] è·å–ä¸€ä¸ªå¥½ç”¨çš„å¤§è¯å…¸
- size (å¤§å°) :: 80 (è¶…å¤§)
- spellings (æ‹¼å†™) :: è‹±è¯­ (è‹±å›½, -ise) å’Œ è‹±è¯­ (æ¾³å¤§åˆ©äºš)
- spelling variants level (æ‹¼å†™å˜ä½“ç­‰çº§) :: 0
- diacritics (å˜éŸ³ç¬¦å·) :: ä¿ç•™
- extra lists (é¢å¤–åˆ—è¡¨) :: hackerã€ç½—é©¬æ•°å­—

***** Hunspell
#+begin_src shell :tangle (if (file-exists-p "/usr/share/myspell/en-custom.dic") "no" "setup.sh")
cd /tmp
curl -o "hunspell-en-custom.zip" 'http://app.aspell.net/create?max_size=80&spelling=GBs&spelling=AU&max_variant=0&diacritic=keep&special=hacker&special=roman-numerals&encoding=utf-8&format=inline&download=hunspell'
unzip "hunspell-en-custom.zip"
sudo chown root:root en-custom.*
sudo mv en-custom.{aff,dic} /usr/share/myspell/
#+end_src
***** Aspell
#+begin_src shell :tangle (if (file-expand-wildcards "/usr/lib64/aspell*/en-custom.multi") "no" "setup.sh")
cd /tmp
curl -o "aspell6-en-custom.tar.bz2" 'http://app.aspell.net/create?max_size=80&spelling=GBs&spelling=AU&max_variant=0&diacritic=keep&special=hacker&special=roman-numerals&encoding=utf-8&format=inline&download=aspell'
tar -xjf "aspell6-en-custom.tar.bz2"

cd aspell6-en-custom
./configure && make && sudo make install
#+end_src
**** é…ç½®
#+begin_src emacs-lisp
(setq ispell-dictionary "en-custom")
#+end_src

é¡ºä¾¿ä¸€è¯´ï¼Œå¦‚æœ ~company-ispell-dictionary~ è¢«è®¾ç½®ä¸º ~nil~ ï¼Œ åˆ™ä¼šä½¿ç”¨
~ispell-complete-word-dict~ ï¼›è€Œ ~ispell-alternate-dictionary~ åœ¨æ­¤è¢«è®¾
ç½®ä¸º ~nil~ æ—¶ï¼Œåˆ™ä¼šä½¿ç”¨ä¹‹å‰æåˆ°çš„æ˜æ–‡ç‰ˆæœ¬ã€‚

é‡ç‚¹å…³æ³¨è‡ªå·±çš„ç§äººå­—å…¸æ˜¯åˆç†çš„ï¼Œæˆ‘ä»¬æŠŠå®ƒæ”¾åœ¨æŒ‡å®šä½ç½® (è¿™æ„å‘³ç€æˆ‘æ›´æ”¹ä¸»å­—å…¸æ—¶ä¾ç„¶å¯
ä»¥ä¿ç•™æˆ‘æ·»åŠ çš„å•è¯)ã€‚
#+begin_src emacs-lisp
(setq ispell-personal-dictionary
      (expand-file-name ".ispell_personal" doom-private-dir))
#+end_src
*** TRAMP
å…³äºå…¶ä»–å¾ˆæœ‰ç”¨çš„åŠŸèƒ½ï¼ŒTRAMP ç®—ä¸€ä¸ªï¼Œå®ƒæ˜¯å¤šåè®®é€æ˜è¿œç¨‹è®¿é—® (/Transparent Remote
Access, Multiple Protocol/) å·¥å…·ã€‚ç®€å•è¯´è¿™æ˜¯ç®€å•è®¿é—®å…¶ä»–ä¸»æœºæ–‡ä»¶ç³»ç»Ÿçš„æ–¹æ³•ã€‚

**** æç¤ºåŒºåŸŸ

ä¸å¹¸çš„æ˜¯ï¼ŒTRAMP å¯¹è¿œç¨‹è¿æ¥æ—¶ SHELL çš„æç¤ºæ ¼å¼å¾ˆæŒ‘å‰”ï¼Œå°è¯•ä½¿ç”¨ bash å¹¶æ”¾å®½æ¾æç¤º
åŒºåŸŸçš„è¯†åˆ«ã€‚

#+begin_src emacs-lisp
(after! tramp
  (setenv "SHELL" "/bin/bash")
  (setq tramp-shell-prompt-pattern
        "\\(?:^\\|\\)[^]#$%>\n]*#?[]#$%>î‚°] *\\(\\[[0-9;]*[a-zA-Z] *\\)*"))  ;; default + î‚°
#+end_src

**** æ•…éšœæ’é™¤
å¦‚æœè¿œç¨‹è¿æ¥çš„è¡Œä¸ºä¸ç¬¦åˆé¢„æœŸï¼Œå¯ä»¥å°è¯•ä»¥ä¸‹æ–¹æ³•

***** Zsh
ä½ å¯èƒ½ä¸å–œæ¬¢ä¸€äº›è½¬ä¹‰ç ï¼Œè®©æˆ‘ä»¬å°†å…¶è¡Œä¸ºä¿®æ”¹å¾—æ›´ç¬¦åˆé¢„æœŸã€‚
#+begin_src shell :eval no :tangle no
if [[ "$TERM" == "dumb" ]]; then
    unset zle_bracketed_paste
    unset zle
    PS1='$ '
    return
fi
#+end_src

**** Guix
[[https://guix.gnu.org/][Guix]] å°†ä¸€äº› TRAMP éœ€è¦çš„äºŒè¿›åˆ¶æ–‡ä»¶æ”¾ç½®åœ¨äº†æ„æƒ³ä¸åˆ°çš„ä½ç½®ã€‚è¿™ä¸æ˜¯ä¸ªé—®é¢˜ï¼Œæˆ‘ä»¬å¯ä»¥
æ‰‹åŠ¨å¸®åŠ© TRAMP æ‰¾åˆ°å®ƒä»¬ã€‚
#+begin_src emacs-lisp
(after! tramp
  (appendq! tramp-remote-path
            '("~/.guix-profile/bin" "~/.guix-profile/sbin"
              "/run/current-system/profile/bin"
              "/run/current-system/profile/sbin")))
#+end_src
*** è‡ªåŠ¨æ¿€æ´» snippets
æœ‰æ—¶æŒ‰ =TAB= å®åœ¨æ˜¯å¤ªè ¢äº†ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! aas :recipe (:host github :repo "ymarco/auto-activating-snippets")
  :pin "b1a436922ba06ab9e1a5cc222f1a4f25a7697231")
#+end_src

#+begin_src emacs-lisp
(use-package! aas :commands aas-mode)
#+end_src

*** æˆªå±

è®©æˆªå±å˜å¾—è½»è€Œæ˜“ä¸¾ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! screenshot :recipe (:local-repo "lisp/screenshot"))
#+end_src

#+attr_html: :class invertible :alt Example screenshot.el screenshot
[[https://tecosaur.com/lfs/emacs-config/screenshots/screenshot.png]]

åªéœ€è¦ä¸€ç‚¹ç®€å•åœ°é…ç½®ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨ [[https://github.com/Calinou/0x0][0x0]] çš„åŒ…è£…æ–‡ä»¶ä¸Šä¼ è„šæœ¬ (æˆ‘å°†å…¶é‡å‘½åä¸º
~upload~)ã€‚
#+begin_src emacs-lisp
(use-package! screenshot
  :defer t
  :config (setq screenshot-upload-fn "upload %s 2>/dev/null"))
#+end_src

*** Etrace

/Emacs Lisp Profiler/ (emacs lisp åˆ†æå™¨, ELP) æ˜¯è®°å½•ä¿¡æ¯çš„å¥½æ‰‹ï¼Œä½†å®ƒå¹¶ä¸å–„äº
æŸ¥çœ‹ä¿¡æ¯ã€‚ =etrace= å¯ä»¥å°† ELP çš„ç»“æœè½¬åŒ–ä¸º "Chromium Catapult Trace Event
Format" (Chromium Catapult æ€§èƒ½è§†å›¾)ã€‚è¿™æ„å‘³ç€ =etrace= çš„è¾“å‡ºå¯ä»¥åŠ è½½åˆ°
[[https://www.speedscope.app/][speedscope]] ç­‰ç½‘ç»œåº”ç”¨ä¸­ï¼Œæ–¹ä¾¿åˆ†æè§†å›¾ã€‚

#+begin_src emacs-lisp :tangle packages.el
(package! etrace :recipe (:host github :repo "aspiers/etrace"))
#+end_src

#+begin_src emacs-lisp :tangle packages.el
(use-package! etrace :after elp)
#+end_src

*** YASnippet
#+begin_quote
æ¥è‡ª =:editor snippets= æ¨¡å—
#+end_quote

snippets å¥—å¨ƒè°ç”¨è°çŸ¥é“ï¼
#+begin_src emacs-lisp
(setq yas-triggers-in-field t)
#+end_src

*** String inflection

å¦‚æœä½ æƒ³è¦æ”¹å˜ç¬¦å·çš„åŒ¹é…è¡Œä¸ºã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! string-inflection :pin "fd7926ac17293e9124b31f706a4e8f38f6a9b855")
#+end_src

#+begin_src emacs-lisp
(use-package! string-inflection
  :commands (string-inflection-all-cycle
             string-inflection-toggle
             string-inflection-camelcase
             string-inflection-lower-camelcase
             string-inflection-kebab-case
             string-inflection-underscore
             string-inflection-capital-underscore
             string-inflection-upcase)
  :init
  (map! :leader :prefix ("c~" . "naming convention")
        :desc "cycle" "~" #'string-inflection-all-cycle
        :desc "toggle" "t" #'string-inflection-toggle
        :desc "CamelCase" "c" #'string-inflection-camelcase
        :desc "downCase" "d" #'string-inflection-lower-camelcase
        :desc "kebab-case" "k" #'string-inflection-kebab-case
        :desc "under_score" "_" #'string-inflection-underscore
        :desc "Upper_Score" "u" #'string-inflection-capital-underscore
        :desc "UP_CASE" "U" #'string-inflection-upcase)
  (after! evil
    (evil-define-operator evil-operator-string-inflection (beg end _type)
      "Define a new evil operator that cycles symbol casing."
      :move-point nil
      (interactive "<R>")
      (string-inflection-all-cycle)
      (setq evil-repeat-info '([?g ?~])))
    (define-key evil-normal-state-map (kbd "g~") 'evil-operator-string-inflection)))
#+end_src

*** Smart parentheses
#+begin_quote
æ¥è‡ª =:core packages= æ¨¡å—
#+end_quote

#+begin_src emacs-lisp
(sp-local-pair '(org-mode) "<<" ">>" :actions '(insert))
#+end_src

** è§†è§‰
*** ä¿¡æ¯è‰²å½©
æ·»åŠ å¯å˜å­—ä½“å’Œç€è‰² ğŸ™‚ï¼Œè®© man æ‰‹å†Œé¡µä½“éªŒæ›´å¥½ã€‚

#+attr_html: :class invertible :style width:80% :alt Example info-colours page.
[[https://tecosaur.com/lfs/emacs-config/screenshots/info-colours.png]]

#+begin_src emacs-lisp
(package! info-colors :pin "47ee73cc19b1049eef32c9f3e264ea7ef2aaf8a5")
#+end_src

hook åˆ° =Info= å¯ç”¨å®ƒã€‚
#+begin_src emacs-lisp
(use-package! info-colors
  :commands (info-colors-fontify-node))

(add-hook 'Info-selection-hook 'info-colors-fontify-node)
#+end_src

#+attr_html: :class invertible :alt Example colourised info page
[[https://tecosaur.com/lfs/emacs-config/screenshots/info-coloured.png]]

*** Modus ä¸»é¢˜
Proteolas åœ¨ Modus ä¸»é¢˜ä¸Šåšäº†å¾ˆå¤šå¾ˆæ£’çš„å·¥ä½œï¼Œè¿™ä¹Ÿæ˜¯ä¸ºä»€ä¹ˆè¯¥ä¸»é¢˜åœ¨ Emacs 28 å¦‚æ­¤å—
æ¬¢è¿çš„åŸå› ã€‚å½“ç„¶ä»–çš„æ›´æ–°é€Ÿåº¦ç¨å¾®æœ‰äº›å¿«ï¼Œæˆ‘å¸Œæœ›éšæ—¶æ¥å—æœ€æ–°çš„ç‰ˆæœ¬ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! modus-themes :pin "59422c05e00d65582a005ccb06c3767622d14e03")
#+end_src

*** ä¸»é¢˜é­”æ³•

æœ‰äº†æ‰€æœ‰çš„æ–°æ½®æœ±ä¸»é¢˜ï¼Œç»ˆç«¯ä»æ­¤æ¶ˆå¤±ä¸è§ï¼
#+begin_src emacs-lisp :tangle packages.el
(package! theme-magic :pin "844c4311bd26ebafd4b6a1d72ddcc65d87f074e3")
#+end_src

è¿™ä¸ªæ“ä½œä½¿ç”¨ =pywal= ï¼Œä½ å¯ä»¥é€šè¿‡ä»“åº“å®‰è£…å®ƒï¼Œä¸è¿‡æœ€ç®€å•çš„æ–¹å¼å°±æ˜¯ =pip= ã€‚
#+begin_src shell :eval no :tangle (if (executable-find "wal") "no" "setup.sh")
sudo python3 -m pip install pywal
#+end_src

Theme Magic æä¾›äº†ä¸€ä¸ªæ•°å­—ç•Œé¢ï¼Œå°è¯•ä»é¥±å’Œåº¦ã€è‰²å½©å·®å¼‚æ¥æœ‰æ•ˆçš„é€‰å–å…«ä¸ªé¢œè‰²ã€‚ç„¶è€Œï¼Œå®ƒ
å¯èƒ½ä¼šä¸º light é€‰æ‹©ç›¸åŒçš„é¢œè‰²ï¼Œå¹¶ä¸æ€»èƒ½å¤Ÿé€‰å–æœ€ä½³é¢œè‰²ã€‚è‡ªä»ä½¿ç”¨ =doom-themes= ï¼Œ
æˆ‘ä»¬æ„Ÿåˆ°äº†å‰æ‰€æœªæœ‰çš„è½»æ¾ï¼Œæˆ‘ä»¬å¯ä»¥ç”¨ Doom themes æä¾›çš„è‰²å½©å·¥å…·æ¥è½»æ¾è·å–åˆç†çš„é…è‰²
æ¥ç”Ÿæˆ light ç‰ˆæœ¬ --- ç°åœ¨å°±å¼€å§‹ï¼

#+begin_src emacs-lisp
(use-package! theme-magic
  :commands theme-magic-from-emacs
  :config
  (defadvice! theme-magic--auto-extract-16-doom-colors ()
    :override #'theme-magic--auto-extract-16-colors
    (list
     (face-attribute 'default :background)
     (doom-color 'error)
     (doom-color 'success)
     (doom-color 'type)
     (doom-color 'keywords)
     (doom-color 'constants)
     (doom-color 'functions)
     (face-attribute 'default :foreground)
     (face-attribute 'shadow :foreground)
     (doom-blend 'base8 'error 0.1)
     (doom-blend 'base8 'success 0.1)
     (doom-blend 'base8 'type 0.1)
     (doom-blend 'base8 'keywords 0.1)
     (doom-blend 'base8 'constants 0.1)
     (doom-blend 'base8 'functions 0.1)
     (face-attribute 'default :foreground))))
#+end_src

*** Emojify
#+begin_quote
æ¥è‡ª =:ui emoji= æ¨¡å—.
#+end_quote

å¯¹äºåˆå­¦è€…ï¼Œtwitter çš„ emoji çœ‹èµ·æ¥æ¯” emoji-one å¥½ã€‚å‰©ä¸‹çš„æœ€æ£’çš„æ˜¯ OOTB ğŸ˜€ã€‚

#+begin_src emacs-lisp
(setq emojify-emoji-set "twemoji-v2")
#+end_src

æœ€çƒ¦äººçš„ä¸€ç‚¹æ˜¯è®¾ç½®å¥½é»˜è®¤å­—ç¬¦æ—¶ï¼Œä½¿ç”¨çš„æ˜¯ emoji è€Œéé»˜è®¤å­—ç¬¦ã€‚è¿™å‘ç”Ÿåœ¨æˆ‘å¤å†™çš„ Org
ç¬¦å·ä¸Šï¼Œæ¯”å¦‚è¯´å¤é€‰æ¡†å’Œå…¶ä»–æ‚é¡¹ã€‚

æˆ‘ä»¬å¯ä»¥ä» emoji æ•£åˆ—è¡¨åˆ é™¤è¿™äº›ç¬¦å·æ¥é€‚åº”æˆ‘ä»¬çš„å·¥ä½œæµã€‚

#+begin_src emacs-lisp
(defvar emojify-disabled-emojis
  '(;; Org
    "â—¼" "â˜‘" "â˜¸" "âš™" "â©" "âª" "â¬†" "â¬‡" "â“"
    ;; Terminal powerline
    "âœ”"
    ;; Box drawing
    "â–¶" "â—€"
    ;; I just want to see this as text
    "Â©" "â„¢")
  "Characters that should never be affected by `emojify-mode'.")

(defadvice! emojify-delete-from-data ()
  "Ensure `emojify-disabled-emojis' don't appear in `emojify-emojis'."
  :after #'emojify-set-emoji-data
  (dolist (emoji emojify-disabled-emojis)
    (remhash emoji emojify-emojis)))
#+end_src

ç°åœ¨æœ€å¥½æœ‰ä¸€ä¸ªæ¬¡è¦æ¨¡å¼ç”¨æ¥å°†è¾“å…¥çš„ ascii/gh ç±»å‹çš„ emoji è½¬åŒ–ä¸º unicode ç±»å‹ã€‚
#+begin_src emacs-lisp
(defun emojify--replace-text-with-emoji (orig-fn emoji text buffer start end &optional target)
  "Modify `emojify--propertize-text-for-emoji' to replace ascii/github emoticons with unicode emojis, on the fly."
  (if (or (not emoticon-to-emoji) (= 1 (length text)))
      (funcall orig-fn emoji text buffer start end target)
    (delete-region start end)
    (insert (ht-get emoji "unicode"))))

(define-minor-mode emoticon-to-emoji
  "Write ascii/gh emojis, and have them converted to unicode live."
  :global nil
  :init-value nil
  (if emoticon-to-emoji
      (progn
        (setq-local emojify-emoji-styles '(ascii github unicode))
        (advice-add 'emojify--propertize-text-for-emoji :around #'emojify--replace-text-with-emoji)
        (unless emojify-mode
          (emojify-turn-on-emojify-mode)))
    (setq-local emojify-emoji-styles (default-value 'emojify-emoji-styles))
    (advice-remove 'emojify--propertize-text-for-emoji #'emojify--replace-text-with-emoji)))
#+end_src

è¿™ä¸ªæ–°çš„æ¬¡çº§æ¨¡å¼åœ¨æ¶ˆæ¯ä¸­ååˆ†å¥½ç”¨ï¼ŒæŠŠå®ƒ hook åˆ° email å’Œ IRC ä¸­ã€‚
#+begin_src emacs-lisp
(add-hook! '(mu4e-compose-mode org-msg-edit-mode circe-channel-mode) (emoticon-to-emoji 1))
#+end_src

*** Doom modeline
#+begin_quote
æ¥è‡ª =:ui modeline= æ¨¡å—
#+end_quote

éå¸¸ç¾è§‚ï¼Œæˆ‘è§‰å¾— PDF çš„ modeline åº”è¿›è¡Œä¸€äº›è°ƒæ•´ã€‚

æˆ‘ä¹‹å‰æè¿‡ä¸€ä¸ª [[https://github.com/seagle0128/doom-modeline/pull/425][issue]]ï¼Œä¸è¿‡å¼€å‘è€…å›å¤çš„ç›¸å½“ç®€å•ã€Œå°†ä½ çš„è®¾ç½®æ”¾åœ¨ä½ çš„ä¸ªäººé…ç½®ä¹‹ä¸­ï¼Œå½“å‰
é»˜è®¤è®¾ç½®æ˜¯æœ€å¥½çš„ã€ --- å› æ­¤æˆ‘å°†å®ƒæ”¾åœ¨äº†è¿™é‡Œã€‚

é¦–å…ˆæˆ‘æƒ³å…ˆå±•ç¤ºå½“å‰ buffer çš„æ–‡ä»¶åä¸ PDF å›¾æ ‡ã€‚ä¹‹åæˆ‘å°†é‡å®šä¹‰ä¸¤ä¸ªå‡½æ•°æ¥å®ç°å®ƒã€‚

#+begin_src emacs-lisp
(after! doom-modeline
  (doom-modeline-def-segment buffer-name
    "Display the current buffer's name, without any other information."
    (concat
     (doom-modeline-spc)
     (doom-modeline--buffer-name)))

  (doom-modeline-def-segment pdf-icon
    "PDF icon from all-the-icons."
    (concat
     (doom-modeline-spc)
     (doom-modeline-icon 'octicon "file-pdf" nil nil
                         :face (if (doom-modeline--active)
                                   'all-the-icons-red
                                 'mode-line-inactive)
                         :v-adjust 0.02)))

  (defun doom-modeline-update-pdf-pages ()
    "Update PDF pages."
    (setq doom-modeline--pdf-pages
          (let ((current-page-str (number-to-string (eval `(pdf-view-current-page))))
                (total-page-str (number-to-string (pdf-cache-number-of-pages))))
            (concat
             (propertize
              (concat (make-string (- (length total-page-str) (length current-page-str)) ? )
                      " P" current-page-str)
              'face 'mode-line)
             (propertize (concat "/" total-page-str) 'face 'doom-modeline-buffer-minor-mode)))))

  (doom-modeline-def-segment pdf-pages
    "Display PDF pages."
    (if (doom-modeline--active) doom-modeline--pdf-pages
      (propertize doom-modeline--pdf-pages 'face 'mode-line-inactive)))

  (doom-modeline-def-modeline 'pdf
    '(bar window-number pdf-pages pdf-icon buffer-name)
    '(misc-info matches major-mode process vcs)))
#+end_src

*** Keycast

å‡ºäºä¸€äº›åŸå› ï¼Œæˆ‘éœ€è¦æ—¶ä¸æ—¶åœ°åœ¨ Emacs è¿›è¡Œæ¼”ç¤ºæ“ä½œï¼Œå› æ­¤åœ¨æˆ‘çš„å±å¹•ä¸Šæ˜¾ç¤ºæ­£åœ¨è¿›è¡Œçš„æ“ä½œ
æŒ‰é”®æ˜¯éå¸¸æœ‰ç”¨çš„ã€‚è™½ç„¶æœ‰ [[https://gitlab.com/screenkey/screenkey][screenkey]] çš„å­˜åœ¨ï¼Œä½†æˆ‘è¿˜æ˜¯éœ€è¦ä¸€äº›ä¸é®æŒ¡å±å¹•çš„æ–¹æ³•ã€‚

#+attr_html: :class invertible :alt Screenshot of Keycast-mode in action
[[https://tecosaur.com/lfs/emacs-config/screenshots/keycast.png]]

#+begin_src emacs-lisp :tangle packages.el
(package! keycast :pin "72d9add8ba16e0cae8cfcff7fc050fa75e493b4e")
#+end_src

æŠŠå®ƒè®¾ç½®æˆéœ€è¦æ—¶å¯åŠ¨å§ã€‚
#+begin_src emacs-lisp
(use-package! keycast
  :commands keycast-mode
  :config
  (define-minor-mode keycast-mode
    "Show current command and its key binding in the mode line."
    :global t
    (if keycast-mode
        (progn
          (add-hook 'pre-command-hook 'keycast--update t)
          (add-to-list 'global-mode-string '("" mode-line-keycast " ")))
      (remove-hook 'pre-command-hook 'keycast--update)
      (setq global-mode-string (remove '("" mode-line-keycast " ") global-mode-string))))
  (custom-set-faces!
    '(keycast-command :inherit doom-modeline-debug
                      :height 0.9)
    '(keycast-key :inherit custom-modified
                  :height 1.1
                  :weight bold)))
#+end_src

*** screencast

ä¸ [[Keycast]] ç±»ä¼¼ï¼Œå¯èƒ½ä¹Ÿä¼šç”¨å¾—ä¸Š [[https://gitlab.com/ambrevar/emacs-gif-screencast][gif-screencast]]ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! gif-screencast :pin "5517a557a17d8016c9e26b0acb74197550f829b9")
#+end_src

å½“ç„¶ä»…åœ¨ä½¿ç”¨å‘½ä»¤åŠ è½½ã€‚

æˆ‘æœ€åˆå®‰è£…äº†é»˜è®¤æ•è·ç¨‹åº ~scrot~ ï¼Œä½†æˆ‘åœ¨æ¯æ¬¡å¯åŠ¨æ—¶éƒ½ä¼šæ”¶åˆ° ~glib error: Saving
to file file ... failed~ ã€‚Google å¹¶æ²¡æœ‰ä»»ä½•ç®€å•åœ°ä¿®å¤æ–¹æ³•ï¼Œæ‰€ä»¥æˆ‘æ”¹ç”¨äº† [[https://github.com/naelstrof/maim][maim]]ã€‚
æˆ‘ä»¬åªéœ€è¦å°†çª—å£ ID ä¼ é€’ç»™å®ƒï¼Œç”±äºè¯¥å€¼åœ¨ Emacs å®ä¾‹çš„ç”Ÿå‘½å‘¨æœŸå†…ä¸ä¼šæ”¹å˜ï¼Œå› æ­¤åªéœ€è¦
å¯¹å•ä¸ªçª—å£ä½¿ç”¨ ~xdotool-getactivewindow~ å°±èƒ½è¾¾åˆ°éå¸¸å¥½çš„æ•ˆæœã€‚

ä¼¼ä¹æ–°é¢œè‰²å‡ºç°æ—¶ï¼Œè¿™å¾€å¾€ä¼šä½¿ ~gifsicle~ å¼•å…¥ artefactsã€‚ä¸ºäº†é¿å…è¿™ç§æƒ…å†µï¼Œæˆ‘ä»¬ä½¿
ç”¨å½“å‰çš„ doom theme è¿›è¡Œé¢„å¡«å……ã€‚

#+begin_src emacs-lisp
(use-package! gif-screencast
  :commands gif-screencast-mode
  :config
  (map! :map gif-screencast-mode-map
        :g "<f8>" #'gif-screencast-toggle-pause
        :g "<f9>" #'gif-screencast-stop)
  (setq gif-screencast-program "maim"
        gif-screencast-args `("--quality" "3" "-i" ,(string-trim-right
                                                     (shell-command-to-string
                                                      "xdotool getactivewindow")))
        gif-screencast-optimize-args '("--batch" "--optimize=3" "--usecolormap=/tmp/doom-color-theme"))
  (defun gif-screencast-write-colormap ()
    (f-write-text
     (replace-regexp-in-string
      "\n+" "\n"
      (mapconcat (lambda (c) (if (listp (cdr c))
                                 (cadr c))) doom-themes--colors "\n"))
     'utf-8
     "/tmp/doom-color-theme" ))
  (gif-screencast-write-colormap)
  (add-hook 'doom-load-theme-hook #'gif-screencast-write-colormap))
#+end_src
*** Mixed pitch
#+begin_quote
æ¥è‡ª =:ui zen= æ¨¡å—
#+end_quote

åœ¨é»˜å†™æ¨¡å¼ä¸‹æˆ‘è¿˜æ˜¯éå¸¸æƒ³ç”¨ Mixed Pitchã€‚å¦‚æœæˆ‘ä»¬åªæ˜¯ç®€å•å¢åŠ ä¸€ä¸ª hookï¼Œåœ¨åˆ›å»ºæ–°çš„
Emacs å®ä¾‹åˆå§‹åŒ– UI ä¹‹å‰ç›´æ¥ä½¿ç”¨ =mixed-pitch-mode= æ‰“å¼€æ–‡ä»¶æ—¶ï¼Œå¯èƒ½å‘ç”Ÿé”™è¯¯ã€‚å› 
æ­¤åˆ›å»ºä¸€ä¸ª hook åœ¨ UI åˆå§‹åŒ–ä¹‹åæ‰§è¡Œ
+ æ¡ä»¶å¼€å¯ =mixed-pitch-mode=
+ å¯ç”¨ mixed pitch hook

#+begin_src emacs-lisp
(defvar mixed-pitch-modes '(org-mode LaTeX-mode markdown-mode gfm-mode Info-mode)
  "Modes that `mixed-pitch-mode' should be enabled in, but only after UI initialisation.")
(defun init-mixed-pitch-h ()
  "Hook `mixed-pitch-mode' into each mode in `mixed-pitch-modes'.
Also immediately enables `mixed-pitch-modes' if currently in one of the modes."
  (when (memq major-mode mixed-pitch-modes)
    (mixed-pitch-mode 1))
  (dolist (hook mixed-pitch-modes)
    (add-hook (intern (concat (symbol-name hook) "-hook")) #'mixed-pitch-mode)))
(add-hook 'doom-init-ui-hook #'init-mixed-pitch-h)
#+end_src

å¯¹äº mixed pitch å˜é‡ =mixed-pitch-face= ï¼Œæˆ‘ä»¬é€šè¿‡åˆ›å»ºä¸€ä¸ªå‡½æ•°ï¼Œç”¨ serif
face æ¥æ›¿æ¢é»˜è®¤çš„ mixed pitchã€‚è¿™æ˜¯ä¸ºå†™ä½œå®¤åˆ›å»ºçš„ã€‚

#+begin_src emacs-lisp
(autoload #'mixed-pitch-serif-mode "mixed-pitch"
  "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch." t)

(after! mixed-pitch
  (defface variable-pitch-serif
    '((t (:family "serif")))
    "A variable-pitch face with serifs."
    :group 'basic-faces)
  (setq mixed-pitch-set-height t)
  (setq variable-pitch-serif-font (font-spec :family "Alegreya" :size 27))
  (set-face-attribute 'variable-pitch-serif nil :font variable-pitch-serif-font)
  (defun mixed-pitch-serif-mode (&optional arg)
    "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch."
    (interactive)
    (let ((mixed-pitch-face 'variable-pitch-serif))
      (mixed-pitch-mode (or arg 'toggle)))))
#+end_src

Emacs å½“å‰ä½¿ç”¨çš„æ˜¯ Harfbuzzï¼Œé‚£æˆ‘ä»¬å¯èƒ½å°±ä¼šå¤±å» Alegreya è¿å­—ç‰¹æ•ˆã€‚
#+begin_center
ff /ff/ ffi /ffi/ ffj /ffj/ ffl /ffl/
fft /fft/ fi /fi/ fj /fj/ ft /ft/
Th /Th/
#+end_center

åº†å¹¸çš„æ˜¯å°†å®ƒæ·»åŠ åˆ° ~composition-function-table~ ä¸­å¹¶ä¸éº»çƒ¦ã€‚
#+begin_src emacs-lisp
(set-char-table-range composition-function-table ?f '(["\\(?:ff?[fijlt]\\)" 0 font-shape-gstring]))
(set-char-table-range composition-function-table ?T '(["\\(?:Th\\)" 0 font-shape-gstring]))
#+end_src

*** Marginalia
#+begin_quote
æ¨¡å— =:completion vertico= çš„ä¸€éƒ¨åˆ†ã€‚
#+end_quote

Marginalia ä¸å¥½çš„ä¸€ç‚¹å°±æ˜¯æ–‡ä»¶å…ƒæ•°æ®æ³¨é‡Šå¤ªç®€å•äº†ã€‚ç‰¹åˆ«æ˜¯ä»¥ä¸‹å‡ ç‚¹
+ å¦‚æœæ–‡ä»¶å±æ€§å¯ä»¥ç€è‰²å°±æ›´å¥½äº†
+ å¦‚æœæˆ‘æ‹¥æœ‰æ–‡ä»¶ï¼Œæˆ‘æ‰ä¸åœ¨ä¹æ–‡ä»¶çš„æ‰€å±ç”¨æˆ· / ç»„
+ å¦‚æœæ–‡ä»¶æœ€è¿‘æ”¹è¿‡ï¼Œè¿™ä¸ªç›¸å¯¹æ—¶é—´ (æ¯”å¦‚ =2h ago=) æ¯”æ—¥æœŸæœ‰ç”¨
+ æŒ‡ç¤ºå¤§æ–‡ä»¶çš„è¯ï¼Œä½“éªŒä¼šæ›´å¥½

æ„Ÿè°¢æœ‰ ~marginalia-annotator-registry~ ï¼Œæˆ‘ä»¬ä¸ç”¨å†æå»ºè®®äº†ï¼Œç›´æ¥è‡ªå·±æ·»åŠ ä¸€ä¸ªã€‚
#+begin_src emacs-lisp
(after! marginalia
  (setq marginalia-censor-variables nil)

  (defadvice! +marginalia--anotate-local-file-colorful (cand)
    "Just a more colourful version of `marginalia--anotate-local-file'."
    :override #'marginalia--annotate-local-file
    (when-let (attrs (file-attributes (substitute-in-file-name
                                       (marginalia--full-candidate cand))
                                      'integer))
      (marginalia--fields
       ((marginalia--file-owner attrs)
        :width 12 :face 'marginalia-file-owner)
       ((marginalia--file-modes attrs))
       ((+marginalia-file-size-colorful (file-attribute-size attrs))
        :width 7)
       ((+marginalia--time-colorful (file-attribute-modification-time attrs))
        :width 12))))

  (defun +marginalia--time-colorful (time)
    (let* ((seconds (float-time (time-subtract (current-time) time)))
           (color (doom-blend
                   (face-attribute 'marginalia-date :foreground nil t)
                   (face-attribute 'marginalia-documentation :foreground nil t)
                   (/ 1.0 (log (+ 3 (/ (+ 1 seconds) 345600.0)))))))
      ;; 1 - log(3 + 1/(days + 1)) % grey
      (propertize (marginalia--time time) 'face (list :foreground color))))

  (defun +marginalia-file-size-colorful (size)
    (let* ((size-index (/ (log10 (+ 1 size)) 7.0))
           (color (if (< size-index 10000000) ; 10m
                      (doom-blend 'orange 'green size-index)
                    (doom-blend 'red 'orange (- size-index 1)))))
      (propertize (file-size-human-readable size) 'face (list :foreground color)))))
#+end_src

*** Centaur Tabs
#+begin_quote
æ¥è‡ª =:ui tabs= æ¨¡å—
#+end_quote

åšä¸€ä¸ªç¾è§‚ã€å¤§å°èˆ’é€‚ (~36~) ä¸”å¸¦æœ‰å›¾æ ‡çš„æ ‡ç­¾ (åˆ nice)ã€‚å¸¦æœ‰ä¿®æ”¹æ ‡è®°æ˜¯ä¸€ä¸ªå¥½æƒ³æ³•ï¼Œ
ä½†é»˜è®¤çš„ unicode å®åœ¨æ˜¯å»¶è¿Ÿå¤ªå¤§äº†ï¼Œæ‰€ä»¥æˆ‘æ”¹ä¸ºäº† ~o~ ï¼Œä¹Ÿä¸ä¼šæœ‰ä»»ä½•é—®é¢˜ã€‚
å¯¹ 'active-bar' åˆæƒ³æ³•ï¼Ÿåšä¸€ä¸ªï¼ä¸è¿‡ ~å‰æ~ æ˜¯æ‰“å¼€ ~x-underline-at-decent~ ã€‚
æŸç§åŸå› è¿™ä¼¼ä¹åœ¨ src_elisp{(after! ... )} å†…ä¸èµ·ä½œç”¨ Â¯\_(ãƒ„)_/Â¯ã€‚é»˜è®¤å­—ä½“ä¸èˆ’æœ
ï¼Œæ¢æˆæ— è¡¬çº¿å­—ä½“ï¼Œ 'P22 Underground Book' çœŸæ˜¯èˆ’æœå¤šäº†ã€‚
#+begin_src emacs-lisp
(after! centaur-tabs
  (centaur-tabs-mode -1)
  (setq centaur-tabs-height 36
        centaur-tabs-set-icons t
        centaur-tabs-modified-marker "o"
        centaur-tabs-close-button "Ã—"
        centaur-tabs-set-bar 'above
        centaur-tabs-gray-out-icons 'buffer)
  (centaur-tabs-change-fonts "P22 Underground Book" 160))
;; (setq x-underline-at-descent-line t)
#+end_src

*** All the icons
#+begin_quote
æ¥è‡ª =:core packages= æ¨¡å—
#+end_quote

=all-the-icons= æ˜¯å±•ç¤ºæ–‡ä»¶ç±»å‹çš„é€šç”¨å›¾æ ‡ï¼Œæˆ‘å”¯ä¸€é‡åˆ°çš„å°é—®é¢˜æ˜¯ï¼Œå½“ /æˆ‘/ æ‰“å¼€ä¸€ä¸ª =.m= æ–‡ä»¶æ—¶ï¼Œå®ƒæ›´å¯èƒ½æ˜¯ Matlab æ–‡ä»¶ï¼Œè€Œé Objective-C ä»£ç ã€‚æ‰€ä»¥æˆ‘è¦ä¿®æ”¹è¿™ä¸ªã€‚
#+begin_src emacs-lisp
(after! all-the-icons
  (setcdr (assoc "m" all-the-icons-extension-icon-alist)
          (cdr (assoc "matlab" all-the-icons-extension-icon-alist))))
#+end_src

*** æ¼‚äº®çš„åˆ†é¡µç¬¦

ä¸€äº›æ–‡ä»¶é‡‡ç”¨ =^L= ä½œä¸ºåˆ†é¡µç¬¦ï¼Œè¿™çœ‹èµ·æ¥æœ‰ç‚¹ä¸‘ï¼ŒSteve Purcell åšäº†ä¸€ä¸ªåŒ…ï¼Œå°†å…¶ä¼˜åŒ–ä¸º
æ°´å¹³å±•ç¤ºã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! page-break-lines :recipe (:host github :repo "purcell/page-break-lines"))
#+end_src

#+begin_src emacs-lisp
(use-package! page-break-lines
  :commands page-break-lines-mode
  :init
  (autoload 'turn-on-page-break-lines-mode "page-break-lines")
  :config
  (setq page-break-lines-max-width fill-column)
  (map! :prefix "g"
        :desc "Prev page break" :nv "[" #'backward-page
        :desc "Next page break" :nv "]" #'forward-page))
#+end_src

*** Writeroom
#+begin_quote
æ¥è‡ª =:ui zen= æ¨¡å—
#+end_quote

å¯¹äºåˆå­¦è€…æ¥è¯´ï¼ŒDoom ååˆ†çƒ­è¡·äºæ”¾å¤§
#+begin_src emacs-lisp
(setq +zen-text-scale 0.8)
#+end_src

åœ¨ä½¿ç”¨ Org æ—¶æœ€å¥½è¿›è¡Œä¸€äº›ç¾åŒ–è°ƒæ•´ï¼Œæ¯”å¦‚è¯´ï¼š
+ ä½¿ç”¨å¯å˜çš„è¡¬çº¿å­—ä½“
+ éšè—å¼€å¤´çš„ headline
+ ç”¨ fleurons ä½œä¸ºæ ‡é¢˜ç¬¦å·
+ éšè—è¡Œå·
+ ç§»é™¤è¡Œçªå‡ºæ˜¾ç¤º
+ å±…ä¸­æ–‡æœ¬
+ æ‰“å¼€ ~org-pretty-table-mode~

#+begin_src emacs-lisp
(defvar +zen-serif-p t
  "Whether to use a serifed font with `mixed-pitch-mode'.")
(after! writeroom-mode
  (defvar-local +zen--original-org-indent-mode-p nil)
  (defvar-local +zen--original-mixed-pitch-mode-p nil)
  (defvar-local +zen--original-org-pretty-table-mode-p nil)
  (defun +zen-enable-mixed-pitch-mode-h ()
    "Enable `mixed-pitch-mode' when in `+zen-mixed-pitch-modes'."
    (when (apply #'derived-mode-p +zen-mixed-pitch-modes)
      (if writeroom-mode
          (progn
            (setq +zen--original-mixed-pitch-mode-p mixed-pitch-mode)
            (funcall (if +zen-serif-p #'mixed-pitch-serif-mode #'mixed-pitch-mode) 1))
        (funcall #'mixed-pitch-mode (if +zen--original-mixed-pitch-mode-p 1 -1)))))
  (pushnew! writeroom--local-variables
            'display-line-numbers
            'visual-fill-column-width
            'org-adapt-indentation
            'org-superstar-headline-bullets-list
            'org-superstar-remove-leading-stars)
  (add-hook 'writeroom-mode-enable-hook
            (defun +zen-prose-org-h ()
              "Reformat the current Org buffer appearance for prose."
              (when (eq major-mode 'org-mode)
                (setq display-line-numbers nil
                      visual-fill-column-width 60
                      org-adapt-indentation nil)
                (when (featurep 'org-superstar)
                  (setq-local org-superstar-headline-bullets-list '("ğŸ™˜" "ğŸ™™" "ğŸ™š" "ğŸ™›")
                              ;; org-superstar-headline-bullets-list '("ğŸ™" "ğŸ™‘" "ğŸ™’" "ğŸ™“" "ğŸ™”" "ğŸ™•" "ğŸ™–" "ğŸ™—")
                              org-superstar-remove-leading-stars t)
                  (org-superstar-restart))
                (setq
                 +zen--original-org-indent-mode-p org-indent-mode
                 +zen--original-org-pretty-table-mode-p (bound-and-true-p org-pretty-table-mode))
                (org-indent-mode -1)
                (org-pretty-table-mode 1))))
  (add-hook 'writeroom-mode-disable-hook
            (defun +zen-nonprose-org-h ()
              "Reverse the effect of `+zen-prose-org'."
              (when (eq major-mode 'org-mode)
                (when (featurep 'org-superstar)
                  (org-superstar-restart))
                (when +zen--original-org-indent-mode-p (org-indent-mode 1))
                ;; (unless +zen--original-org-pretty-table-mode-p (org-pretty-table-mode -1))
                ))))
#+end_src

#+attr_html: :class invertible :alt Writeroom applied to an Org file
[[https://tecosaur.com/lfs/emacs-config/screenshots/writeroom-and-org.png]]

*** Treemacs
#+begin_quote
æ¥è‡ª =:ui treemacs= æ¨¡å—
#+end_quote

å¾ˆå¤šæ—¶å€™æˆ‘å¯¹å¤šä½™çš„æ–‡ä»¶å¹¶ä¸æ„Ÿå…´è¶£ï¼Œå› æ­¤æ²¡æœ‰ç†ç”±è®©å®ƒä»¬å ç”¨æ˜¾ç¤ºç©ºé—´ï¼Œå¿…é¡»å¿½ç•¥å®ƒä»¬çš„å­˜åœ¨ã€‚
#+begin_src emacs-lisp
(after! treemacs
  (defvar treemacs-file-ignore-extensions '()
    "File extension which `treemacs-ignore-filter' will ensure are ignored")
  (defvar treemacs-file-ignore-globs '()
    "Globs which will are transformed to `treemacs-file-ignore-regexps' which `treemacs-ignore-filter' will ensure are ignored")
  (defvar treemacs-file-ignore-regexps '()
    "RegExps to be tested to ignore files, generated from `treeemacs-file-ignore-globs'")
  (defun treemacs-file-ignore-generate-regexps ()
    "Generate `treemacs-file-ignore-regexps' from `treemacs-file-ignore-globs'"
    (setq treemacs-file-ignore-regexps (mapcar 'dired-glob-regexp treemacs-file-ignore-globs)))
  (if (equal treemacs-file-ignore-globs '()) nil (treemacs-file-ignore-generate-regexps))
  (defun treemacs-ignore-filter (file full-path)
    "Ignore files specified by `treemacs-file-ignore-extensions', and `treemacs-file-ignore-regexps'"
    (or (member (file-name-extension file) treemacs-file-ignore-extensions)
        (let ((ignore-file nil))
          (dolist (regexp treemacs-file-ignore-regexps ignore-file)
            (setq ignore-file (or ignore-file (if (string-match-p regexp full-path) t nil)))))))
  (add-to-list 'treemacs-ignored-file-predicates #'treemacs-ignore-filter))
#+end_src

ç°åœ¨è¯†åˆ«æœ‰é—®é¢˜çš„æ–‡ä»¶ã€‚
#+begin_src emacs-lisp
(setq treemacs-file-ignore-extensions
      '(;; LaTeX
        "aux"
        "ptc"
        "fdb_latexmk"
        "fls"
        "synctex.gz"
        "toc"
        ;; LaTeX - glossary
        "glg"
        "glo"
        "gls"
        "glsdefs"
        "ist"
        "acn"
        "acr"
        "alg"
        ;; LaTeX - pgfplots
        "mw"
        ;; LaTeX - pdfx
        "pdfa.xmpi"
        ))
(setq treemacs-file-ignore-globs
      '(;; LaTeX
        "*/_minted-*"
        ;; AucTeX
        "*/.auctex-auto"
        "*/_region_.log"
        "*/_region_.tex"))
#+end_src
** Frivolities (è½»æµ®)
*** xkcd
XKCD æ¼«ç”»å¾ˆæœ‰è¶£ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! xkcd :pin "688d0b4ea234adda0c05784e6bb22ab9d71f0884")
#+end_src

åœ¨ [[*Extra links][Extra links]] è®¾ç½®æ›´å¥½çš„åŠ è½½å®ƒã€‚
#+begin_src emacs-lisp
(use-package! xkcd
  :commands (xkcd-get-json
             xkcd-download xkcd-get
             ;; now for funcs from my extension of this pkg
             +xkcd-find-and-copy +xkcd-find-and-view
             +xkcd-fetch-info +xkcd-select)
  :config
  (setq xkcd-cache-dir (expand-file-name "xkcd/" doom-cache-dir)
        xkcd-cache-latest (concat xkcd-cache-dir "latest"))
  (unless (file-exists-p xkcd-cache-dir)
    (make-directory xkcd-cache-dir))
  (after! evil-snipe
    (add-to-list 'evil-snipe-disabled-modes 'xkcd-mode))
  :general (:states 'normal
            :keymaps 'xkcd-mode-map
            "<right>" #'xkcd-next
            "n"       #'xkcd-next ; evil-ish
            "<left>"  #'xkcd-prev
            "N"       #'xkcd-prev ; evil-ish
            "r"       #'xkcd-rand
            "a"       #'xkcd-rand ; because image-rotate can interfere
            "t"       #'xkcd-alt-text
            "q"       #'xkcd-kill-buffer
            "o"       #'xkcd-open-browser
            "e"       #'xkcd-open-explanation-browser
            ;; extras
            "s"       #'+xkcd-find-and-view
            "/"       #'+xkcd-find-and-view
            "y"       #'+xkcd-copy))
#+end_src

åœ¨æ‰©å±•ä¸€å¤§å †åŠŸèƒ½ã€‚
#+begin_src emacs-lisp
(after! xkcd
  (require 'emacsql-sqlite)

  (defun +xkcd-select ()
    "Prompt the user for an xkcd using `completing-read' and `+xkcd-select-format'. Return the xkcd number or nil"
    (let* (prompt-lines
           (-dummy (maphash (lambda (key xkcd-info)
                              (push (+xkcd-select-format xkcd-info) prompt-lines))
                            +xkcd-stored-info))
           (num (completing-read (format "xkcd (%s): " xkcd-latest) prompt-lines)))
      (if (equal "" num) xkcd-latest
        (string-to-number (replace-regexp-in-string "\\([0-9]+\\).*" "\\1" num)))))

  (defun +xkcd-select-format (xkcd-info)
    "Creates each completing-read line from an xkcd info plist. Must start with the xkcd number"
    (format "%-4s  %-30s %s"
            (propertize (number-to-string (plist-get xkcd-info :num))
                        'face 'counsel-key-binding)
            (plist-get xkcd-info :title)
            (propertize (plist-get xkcd-info :alt)
                        'face '(variable-pitch font-lock-comment-face))))

  (defun +xkcd-fetch-info (&optional num)
    "Fetch the parsed json info for comic NUM. Fetches latest when omitted or 0"
    (require 'xkcd)
    (when (or (not num) (= num 0))
      (+xkcd-check-latest)
      (setq num xkcd-latest))
    (let ((res (or (gethash num +xkcd-stored-info)
                   (puthash num (+xkcd-db-read num) +xkcd-stored-info))))
      (unless res
        (+xkcd-db-write
         (let* ((url (format "https://xkcd.com/%d/info.0.json" num))
                (json-assoc
                 (if (gethash num +xkcd-stored-info)
                     (gethash num +xkcd-stored-info)
                   (json-read-from-string (xkcd-get-json url num)))))
           json-assoc))
        (setq res (+xkcd-db-read num)))
      res))

  ;; since we've done this, we may as well go one little step further
  (defun +xkcd-find-and-copy ()
    "Prompt for an xkcd using `+xkcd-select' and copy url to clipboard"
    (interactive)
    (+xkcd-copy (+xkcd-select)))

  (defun +xkcd-copy (&optional num)
    "Copy a url to xkcd NUM to the clipboard"
    (interactive "i")
    (let ((num (or num xkcd-cur)))
      (gui-select-text (format "https://xkcd.com/%d" num))
      (message "xkcd.com/%d copied to clipboard" num)))

  (defun +xkcd-find-and-view ()
    "Prompt for an xkcd using `+xkcd-select' and view it"
    (interactive)
    (xkcd-get (+xkcd-select))
    (switch-to-buffer "*xkcd*"))

  (defvar +xkcd-latest-max-age (* 60 60) ; 1 hour
    "Time after which xkcd-latest should be refreshed, in seconds")

  ;; initialise `xkcd-latest' and `+xkcd-stored-info' with latest xkcd
  (add-transient-hook! '+xkcd-select
    (require 'xkcd)
    (+xkcd-fetch-info xkcd-latest)
    (setq +xkcd-stored-info (+xkcd-db-read-all)))

  (add-transient-hook! '+xkcd-fetch-info
    (xkcd-update-latest))

  (defun +xkcd-check-latest ()
    "Use value in `xkcd-cache-latest' as long as it isn't older thabn `+xkcd-latest-max-age'"
    (unless (and (file-exists-p xkcd-cache-latest)
                 (< (- (time-to-seconds (current-time))
                       (time-to-seconds (file-attribute-modification-time (file-attributes xkcd-cache-latest))))
                    +xkcd-latest-max-age))
      (let* ((out (xkcd-get-json "http://xkcd.com/info.0.json" 0))
             (json-assoc (json-read-from-string out))
             (latest (cdr (assoc 'num json-assoc))))
        (when (/= xkcd-latest latest)
          (+xkcd-db-write json-assoc)
          (with-current-buffer (find-file xkcd-cache-latest)
            (setq xkcd-latest latest)
            (erase-buffer)
            (insert (number-to-string latest))
            (save-buffer)
            (kill-buffer (current-buffer)))))
      (shell-command (format "touch %s" xkcd-cache-latest))))

  (defvar +xkcd-stored-info (make-hash-table :test 'eql)
    "Basic info on downloaded xkcds, in the form of a hashtable")

  (defadvice! xkcd-get-json--and-cache (url &optional num)
    "Fetch the Json coming from URL.
If the file NUM.json exists, use it instead.
If NUM is 0, always download from URL.
The return value is a string."
    :override #'xkcd-get-json
    (let* ((file (format "%s%d.json" xkcd-cache-dir num))
           (cached (and (file-exists-p file) (not (eq num 0))))
           (out (with-current-buffer (if cached
                                         (find-file file)
                                       (url-retrieve-synchronously url))
                  (goto-char (point-min))
                  (unless cached (re-search-forward "^$"))
                  (prog1
                      (buffer-substring-no-properties (point) (point-max))
                    (kill-buffer (current-buffer))))))
      (unless (or cached (eq num 0))
        (xkcd-cache-json num out))
      out))

  (defadvice! +xkcd-get (num)
    "Get the xkcd number NUM."
    :override 'xkcd-get
    (interactive "nEnter comic number: ")
    (xkcd-update-latest)
    (get-buffer-create "*xkcd*")
    (switch-to-buffer "*xkcd*")
    (xkcd-mode)
    (let (buffer-read-only)
      (erase-buffer)
      (setq xkcd-cur num)
      (let* ((xkcd-data (+xkcd-fetch-info num))
             (num (plist-get xkcd-data :num))
             (img (plist-get xkcd-data :img))
             (safe-title (plist-get xkcd-data :safe-title))
             (alt (plist-get xkcd-data :alt))
             title file)
        (message "Getting comic...")
        (setq file (xkcd-download img num))
        (setq title (format "%d: %s" num safe-title))
        (insert (propertize title
                            'face 'outline-1))
        (center-line)
        (insert "\n")
        (xkcd-insert-image file num)
        (if (eq xkcd-cur 0)
            (setq xkcd-cur num))
        (setq xkcd-alt alt)
        (message "%s" title))))

  (defconst +xkcd-db--sqlite-available-p
    (with-demoted-errors "+org-xkcd initialization: %S"
      (emacsql-sqlite-ensure-binary)
      t))

  (defvar +xkcd-db--connection (make-hash-table :test #'equal)
    "Database connection to +org-xkcd database.")

  (defun +xkcd-db--get ()
    "Return the sqlite db file."
    (expand-file-name "xkcd.db" xkcd-cache-dir))

  (defun +xkcd-db--get-connection ()
    "Return the database connection, if any."
    (gethash (file-truename xkcd-cache-dir)
             +xkcd-db--connection))

  (defconst +xkcd-db--table-schema
    '((xkcds
       [(num integer :unique :primary-key)
        (year        :not-null)
        (month       :not-null)
        (link        :not-null)
        (news        :not-null)
        (safe_title  :not-null)
        (title       :not-null)
        (transcript  :not-null)
        (alt         :not-null)
        (img         :not-null)])))

  (defun +xkcd-db--init (db)
    "Initialize database DB with the correct schema and user version."
    (emacsql-with-transaction db
      (pcase-dolist (`(,table . ,schema) +xkcd-db--table-schema)
        (emacsql db [:create-table $i1 $S2] table schema))))

  (defun +xkcd-db ()
    "Entrypoint to the +org-xkcd sqlite database.
Initializes and stores the database, and the database connection.
Performs a database upgrade when required."
    (unless (and (+xkcd-db--get-connection)
                 (emacsql-live-p (+xkcd-db--get-connection)))
      (let* ((db-file (+xkcd-db--get))
             (init-db (not (file-exists-p db-file))))
        (make-directory (file-name-directory db-file) t)
        (let ((conn (emacsql-sqlite db-file)))
          (set-process-query-on-exit-flag (emacsql-process conn) nil)
          (puthash (file-truename xkcd-cache-dir)
                   conn
                   +xkcd-db--connection)
          (when init-db
            (+xkcd-db--init conn)))))
    (+xkcd-db--get-connection))

  (defun +xkcd-db-query (sql &rest args)
    "Run SQL query on +org-xkcd database with ARGS.
SQL can be either the emacsql vector representation, or a string."
    (if  (stringp sql)
        (emacsql (+xkcd-db) (apply #'format sql args))
      (apply #'emacsql (+xkcd-db) sql args)))

  (defun +xkcd-db-read (num)
    (when-let ((res
                (car (+xkcd-db-query [:select * :from xkcds
                                      :where (= num $s1)]
                                     num
                                     :limit 1))))
      (+xkcd-db-list-to-plist res)))

  (defun +xkcd-db-read-all ()
    (let ((xkcd-table (make-hash-table :test 'eql :size 4000)))
      (mapcar (lambda (xkcd-info-list)
                (puthash (car xkcd-info-list) (+xkcd-db-list-to-plist xkcd-info-list) xkcd-table))
              (+xkcd-db-query [:select * :from xkcds]))
      xkcd-table))

  (defun +xkcd-db-list-to-plist (xkcd-datalist)
    `(:num ,(nth 0 xkcd-datalist)
      :year ,(nth 1 xkcd-datalist)
      :month ,(nth 2 xkcd-datalist)
      :link ,(nth 3 xkcd-datalist)
      :news ,(nth 4 xkcd-datalist)
      :safe-title ,(nth 5 xkcd-datalist)
      :title ,(nth 6 xkcd-datalist)
      :transcript ,(nth 7 xkcd-datalist)
      :alt ,(nth 8 xkcd-datalist)
      :img ,(nth 9 xkcd-datalist)))

  (defun +xkcd-db-write (data)
    (+xkcd-db-query [:insert-into xkcds
                     :values $v1]
                    (list (vector
                           (cdr (assoc 'num        data))
                           (cdr (assoc 'year       data))
                           (cdr (assoc 'month      data))
                           (cdr (assoc 'link       data))
                           (cdr (assoc 'news       data))
                           (cdr (assoc 'safe_title data))
                           (cdr (assoc 'title      data))
                           (cdr (assoc 'transcript data))
                           (cdr (assoc 'alt        data))
                           (cdr (assoc 'img        data))
                           )))))
#+end_src

*** Selectric

æ¯éš”ä¸€æ®µæ—¶é—´ï¼Œä½ æƒ³è®©åˆ«äºº /çŸ¥é“/ ä½ æ­£åœ¨æ‰“å­—ï¼Œæˆ–è‡ªå¨±è‡ªä¹ï¼Œéš†é‡ä»‹ç»ï¼šæ‰“å­—æœºçš„å£°éŸ³ï¼
#+begin_src emacs-lisp :tangle packages.el
(package! selectric-mode :pin "1840de71f7414b7cd6ce425747c8e26a413233aa")
#+end_src

#+begin_src emacs-lisp
(use-package! selectic-mode
  :commands selectic-mode)
#+end_src

*** Wttrin

è®©æˆ‘ä»¬å¿«è®¾ç½®ä¸€ä¸‹å¤©æ°”ã€‚è‰ (ä¸€ç§æ¤ç‰©)ï¼Œä¼¼ä¹ slightly å¹¶æ²¡æœ‰æœ‰æ•ˆç»´æŠ¤
([[https://github.com/bcbcarl/emacs-wttrin/pulls][few open bugfix PRs]]) è¿™ä¸ªåŒ…ï¼Œç”¨æˆ‘ä»¬ [[https://tecosaur.github.io/emacs-config/lisp/wttrin/wttrin.el][è‡ªå·±çš„ç‰ˆæœ¬]] å§ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! wttrin :recipe (:local-repo "lisp/wttrin"))
#+end_src

#+begin_src emacs-lisp
(use-package! wttrin
  :commands wttrin)
#+end_src

*** Spray

ä¸ºä»€ä¹ˆä¸è®©å±å¹•ä¸Šé£å‡ºä¸€äº›å•è¯å‘¢ï¼Ÿä¸ºä»€ä¹ˆï¼ --- å¿«ç‚¹è®©å®ƒé£å‡ºæ¥ã€‚
#+begin_src emacs-lisp
(use-package! spray
  :commands spray-mode
  :config
  (setq spray-wpm 600
        spray-height 800)
  (defun spray-mode-hide-cursor ()
    "Hide or unhide the cursor as is appropriate."
    (if spray-mode
        (setq-local spray--last-evil-cursor-state evil-normal-state-cursor
                    evil-normal-state-cursor '(nil))
      (setq-local evil-normal-state-cursor spray--last-evil-cursor-state)))
  (add-hook 'spray-mode-hook #'spray-mode-hide-cursor)
  (map! :map spray-mode-map
        "<return>" #'spray-start/stop
        "f" #'spray-faster
        "s" #'spray-slower
        "t" #'spray-time
        "<right>" #'spray-forward-word
        "h" #'spray-forward-word
        "<left>" #'spray-backward-word
        "l" #'spray-backward-word
        "q" #'spray-quit))
#+end_src

*** Elcord

é™¤éä½ ä¸æ–­å‘Šè¯‰èº«è¾¹çš„æ¯ä¸ªäººï¼Œä¸ç„¶ä½ ä½¿ç”¨ Emacs çš„æ„ä¹‰æ˜¯ä»€ä¹ˆï¼Ÿ
#+begin_src emacs-lisp :tangle packages.el
(package! elcord :pin "eb4ae2e7e03a5fc26b054ba2fa9a1d308e239c76")
#+end_src

#+begin_src emacs-lisp
(use-package! elcord
  :commands elcord-mode
  :config
  (setq elcord-use-major-mode-as-main-icon t))
#+end_src

** æ–‡ä»¶ç±»å‹
*** æˆæƒä¿¡æ¯

æˆ‘æä¾›äº†ä¸€ä¸ª =authinfo-mode= çš„è¯­æ³•é«˜äº®ï¼Œä¸è¿‡éœ€è¦ Emacs 28+ æ‰èƒ½ç”¨ã€‚å¯¹äºæ—§ç‰ˆæœ¬ï¼Œ
è¿˜æ˜¯ç”¨åŒ…æ¥è§£å†³å§ã€‚

#+begin_src emacs-lisp :tangle (if (< emacs-major-version 28) "packages.el" "no")
(package! authinfo-color-mode
  :recipe (:local-repo "lisp/authinfo-color-mode"))
#+end_src

ç°åœ¨åªéœ€è¦åœ¨åˆé€‚çš„æ—¶å€™åŠ è½½å®ƒã€‚
#+begin_src emacs-lisp :tangle (if (< emacs-major-version 28) "yes" "no")
(use-package! authinfo-color-mode
  :mode ("authinfo.gpg\\'" . authinfo-color-mode)
  :init (advice-add 'authinfo-mode :override #'authinfo-color-mode))
#+end_src

*** Systemd
ä¸ºäº†ç¼–è¾‘ Systemd å•å…ƒæ–‡ä»¶ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! systemd :pin "b6ae63a236605b1c5e1069f7d3afe06ae32a7bae")
#+end_src

#+begin_src emacs-lisp
(use-package! systemd :defer t)
#+end_src

* åº”ç”¨
** Ebooks
[[xkcd:548]]

ä¸ºç®¡ç†æˆ‘çš„ ebookï¼Œæˆ‘ hook äº†å·²ç»å»ºå¥½çš„ ebook å›¾ä¹¦ç®¡ç†å™¨ [[https://calibre-ebook.com/][calibre]]ã€‚æœ‰è®¸å¤šè¿™ç±»å‹çš„
Emacs å®¢æˆ·ç«¯ï¼Œè¿™ä¸ªæ˜¯æˆ‘è§‰å¾—æŒºå¥½çš„ä¸€ä¸ªã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! calibredb :pin "cb93563d0ec9e0c653210bc574f9546d1e7db437")
#+end_src

ä¸ºäº†é˜…è¯»ï¼Œç›®å‰å”¯ä¸€å¯ç”¨çš„åšæ³•ä¼¼ä¹æ˜¯ [[https://depp.brause.cc/nov.el/][nov.el]]ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! nov :pin "2b4a7231aff6211a5a2f28719d830887aec6cc57")
#+end_src

è¿™äº›åŒ…ä¸€èµ·ç»™äº†æˆ‘ç›¸å½“ä¸é”™çš„ä½“éªŒã€‚

=calibredb= å¯ä»¥è®©æˆ‘ä»¬é€šè¿‡ Emacs ä½¿ç”¨ calibredbï¼Œè°ä¸æƒ³åœ¨ Emacs é‡Œåšä»»ä½•äº‹ï¼ï¼Ÿ

#+begin_src emacs-lisp
(use-package! calibredb
  :commands calibredb
  :config
  (setq calibredb-root-dir "~/Desktop/TEC/Other/Ebooks"
        calibredb-db-dir (expand-file-name "metadata.db" calibredb-root-dir))
  (map! :map calibredb-show-mode-map
        :ne "?" #'calibredb-entry-dispatch
        :ne "o" #'calibredb-find-file
        :ne "O" #'calibredb-find-file-other-frame
        :ne "V" #'calibredb-open-file-with-default-tool
        :ne "s" #'calibredb-set-metadata-dispatch
        :ne "e" #'calibredb-export-dispatch
        :ne "q" #'calibredb-entry-quit
        :ne "." #'calibredb-open-dired
        :ne [tab] #'calibredb-toggle-view-at-point
        :ne "M-t" #'calibredb-set-metadata--tags
        :ne "M-a" #'calibredb-set-metadata--author_sort
        :ne "M-A" #'calibredb-set-metadata--authors
        :ne "M-T" #'calibredb-set-metadata--title
        :ne "M-c" #'calibredb-set-metadata--comments)
  (map! :map calibredb-search-mode-map
        :ne [mouse-3] #'calibredb-search-mouse
        :ne "RET" #'calibredb-find-file
        :ne "?" #'calibredb-dispatch
        :ne "a" #'calibredb-add
        :ne "A" #'calibredb-add-dir
        :ne "c" #'calibredb-clone
        :ne "d" #'calibredb-remove
        :ne "D" #'calibredb-remove-marked-items
        :ne "j" #'calibredb-next-entry
        :ne "k" #'calibredb-previous-entry
        :ne "l" #'calibredb-virtual-library-list
        :ne "L" #'calibredb-library-list
        :ne "n" #'calibredb-virtual-library-next
        :ne "N" #'calibredb-library-next
        :ne "p" #'calibredb-virtual-library-previous
        :ne "P" #'calibredb-library-previous
        :ne "s" #'calibredb-set-metadata-dispatch
        :ne "S" #'calibredb-switch-library
        :ne "o" #'calibredb-find-file
        :ne "O" #'calibredb-find-file-other-frame
        :ne "v" #'calibredb-view
        :ne "V" #'calibredb-open-file-with-default-tool
        :ne "." #'calibredb-open-dired
        :ne "b" #'calibredb-catalog-bib-dispatch
        :ne "e" #'calibredb-export-dispatch
        :ne "r" #'calibredb-search-refresh-and-clear-filter
        :ne "R" #'calibredb-search-clear-filter
        :ne "q" #'calibredb-search-quit
        :ne "m" #'calibredb-mark-and-forward
        :ne "f" #'calibredb-toggle-favorite-at-point
        :ne "x" #'calibredb-toggle-archive-at-point
        :ne "h" #'calibredb-toggle-highlight-at-point
        :ne "u" #'calibredb-unmark-and-forward
        :ne "i" #'calibredb-edit-annotation
        :ne "DEL" #'calibredb-unmark-and-backward
        :ne [backtab] #'calibredb-toggle-view
        :ne [tab] #'calibredb-toggle-view-at-point
        :ne "M-n" #'calibredb-show-next-entry
        :ne "M-p" #'calibredb-show-previous-entry
        :ne "/" #'calibredb-search-live-filter
        :ne "M-t" #'calibredb-set-metadata--tags
        :ne "M-a" #'calibredb-set-metadata--author_sort
        :ne "M-A" #'calibredb-set-metadata--authors
        :ne "M-T" #'calibredb-set-metadata--title
        :ne "M-c" #'calibredb-set-metadata--comments))
#+end_src

=nov= è®©æˆ‘ä»¬çœŸçœŸåˆ‡åˆ‡çš„é˜…è¯»åˆ°äº† ebookã€‚

#+attr_html: :class invertible :alt Excerpt of the GNU Emacs manual viewed through nov.el
[[https://tecosaur.com/lfs/emacs-config/screenshots/nov.png]]

#+begin_src emacs-lisp
(use-package! nov
  :mode ("\\.epub\\'" . nov-mode)
  :config
  (map! :map nov-mode-map
        :n "RET" #'nov-scroll-up)

  (defun doom-modeline-segment--nov-info ()
    (concat
     " "
     (propertize
      (cdr (assoc 'creator nov-metadata))
      'face 'doom-modeline-project-parent-dir)
     " "
     (cdr (assoc 'title nov-metadata))
     " "
     (propertize
      (format "%d/%d"
              (1+ nov-documents-index)
              (length nov-documents))
      'face 'doom-modeline-info)))

  (advice-add 'nov-render-title :override #'ignore)

  (defun +nov-mode-setup ()
    (face-remap-add-relative 'variable-pitch
                             :family "Merriweather"
                             :height 1.4
                             :width 'semi-expanded)
    (face-remap-add-relative 'default :height 1.3)
    (setq-local line-spacing 0.2
                next-screen-context-lines 4
                shr-use-colors nil)
    (require 'visual-fill-column nil t)
    (setq-local visual-fill-column-center-text t
                visual-fill-column-width 81
                nov-text-width 80)
    (visual-fill-column-mode 1)
    (hl-line-mode -1)

    (add-to-list '+lookup-definition-functions #'+lookup/dictionary-definition)

    (setq-local mode-line-format
                `((:eval
                   (doom-modeline-segment--workspace-name))
                  (:eval
                   (doom-modeline-segment--window-number))
                  (:eval
                   (doom-modeline-segment--nov-info))
                  ,(propertize
                    " %P "
                    'face 'doom-modeline-buffer-minor-mode)
                  ,(propertize
                    " "
                    'face (if (doom-modeline--active) 'mode-line 'mode-line-inactive)
                    'display `((space
                                :align-to
                                (- (+ right right-fringe right-margin)
                                   ,(* (let ((width (doom-modeline--font-width)))
                                         (or (and (= width 1) 1)
                                             (/ width (frame-char-width) 1.0)))
                                       (string-width
                                        (format-mode-line (cons "" '(:eval (doom-modeline-segment--major-mode))))))))))
                  (:eval (doom-modeline-segment--major-mode)))))

  (add-hook 'nov-mode-hook #'+nov-mode-setup))
#+end_src

** è®¡ç®—å™¨
Emacs åŒ…å«äº†ä¸€ä¸ªè¿‡æ—¶çš„è®¡ç®—å™¨ =calc= ï¼Œè¿™æ˜¯ä¸€ä¸ªä»¤äººå° (ji) è±¡ (qi) æ·± (tong)
åˆ» (ku) çš„ RPN (Reverse Polish Notation, é€†æ³¢å…°è¡¨ç¤ºæ³•) è®¡ç®—å™¨ã€‚æˆ‘ä»¬è¿˜æ˜¯å¯ä»¥æé«˜ä½“
éªŒçš„ã€‚

*** ç¼ºçœå€¼

ç†æ™ºçš„äººéƒ½å–œæ¬¢å¼§åº¦å’Œç²¾ç¡®çš„å€¼ã€‚
#+begin_src emacs-lisp
(setq calc-angle-mode 'rad  ; radians are rad
      calc-symbolic-mode t) ; keeps expressions like \sqrt{2} irrational for as long as possible
#+end_src

*** CalcTeX

æ‰€æœ‰äººéƒ½çŸ¥é“ LaTeX å†™çš„æ•°å­¦è¡¨è¾¾å¼å¾ˆå¥½çœ‹ï¼Œ =calc= å¯ä»¥ä¸ºè¡¨è¾¾å¼æä¾› LaTeX è¡¨ç¤ºï¼Œè¿™å¯
ä»¥è®© CalcTeX å°½æƒ…çš„ä½¿ç”¨ã€‚

#+begin_src emacs-lisp :tangle packages.el
(package! calctex :recipe (:host github :repo "johnbcoughlin/calctex"
                           :files ("*.el" "calctex/*.el" "calctex-contrib/*.el" "org-calctex/*.el" "vendor"))
  :pin "67a2e76847a9ea9eff1f8e4eb37607f84b380ebb")
#+end_src

#+attr_html: :class invertible :alt Demonstration of calc, prettified by calctex.
[[https://tecosaur.com/lfs/emacs-config/screenshots/calc-with-calctex.png]]

æˆ‘ä»¬å–œæ¬¢ä½¿ç”¨ CalcTeXï¼Œå¯ç”¨å®ƒï¼Œå¹¶ä¿®å¤ä¸€äº›å·¨å¤§çš„ç—›ç‚¹ --- ç”Ÿæ´»åœ¨åœ°çƒä¸Šï¼Œä½ å´ /åªèƒ½åœ¨
ä½ çš„æœ¬åœ°æœºå™¨ä¸Š/ è¿ç®—ï¼Œè¦è®©è¿™ä¸ªåŒ…æœåŠ¡äºæ‰€æœ‰äººï¼

#+begin_src emacs-lisp
(use-package! calctex
  :commands calctex-mode
  :init
  (add-hook 'calc-mode-hook #'calctex-mode)
  :config
  (setq calctex-additional-latex-packages "
\\usepackage[usenames]{xcolor}
\\usepackage{soul}
\\usepackage{adjustbox}
\\usepackage{amsmath}
\\usepackage{amssymb}
\\usepackage{siunitx}
\\usepackage{cancel}
\\usepackage{mathtools}
\\usepackage{mathalpha}
\\usepackage{xparse}
\\usepackage{arevmath}"
        calctex-additional-latex-macros
        (concat calctex-additional-latex-macros
                "\n\\let\\evalto\\Rightarrow"))
  (defadvice! no-messaging-a (orig-fn &rest args)
    :around #'calctex-default-dispatching-render-process
    (let ((inhibit-message t) message-log-max)
      (apply orig-fn args)))
  ;; Fix hardcoded dvichop path (whyyyyyyy)
  (let ((vendor-folder (concat (file-truename doom-local-dir)
                               "straight/"
                               (format "build-%s" emacs-version)
                               "/calctex/vendor/")))
    (setq calctex-dvichop-sty (concat vendor-folder "texd/dvichop")
          calctex-dvichop-bin (concat vendor-folder "texd/dvichop")))
  (unless (file-exists-p calctex-dvichop-bin)
    (message "CalcTeX: Building dvichop binary")
    (let ((default-directory (file-name-directory calctex-dvichop-bin)))
      (call-process "make" nil nil nil))))
#+end_src

*** Embedded calc

åœ¨ LaTeX æ•°å­¦è¡¨è¾¾å¼ä¸­ä½¿ç”¨åµŒå…¥å¼çš„è®¡ç®—å™¨ï¼é»˜è®¤çš„å¿«æ·é”®åªèƒ½ç”¨æ“è›‹æ¥å½¢å®¹ (=C-x * e=)
ï¼Œæ›´æ”¹ä¸ºé‡‡å–åŸºäº localleader çš„æ–¹æ¡ˆã€‚

#+begin_src emacs-lisp
(map! :map calc-mode-map
      :after calc
      :localleader
      :desc "Embedded calc (toggle)" "e" #'calc-embedded)
(map! :map org-mode-map
      :after org
      :localleader
      :desc "Embedded calc (toggle)" "E" #'calc-embedded)
(map! :map latex-mode-map
      :after latex
      :localleader
      :desc "Embedded calc (toggle)" "e" #'calc-embedded)
#+end_src

ä¸å¹¸çš„æ˜¯ï¼Œè®¡ç®—å°†åœ¨æ²¡æœ‰ (å†…å®¹æ›´ä¸°å¯Œçš„) è®¡ç®—å™¨å’Œè·Ÿè¸ªç¼“å†²åŒºçš„æƒ…å†µä¸‹è¿è¡Œï¼Œæˆ–è®¸å¯ä»¥å°è¯•æ”¾
åœ¨æµ‹é¢æ¿ã€‚
#+begin_src emacs-lisp
(defvar calc-embedded-trail-window nil)
(defvar calc-embedded-calculator-window nil)

(defadvice! calc-embedded-with-side-pannel (&rest _)
  :after #'calc-do-embedded
  (when calc-embedded-trail-window
    (ignore-errors
      (delete-window calc-embedded-trail-window))
    (setq calc-embedded-trail-window nil))
  (when calc-embedded-calculator-window
    (ignore-errors
      (delete-window calc-embedded-calculator-window))
    (setq calc-embedded-calculator-window nil))
  (when (and calc-embedded-info
             (> (* (window-width) (window-height)) 1200))
    (let ((main-window (selected-window))
          (vertical-p (> (window-width) 80)))
      (select-window
       (setq calc-embedded-trail-window
             (if vertical-p
                 (split-window-horizontally (- (max 30 (/ (window-width) 3))))
               (split-window-vertically (- (max 8 (/ (window-height) 4)))))))
      (switch-to-buffer "*Calc Trail*")
      (select-window
       (setq calc-embedded-calculator-window
             (if vertical-p
                 (split-window-vertically -6)
               (split-window-horizontally (- (/ (window-width) 2))))))
      (switch-to-buffer "*Calculator*")
      (select-window main-window))))
#+end_src

** IRC
=circe= æ˜¯ Emacs çš„ IRC å®¢æˆ·ç«¯ (è¿™ä¸ªåå­—+ç¼©å†™ç®€ç›´å¤ªæ£’äº†)ï¼Œè¿˜æ˜¯å°†äººå˜ä¸ºæ€ªç‰©çš„å¸Œè…Šå¥³å·«
--- å–€è€³åˆ»ã€‚

è¿˜æ˜¯ç”¨å‰é¢è¯´çš„æ„æ€å§ï¼Œç”¨å®ƒä¸é‚£äº›åœ¨çº¿ +éšèº«+ çš„äººèŠå¤©ï¼

[[xkcd:1782]]

åœ¨æˆ‘ä»¬å¼€å§‹å‘æ¶ˆæ¯å‰ï¼Œæˆ‘ä»¬éœ€è¦ä» IRC å®¢æˆ·ç«¯è·å–æƒé™ã€‚circe æ‰‹å†Œåœ¨ =.authinfo.gpg= ä¸­
ä¸ºæˆ‘ä»¬æä¾›äº†ä¸€äº›éªŒè¯æ¨¡æ¿ --- ä¸è¿‡æˆ‘è§‰å¾—æˆ‘ä»¬åº”è¯¥æ›´è¿›ä¸€æ­¥ï¼šè¿˜è¦æ•´ä¸ªæœåŠ¡å™¨ä¿¡æ¯ã€‚

é¦–å…ˆè®©æˆ‘ä»¬æ¥åº·åº·éœ€è¦å“ªäº›åˆç†çš„å­—æ®µï¼š
+ æœåŠ¡å™¨
+ ç«¯å£
+ SASL username
+ SASL password
+ åŠ å…¥çš„é¢‘é“

æˆ‘ä»¬è¿™æ ·å­˜å‚¨å®ƒä»¬
#+begin_src authinfo
machine chat.freenode.net login USERNAME password PASSWORD port PORT for irc channels emacs,org-mode
#+end_src

~for irc~ å¯ä»¥è®©æˆ‘ä»¬ä¸ºæ‰€æœ‰ IRC éªŒè¯ä¿¡æ¯è®¾ç½®ç‹¬ç«‹çš„ä¿¡æ¯ã€‚å¿½ç•¥é¢‘é“åå­—ä¸­çš„ =#= ï¼Œé€šè¿‡ç§
æœ‰ API æˆ‘ä»¬å¯ä»¥å¾—åˆ°ä¸€ä¸²é€—å·åˆ†å‰²çš„ (æ²¡æœ‰ç©ºæ ¼ï¼) é¢‘é“åå­—ç¬¦ä¸²ã€‚

#+name: irc-authinfo-reader
#+begin_src emacs-lisp :tangle no
(defun auth-server-pass (server)
  (if-let ((secret (plist-get (car (auth-source-search :host server)) :secret)))
      (if (functionp secret)
          (funcall secret) secret)
    (error "Could not fetch password for host %s" server)))

(defun register-irc-auths ()
  (require 'circe)
  (require 'dash)
  (let ((accounts (-filter (lambda (a) (string= "irc" (plist-get a :for)))
                           (auth-source-search :require '(:for) :max 10))))
    (appendq! circe-network-options
              (mapcar (lambda (entry)
                        (let* ((host (plist-get entry :host))
                               (label (or (plist-get entry :label) host))
                               (_ports (mapcar #'string-to-number
                                               (s-split "," (plist-get entry :port))))
                               (port (if (= 1 (length _ports)) (car _ports) _ports))
                               (user (plist-get entry :user))
                               (nick (or (plist-get entry :nick) user))
                               (channels (mapcar (lambda (c) (concat "#" c))
                                                 (s-split "," (plist-get entry :channels)))))
                          `(,label
                            :host ,host :port ,port :nick ,nick
                            :sasl-username ,user :sasl-password auth-server-pass
                            :channels ,channels)))
                      accounts))))
#+end_src

å¯åŠ¨ Circe æ—¶ä¸ºè°ƒç”¨ src_elisp{(register-irc-auths)} ä¸ºå…¶æ·»åŠ  hookã€‚

ä¸€åˆ‡å‡†å¤‡å°±ç»ªï¼Œå¼€å§‹è°ƒæ•´ä¸€ä¸¤ä¸ªé…ç½®æ–‡ä»¶æ¥è¿æ¥ Circeã€‚
#+begin_src emacs-lisp
(after! circe
  (setq-default circe-use-tls t)
  (setq circe-notifications-alert-icon "/usr/share/icons/breeze/actions/24/network-connect.svg"
        lui-logging-directory "~/.emacs.d/.local/etc/irc"
        lui-logging-file-format "{buffer}/%Y/%m-%d.txt"
        circe-format-self-say "{nick:+13s} â”ƒ {body}")

  (custom-set-faces!
    '(circe-my-message-face :weight unspecified))

  (enable-lui-logging-globally)
  (enable-circe-display-images)

  <<org-emph-to-irc>>

  <<circe-emojis>>
  <<circe-emoji-alists>>

  (defun named-circe-prompt ()
    (lui-set-prompt
     (concat (propertize (format "%13s > " (circe-nick))
                         'face 'circe-prompt-face)
             "")))
  (add-hook 'circe-chat-mode-hook #'named-circe-prompt)

  (appendq! all-the-icons-mode-icon-alist
            '((circe-channel-mode all-the-icons-material "message" :face all-the-icons-lblue)
              (circe-server-mode all-the-icons-material "chat_bubble_outline" :face all-the-icons-purple))))

<<irc-authinfo-reader>>

(add-transient-hook! #'=irc (register-irc-auths))
#+end_src

*** Org é£æ ¼ç€é‡
åœ¨ Org è¯­æ³•ä¸­ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨ *é»‘ä½“* ã€ /æ–œä½“/ å’Œ _ä¸‹åˆ’çº¿_ ï¼Œåœ¨ IRC ä¹Ÿè¿™æ ·æ˜¾ç¤ºã€‚
#+name: org-emph-to-irc
#+begin_src emacs-lisp
(defun lui-org-to-irc ()
  "Examine a buffer with simple org-mode formatting, and converts the empasis:
,*bold*, /italic/, and _underline_ to IRC semi-standard escape codes.
=code= is converted to inverse (highlighted) text."
  (goto-char (point-min))
  (while (re-search-forward "\\_<\\(?1:[*/_=]\\)\\(?2:[^[:space:]]\\(?:.*?[^[:space:]]\\)?\\)\\1\\_>" nil t)
    (replace-match
     (concat (pcase (match-string 1)
               ("*" "")
               ("/" "")
               ("_" "")
               ("=" ""))
             (match-string 2)
             "") nil nil)))

(add-hook 'lui-pre-input-hook #'lui-org-to-irc)
#+end_src
*** Emojis
äººäººéƒ½å–œæ¬¢ emojis
#+begin_src emacs-lisp
(defun lui-ascii-to-emoji ()
  (goto-char (point-min))
  (while (re-search-forward "\\( \\)?::?\\([^[:space:]:]+\\):\\( \\)?" nil t)
    (replace-match
     (concat
      (match-string 1)
      (or (cdr (assoc (match-string 2) lui-emojis-alist))
          (concat ":" (match-string 2) ":"))
      (match-string 3))
     nil nil)))

(defun lui-emoticon-to-emoji ()
  (dolist (emoticon lui-emoticons-alist)
    (goto-char (point-min))
    (while (re-search-forward (concat " " (car emoticon) "\\( \\)?") nil t)
      (replace-match (concat " "
                             (cdr (assoc (cdr emoticon) lui-emojis-alist))
                             (match-string 1))))))

(define-minor-mode lui-emojify
  "Replace :emojis: and ;) emoticons with unicode emoji chars."
  :global t
  :init-value t
  (if lui-emojify
      (add-hook! lui-pre-input #'lui-ascii-to-emoji #'lui-emoticon-to-emoji)
    (remove-hook! lui-pre-input #'lui-ascii-to-emoji #'lui-emoticon-to-emoji)))
#+end_src

çœ‹çœ‹æ€ä¹ˆç”¨å§
#+begin_src emacs-lisp :tangle no
(defvar lui-emojis-alist
  '(("grinning"                      . "ğŸ˜€")
    ("smiley"                        . "ğŸ˜ƒ")
    ("smile"                         . "ğŸ˜„")
    ("grin"                          . "ğŸ˜")
    ("laughing"                      . "ğŸ˜†")
    ("sweat_smile"                   . "ğŸ˜…")
    ("joy"                           . "ğŸ˜‚")
    ("rofl"                          . "ğŸ¤£")
    ("relaxed"                       . "â˜ºï¸")
    ("blush"                         . "ğŸ˜Š")
    ("innocent"                      . "ğŸ˜‡")
    ("slight_smile"                  . "ğŸ™‚")
    ("upside_down"                   . "ğŸ™ƒ")
    ("wink"                          . "ğŸ˜‰")
    ("relieved"                      . "ğŸ˜Œ")
    ("heart_eyes"                    . "ğŸ˜")
    ("yum"                           . "ğŸ˜‹")
    ("stuck_out_tongue"              . "ğŸ˜›")
    ("stuck_out_tongue_closed_eyes"  . "ğŸ˜")
    ("stuck_out_tongue_wink"         . "ğŸ˜œ")
    ("zanzy"                         . "ğŸ¤ª")
    ("raised_eyebrow"                . "ğŸ¤¨")
    ("monocle"                       . "ğŸ§")
    ("nerd"                          . "ğŸ¤“")
    ("cool"                          . "ğŸ˜")
    ("star_struck"                   . "ğŸ¤©")
    ("party"                         . "ğŸ¥³")
    ("smirk"                         . "ğŸ˜")
    ("unamused"                      . "ğŸ˜’")
    ("disapointed"                   . "ğŸ˜")
    ("pensive"                       . "ğŸ˜”")
    ("worried"                       . "ğŸ˜Ÿ")
    ("confused"                      . "ğŸ˜•")
    ("slight_frown"                  . "ğŸ™")
    ("frown"                         . "â˜¹ï¸")
    ("persevere"                     . "ğŸ˜£")
    ("confounded"                    . "ğŸ˜–")
    ("tired"                         . "ğŸ˜«")
    ("weary"                         . "ğŸ˜©")
    ("pleading"                      . "ğŸ¥º")
    ("tear"                          . "ğŸ˜¢")
    ("cry"                           . "ğŸ˜¢")
    ("sob"                           . "ğŸ˜­")
    ("triumph"                       . "ğŸ˜¤")
    ("angry"                         . "ğŸ˜ ")
    ("rage"                          . "ğŸ˜¡")
    ("exploding_head"                . "ğŸ¤¯")
    ("flushed"                       . "ğŸ˜³")
    ("hot"                           . "ğŸ¥µ")
    ("cold"                          . "ğŸ¥¶")
    ("scream"                        . "ğŸ˜±")
    ("fearful"                       . "ğŸ˜¨")
    ("disapointed"                   . "ğŸ˜°")
    ("relieved"                      . "ğŸ˜¥")
    ("sweat"                         . "ğŸ˜“")
    ("thinking"                      . "ğŸ¤”")
    ("shush"                         . "ğŸ¤«")
    ("liar"                          . "ğŸ¤¥")
    ("blank_face"                    . "ğŸ˜¶")
    ("neutral"                       . "ğŸ˜")
    ("expressionless"                . "ğŸ˜‘")
    ("grimace"                       . "ğŸ˜¬")
    ("rolling_eyes"                  . "ğŸ™„")
    ("hushed"                        . "ğŸ˜¯")
    ("frowning"                      . "ğŸ˜¦")
    ("anguished"                     . "ğŸ˜§")
    ("wow"                           . "ğŸ˜®")
    ("astonished"                    . "ğŸ˜²")
    ("sleeping"                      . "ğŸ˜´")
    ("drooling"                      . "ğŸ¤¤")
    ("sleepy"                        . "ğŸ˜ª")
    ("dizzy"                         . "ğŸ˜µ")
    ("zipper_mouth"                  . "ğŸ¤")
    ("woozy"                         . "ğŸ¥´")
    ("sick"                          . "ğŸ¤¢")
    ("vomiting"                      . "ğŸ¤®")
    ("sneeze"                        . "ğŸ¤§")
    ("mask"                          . "ğŸ˜·")
    ("bandaged_head"                 . "ğŸ¤•")
    ("money_face"                    . "ğŸ¤‘")
    ("cowboy"                        . "ğŸ¤ ")
    ("imp"                           . "ğŸ˜ˆ")
    ("ghost"                         . "ğŸ‘»")
    ("alien"                         . "ğŸ‘½")
    ("robot"                         . "ğŸ¤–")
    ("clap"                          . "ğŸ‘")
    ("thumpup"                       . "ğŸ‘")
    ("+1"                            . "ğŸ‘")
    ("thumbdown"                     . "ğŸ‘")
    ("-1"                            . "ğŸ‘")
    ("ok"                            . "ğŸ‘Œ")
    ("pinch"                         . "ğŸ¤")
    ("left"                          . "ğŸ‘ˆ")
    ("right"                         . "ğŸ‘‰")
    ("down"                          . "ğŸ‘‡")
    ("wave"                          . "ğŸ‘‹")
    ("pray"                          . "ğŸ™")
    ("eyes"                          . "ğŸ‘€")
    ("brain"                         . "ğŸ§ ")
    ("facepalm"                      . "ğŸ¤¦")
    ("tada"                          . "ğŸ‰")
    ("fire"                          . "ğŸ”¥")
    ("flying_money"                  . "ğŸ’¸")
    ("lighbulb"                      . "ğŸ’¡")
    ("heart"                         . "â¤ï¸")
    ("sparkling_heart"               . "ğŸ’–")
    ("heartbreak"                    . "ğŸ’”")
    ("100"                           . "ğŸ’¯")))

(defvar lui-emoticons-alist
  '((":)"   . "slight_smile")
    (";)"   . "wink")
    (":D"   . "smile")
    ("=D"   . "grin")
    ("xD"   . "laughing")
    (";("   . "joy")
    (":P"   . "stuck_out_tongue")
    (";D"   . "stuck_out_tongue_wink")
    ("xP"   . "stuck_out_tongue_closed_eyes")
    (":("   . "slight_frown")
    (";("   . "cry")
    (";'("  . "sob")
    (">:("  . "angry")
    (">>:(" . "rage")
    (":o"   . "wow")
    (":O"   . "astonished")
    (":/"   . "confused")
    (":-/"  . "thinking")
    (":|"   . "neutral")
    (":-|"  . "expressionless")))
#+end_src

** æ–°é—»æè¦
RSS æè¦æ˜¯ä¸ªå¤§äº‹ (è™½ç„¶å¯èƒ½å¾ˆå¤šäººæ²¡å¬è¿‡)ï¼Œæœ‰ä»€ä¹ˆç†ç”±ä¸ç”¨ =elfeed= ã€‚æˆ‘çœŸçš„éå¸¸å–œæ¬¢
[[https://github.com/fuxialexander/doom-emacs-private-xfu/tree/master/modules/app/rss][fuxialexander]] çš„é…ç½®ï¼Œä¸è¿‡æˆ‘è§‰å¾—æˆ‘ä¸éœ€è¦è‡ªå®šä¹‰æ¨¡å—ã€‚å°è¯•ä¿®æ”¹ä¸€ä¸‹è®©æˆ‘ä»¬ç”¨å¾—æ›´èˆ’æœã€‚

#+attr_html: :class invertible :alt Example elfeed entry
[[https://tecosaur.com/lfs/emacs-config/screenshots/elfeed.png]]

*** å¿«æ·é”®
#+begin_src emacs-lisp
(map! :map elfeed-search-mode-map
      :after elfeed-search
      [remap kill-this-buffer] "q"
      [remap kill-buffer] "q"
      :n doom-leader-key nil
      :n "q" #'+rss/quit
      :n "e" #'elfeed-update
      :n "r" #'elfeed-search-untag-all-unread
      :n "u" #'elfeed-search-tag-all-unread
      :n "s" #'elfeed-search-live-filter
      :n "RET" #'elfeed-search-show-entry
      :n "p" #'elfeed-show-pdf
      :n "+" #'elfeed-search-tag-all
      :n "-" #'elfeed-search-untag-all
      :n "S" #'elfeed-search-set-filter
      :n "b" #'elfeed-search-browse-url
      :n "y" #'elfeed-search-yank)
(map! :map elfeed-show-mode-map
      :after elfeed-show
      [remap kill-this-buffer] "q"
      [remap kill-buffer] "q"
      :n doom-leader-key nil
      :nm "q" #'+rss/delete-pane
      :nm "o" #'ace-link-elfeed
      :nm "RET" #'org-ref-elfeed-add
      :nm "n" #'elfeed-show-next
      :nm "N" #'elfeed-show-prev
      :nm "p" #'elfeed-show-pdf
      :nm "+" #'elfeed-show-tag
      :nm "-" #'elfeed-show-untag
      :nm "s" #'elfeed-show-new-live-search
      :nm "y" #'elfeed-show-yank)
#+end_src
*** å¯ç”¨æ€§å¢å¼º
#+begin_src emacs-lisp
(after! elfeed-search
  (set-evil-initial-state! 'elfeed-search-mode 'normal))
(after! elfeed-show-mode
  (set-evil-initial-state! 'elfeed-show-mode   'normal))

(after! evil-snipe
  (push 'elfeed-show-mode   evil-snipe-disabled-modes)
  (push 'elfeed-search-mode evil-snipe-disabled-modes))
#+end_src
*** å¤–è§‚å¢å¼º
#+begin_src emacs-lisp
(after! elfeed

  (elfeed-org)
  (use-package! elfeed-link)

  (setq elfeed-search-filter "@1-week-ago +unread"
        elfeed-search-print-entry-function '+rss/elfeed-search-print-entry
        elfeed-search-title-min-width 80
        elfeed-show-entry-switch #'pop-to-buffer
        elfeed-show-entry-delete #'+rss/delete-pane
        elfeed-show-refresh-function #'+rss/elfeed-show-refresh--better-style
        shr-max-image-proportion 0.6)

  (add-hook! 'elfeed-show-mode-hook (hide-mode-line-mode 1))
  (add-hook! 'elfeed-search-update-hook #'hide-mode-line-mode)

  (defface elfeed-show-title-face '((t (:weight ultrabold :slant italic :height 1.5)))
    "title face in elfeed show buffer"
    :group 'elfeed)
  (defface elfeed-show-author-face `((t (:weight light)))
    "title face in elfeed show buffer"
    :group 'elfeed)
  (set-face-attribute 'elfeed-search-title-face nil
                      :foreground 'nil
                      :weight 'light)

  (defadvice! +rss-elfeed-wrap-h-nicer ()
    "Enhances an elfeed entry's readability by wrapping it to a width of
`fill-column' and centering it with `visual-fill-column-mode'."
    :override #'+rss-elfeed-wrap-h
    (setq-local truncate-lines nil
                shr-width 120
                visual-fill-column-center-text t
                default-text-properties '(line-height 1.1))
    (let ((inhibit-read-only t)
          (inhibit-modification-hooks t))
      (visual-fill-column-mode)
      ;; (setq-local shr-current-font '(:family "Merriweather" :height 1.2))
      (set-buffer-modified-p nil)))

  (defun +rss/elfeed-search-print-entry (entry)
    "Print ENTRY to the buffer."
    (let* ((elfeed-goodies/tag-column-width 40)
           (elfeed-goodies/feed-source-column-width 30)
           (title (or (elfeed-meta entry :title) (elfeed-entry-title entry) ""))
           (title-faces (elfeed-search--faces (elfeed-entry-tags entry)))
           (feed (elfeed-entry-feed entry))
           (feed-title
            (when feed
              (or (elfeed-meta feed :title) (elfeed-feed-title feed))))
           (tags (mapcar #'symbol-name (elfeed-entry-tags entry)))
           (tags-str (concat (mapconcat 'identity tags ",")))
           (title-width (- (window-width) elfeed-goodies/feed-source-column-width
                           elfeed-goodies/tag-column-width 4))

           (tag-column (elfeed-format-column
                        tags-str (elfeed-clamp (length tags-str)
                                               elfeed-goodies/tag-column-width
                                               elfeed-goodies/tag-column-width)
                        :left))
           (feed-column (elfeed-format-column
                         feed-title (elfeed-clamp elfeed-goodies/feed-source-column-width
                                                  elfeed-goodies/feed-source-column-width
                                                  elfeed-goodies/feed-source-column-width)
                         :left)))

      (insert (propertize feed-column 'face 'elfeed-search-feed-face) " ")
      (insert (propertize tag-column 'face 'elfeed-search-tag-face) " ")
      (insert (propertize title 'face title-faces 'kbd-help title))
      (setq-local line-spacing 0.2)))

  (defun +rss/elfeed-show-refresh--better-style ()
    "Update the buffer to match the selected entry, using a mail-style."
    (interactive)
    (let* ((inhibit-read-only t)
           (title (elfeed-entry-title elfeed-show-entry))
           (date (seconds-to-time (elfeed-entry-date elfeed-show-entry)))
           (author (elfeed-meta elfeed-show-entry :author))
           (link (elfeed-entry-link elfeed-show-entry))
           (tags (elfeed-entry-tags elfeed-show-entry))
           (tagsstr (mapconcat #'symbol-name tags ", "))
           (nicedate (format-time-string "%a, %e %b %Y %T %Z" date))
           (content (elfeed-deref (elfeed-entry-content elfeed-show-entry)))
           (type (elfeed-entry-content-type elfeed-show-entry))
           (feed (elfeed-entry-feed elfeed-show-entry))
           (feed-title (elfeed-feed-title feed))
           (base (and feed (elfeed-compute-base (elfeed-feed-url feed)))))
      (erase-buffer)
      (insert "\n")
      (insert (format "%s\n\n" (propertize title 'face 'elfeed-show-title-face)))
      (insert (format "%s\t" (propertize feed-title 'face 'elfeed-search-feed-face)))
      (when (and author elfeed-show-entry-author)
        (insert (format "%s\n" (propertize author 'face 'elfeed-show-author-face))))
      (insert (format "%s\n\n" (propertize nicedate 'face 'elfeed-log-date-face)))
      (when tags
        (insert (format "%s\n"
                        (propertize tagsstr 'face 'elfeed-search-tag-face))))
      ;; (insert (propertize "Link: " 'face 'message-header-name))
      ;; (elfeed-insert-link link link)
      ;; (insert "\n")
      (cl-loop for enclosure in (elfeed-entry-enclosures elfeed-show-entry)
               do (insert (propertize "Enclosure: " 'face 'message-header-name))
               do (elfeed-insert-link (car enclosure))
               do (insert "\n"))
      (insert "\n")
      (if content
          (if (eq type 'html)
              (elfeed-insert-html content base)
            (insert content))
        (insert (propertize "(empty)\n" 'face 'italic)))
      (goto-char (point-min))))

  )
#+end_src
*** åŠŸèƒ½æ€§å¢å¼º
#+begin_src emacs-lisp
(after! elfeed-show
  (require 'url)

  (defvar elfeed-pdf-dir
    (expand-file-name "pdfs/"
                      (file-name-directory (directory-file-name elfeed-enclosure-default-dir))))

  (defvar elfeed-link-pdfs
    '(("https://www.jstatsoft.org/index.php/jss/article/view/v0\\([^/]+\\)" . "https://www.jstatsoft.org/index.php/jss/article/view/v0\\1/v\\1.pdf")
      ("http://arxiv.org/abs/\\([^/]+\\)" . "https://arxiv.org/pdf/\\1.pdf"))
    "List of alists of the form (REGEX-FOR-LINK . FORM-FOR-PDF)")

  (defun elfeed-show-pdf (entry)
    (interactive
     (list (or elfeed-show-entry (elfeed-search-selected :ignore-region))))
    (let ((link (elfeed-entry-link entry))
          (feed-name (plist-get (elfeed-feed-meta (elfeed-entry-feed entry)) :title))
          (title (elfeed-entry-title entry))
          (file-view-function
           (lambda (f)
             (when elfeed-show-entry
               (elfeed-kill-buffer))
             (pop-to-buffer (find-file-noselect f))))
          pdf)

      (let ((file (expand-file-name
                   (concat (subst-char-in-string ?/ ?, title) ".pdf")
                   (expand-file-name (subst-char-in-string ?/ ?, feed-name)
                                     elfeed-pdf-dir))))
        (if (file-exists-p file)
            (funcall file-view-function file)
          (dolist (link-pdf elfeed-link-pdfs)
            (when (and (string-match-p (car link-pdf) link)
                       (not pdf))
              (setq pdf (replace-regexp-in-string (car link-pdf) (cdr link-pdf) link))))
          (if (not pdf)
              (message "No associated PDF for entry")
            (message "Fetching %s" pdf)
            (unless (file-exists-p (file-name-directory file))
              (make-directory (file-name-directory file) t))
            (url-copy-file pdf file)
            (funcall file-view-function file))))))

  )
#+end_src

** å­—å…¸
Doom å·²ç»åœ¨é‡‡ç”¨ =define-word= å¹¶ç”¨ [[https://github.com/gromnitsky/wordnut][wordnut]] æä¾›äº†ç§æœ‰æœåŠ¡ã€‚ä½†æ˜¯ä½¿ç”¨ç¦»çº¿å­—å…¸æœ‰ä¸€äº›å·¨
å¤§çš„ä¼˜åŠ¿ï¼Œæ¯”å¦‚ï¼š
+ é€Ÿåº¦
+ é›†æˆå¤šä¸ªå­—å…¸

[[http://goldendict.org/][GoldenDict]] ä¼¼ä¹æ˜¯å½“å‰å¯ç”¨çš„æœ€å¥½çš„é€‰æ‹©ï¼Œä¸è¿‡ç¼ºå°‘ CLIã€‚å› æ­¤ï¼Œé€€å›ä½¿ç”¨ [[https://dushistov.github.io/sdcv/][sdcv]] (StarDict
çš„ CLI ç‰ˆæœ¬) å§ã€‚ä½¿ç”¨æ¥å£ï¼Œç”¨æˆ‘çš„ =lexic= åŒ…å§ã€‚

#+attr_html: :class invertible :alt Screenshot of the lexic-mode view of "literate"
[[https://tecosaur.com/lfs/emacs-config/screenshots/lexic.png]]

#+begin_src emacs-lisp :tangle (if (executable-find "sdcv") "packages.el" "no")
(package! lexic :recipe (:local-repo "lisp/lexic"))
#+end_src

GoldenDict CLI åœ¨ GitHub ä¸Šæ˜¯ [[https://github.com/goldendict/goldendict/issues/37][æœ€å—å¥½è¯„çš„é—®é¢˜]]ï¼Œåº”è¯¥å¾ˆå¿«å°±èƒ½ä» ~sdcv~ åˆ‡æ¢è¿‡æ¥äº†ã€‚

è‡ªä» GoldenDict æ”¯æŒ StarDict æ–‡ä»¶ï¼Œåˆ‡æ¢çš„ç—›ç‚¹åº”è¯¥ä¼šå°‘å¾ˆå¤šã€‚

æˆ‘ä»¬é¦–å…ˆåŠ è½½ =è¯å…¸= ï¼Œç„¶åé›†æˆåˆ°é¢„å…ˆå®šä¹‰çš„å‡½æ•° (æ¯”å¦‚
~+lookup/dictionary-definition~)ã€‚
#+begin_src emacs-lisp :tangle (if (executable-find "sdcv") "yes" "no")
(use-package! lexic
  :commands lexic-search lexic-list-dictionary
  :config
  (map! :map lexic-mode-map
        :n "q" #'lexic-return-from-lexic
        :nv "RET" #'lexic-search-word-at-point
        :n "a" #'outline-show-all
        :n "h" (cmd! (outline-hide-sublevels 3))
        :n "o" #'lexic-toggle-entry
        :n "n" #'lexic-next-entry
        :n "N" (cmd! (lexic-next-entry t))
        :n "p" #'lexic-previous-entry
        :n "P" (cmd! (lexic-previous-entry t))
        :n "E" (cmd! (lexic-return-from-lexic) ; expand
                     (switch-to-buffer (lexic-get-buffer)))
        :n "M" (cmd! (lexic-return-from-lexic) ; minimise
                     (lexic-goto-lexic))
        :n "C-p" #'lexic-search-history-backwards
        :n "C-n" #'lexic-search-history-forwards
        :n "/" (cmd! (call-interactively #'lexic-search))))
#+end_src

ç°åœ¨æ›¿æ¢ wordnetã€‚
#+begin_src emacs-lisp :tangle (if (executable-find "sdcv") "yes" "no")
(defadvice! +lookup/dictionary-definition-lexic (identifier &optional arg)
  "Look up the definition of the word at point (or selection) using `lexic-search'."
  :override #'+lookup/dictionary-definition
  (interactive
   (list (or (doom-thing-at-point-or-region 'word)
             (read-string "Look up in dictionary: "))
         current-prefix-arg))
  (lexic-search identifier nil nil t))
#+end_src

æœ€åä¸€æ­¥ï¼Œæˆ‘æƒ³ç¡®å®šæˆ‘æœ‰ä¸€äº›è¯å…¸ã€‚æˆ‘åœ¨ç½‘ä¸Šæ”¾äº†è¯å…¸çš„å‹ç¼©åŒ…ï¼Œå¦‚æœç³»ç»Ÿä¸­æ²¡æœ‰å°±å¯ä»¥ç›´æ¥ä¸‹è½½ã€‚
#+begin_src shell :tangle (if (and (executable-find "sdsv") (not (file-exists-p (concat (or (getenv "STARDICT_DATA_DIR") (concat (or "~/.local/share" (getenv "XDG_DATA_HOME")) "/stardict")) "/dic")))) "setup.sh" "no")
DIC_FOLDER=${STARDICT_DATA_DIR:-${XDG_DATA_HOME:-$HOME/.local/share}/stardict}/dic
if [ ! -d "$DIC_FOLDER" ]; then
    TMP="$(mktemp -d /tmp/dict-XXX)"
    cd "$TMP"
    curl -A "Mozilla/4.0" -o "stardict.tar.gz" "https://tecosaur.com/resources/config/stardict.tar.gz"
    tar -xf "stardict.tar.gz"
    rm "stardict.tar.gz"
    mkdir -p "$DIC_FOLDER"
    mv * "$DIC_FOLDER"
fi
#+end_src

** Mail
[[xkcd:1467]]

*** æ‹‰å–
è¿™æœ‰ä¸€äº›ç›¸ä¼¼çš„ç«äº‰å…³ç³»çš„åº”ç”¨ï¼š
+ [[https://www.offlineimap.org/][OfflineIMAP]] ([[https://wiki.archlinux.org/index.php/OfflineIMAP][ArchWiki page]])
+ [[http://isync.sourceforge.net/mbsync.html][isync/mbsync]] ([[https://wiki.archlinux.org/index.php/isync][ArchWiki page]])


ä»”ç»†ç¿»é˜… r/emacsï¼Œä¸€ä¼šå‘ç°ä¸»æµè§‚ç‚¹ä¼¼ä¹æ˜¯
+ isync è¶…å¿«
+ isync è¶…å¯é 
è¿˜ä¸å¿«ç”¨ï¼

è¿™ä¸ªé…ç½®å¾ˆç®€å•ï¼Œç›´æ¥æ”¾åœ¨ [[file:~/.mbsyncrc][~/.mbsyncrc]] é‡Œå°±è¡Œã€‚æˆ‘ç›®å‰ä¸ºæ­¢æˆåŠŸè¿æ¥åˆ°äº† Gmailã€
office365mail å’Œ dovecotã€‚æˆ‘ä¹Ÿæ˜¯å°†å¯†ç ä¿å­˜åœ¨ [[file:authinfo.gpg][authinfo.gpg]] ä¸­ï¼Œç”¨ ~PassCmd~ æ‹‰
å–ä½¿ç”¨å®ƒä»¬ï¼š
#+begin_src shell :tangle no :eval no
gpg2 -q --for-your-eyes-only --no-tty -d ~/.authinfo.gpg | awk '/machine IMAP_SERCER login EMAIL_ADDR/ {print $NF}'
#+end_src

æˆ‘ä»¬å¯ä»¥åœ¨ systemd service æ–‡ä»¶ä¸­æˆ–å…¶ä»–ä»€ä¹ˆä¸­è¿è¡Œ ~mbsync -a~ ï¼Œä½†æˆ‘ä»¬è¦åšçš„æ›´å¥½
ã€‚[[https://github.com/vsemyonoff/easymail#usage][vsemyonoff/easymail]] ä¼¼ä¹æ˜¯æˆ‘ä»¬æƒ³è¦çš„ç±»å‹ï¼Œä½†ä¸å¹¸çš„æ˜¯å®ƒå†™çš„ =å¾ˆå°‘= (??? but is
written for =notmuch= unfortunately)ã€‚æˆ‘ä»¬åº”è¯¥ä½¿ç”¨ [[https://gitlab.com/shackra/goimapnotify][goimapnotify]] åŒæ­¥æ–°é‚®ä»¶ã€‚
è¿™æ„å‘³ç€éœ€è¦å¦ä¸€ä¸ªé…ç½®æ–‡ä»¶ :(

å®‰è£…ï¼
#+begin_src shell :eval no :tangle (if (and (executable-find "mu") (not (executable-find "goimapnotify"))) "setup.sh" "no")
go get -u gitlab.com/shackra/goimapnotify
ln -s ~/.local/share/go/bin/goimapnotify ~/.local/bin/
#+end_src

è¿™æ˜¯æ€»ä½“è®¡åˆ’ï¼š
1. ä½¿ç”¨ ~goimapnotify~ ç›‘è§†é‚®ç®±ã€‚
   è¿™éœ€è¦æˆ‘ä»¬è‡ªå·±è®¾ç½®é…ç½®æ–‡ä»¶å’Œ =systemd= serviceï¼ŒçœŸ tmd ç—›è‹¦ã€‚å†™ä¸€ä¸ª python
   è„šæœ¬ç»ˆç»“è¿™ç—›è‹¦å§ï¼Œé¡ºä¾¿è®©ä»–è‡ªå·±é€šè¿‡è§£æ [[file:~/.mbsyncrc][~/.mbsyncrc]] åšé…ç½®æ–‡ä»¶å’Œ systemdã€‚
2. å¯¹æ–°é‚®ä»¶ï¼Œä½¿ç”¨ ~mbsync --pull --new ACCOUNT:BOX~ ã€‚
   å¯¹äºå°è¯•å°½å¯èƒ½å…·ä½“ï¼Œæ‰€ä»¥ ~mbsync~ å°½å¯èƒ½å¿«åœ°è¿”å›ï¼Œæˆ‘ä»¬ /å°½å¿«æ¥æ”¶ email/ ã€‚
3. å°è¯•è¿è¡Œ ~mu index --lazy-fetch~ ã€‚
   å¦‚æœ mu4e å·²ç»æ‰“å¼€è€Œå¤±è´¥ (ç”±äºæ•°æ®åº“ä¸Šé”)ï¼Œè¿™ç§æƒ…å†µä¸‹æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªä¸´æ—¶æ–‡ä»¶
   (=/tmp/mu_reindex_now=)ã€‚
4. å¦å¤–ï¼Œæˆ‘ä»¬åœ¨è¿è¡Œ mu4e æ—¶ï¼Œè®© Emacs æ¯ç§’æ£€æŸ¥ä¸€æ¬¡æ–‡ä»¶ =/tmp/mu_reindex_now=
   æ˜¯å¦å­˜åœ¨ï¼Œå­˜åœ¨å°±ä½¿ç”¨ ~mu4e-update-index~ (åˆ é™¤æ–‡ä»¶å)ã€‚

å¼€å§‹ç€æ‰‹å¤„ç† elisp æ–¹é¢çš„äº‹å§ã€‚

**** ä½¿ç”¨ mu4e é‡å»ºé‚®ä»¶ç´¢å¼•
#+begin_src emacs-lisp
(after! mu4e
  (defvar mu4e-reindex-request-file "/tmp/mu_reindex_now"
    "Location of the reindex request, signaled by existance")
  (defvar mu4e-reindex-request-min-seperation 5.0
    "Don't refresh again until this many second have elapsed.
Prevents a series of redisplays from being called (when set to an appropriate value)")

  (defvar mu4e-reindex-request--file-watcher nil)
  (defvar mu4e-reindex-request--file-just-deleted nil)
  (defvar mu4e-reindex-request--last-time 0)

  (defun mu4e-reindex-request--add-watcher ()
    (setq mu4e-reindex-request--file-just-deleted nil)
    (setq mu4e-reindex-request--file-watcher
          (file-notify-add-watch mu4e-reindex-request-file
                                 '(change)
                                 #'mu4e-file-reindex-request)))

  (defadvice! mu4e-stop-watching-for-reindex-request ()
    :after #'mu4e~proc-kill
    (if mu4e-reindex-request--file-watcher
        (file-notify-rm-watch mu4e-reindex-request--file-watcher)))

  (defadvice! mu4e-watch-for-reindex-request ()
    :after #'mu4e~proc-start
    (mu4e-stop-watching-for-reindex-request)
    (when (file-exists-p mu4e-reindex-request-file)
      (delete-file mu4e-reindex-request-file))
    (mu4e-reindex-request--add-watcher))

  (defun mu4e-file-reindex-request (event)
    "Act based on the existance of `mu4e-reindex-request-file'"
    (if mu4e-reindex-request--file-just-deleted
        (mu4e-reindex-request--add-watcher)
      (when (equal (nth 1 event) 'created)
        (delete-file mu4e-reindex-request-file)
        (setq mu4e-reindex-request--file-just-deleted t)
        (mu4e-reindex-maybe t))))

  (defun mu4e-reindex-maybe (&optional new-request)
    "Run `mu4e~proc-index' if it's been more than
`mu4e-reindex-request-min-seperation'seconds since the last request,"
    (let ((time-since-last-request (- (float-time)
                                      mu4e-reindex-request--last-time)))
      (when new-request
        (setq mu4e-reindex-request--last-time (float-time)))
      (if (> time-since-last-request mu4e-reindex-request-min-seperation)
          (mu4e~proc-index nil t)
        (when new-request
          (run-at-time (* 1.1 mu4e-reindex-request-min-seperation) nil
                       #'mu4e-reindex-maybe))))))
#+end_src

**** é…ç½®è½¬ç å’ŒæœåŠ¡ç®¡ç†
åªè¦ =mbsyncrc= æ–‡ä»¶å­˜åœ¨ï¼Œå°±åƒè¿è¡Œä¸€æ ·ç®€å•
#+begin_src shell :tangle (if (and (executable-find "mu") (not (file-exists-p "~/.config/imapnotify"))) "setup.sh" "no")
~/.config/doom/misc/mbsync-imapnotify.py
#+end_src

æ²¡æœ‰æ·»åŠ æ ‡è®°è¿è¡Œæ—¶ï¼Œå°†æ‰§è¡Œä»¥ä¸‹æ“ä½œ
+ è¯»å–å¹¶è§£æ [[file:~/.mbsyncrc][~/.mbsyncrc]]ï¼ŒæŒ‡å®šè¯†åˆ«ä»¥ä¸‹å±æ€§
  - ~IMAPAccount~
  - ~Host~
  - ~Port~
  - ~User~
  - ~Password~
  - ~PassCmd~
  - ~Patterns~
+ è¿è¡Œ ~mbsync --list ACCOUNT~ ï¼Œå¹¶é‡‡ç”¨ ~æ¨¡å¼~ è¿‡æ»¤ç»“æœ
+ ä¸ºæ¯ä¸ªå¸å·æ„å»º imapnotify é…ç½®ï¼Œå¹¶æ·»åŠ ä»¥ä¸‹ hook
  - onNewMail :: src_shell{mbsync --pull ACCOUNT:MAILBOX}
  - onNewMailPost :: src_shell{if mu index --lazy-check; then test -f /tmp/mu_reindex_now && rm /tmp/mu_reindex_now; else touch /tmp/mu_reindex_now; fi}
+ æ¯”è¾ƒå¸å·è¡¨ä¸­çš„å¸å·ä¸ä¹‹å‰çš„å¸å·ï¼Œå¯ç”¨/ç¦ç”¨ç›¸å…³çš„ systemd æœåŠ¡ï¼Œå¹¶ç”¨ ~--now~ æ ‡
  è®°å¯åŠ¨ (å¯ç”¨æˆ–åœæ­¢æœåŠ¡)

è¿™ä¸ªè„šæœ¬è¿˜æ”¯æŒä»¥ä¸‹å‚æ•°
+ ~--status~ è·å–ç›¸å…³ systemd æœåŠ¡çš„ä¿¡æ¯ï¼ŒåŒ…æ‹¬ =active= ã€ =failing= å’Œ
  =disabled=
+ ~--enable~ å¯ä»¥å¼€å¯ç›¸å…³æ‰€æœ‰çš„ systemd æœåŠ¡
+ ~--disable~ å¯ä»¥å…³é—­ç›¸å…³æ‰€æœ‰çš„ systemd æœåŠ¡
#+begin_src python :tangle misc/mbsync-imapnotify.py :shebang "#!/usr/bin/env python3"
from pathlib import Path
import json
import re
import shutil
import subprocess
import sys
import fnmatch

mbsyncFile = Path("~/.mbsyncrc").expanduser()

imapnotifyConfigFolder = Path("~/.config/imapnotify/").expanduser()
imapnotifyConfigFolder.mkdir(exist_ok=True)
imapnotifyConfigFilename = "notify.conf"

imapnotifyDefault = {
    "host": "",
    "port": 993,
    "tls": True,
    "tlsOptions": {"rejectUnauthorized": True},
    "onNewMail": "",
    "onNewMailPost": "if mu index --lazy-check; then test -f /tmp/mu_reindex_now && rm /tmp/mu_reindex_now; else touch /tmp/mu_reindex_now; fi",
}


def stripQuotes(string):
    if string[0] == '"' and string[-1] == '"':
        return string[1:-1].replace('\\"', '"')


mbsyncInotifyMapping = {
    "Host": (str, "host"),
    "Port": (int, "port"),
    "User": (str, "username"),
    "Password": (str, "password"),
    "PassCmd": (stripQuotes, "passwordCmd"),
    "Patterns": (str, "_patterns"),
}

oldAccounts = [d.name for d in imapnotifyConfigFolder.iterdir() if d.is_dir()]

currentAccount = ""
currentAccountData = {}

successfulAdditions = []


def processLine(line):
    newAcc = re.match(r"^IMAPAccount ([^#]+)", line)

    linecontent = re.sub(r"(^|[^\\])#.*", "", line).split(" ", 1)
    if len(linecontent) != 2:
        return

    parameter, value = linecontent

    if parameter == "IMAPAccount":
        if currentAccountNumber > 0:
            finaliseAccount()
        newAccount(value)
    elif parameter in mbsyncInotifyMapping.keys():
        parser, key = mbsyncInotifyMapping[parameter]
        currentAccountData[key] = parser(value)
    elif parameter == "Channel":
        currentAccountData["onNewMail"] = f"mbsync --pull --new {value}:'%s'"


def newAccount(name):
    global currentAccountNumber
    global currentAccount
    global currentAccountData
    currentAccountNumber += 1
    currentAccount = name
    currentAccountData = {}
    print(f"\n\033[1;32m{currentAccountNumber}\033[0;32m - {name}\033[0;37m")


def accountToFoldername(name):
    return re.sub(r"[^A-Za-z0-9]", "", name)


def finaliseAccount():
    if currentAccountNumber == 0:
        return

    global currentAccountData
    try:
        currentAccountData["boxes"] = getMailBoxes(currentAccount)
    except subprocess.CalledProcessError as e:
        print(
            f"\033[1;31mError:\033[0;31m failed to fetch mailboxes (skipping): "
            + f"`{' '.join(e.cmd)}' returned code {e.returncode}\033[0;37m"
        )
        return
    except subprocess.TimeoutExpired as e:
        print(
            f"\033[1;31mError:\033[0;31m failed to fetch mailboxes (skipping): "
            + f"`{' '.join(e.cmd)}' timed out after {e.timeout:.2f} seconds\033[0;37m"
        )
        return

    if "_patterns" in currentAccountData:
        currentAccountData["boxes"] = applyPatternFilter(
            currentAccountData["_patterns"], currentAccountData["boxes"]
        )

    # strip not-to-be-exported data
    currentAccountData = {
        k: currentAccountData[k] for k in currentAccountData if k[0] != "_"
    }

    parametersSet = currentAccountData.keys()
    currentAccountData = {**imapnotifyDefault, **currentAccountData}
    for key, val in currentAccountData.items():
        valColor = "\033[0;33m" if key in parametersSet else "\033[0;37m"
        print(f"  \033[1;37m{key:<13} {valColor}{val}\033[0;37m")

    if (
            len(currentAccountData["boxes"]) > 15
            and "@gmail.com" in currentAccountData["username"]
    ):
        print(
            "  \033[1;31mWarning:\033[0;31m Gmail raises an error when more than"
            + "\033[1;31m15\033[0;31m simultanious connections are attempted."
            + "\n           You are attempting to monitor "
            + f"\033[1;31m{len(currentAccountData['boxes'])}\033[0;31m mailboxes.\033[0;37m"
        )

    configFile = (
        imapnotifyConfigFolder
        / accountToFoldername(currentAccount)
        / imapnotifyConfigFilename
    )
    configFile.parent.mkdir(exist_ok=True)

    json.dump(currentAccountData, open(configFile, "w"), indent=2)
    print(f" \033[0;35mConfig generated and saved to {configFile}\033[0;37m")

    global successfulAdditions
    successfulAdditions.append(accountToFoldername(currentAccount))


def getMailBoxes(account):
    boxes = subprocess.run(
        ["mbsync", "--list", account], check=True, stdout=subprocess.PIPE, timeout=10.0
    )
    return boxes.stdout.decode("utf-8").strip().split("\n")


def applyPatternFilter(pattern, mailboxes):
    patternRegexs = getPatternRegexes(pattern)
    return [m for m in mailboxes if testPatternRegexs(patternRegexs, m)]


def getPatternRegexes(pattern):
    def addGlob(b):
        blobs.append(b.replace('\\"', '"'))
        return ""

    blobs = []
    pattern = re.sub(r' ?"([^"]+)"', lambda m: addGlob(m.groups()[0]), pattern)
    blobs.extend(pattern.split(" "))
    blobs = [
        (-1, fnmatch.translate(b[1::])) if b[0] == "!" else (1, fnmatch.translate(b))
        for b in blobs
    ]
    return blobs


def testPatternRegexs(regexCond, case):
    for factor, regex in regexCond:
        if factor * bool(re.match(regex, case)) < 0:
            return False
    return True


def processSystemdServices():
    keptAccounts = [acc for acc in successfulAdditions if acc in oldAccounts]
    freshAccounts = [acc for acc in successfulAdditions if acc not in oldAccounts]
    staleAccounts = [acc for acc in oldAccounts if acc not in successfulAdditions]

    if keptAccounts:
        print(f"\033[1;34m{len(keptAccounts)}\033[0;34m kept accounts:\033[0;37m")
        restartAccountSystemdServices(keptAccounts)

    if freshAccounts:
        print(f"\033[1;32m{len(freshAccounts)}\033[0;32m new accounts:\033[0;37m")
        enableAccountSystemdServices(freshAccounts)
    else:
        print(f"\033[0;32mNo new accounts.\033[0;37m")

    notActuallyEnabledAccounts = [
        acc for acc in successfulAdditions if not getAccountServiceState(acc)["enabled"]
    ]
    if notActuallyEnabledAccounts:
        print(
            f"\033[1;32m{len(notActuallyEnabledAccounts)}\033[0;32m accounts need re-enabling:\033[0;37m"
        )
        enableAccountSystemdServices(notActuallyEnabledAccounts)

    if staleAccounts:
        print(f"\033[1;33m{len(staleAccounts)}\033[0;33m removed accounts:\033[0;37m")
        disableAccountSystemdServices(staleAccounts)
    else:
        print(f"\033[0;33mNo removed accounts.\033[0;37m")


def enableAccountSystemdServices(accounts):
    for account in accounts:
        print(f" \033[0;32m - \033[1;37m{account:<18}", end="\033[0;37m", flush=True)
        if setSystemdServiceState(
                "enable", f"goimapnotify@{accountToFoldername(account)}.service"
        ):
            print("\033[1;32m enabled")


def disableAccountSystemdServices(accounts):
    for account in accounts:
        print(f" \033[0;33m - \033[1;37m{account:<18}", end="\033[0;37m", flush=True)
        if setSystemdServiceState(
                "disable", f"goimapnotify@{accountToFoldername(account)}.service"
        ):
            print("\033[1;33m disabled")


def restartAccountSystemdServices(accounts):
    for account in accounts:
        print(f" \033[0;34m - \033[1;37m{account:<18}", end="\033[0;37m", flush=True)
        if setSystemdServiceState(
                "restart", f"goimapnotify@{accountToFoldername(account)}.service"
        ):
            print("\033[1;34m restarted")


def setSystemdServiceState(state, service):
    try:
        enabler = subprocess.run(
            ["systemctl", "--user", state, service, "--now"],
            check=True,
            stderr=subprocess.DEVNULL,
            timeout=5.0,
        )
        return True
    except subprocess.CalledProcessError as e:
        print(
            f" \033[1;31mfailed\033[0;31m to {state}, `{' '.join(e.cmd)}'"
            + f"returned code {e.returncode}\033[0;37m"
        )
    except subprocess.TimeoutExpired as e:
        print(f" \033[1;31mtimed out after {e.timeout:.2f} seconds\033[0;37m")
        return False


def getAccountServiceState(account):
    return {
        state: bool(
            1
            - subprocess.run(
                [
                    "systemctl",
                    "--user",
                    f"is-{state}",
                    "--quiet",
                    f"goimapnotify@{accountToFoldername(account)}.service",
                ],
                stderr=subprocess.DEVNULL,
            ).returncode
        )
        for state in ("enabled", "active", "failing")
    }


def getAccountServiceStates(accounts):
    for account in accounts:
        enabled, active, failing = getAccountServiceState(account).values()
        print(f"  - \033[1;37m{account:<18}\033[0;37m ", end="", flush=True)
        if not enabled:
            print("\033[1;33mdisabled\033[0;37m")
        elif active:
            print("\033[1;32mactive\033[0;37m")
        elif failing:
            print("\033[1;31mfailing\033[0;37m")
        else:
            print("\033[1;35min an unrecognised state\033[0;37m")


if len(sys.argv) > 1:
    if sys.argv[1]   in ["-e", "--enable"]:
        enableAccountSystemdServices(oldAccounts)
        exit()
    elif sys.argv[1] in ["-d", "--disable"]:
        disableAccountSystemdServices(oldAccounts)
        exit()
    elif sys.argv[1] in ["-r", "--restart"]:
        restartAccountSystemdServices(oldAccounts)
        exit()
    elif sys.argv[1] in ["-s", "--status"]:
        getAccountServiceStates(oldAccounts)
        exit()
    elif sys.argv[1] in ["-h", "--help"]:
        print("""\033[1;37mMbsync to IMAP Notify config generator.\033[0;37m

Usage: mbsync-imapnotify [options]

Options:
    -e, --enable       enable all services
    -d, --disable      disable all services
    -r, --restart      restart all services
    -s, --status       fetch the status for all services
    -h, --help         show this help
""", end='')
        exit()
    else:
        print(f"\033[0;31mFlag {sys.argv[1]} not recognised, try --help\033[0;37m")
        exit()


mbsyncData = open(mbsyncFile, "r").read()

currentAccountNumber = 0

totalAccounts = len(re.findall(r"^IMAPAccount", mbsyncData, re.M))


def main():
    print("\033[1;34m:: MbSync to Go IMAP notify config file creator ::\033[0;37m")

    shutil.rmtree(imapnotifyConfigFolder)
    imapnotifyConfigFolder.mkdir(exist_ok=False)
    print("\033[1;30mImap Notify config dir purged\033[0;37m")

    print(f"Identified \033[1;32m{totalAccounts}\033[0;32m accounts.\033[0;37m")

    for line in mbsyncData.split("\n"):
        processLine(line)

    finaliseAccount()

    print(
        f"\nConfig files generated for \033[1;36m{len(successfulAdditions)}\033[0;36m"
        + f" out of \033[1;36m{totalAccounts}\033[0;37m accounts.\n"
    )

    processSystemdServices()


if __name__ == "__main__":
    main()
#+end_src

*** ç´¢å¼• / æœç´¢
è¿™ç”± [[https://www.djcbsoftware.nl/code/mu/][Mu]] æ¥è§£å†³ã€‚è¿™æ˜¯ä¸€ä¸ªæŸ¥æ‰¾ä»¥ [[http://en.wikipedia.org/wiki/Maildir][Maildir]] æ ¼å¼å­˜å‚¨çš„ emai çš„å·¥å…·ã€‚æ ¹æ®å…¶ä¸»é¡µä»‹ç»ï¼Œå®ƒ
çš„ä¸»è¦åŠŸèƒ½å¦‚ä¸‹ï¼š
+ å¿«é€Ÿç´¢å¼•
+ è‰¯å¥½çš„æœç´¢
+ æ”¯æŒåŠ å¯†å’Œç­¾åæ¶ˆæ¯
+ ä¸°å¯Œçš„ CLI å·¥å…·
+ è§„èŒƒåŒ–é‡éŸ³ä¸å¤§å°å†™
+ å¼ºå¤§çš„é›†æˆ email å®¢æˆ·ç«¯

ä¸è¿‡å¾ˆé—æ†¾ï¼Œæˆ‘æ²¡æœ‰æ‰“åŒ… ~mu~ ï¼Œæˆ‘åªæ˜¯æƒ³ä»æºç æ„å»ºå®ƒï¼Œéœ€è¦è¿™äº›ä¾èµ–
+ =gmime-devel=
+ =xapian-core-devel=

#+name: Install mu from source
#+begin_src shell :eval no :tangle (if (and (file-directory-p "~/.mail") (not (executable-find "mu"))) "setup.sh" "no")
cd ~/.local/lib/
git clone https://github.com/djcb/mu.git
cd ./mu
./autogen.sh
make
sudo make install
#+end_src

æ£€æŸ¥æˆ‘çš„ç‰ˆæœ¬æ˜¯ä¸æ˜¯æœ€æ–°å‘å¸ƒçš„ï¼š

#+begin_src shell :tangle no
curl --silent "https://api.github.com/repos/djcb/mu/releases/latest" | grep '"tag_name":' | sed -E 's/.*"([^"]+)".*/\1/'
mu --version | head -n 1 | sed 's/.* version //'
#+end_src

#+results:
| 1.4.6 |
| 1.4.6 |
*** å‘é€
[[https://www.nongnu.org/smtpmail/][SmtpMail]] ä¼¼ä¹æ˜¯ 'é»˜è®¤çš„' å‡ºå‘ç‚¹ï¼Œä½†ä¸æ˜¯æˆ‘çš„ï¼Œä½† [[https://marlam.de/msmtp/][msmtp]] æ˜¯ã€‚æ‰€ä»¥æˆ‘ä¼šè¯•ä¸€è¯•ã€‚ä»”ç»†é˜…è¯»
(æ¯”å¦‚ google "msmtp vs sendmail")ï¼Œä¼šå‘ç°å‡ ä¹æ‰€æœ‰çš„æ¯”è¾ƒéƒ½è¡¨æ˜ msmtp æ˜¯æ›´å¥½çš„é€‰
æ‹©ã€‚æˆ‘çœ‹åˆ°äº†è¿™äº›è§‚ç‚¹
+ ~sendmail~ æœ‰ä¸¥é‡æ¼æ´
+ ~sendmail~ æœ‰ç¹ççš„é…ç½®
+ ~ssmtp~ å¾ˆä¹…æ²¡ç»´æŠ¤äº†
+ ~msmtp~ æ˜¯ ~ssmtp~ çš„ç»´æŠ¤æ›¿ä»£å“
+ ~msmtp~ çš„é…ç½®å¾ˆç®€å•

é…ç½®æ–‡ä»¶æ˜¯ [[file:~/.config/msmtp/config][~/.config/msmtp/config]]ã€‚

**** ç³»ç»Ÿé»‘å®¢
ä¸å¹¸çš„æ˜¯åœ¨æˆ‘çš„æ‰“åŒ…ç‰ˆæœ¬ä¸­ä¼¼ä¹æœ‰ä¸€ä¸ª [[https://bugs.archlinux.org/task/44994][bug]]ï¼Œå› æ­¤é‡‡ç”¨æœ€æ–°ç‰ˆæºç çš„æ–¹å¼å®‰è£…ã€‚

å¯¹äºç”¨æˆ· ~auth~ æ“ä½œï¼Œæˆ‘éœ€è¦ =GNU SASL= ï¼Œæˆ‘å¹¶æ²¡æœ‰æ‰“åŒ…å®ƒã€‚æˆ‘ä¸è§‰å¾—æˆ‘æƒ³ç”¨å®ƒï¼Œä½†æˆ‘
ç¡®å®éœ€è¦ã€‚
#+name: ä»æºç å®‰è£… gsasl
#+begin_src shell :eval no :tangle (if (and (executable-find "mu") (not (executable-find "msmtp"))) "setup.sh" "no")
export GSASL_VERSION=1.8.1
cd ~/.local/lib/
curl "ftp://ftp.gnu.org/gnu/gsasl/libgsasl-$GSASL_VERSION.tar.gz" | tar xz
curl "ftp://ftp.gnu.org/gnu/gsasl/gsasl-$GSASL_VERSION.tar.gz" | tar xz
cd "./libgsasl-$GSASL_VERSION"
./configure
make
sudo make install
cd ..
cd "./gsasl-$VERSION"
./configure
make
sudo make install
cd ..
#+end_src

ç¼–è¯‘ ~msmtp~
#+name: ä»æºç å®‰è£… msmtp
#+begin_src shell :eval no :tangle (if (and (executable-find "mu") (not (executable-find "msmtp"))) "setup.sh" "no")
cd ~/.local/lib/
git clone https://github.com/marlam/msmtp-mirror.git ./msmtp
cd ./msmtp
libtoolize --force
aclocal
autoheader
automake --force-missing --add-missing
autoconf
# if using GSASL
# PKG_CONFIG_PATH=/usr/local/lib/pkgconfig ./configure --with-libgsasl
./configure
make
sudo make install
#+end_src

å¦‚æœå¾ˆæ—©å°±å¼€å§‹ä½¿ç”¨ =GSASL= ï¼Œæˆ‘ä»¬éœ€è¦ç¡®å®šåŠ¨æ€é“¾æ¥åº“æ˜¯å¦åœ¨åº“ç›®å½•ä¸­ã€‚æˆ‘ä»¬å¯ä»¥å°†ä¸€ä¸ª
ä¸æ›´æ—©å®‰è£…çš„åŒåçš„å¯æ‰§è¡Œæ–‡ä»¶æ·»åŠ åˆ° ~$PATH~ ä¸­ã€‚
#+begin_src sh :tangle no :shebang "#!/bin/sh"
LD_LIBRARY_PATH=/usr/local/lib exec /usr/local/bin/msmtp "$@"
#+end_src

*** Mu4e
:PROPERTIES:
:header-args:emacs-lisp: :tangle no :noweb-ref mu4e-conf
:END:

Webmail å®¢æˆ·ç«¯éå¸¸çš„å…¨ä¸”æ£’ï¼Œä½†æˆ‘ä¾ç„¶ä¸ç›¸ä¿¡æµè§ˆå™¨ä¸­çš„ SPAs å¯ä»¥æ›¿æ¢æ¡Œé¢çš„åº”ç”¨â€¦â€¦å¯¹ä¸
èµ·äº† Gmailã€‚æˆ‘ä¹Ÿè¶Šæ¥è¶Šä¸å–œæ¬¢ Google äº†ã€‚

Mailspring æ˜¯è¿‘åå¹´çš„æ¡Œé¢å®¢æˆ·ç«¯ï¼Œè½»é‡çš„ electron (å¤§æ¦‚æ˜¯å› ä¸ºåç«¯æ˜¯ =C= ï¼Œåº”è¯¥å¾ˆæœ‰
å¸®åŠ©)ï¼Œä½†æ˜¯æˆ‘æ€€å¿µ Emacsã€‚

=Notmuch= çœ‹èµ·æ¥æŒºä¸é”™ï¼Œæˆ‘å¬è¯´è¿‡å…³äºå®ƒçš„ä¸€äº›æ­£é¢è¯„ä»·ï¼Œä¸è¿‡å®ƒä¼¼ä¹æ²¡æœ‰å¯¹é‚®ä»¶æœ¬èº«æœ‰ä»»ä½•
ä¿®æ”¹ã€‚æ‰€æœ‰æ•°æ®éƒ½å­˜å‚¨åœ¨ Notmuch çš„æ•°æ®åº“ä¸­ã€‚è¿™æ˜¯ä¸€ä¸ªå¾ˆæœ‰æ„æ€çš„æ¨¡å‹ï¼Œå¶å°”æˆ‘éœ€è¦å°†é‚®ä»¶
æ‹‰å–åˆ°æˆ‘çš„æ‰‹æœºä¸Šï¼Œå› æ­¤æˆ‘ä¸å¸Œæœ›å°†æ ‡ç­¾æˆ–æ–‡ä»¶å¤¹åº”ç”¨åœ¨é‚®ä»¶ä¸Š --- ä¸å­˜å‚¨åœ¨æ•°æ®åº“ä¸­ã€‚

å¦ä¸€æ–¹é¢ï¼ŒMu4e ä¹Ÿæœ‰å¾ˆå¤šç§¯æçš„è®¨è®ºï¼Œå¹¶ä¸”ä¼¼ä¹æœ‰ä¸€ä¸ªå¼ºå¤§çš„åŠŸèƒ½ --- ä¿®æ”¹é‚®ä»¶æœ¬èº« (æ— éœ€è®¿
é—®æ•°æ®åº“)ã€‚ =Mu4e= åŒæ ·ä¹Ÿæ‹¥æœ‰å¤§é‡çš„ç”¨æˆ·ç¾¤ä½“ï¼Œå¾€å¾€ä¸æ›´å¥½çš„æ”¯æŒä¸å…³æ³¨æœ‰å…³ã€‚

å½“é‡‡ç”¨æºç å®‰è£… mu4e æ—¶ï¼Œæˆ‘éœ€è¦å°† =/usr/local/= åŠ å…¥åŠ è½½ç›®å½•ä»¥ä¾¿è®© mu4e å¯ä»¥åŠ è½½ã€‚

#+begin_src emacs-lisp :tangle (if (file-directory-p "/usr/local/share/emacs/site-lisp/mu4e") "yes" "no")
(add-to-list 'load-path "/usr/local/share/emacs/site-lisp/mu4e")
#+end_src

ä¹Ÿæœ‰å¯èƒ½éœ€è¦ =/usr/share/= ç›®å½•ã€‚
#+begin_src emacs-lisp :tangle (if (file-directory-p "/usr/share/emacs/site-lisp/mu4e") "yes" "no")
(add-to-list 'load-path "/usr/share/emacs/site-lisp/mu4e")
#+end_src

æŠŠæ‰€æœ‰ emacs ä»£ç éƒ½æ”¾åœ¨ src_elisp{(after! ...)} å—ä¸­ã€‚
#+begin_src emacs-lisp :noweb no-export :tangle yes :noweb-ref nil
(after! mu4e
  <<mu4e-conf>>
)
#+end_src

**** æµè§ˆé‚®ä»¶
ä½¿ç”¨ Gnus çš„æ–‡ç« è§†å›¾ (ä¾‹å¦‚å†…è”å›¾åƒ) ä¼¼ä¹æœ‰ä¸€äº›ä¼˜åŠ¿ï¼Œä» [[https://github.com/djcb/mu/pull/1442#issuecomment-591695814][djcb/mu!1442 (è¯„è®º)]] æ¥
çœ‹ï¼Œè¿™ä¼¼ä¹æ˜¯ mu4e çš„ ã€Œæœªæ¥ä¹‹è·¯ã€ã€‚

æœ‰ä¸€äº› all-the-icons å­—ä½“ç›¸å…³çš„é—®é¢˜ï¼Œæ‰€ä»¥æˆ‘ä»¬éœ€è¦é‡æ–°å®šä¹‰èŠ±å“¨çš„å­—ç¬¦ï¼Œå¹¶ç¡®ä¿å®ƒä»¬æ˜¯
æ­£ç¡®çš„å®½åº¦ã€‚

ç”±äºæ¯ä¸ªæ ‡å¿—å­—ç¬¦çš„å®½åº¦å¢åŠ ï¼Œå¹¶è¿›è¡Œæ›´å¤šçš„è§†è§‰è°ƒæ•´ï¼Œæˆ‘ä»¬å°†å¯¹æ ‡é¢˜è¿›è¡Œä¸€äº›è°ƒæ•´ã€‚

#+begin_src emacs-lisp
(setq mu4e-headers-fields
      '((:flags . 6)
        (:account-stripe . 2)
        (:from-or-to . 25)
        (:folder . 10)
        (:recipnum . 2)
        (:subject . 80)
        (:human-date . 8))
      +mu4e-min-header-frame-width 142
      mu4e-headers-date-format "%d/%m/%y"
      mu4e-headers-time-format "â§– %H:%M"
      mu4e-headers-results-limit 1000
      mu4e-index-cleanup t)

(add-to-list 'mu4e-bookmarks
             '(:name "Yesterday's messages" :query "date:2d..1d" :key ?y) t)

(defvar +mu4e-header--folder-colors nil)
(appendq! mu4e-header-info-custom
          '((:folder .
             (:name "Folder" :shortname "Folder" :help "Lowest level folder" :function
              (lambda (msg)
                (+mu4e-colorize-str
                 (replace-regexp-in-string "\\`.*/" "" (mu4e-message-field msg :maildir))
                 '+mu4e-header--folder-colors))))))
#+end_src

æˆ‘ä»¬è¿˜å°†ä½¿ç”¨æ›´å¥½çš„è­¦æŠ¥å›¾æ ‡
#+begin_src emacs-lisp
(setq mu4e-alert-icon "/usr/share/icons/Papirus/64x64/apps/evolution.svg")
#+end_src
**** å‘é€é‚®ä»¶
å¼€å§‹å‘é€é‚®ä»¶ï¼
#+begin_src emacs-lisp
(setq sendmail-program "/usr/bin/msmtp"
      send-mail-function #'smtpmail-send-it
      message-sendmail-f-is-evil t
      message-sendmail-extra-arguments '("--read-envelope-from"); , "--read-recipients")
      message-send-mail-function #'message-send-mail-with-sendmail)
#+end_src

é¿å…æ„å¤–ä½¿ç”¨é”™è¯¯çš„å¸æˆ·å‘é€ç”µå­é‚®ä»¶ã€‚å¦‚æœæˆ‘ä»¬å¯ä»¥ä» "æ”¶ä»¶äºº" å­—æ®µä¸­çš„åœ°å€å‘é€ï¼Œè®©æˆ‘
ä»¬è¿™æ ·åšï¼Œå¦åˆ™æ˜æ™ºçš„åšæ³•æ˜¯æ‰“å¼€æç¤ºã€‚

æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ desktop æ–‡ä»¶å°† Emacs æ³¨å†Œä¸ºæ½œåœ¨çš„ç”µå­é‚®ä»¶å®¢æˆ·ç«¯ã€‚æˆ‘ä»¬å¯ä»¥åœ¨ =Exec=
å­—æ®µä¸­æ”¾ç½®ä¸€ä¸ª =emacsclient ...= æ¡ç›®ï¼Œä½†æˆ‘å‘ç°è¿™æœ‰ç‚¹ä¸é è°±ã€‚ç›¸åï¼Œè®©æˆ‘ä»¬å°†
=emacslient= è¡Œä¸ºæ‰“åŒ…åˆ°ä¸€ä¸ªå°çš„å¯æ‰§è¡Œæ–‡ä»¶ =~/.local/bin/emacsmail= ä¸­ã€‚

#+begin_src shell :tangle ~/.local/bin/emacsmail :shebang "#!/usr/bin/env sh" :mkdirp yes :tangle-mode (identity #o755) :comments no
emacsclient -create-frame --alternate-editor='' --no-wait --eval \
"(progn (x-focus-frame nil) (mu4e-compose-from-mailto \"$1\" t))"
#+end_src

ç°åœ¨æˆ‘ä»¬å¯ä»¥åœ¨ desktop æ–‡ä»¶ä¸­ä½¿ç”¨å®ƒã€‚

#+begin_src conf :tangle ~/.local/share/applications/emacsmail.desktop :mkdirp yes
[Desktop Entry]
Name=Mu4e
GenericName=Compose a new message with Mu4e in Emacs
Comment=Open mu4e compose window
MimeType=x-scheme-handler/mailto;
Exec=emacsmail %u
Icon=emacs
Type=Application
Terminal=false
Categories=Network;Email;
StartupWMClass=Emacs
#+end_src

æ³¨å†Œï¼
#+begin_src shell :tangle (if (or (not (executable-find "mu")) (string= (shell-command-to-string "xdg-mime query default x-scheme-handler/mailto") "emacsmail.desktop\n")) "no" "setup.sh")
update-desktop-database ~/.local/share/applications
#+end_src

å†å®šä¹‰ä¸€ä¸ª ~mu4e-compose-from-mailto~ å§ã€‚
#+begin_src emacs-lisp
(defun mu4e-compose-from-mailto (mailto-string &optional quit-frame-after)
  (require 'mu4e)
  (unless mu4e~server-props (mu4e t) (sleep-for 0.1))
  (let* ((mailto (message-parse-mailto-url mailto-string))
         (to (cdr (assoc "To" mailto)))
         (subject (or (cdr (assoc "Subject" mailto)) ""))
         (body (cdr (assoc "Body" mailto)))
         (headers (-filter (lambda (spec) (not (-contains-p '("To" "Subject" "Body") (car spec)))) mailto)))
    (when-let ((mu4e-main (get-buffer mu4e-main-buffer-name)))
      (switch-to-buffer mu4e-main))
    (mu4e~compose-mail to subject headers)
    (when body
      (goto-char (point-min))
      (if (eq major-mode 'org-msg-edit-mode)
          (org-msg-goto-body)
        (mu4e-compose-goto-bottom))
      (insert body))
    (goto-char (point-min))
    (cond ((null to) (search-forward "To: "))
          ((string= "" subject) (search-forward "Subject: "))
          (t (if (eq major-mode 'org-msg-edit-mode)
                 (org-msg-goto-body)
               (mu4e-compose-goto-bottom))))
    (font-lock-ensure)
    (when evil-normal-state-minor-mode
      (evil-append 1))
    (when quit-frame-after
      (add-hook 'kill-buffer-hook
                `(lambda ()
                   (when (eq (selected-frame) ,(selected-frame))
                     (delete-frame)))))))
#+end_src

åœ¨èµ·è‰æ—¶æ›´æ”¹é¢„å¡« =From:= çš„åç§°ä¹Ÿå¾ˆå¥½ã€‚
#+begin_src emacs-lisp
(defvar mu4e-from-name "Timothy"
  "Name used in \"From:\" template.")
(defadvice! mu4e~draft-from-construct-renamed (orig-fn)
  "Wrap `mu4e~draft-from-construct-renamed' to change the name."
  :around #'mu4e~draft-from-construct
  (let ((user-full-name mu4e-from-name))
    (funcall orig-fn)))
#+end_src

ä½¿ç”¨ç­¾åä¹Ÿä¸é”™ï¼Œ
#+begin_src emacs-lisp
(setq message-signature mu4e-from-name)
#+end_src
**** åœ¨ Org mailing åˆ—è¡¨ä¸‹å·¥ä½œ
***** æ·»åŠ  =X-Woof= å¤´
æˆ‘åœ¨ Org é‚®ä»¶åˆ—è¡¨ (ML) ä¸Šç›¸å½“æ´»è·ƒã€‚ Org ML æœ‰ä¸€ä¸ªé“¾æ¥çš„é”™è¯¯/è¡¥ä¸è·Ÿè¸ªå™¨ï¼Œ
https://updates.orgmode.org/ ç”± Woof ç®¡ç†ã€‚ä½†æ˜¯ï¼Œæˆ‘è§‰å¾—æˆ‘èŠ±äº†å¤ªå¤šæ—¶é—´æ¥æŸ¥
æ‰¾åˆé€‚çš„æ ‡å¤´ä»¥æ›´æ–°é”™è¯¯å’Œè¡¥ä¸çš„çŠ¶æ€ã€‚æˆ‘éœ€è¦çš„æ˜¯æŸç§æ–¹ä¾¿çš„å·¥å…·ã€‚è®©æˆ‘ä»¬å†™ä¸€ä¸ªã€‚

é¦–å…ˆï¼Œä¸€ä¸ªå‡½æ•°è¯¢é—®æˆ‘æƒ³è¦åšä»€ä¹ˆå¹¶è¿”å›é€‚å½“çš„ X-Woof æ ‡å¤´ã€‚
#+begin_src emacs-lisp
(defun +mu4e-get-woof-header ()
  (pcase (read-char
          (format "\
%s
  %s Declare %s Applied %s Aborted
%s
  %s Confirm %s Fixed
%s
  %s Request %s Resolved

%s remove X-Woof header"
                  (propertize "Patch" 'face 'outline-3)
                  (propertize "p" 'face '(bold consult-key))
                  (propertize "a" 'face '(bold consult-key))
                  (propertize "c" 'face '(bold consult-key))
                  (propertize "Bug" 'face 'outline-3)
                  (propertize "b" 'face '(bold consult-key))
                  (propertize "f" 'face '(bold consult-key))
                  (propertize "Help" 'face 'outline-3)
                  (propertize "h" 'face '(bold consult-key))
                  (propertize "r" 'face '(bold consult-key))
                  (propertize "x" 'face '(bold error))))
    (?p "X-Woof-Patch: confirmed")
    (?a "X-Woof-Patch: applied")
    (?c "X-Woof-Patch: cancelled")
    (?b "X-Woof-Bug: confirmed")
    (?f "X-Woof-Bug: fixed")
    (?h "X-Woof-Help: confirmed")
    (?r "X-Woof-Help: cancelled")
    (?x 'delete)))
#+end_src

ç°åœ¨æˆ‘ä»¬åªéœ€è¦ä¸€ä¸ªå°†è¿™æ ·çš„å¤´æ·»åŠ åˆ°ç¼“å†²åŒºçš„å‡½æ•°ä¸­
#+begin_src emacs-lisp
(defun +mu4e-insert-woof-header ()
  "Insert an X-Woof header into the current message."
  (interactive)
  (when-let ((header (+mu4e-get-woof-header)))
    (save-excursion
      (goto-char (point-min))
      (search-forward "--text follows this line--")
      (unless (eq header 'delete)
        (beginning-of-line)
        (insert header "\n")
        (forward-line -1))
      (when (re-search-backward "^X-Woof-" nil t)
        (kill-whole-line)))))

(map! :map mu4e-compose-mode-map
      :localleader
      :desc "Insert X-Woof Header" "w" #'+mu4e-insert-woof-header)

(map! :map org-msg-edit-mode-map
      :after org-msg
      :localleader
      :desc "Insert X-Woof Header" "w" #'+mu4e-insert-woof-header)
#+end_src

å¤ªæ£’äº†ï¼è¿™åº”è¯¥ä½¿æ·»åŠ è¿™äº›å¤´å˜å¾—è½»è€Œæ˜“ä¸¾ã€‚

***** ä¿®æ­£å·¥ä½œæµ

ä» ML æµ‹è¯•è¡¥ä¸ç›®å‰æ¯”å®ƒéœ€è¦çš„æ›´éº»çƒ¦ã€‚è®©æˆ‘ä»¬æ”¹å˜å®ƒã€‚
#+begin_src emacs-lisp
(after! mu4e
  (defvar +org-ml-target-dir "~/.emacs.d/.local/straight/repos/org-mode/")
  (defvar +org-ml-max-age 600
    "Maximum permissible age in seconds.")
  (defvar +org-ml--cache-timestamp 0)
  (defvar +org-ml--cache nil)

  (define-minor-mode +org-ml-patchy-mood-mode
    "Apply patches to Org in bulk."
    :global t
    (let ((action (cons "apply patch to org" #'+org-ml-apply-patch)))
      (if +org-ml-patchy-mood-mode
          (add-to-list 'mu4e-view-actions action)
        (setq mu4e-view-actions (delete action mu4e-view-actions)))))

  (defun +org-ml-apply-patch (msg)
    "Apply the patch in the current message to Org."
    (interactive)
    (unless msg (setq msg (mu4e-message-at-point)))
    (with-current-buffer (get-buffer-create "*Shell: Org apply patches*")
      (erase-buffer)
      (let* ((default-directory +org-ml-target-dir)
             (exit-code (call-process "git" nil t nil "am" (mu4e-message-field msg :path))))
        (magit-refresh)
        (when (not (= 0 exit-code))
          (+popup/buffer)))))

  (defun +org-ml-current-patches ()
    "Get the currently open patches, as a list of alists.
Entries of the form (subject . id)."
    (delq nil
          (mapcar
           (lambda (entry)
             (unless (plist-get entry :fixed)
               (cons
                (format "%-8s  %s"
                        (propertize
                         (replace-regexp-in-string "T.*" ""
                                                   (plist-get entry :date))
                         'face 'font-lock-doc-face)
                        (propertize
                         (replace-regexp-in-string "\\[PATCH\\] ?" ""
                                                   (plist-get entry :summary))
                         'face 'font-lock-keyword-face))
                (plist-get entry :id))))
           (with-current-buffer (url-retrieve-synchronously "https://updates.orgmode.org/data/patches")
             (goto-char url-http-end-of-headers)
             (json-parse-buffer :object-type 'plist)))))

  (defun +org-ml-select-patch-thread ()
    "Find and apply a proposed Org patch."
    (interactive)
    (let* ((current-workspace (+workspace-current))
           (patches (progn
                      (when (or (not +org-ml--cache)
                                (> (- (float-time) +org-ml--cache-timestamp)
                                   +org-ml-max-age))
                        (setq +org-ml--cache (+org-ml-current-patches)
                              +org-ml--cache-timestamp (float-time)))
                      +org-ml--cache))
           (msg-id (cdr (assoc (completing-read
                                "Thread: " (mapcar #'car patches))
                               patches))))
      (+workspace-switch +mu4e-workspace-name)
      (mu4e-view-message-with-message-id msg-id)
      (unless +org-ml-patchy-mood-mode
        (add-to-list 'mu4e-view-actions
                     (cons "apply patch to org" #'+org-ml-transient-mu4e-action)))))

  (defun +org-ml-transient-mu4e-action (msg)
    (setq mu4e-view-actions
          (delete (cons "apply patch to org" #'+org-ml-transient-mu4e-action)
                  mu4e-view-actions))
    (+workspace/other)
    (magit-status +org-ml-target-dir)
    (+org-ml-apply-patch msg)))
#+end_src

***** é‚®ä»¶åˆ—è¡¨å½’æ¡£é“¾æ¥
å¦ä¸€ä»¶å®¹æ˜“åšåˆ°çš„äº‹æƒ…æ˜¯åœ¨ https://list.orgmode.org ä¸Šè·å–æŒ‡å‘å½“å‰æ¶ˆæ¯çš„é“¾æ¥ã€‚

#+begin_src emacs-lisp
(after! mu4e
  (defun +mu4e-ml-message-link (msg)
    "Copy the link to MSG on the mailing list archives."
    (let* ((list-addr (or (mu4e-message-field msg :mailing-list)
                          (cdar (mu4e-message-field-raw msg :list-post))
                          (thread-last (append (mu4e-message-field msg :to)
                                               (mu4e-message-field msg :cc))
                            (mapcar #'last)
                            (mapcar #'cdr)
                            (mapcar (lambda (addr)
                                      (when (string-match-p "emacs.*@gnu\\.org$" addr)
                                        (replace-regexp-in-string "@" "." addr))))
                            (delq nil)
                            (car))))
           (msg-url
            (cond
             ((string= "emacs-orgmode.gnu.org" list-addr)
              (format "https://list.orgmode.org/%s" (mu4e-message-field msg :message-id)))
             (t (user-error "Mailing list %s not supported" list-addr)))))
      (message "Link %s copied to clipboard" (gui-select-text msg-url))
      msg-url))

  (add-to-list 'mu4e-view-actions (cons "link to message ML" #'+mu4e-ml-message-link) t))
#+end_src

ä»¥ç±»ä¼¼çš„æ–¹å¼ï¼Œå½“å•å‡»æ­¤ç±»é“¾æ¥æ—¶ (ä¾‹å¦‚ï¼Œå½“æœ‰äººä½¿ç”¨æŒ‡å‘å­˜æ¡£çš„é“¾æ¥æ¥å¼•ç”¨è¾ƒæ—©çš„ç”µå­é‚®
ä»¶æ—¶) æˆ‘æ›´æ„¿æ„åœ¨ mu4e ä¸­æŸ¥çœ‹å®ƒã€‚

#+begin_src emacs-lisp
(defun +browse-url-orgmode-ml (url &optional _)
  "Open an orgmode list url using notmuch."
  (let ((id (and (or (string-match "^https?://orgmode\\.org/list/\\([^/]+\\)" url)
                     (string-match "^https?://list\\.orgmode\\.org/\\([^/]+\\)" url))
                 (match-string 1 url))))
    (mu4e-view-message-with-message-id id)))

(add-to-list 'browse-url-handlers (cons "^https?://orgmode\\.org/list" #'+browse-url-orgmode-ml))
(add-to-list 'browse-url-handlers (cons "^https?://list\\.orgmode\\.org/" #'+browse-url-orgmode-ml))
#+end_src

*** Org Msg
Doom åœ¨é»˜è®¤æƒ…å†µä¸‹åšäº†å¾ˆæ£’çš„äº‹æƒ…ï¼Œæ‰€ä»¥æˆ‘ä»¬åªåšä¸€äº›å°çš„è°ƒæ•´ã€‚
#+begin_src emacs-lisp
(setq +org-msg-accent-color "#1a5fb4"
      org-msg-greeting-fmt "\nHi%s,\n\n"
      org-msg-signature "\n\n#+begin_signature\nAll the best,\\\\\n@@html:<b>@@Timothy@@html:</b>@@\n#+end_signature")
(map! :map org-msg-edit-mode-map
      :after org-msg
      :n "G" #'org-msg-goto-body)
#+end_src

* ç¼–ç¨‹è¯­è¨€é…ç½®
** é€šç”¨é…ç½®
*** æ–‡ä»¶æ¨¡æ¿
æŸäº›æ–‡ä»¶ç±»å‹ï¼Œæˆ‘ä»¬å¯ä»¥é‡è½½å…¶ [[file:./snippets][snippets]] ç›®å½•çš„é»˜è®¤å€¼ï¼Œè€Œå…¶ä»–æ–‡ä»¶ç±»å‹åˆ™éœ€è¦åˆ†é…æ¨¡æ¿ã€‚
#+begin_src emacs-lisp
(set-file-template! "\\.tex$" :trigger "__" :mode 'latex-mode)
(set-file-template! "\\.org$" :trigger "__" :mode 'org-mode)
(set-file-template! "/LICEN[CS]E$" :trigger '+file-templates/insert-license)
#+end_src

** çº¯æ–‡æœ¬
Emacs å¯ä»¥æ˜¾ç¤º ANSI é¢œè‰²ä»£ç ã€‚ç„¶è€Œï¼Œåœ¨ Emacs 28 ä¹‹å‰ï¼Œå¦‚æœä¸ä¿®æ”¹ç¼“å†²åŒºæ˜¯ä¸å¯èƒ½åšåˆ°è¿™
ä¸€ç‚¹çš„ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬ä»¥æ­¤ä¸ºåŸºç¡€è®¾ç½®è¿™ä¸ªå—ã€‚
#+begin_src emacs-lisp :tangle (if (>= emacs-major-version 28) "yes" "no")
(after! text-mode
  (add-hook! 'text-mode-hook
             ;; Apply ANSI color codes
             (with-silent-modifications
               (ansi-color-apply-on-region (point-min) (point-max) t))))
#+end_src

** Org
:PROPERTIES:
:CUSTOM_ID: org
:header-args:emacs-lisp: :tangle no :noweb-ref org-conf
:END:
:intro:
æˆ‘çœŸçš„éå¸¸å–œæ¬¢ org modeï¼Œæˆ‘è€ƒè™‘è¿‡ä¸ºä»€ä¹ˆï¼Œä¸‹é¢å°±æ˜¯æˆ‘è€ƒè™‘çš„ç»“æœã€‚

#+attr_latex: :align *{8}{p{0.105\linewidth}} :font \small
#+plot: transpose:yes type:radar min:0 max:4 file:"misc/document-format-comparison.svg"
| æ ¼å¼              | ç»†ç²’åº¦æ§åˆ¶ | ä¸Šæ‰‹æ˜“ç”¨æ€§ | è¯­æ³•ç®€å• | ç¼–è¾‘å™¨æ”¯æŒ | é›†æˆåº¦ | æ˜“äºå‚è€ƒ | å¤šåŠŸèƒ½æ€§ |
|-------------------+------------+------------+----------+------------+--------+----------+----------|
| Word              |          2 |          4 |        4 |          2 |      3 |        2 |        2 |
| LaTeX             |          4 |          1 |        1 |          3 |      2 |        4 |        3 |
| Org Mode          |          4 |          2 |      3.5 |          1 |      4 |        4 |        4 |
| Markdown          |          1 |          3 |        3 |          4 |      3 |        3 |        1 |
| Markdown + Pandoc |        2.5 |        2.5 |      2.5 |          3 |      3 |        3 |        2 |

#+attr_html: :class invertible :alt Radar chart comparing my opinions of document formats.
#+attr_latex: :options inkscapelatex=false
[[file:misc/document-format-comparison.svg]]

é™¤äº†æ ‡è®°è¯­è¨€ (Markup Language) çš„ä¼˜é›…å¤–ï¼ŒEmacs é›†æˆäº†ä¸€åœºä¸°å¯Œçš„æœ‰è¶£çš„åŠŸèƒ½ï¼Œæ¯”å¦‚è¯´å¯¹
å½“å‰ä»»ä½•æŠ€æœ¯éƒ½æ”¯æŒçš„æ–‡å­¦ç¼–ç¨‹ã€‚
#+name: Literate programming workflow
#+attr_html: :style line-height:1.13;
#+begin_example
      â•­â”€â•´Codeâ•¶â”€â•®            â•­â”€â•´Raw Codeâ•¶â”€â–¶ Computer
Ideasâ•ºâ”¥        â”â”â–¶ Org Modeâ•ºâ”¥
      â•°â”€â•´Textâ•¶â”€â•¯            â•°â”€â•´Documentâ•¶â”€â–¶ People
#+end_example

åœ¨ =.org= æ–‡ä»¶å¯ä»¥åŒ…å«ä»£ç å— (ä¸æ”¯æŒ noweb æ¨¡æ¿)ï¼Œè¿™äº›ä»£ç å—å¯ä»¥ä¸ä¸“ç”¨æºä»£ç æ–‡ä»¶çº ç¼ 
åœ¨ä¸€èµ·ï¼Œå¹¶é€šè¿‡å„ç§ (å¯æ‰©å±•çš„) æ–¹æ³•ç¼–è¯‘æˆæ–‡æ¡£ (æŠ¥å‘Šã€æ–‡æ¡£ã€æ¼”ç¤ºæ–‡ç¨¿ç­‰)ã€‚è¿™äº›æºå—ç”šè‡³å¯ä»¥
åˆ›å»ºè¦åŒ…å«åœ¨æ–‡æ¡£ä¸­çš„å›¾åƒæˆ–å…¶ä»–å†…å®¹ï¼Œæˆ–è€…ç”Ÿæˆæºä»£ç ã€‚

#+name: Example Org Flowchart
#+attr_html: :style line-height:1.13;
#+begin_example
                   â•­â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .pdf â«
                  pdfLaTeX â–¶â•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•®                 âª
                   â•¿     â•¿                  â”Š                 âª
                   â”‚     â”Š                  â”Š                 âª
                 .tex    â”Š                  â”Š                 âª
                   â•¿     â”Š                  â”Š                 âª
                â•­â”€â”€â”´â•Œâ•Œâ•®  â”Š                  â”Š style.scss      â¬ Weaving
graphc.png â”€â•®   â”‚  embedded TeX             â”Š      â•½          âª (Documents)
image.jpeg â”€â”¤ filters   â•¿                   â”Š    .css         âª
            â•     â•¿     â”Š                   â”Š     â–¾â•          âª
figure.pngâ•¶â”€â•§â”€â–¶ PROJECT.ORG â–¶â”€â”€â”€â•´filtersâ•¶â”€â”€â”€â•§â”€â”€â”€â”€â”€â”€â•ªâ”€â”€â–¶ .html âª
     â•¿           â•¿â”Š â•‘ â”‚ â•°â•Œâ•Œâ•Œâ–·â•Œâ•Œ embedded html â–¶â•Œâ•Œâ•Œâ•Œâ•¯          âª
     â”œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ–·â•Œâ•Œâ•Œâ•¯â”Š â•‘ â”‚                                       âª
    resultâ•¶â•Œâ•Œâ•Œâ•Œâ•Œâ•® â”Š â•‘ â”œâ”€â”€â”€â”€â”€â”€â•´filtersâ•¶â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .txt  âª
     â”Šâ–´         â”Š â”Š â•‘ â”‚                                       âª
    execution   â”Š â”Š â•‘ â•°â”€â”€â”€â”€â”€â”€â•´filtersâ•¶â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .md   â­
     â”Šâ–´         â”Š â”Š â•‘
    code blocksâ—€â•¯ â”Š â•Ÿâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .c    â«
     â•°â•Œâ•Œâ•Œâ•Œâ—â•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•Œâ•¯ â•Ÿâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .sh   â¬ Tangling
                    â•Ÿâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .hs   âª (Code)
                    â•™â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â–¶ .el   â­
#+end_example
:end:

å› ä¸ºè¿™éƒ¨åˆ†åˆå§‹åŒ–æ—¶ç›¸å½“è´¹æ—¶ï¼Œæˆ‘ä»¬éœ€è¦å°†å…¶æ”¾åœ¨ src_elisp{(after! ...)} ä¸­ã€‚
#+begin_src emacs-lisp :noweb no-export :tangle yes :noweb-ref nil
(after! org
  <<org-conf>>
)
#+end_src
*** System config
**** Mime types
é»˜è®¤æƒ…å†µä¸‹ï¼ŒOrg æ¨¡å¼ä¸ä¼šè¢«è¯†åˆ«ä¸ºå®ƒè‡ªå·±çš„ MIME ç±»å‹ï¼Œä½†å¯ä»¥ä½¿ç”¨ä»¥ä¸‹æ–‡ä»¶è½»æ¾æ›´æ”¹ã€‚ å¯¹äºç³»ç»ŸèŒƒå›´çš„æ›´æ”¹ï¼Œè¯·å°è¯• ~/usr/share/mime/packages/org.xml~ ã€‚
#+begin_src xml :tangle ~/.local/share/mime/packages/org.xml :mkdirp yes :comments no
<mime-info xmlns='http://www.freedesktop.org/standards/shared-mime-info'>
  <mime-type type="text/org">
    <comment>Emacs Org-mode File</comment>
    <glob pattern="*.org"/>
    <alias type="text/org"/>
  </mime-type>
</mime-info>
#+end_src
å¥½åœ¨ Papirus [[https://github.com/PapirusDevelopmentTeam/papirus-icon-theme/commit/a10fb7f2423d5e30b9c4477416ccdc93c4f3849d][ç°åœ¨]] æœ‰ä¸€ä¸ª =text/org= çš„å›¾æ ‡ã€‚ åªéœ€åˆ·æ–°ä»–ä»¬çš„ mime æ•°æ®åº“ã€‚
#+begin_src shell :tangle (if (string= (shell-command-to-string "xdg-mime query default text/org") "") "setup.sh" "no")
update-mime-database ~/.local/share/mime
#+end_src
ç°åœ¨è®¾ç½® emacs ä¸ºé»˜è®¤çš„ç¼–è¾‘å™¨ã€‚
#+begin_src shell :tangle (if (string= (shell-command-to-string "xdg-mime query default text/org") "emacs-client.desktop\n") "no" "setup.sh")
xdg-mime default emacs.desktop text/org
#+end_src
**** Git diffs
Protesilaos å†™äº†ä¸€ç¯‡ [[https://protesilaos.com/codelog/2021-01-26-git-diff-hunk-elisp-org/][éå¸¸æœ‰ç”¨çš„æ–‡ç« ]]ï¼Œä»–åœ¨å…¶ä¸­è§£é‡Šäº†å¦‚ä½•å°† git diff å—æ ‡é¢˜æ›´æ”¹ä¸º
æ¯”å¤§å—ä¸Šæ–¹çš„ç›´æ¥è¡Œæ›´æœ‰ç”¨çš„ä¸œè¥¿ --- å°±åƒçˆ¶æ ‡é¢˜ä¸€æ ·ã€‚

è¿™å¯ä»¥é€šè¿‡é¦–å…ˆåœ¨ =~/.config/git/attributes= ä¸­ä¸º git æ·»åŠ æ–°çš„å·®å¼‚æ¨¡å¼æ¥å®ç°ã€‚
#+begin_src fundamental
,*.org   diff=org
#+end_src

ç„¶åä¸ºå®ƒæ·»åŠ ä¸€ä¸ªæ­£åˆ™è¡¨è¾¾å¼åˆ° =~/.config/git/config= ã€‚
#+begin_src gitconfig
[diff "org"]
  xfuncname = "^(\\*+ +.*)$"
#+end_src

*** åŒ…
:PROPERTIES:
:header-args:emacs-lisp: :tangle packages.el :comments no
:END:
**** Org itself
å®é™…ä¸Šï¼Œæˆ‘å¯èƒ½æƒ³å°†ä¸‰ç§å¯èƒ½çš„åŒ…è¯­å¥ç”¨äº Orgã€‚

å¦‚æœæˆ‘åœ¨ä¸€å°å¯ä»¥æ¨é€ä¿®æ”¹çš„æœºå™¨ä¸Šï¼Œæˆ‘å¸Œæœ›å¯ä»¥åœ¨å¼€å‘ä¸­ç”¨ä¸Š Emacsã€‚æˆ‘éœ€è¦æ£€æŸ¥ SSH
key =~/.ssh/id_ed25519.pub= çš„å†…å®¹æ¥ç¡®è®¤ã€‚
1. å¦‚æœ key å­˜åœ¨ä¸” =$straight-base-dir/straight/repos/org-mode= æ²¡æœ‰è®¾ç½®
   æ­£ç¡®çš„è¿œç¨‹ä»“åº“ï¼Œæˆ‘ä»¬åº”è¯¥å®‰è£…å®ƒã€‚
2. å¦‚æœ key å­˜åœ¨ä¸”è®¾ç½®äº†æ­£ç¡®çš„è¿œç¨‹ä»“åº“ï¼Œç›´æ¥å¿½ç•¥å®ƒ
3. å¦‚æœå¯†é’¥ä¸å­˜åœ¨ï¼Œåˆ™åº”è¯¥ä½¿ç”¨ Org çš„ ~HEAD~ ã€‚

ä¸ºäº†æ­£ç¡®è§£é‡Šè¿™ç§æƒ…å†µï¼Œæˆ‘ä»¬éœ€è¦ä¸€ä¸ªç®€çŸ­çš„è„šæœ¬æ¥ç¡®å®šæ‰€éœ€çš„æ­£ç¡®åŒ…è¯­å¥ã€‚
#+name: org-pkg-statement
#+begin_src emacs-lisp :tangle no
(load (expand-file-name "core/core.el" user-emacs-directory) nil t) ; for `doom-local-dir'
(let ((dev-key (and (file-exists-p "~/.ssh/id_ed25519.pub")
                    (= 0 (shell-command "cat ~/.ssh/id_ed25519.pub | grep -q AAAAC3NzaC1lZDI1NTE5AAAAIOZZqcJOLdN+QFHKyW8ST2zz750+8TdvO9IT5geXpQVt"))))
      (dev-pkg (let ((default-directory (expand-file-name "straight/repos/org-mode" doom-local-dir)))
                 (and (file-exists-p default-directory)
                      (string= "tec@git.savannah.gnu.org:/srv/git/emacs/org-mode.git\n" (shell-command-to-string "git remote get-url origin")))))
      (recipe-common '(:files ("*.el" "lisp/*.el" "etc")
                       :pre-build
                       (with-temp-file (doom-path (straight--repos-dir "org-mode") "org-version.el")
                         (insert "(fset 'org-release (lambda () \"9.5\"))\n"
                                 (format "(fset 'org-git-version (lambda () \"%s\"))\n"
                                         (substring (shell-command-to-string "git rev-parse --short HEAD") 0 -1))
                                 "(provide 'org-version)\n"))
                       :includes org)))
  (prin1-to-string
   `(package! org-mode
      :recipe (,@(cond ((and dev-key dev-pkg)
                        (list :host nil :repo nil :local-repo (expand-file-name "straight/repos/org-mode" doom-local-dir)))
                       (dev-key
                        (list :host nil :repo "tec@git.savannah.gnu.org:/srv/git/emacs/org-mode.git"))
                       (t
                        (list :host 'github :repo "emacs-straight/org-mode")))
               ,@recipe-common)
      :pin nil)))
#+end_src

#+begin_src emacs-lisp :noweb no-export
<<org-pkg-statement()>>
(unpin! org-mode) ; there be bugs
(package! org-contrib
  :recipe (:host nil :repo "https://git.sr.ht/~bzg/org-contrib"
           :files ("lisp/*.el"))
  :pin "8c138dc5375dc13a0d1952e61c638ef4d57df368")
#+end_src

**** è§†è§‰
***** è¡¨

Org è¡¨çœ‹èµ·æ¥å¹¶ä¸æ˜¯å¾ˆæ¼‚äº®ã€‚è¿™ä¸ªåŒ…å¯ä»¥æ”¯æŒç¼“å†²åŒºä¸­ç”¨ç”»æ¡†å­—ç¬¦é‡ç»˜å®ƒä»¬ã€‚è¿™å¬èµ·æ¥æ˜¯ä¸€
ç§è¿›æ­¥ï¼Œå°±åœ¨ =writeroom-mode= ä¸­å¯ç”¨å®ƒå§ã€‚

#+begin_src emacs-lisp
(package! org-pretty-table
  :recipe (:host github :repo "Fuco1/org-pretty-table") :pin "7bd68b420d3402826fea16ee5099d04aa9879b78")
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! org-pretty-table
  :commands (org-pretty-table-mode global-org-pretty-table-mode))
#+end_src

***** å¼ºè°ƒæ ‡è®°
~org-hide-emphasis-markers~!æ£’ï¼æœ‰æ—¶åœ¨è¾¹ç•Œä¸Šçš„ç¼–è¾‘ä¼šå¯¼è‡´å˜å¾—éº»çƒ¦ã€‚æˆ‘ä»¬å¯ä»¥åœ¨
ä¸ç‰ºç‰²è§†è§‰ä¾¿åˆ©æ€§çš„æ¡ä»¶ä¸‹ï¼Œä½¿ç”¨åŒ… =org-appear= æ”¹å–„è¿™ç§æƒ…å†µã€‚
#+begin_src emacs-lisp
(package! org-appear :recipe (:host github :repo "awth13/org-appear")
  :pin "303fcc8d5d85a4ebff2798dab50b2ccc0255ea5f")
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autoemphasis t
        org-appear-autosubmarkers t
        org-appear-autolinks nil)
  ;; for proper first-time setup, `org-appear--set-elements'
  ;; needs to be run after other hooks have acted.
  (run-at-time nil nil #'org-appear--set-elements))
#+end_src

***** æ ‡é¢˜ç»“æ„
è¯´èµ·æ ‡é¢˜è¡Œï¼Œæˆ‘æ³¨æ„åˆ°äº†ä¸€ä¸ªéå¸¸æ£’çš„åŒ…ï¼Œå®ƒå¯ä»¥æµè§ˆå¹¶ç®¡ç†æ ‡é¢˜ç»“æ„ã€‚
#+begin_src emacs-lisp
(package! org-ol-tree :recipe (:host github :repo "Townk/org-ol-tree")
  :pin "207c748aa5fea8626be619e8c55bdb1c16118c25")
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! org-ol-tree
  :commands org-ol-tree)
(map! :map org-mode-map
      :after org
      :localleader
      :desc "Outline" "O" #'org-ol-tree)
#+end_src

**** é¢å¤–çš„åŠŸèƒ½

***** Org ref
éœ€è¦æ—¶ä¸æ—¶çš„å¼•ç”¨ã€‚
#+begin_src emacs-lisp :tangle no
(package! org-ref :pin "3ca9beb744621f007d932deb8a4197467012c23a")
#+end_src

***** Julia æ”¯æŒ
=ob-julia= ç°åœ¨çœ‹æ¥æœ‰ç‚¹ç®€é™‹ï¼Œä¸è¿‡è¿˜æ˜¯å¯ä»¥æœ‰æ•ˆæä¾›ç›¸å…³å†…å®¹çš„ã€‚
#+begin_src emacs-lisp
(package! ob-julia :recipe (:local-repo "lisp/ob-julia" :files ("*.el" "julia")))
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! ob-julia
  :commands org-babel-execute:julia
  :config
  (setq org-babel-julia-command-arguments
        `("--sysimage"
          ,(when-let ((img "~/.local/lib/julia.so")
                      (exists? (file-exists-p img)))
             (expand-file-name img))
          "--threads"
          ,(number-to-string (- (doom-system-cpus) 2))
          "--banner=no")))
#+end_src

***** HTTP è¯·æ±‚
åœ¨ babel ä¸­ä½¿ç”¨ HTTP è¯·æ±‚ã€‚
#+begin_src emacs-lisp
(package! ob-http :pin "b1428ea2a63bcb510e7382a1bf5fe82b19c104a7")
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! ob-http
  :commands org-babel-execute:http)
#+end_src

***** åµŒå…¥
å°†å¼€å‘ /åµŒå…¥åˆ°/ Org çœŸçš„æ˜¯å¸…çš„ä¸è¡Œã€‚
#+begin_src emacs-lisp
(package! org-transclusion :recipe (:host github :repo "nobiot/org-transclusion")
  :pin "ccc0aaa72732ea633bf52bcc8a0345cd3ac178fd")
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! org-transclusion
  :commands org-transclusion-mode
  :init
  (map! :after org :map org-mode-map
        "<f12>" #'org-transclusion-mode))
#+end_src

***** æ ‡é¢˜å›¾
å®‰è£…å®ƒï¼ŒçœŸçš„å¸…çš„ä¸è¡Œ
#+begin_src emacs-lisp
(package! org-graph-view :recipe (:host github :repo "alphapapa/org-graph-view") :pin "233c6708c1f37fc60604de49ca192497aef39757")
#+end_src

***** çƒ¹é¥ªé£Ÿè°±
åœ¨ç”Ÿæ´»ä¸­æˆ‘ *éœ€è¦* å®ƒï¼Œå®ƒä¼šåœ¨å…¬å…±ç½‘ç«™ä¸Šç»™ä½ æ‰¾åˆ°ä¸€ä¸ªèœè°±çš„é“¾æ¥ï¼Œåœ¨è¿™æ’å…¥ org-ified ç‰ˆ
æœ¬ã€‚è¿™ä¸æ˜¯ç‰¹åˆ«æ£’å˜›ã€‚

#+begin_src emacs-lisp
(package! org-chef :pin "87e9a6c4844ff32f47c8d1108ec0f087a3148a8e")
#+end_src

åœ¨ org ä¹‹ååŠ è½½ä¼¼ä¹æœ‰äº›æ—©ï¼Œæ— è®ºæ˜¯å‘½ä»¤è¿˜æ˜¯æ¨¡æ¿ï¼Œéƒ½åœ¨ç”¨çš„æ—¶å€™åœ¨åŠ è½½å§ã€‚
#+begin_src emacs-lisp :tangle yes
(use-package! org-chef
  :commands (org-chef-insert-recipe org-chef-get-recipe-from-url))
#+end_src

***** ä» pandoc å¯¼å‡º
æœ‰æ—¶æˆ‘éœ€è¦å¯¼å‡ºé org æ–‡ä»¶ï¼Œè¿™å¾ˆéš¾å—ã€‚ä¸è¿‡å¥½åœ¨æˆ‘ä»¬è¿˜æœ‰ pandoc æä¾›äº†è¿™äº›æ–¹æ³•ï¼Œè¿™ä¸ªåŒ…å¯
ä»¥è®©äº‹æƒ…å˜å¾—å¾ˆç®€å•ã€‚
#+begin_src emacs-lisp
(package! org-pandoc-import :recipe
  (:local-repo "lisp/org-pandoc-import" :files ("*.el" "filters" "preprocessors")))
#+end_src

#+begin_src emacs-lisp :tangle yes
(use-package! org-pandoc-import
  :after org)
#+end_src

***** æ–‡æ¡£æ¯”è¾ƒ
æ¯”è¾ƒ Org æ–‡ä»¶çš„æƒ³æ³•ä¸é”™ï¼Œå…¶ä¸­æ¯”è¾ƒå†…å®¹æœ€ä¸°å¯Œçš„æ˜¯ =latexdiff= ã€‚ä¸è¿‡æ¶‰åŠåˆ°äº†æ¯”è¾ƒçƒ¦äººçš„
æ­¥éª¤ï¼Œæ‰€ä»¥æˆ‘å†™äº†ä¸ªåŒ…æ¥ç²¾ç®€è¿™äº›æ­¥éª¤ã€‚

#+begin_src emacs-lisp
(package! orgdiff :recipe (:local-repo "lisp/orgdiff"))
#+end_src

ç°åœ¨å”¯ä¸€çƒ¦äººçš„æ˜¯ï¼Œ =latexdiff= ç”¨ ~#FF0000~ å’Œ ~#0000FF~ ä½œä¸ºçº¢è‰²å’Œè“è‰²ï¼Œè¿™ä¸¤ä¸ªæ”¹
å˜æ ‡è®°è‰²ã€‚è®©æˆ‘ä»¬åœ¨ =latexdiff= çš„ç»“æœä¸Šè¿›è¡Œå¤„ç†ï¼Œè®©å®ƒæ›´èˆ’æœç‚¹ã€‚
#+begin_src emacs-lisp :tangle yes
(use-package! orgdiff
  :defer t
  :config
  (defun +orgdiff-nicer-change-colours ()
    (goto-char (point-min))
    ;; Set red/blue based on whether chameleon is being used
    (if (search-forward "%% make document follow Emacs theme" nil t)
        (setq red  (substring (doom-blend 'red 'fg 0.8) 1)
              blue (substring (doom-blend 'blue 'teal 0.6) 1))
      (setq red  "c82829"
            blue "00618a"))
    (when (and (search-forward "%DIF PREAMBLE EXTENSION ADDED BY LATEXDIFF" nil t)
               (search-forward "\\RequirePackage{color}" nil t))
      (when (re-search-forward "definecolor{red}{rgb}{1,0,0}" (cdr (bounds-of-thing-at-point 'line)) t)
        (replace-match (format "definecolor{red}{HTML}{%s}" red)))
      (when (re-search-forward "definecolor{blue}{rgb}{0,0,1}" (cdr (bounds-of-thing-at-point 'line)) t)
        (replace-match (format "definecolor{blue}{HTML}{%s}" blue)))))
  (add-to-list 'orgdiff-latexdiff-postprocess-hooks #'+orgdiff-nicer-change-colours))
#+end_src

***** Org Music
é“¾æ¥åˆ°éŸ³ä¹
#+begin_src emacs-lisp
(package! org-music :recipe (:local-repo "lisp/org-music"))
#+end_src
#+begin_src emacs-lisp :tangle yes
(use-package! org-music
  :after org
  :config
  (setq org-music-mpris-player "Lollypop"
        org-music-track-search-method 'beets
        org-music-beets-db "~/Music/library.db"))
#+end_src

*** è¡Œä¸º
[[xkcd:1319]]
**** ä¿®æ”¹é»˜è®¤å€¼
#+begin_src emacs-lisp
(setq org-directory "~/.org"                      ; let's put files here
      org-use-property-inheritance t              ; it's convenient to have properties inherited
      org-log-done 'time                          ; having the time a item is done sounds convenient
      org-list-allow-alphabetical t               ; have a. A. a) A) list bullets
      org-export-in-background t                  ; run export processes in external emacs process
      org-catch-invisible-edits 'smart            ; try not to accidently do weird stuff in invisible regions
      org-export-with-sub-superscripts '{})       ; don't treat lone _ / ^ as sub/superscripts, require _{} / ^{}
#+end_src

åŒæ—¶æˆ‘ä¹Ÿå¾ˆå–œæ¬¢å°† src_elisp{:comments} ä½œä¸ºæ ‡å¤´å‚æ•°,æ‰€ä»¥æ¥è®¾ç½®ä¸€ä¸‹é»˜è®¤å€¼ã€‚
#+begin_src emacs-lisp
(setq org-babel-default-header-args
      '((:session . "none")
        (:results . "replace")
        (:exports . "code")
        (:cache . "no")
        (:noweb . "no")
        (:hlines . "no")
        (:tangle . "no")
        (:comments . "link")))
#+end_src

é»˜è®¤æƒ…å†µä¸‹ï¼Œ ~visual-line-mode~ æ˜¯ =å¼€å¯= çŠ¶æ€ï¼Œä½†æ˜¯ ~auto-fill-mode~ é€šè¿‡
hook =å…³é—­= ã€‚è¿™è®© Org-mode çš„è¡¨æ ¼å’Œå…¶ä»–çº¯æ–‡æœ¬æ–‡ä»¶ (markdown, \LaTeX) æ··æ·†ï¼Œ
æ‰€ä»¥è¦å…³é—­å®ƒï¼Œåœ¨éœ€è¦çš„æ¨¡å¼æ‰‹åŠ¨å¼€å¯å®ƒã€‚
#+begin_src emacs-lisp
(remove-hook 'text-mode-hook #'visual-line-mode)
(add-hook 'text-mode-hook #'auto-fill-mode)
#+end_src

é»˜è®¤ç»‘å®šäº† vim é£æ ¼çš„å¿«æ·é”®ï¼Œä½†å¹¶æ²¡æœ‰ç»‘å®šæ–¹å‘é”®ã€‚
#+begin_src emacs-lisp
(map! :map evil-org-mode-map
      :after evil-org
      :n "g <up>" #'org-backward-heading-same-level
      :n "g <down>" #'org-forward-heading-same-level
      :n "g <left>" #'org-up-element
      :n "g <right>" #'org-down-element)
#+end_src

**** é¢å¤–çš„åŠŸèƒ½
***** åˆ›å»º Org ç¼“å†²åŒº
æŠŠåˆ›å»º Org ç¼“å†²åŒºå˜å¾—æ›´å®¹æ˜“ç‚¹å§ã€‚
#+begin_src emacs-lisp :tangle yes :noweb-ref none
(evil-define-command evil-buffer-org-new (count file)
  "Creates a new ORG buffer replacing the current window, optionally
   editing a certain FILE"
  :repeat nil
  (interactive "P<f>")
  (if file
      (evil-edit file)
    (let ((buffer (generate-new-buffer "*new org*")))
      (set-window-buffer nil buffer)
      (with-current-buffer buffer
        (org-mode)))))
(map! :leader
      (:prefix "b"
       :desc "New empty ORG buffer" "o" #'evil-buffer-org-new))
#+end_src
***** é›¶å®½åº¦ç©ºæ ¼å·¥å…·
å¶å°”åœ¨ç”¨ Org æ˜¯ä½ å¸Œæœ›å°†ä¸¤ä¸ªåˆ†å¼€çš„å—æ”¾åœ¨ä¸€èµ·ï¼Œè¿™ç‚¹æœ‰ç‚¹çƒ¦äººã€‚æ¯”å¦‚å°†åŠ â€‹*é‡*â€‹ä¸€ä¸ªå•è¯
çš„ä¸€éƒ¨åˆ†ï¼Œæˆ–è€…è¯´åœ¨å†…è”æºç å—ä¹‹å‰æ”¾ä¸€äº›ç¬¦å·ã€‚æœ‰ä¸€ä¸ªå¯ä»¥è§£å†³çš„æ–¹æ³• --- é›¶å®½ç©ºæ ¼ã€‚
ç”±äºè¿™æ˜¯ Emacsï¼Œæˆ‘ä»¬å¯ä»¥ä¸º org-mode åšä¸€ä¸ªå¾ˆå°çš„æ”¹åŠ¨å°†å…¶æ·»åŠ åˆ°å¿«æ·é”®ä¸Š ğŸ™‚ã€‚
#+begin_src emacs-lisp
(map! :map org-mode-map
      :nie "M-SPC M-SPC" (cmd! (insert "\u200B")))
#+end_src

ä¸è¿‡åœ¨å¯¼å‡ºæ—¶ï¼Œæˆ‘å¹¶ä¸æƒ³è¦å®ƒï¼ŒåŠ ä¸€ä¸ªç®€å•çš„è¿‡æ»¤å™¨å°†å…¶è¿‡æ»¤æ‰ã€‚
#+begin_src emacs-lisp
(defun +org-export-remove-zero-width-space (text _backend _info)
  "Remove zero width spaces from TEXT."
  (unless (org-export-derived-backend-p 'org)
    (replace-regexp-in-string "\u200B" "" text)))

(after! ox
  (add-to-list 'org-export-filter-final-output-functions #'+org-export-remove-zero-width-space t))
#+end_src
***** åˆ—è¡¨ç¬¦å·åºåˆ—
æˆ‘è§‰å¾—è®©åˆ—è¡¨ç¬¦å·éšç€æ·±åº¦å˜åŒ–å¾ˆæœ‰æ„æ€ã€‚
#+begin_src emacs-lisp
(setq org-list-demote-modify-bullet '(("+" . "-") ("-" . "+") ("*" . "+") ("1." . "a.")))
#+end_src
***** å¼•ç”¨
å¶å°”å¼•ç”¨æŸäº›ä¸œè¥¿ï¼Œç”¨ =org-ref= å°±å¤Ÿäº†ã€‚

ä¸å¹¸çš„æ˜¯ï¼Œå®ƒå¿½ç•¥äº† ~file = {...}~ çš„ =.bib= é”®å€¼å¯¹ï¼Œä¿®æ­£å®ƒï¼
æˆ‘ä¾ =;= åˆ†å‰²äº†æ–‡ä»¶ï¼Œåªæ˜¯ç¨å¾®ç”¨äº†ä¸‹ Zotero/BetterBibLaTeXï¼Œä½†è¿™ç›¸å½“ä¸é”™ã€‚

#+begin_src emacs-lisp :noweb-ref none
(use-package! org-ref
  ;; :after org
  :defer t
  :config
  (defadvice! org-ref-open-bibtex-pdf-a ()
    :override #'org-ref-open-bibtex-pdf
    (save-excursion
      (bibtex-beginning-of-entry)
      (let* ((bibtex-expand-strings t)
             (entry (bibtex-parse-entry t))
             (key (reftex-get-bib-field "=key=" entry))
             (pdf (or
                   (car (-filter (lambda (f) (string-match-p "\\.pdf$" f))
                                 (split-string (reftex-get-bib-field "file" entry) ";")))
                   (funcall org-ref-get-pdf-filename-function key))))
        (if (file-exists-p pdf)
            (org-open-file pdf)
          (ding)))))
  (defadvice! org-ref-open-pdf-at-point-a ()
    "Open the pdf for bibtex key under point if it exists."
    :override #'org-ref-open-pdf-at-point
    (interactive)
    (let* ((results (org-ref-get-bibtex-key-and-file))
           (key (car results))
           (pdf-file (funcall org-ref-get-pdf-filename-function key)))
      (with-current-buffer (find-file-noselect (cdr results))
        (save-excursion
          (bibtex-search-entry (car results))
          (org-ref-open-bibtex-pdf))))))
#+end_src

=org-cite= æ˜¯ä¸€ä¸ªæ–°åŒ…ï¼Œä½†ä½“éªŒç›¸å½“å¥½ï¼Œæ”¹è¿›å®ƒï¼

#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! citar :pin "79512aefdf11071b66908320aa346255dd349234")
(package! citeproc :pin "ba49516265fa24b138346c4918d39d19b4de8a62")
(package! org-cite-csl-activate :recipe (:host github :repo "andras-simonyi/org-cite-csl-activate") :pin "a9b3f3643b587076538a767666372b31b873c294")
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! citar
  :when (featurep! :completion vertico)
  :custom
  (org-cite-insert-processor 'citar)
  (org-cite-follow-processor 'citar)
  (org-cite-activate-processor 'citar)
  :config
  (setq citar-bibliography
        (let ((libfile-search-names '("library.json" "Library.json" "library.bib" "Library.bib"))
              (libfile-dir "~/Zotero")
              paths)
          (dolist (libfile libfile-search-names)
            (when (and (not paths)
                       (file-exists-p (expand-file-name libfile libfile-dir)))
              (setq paths (list (expand-file-name libfile libfile-dir)))))
          paths))
  (setq citar-symbols
      `((file ,(all-the-icons-faicon "file-o" :face 'all-the-icons-green :v-adjust -0.1) . " ")
        (note ,(all-the-icons-material "speaker_notes" :face 'all-the-icons-blue :v-adjust -0.3) . " ")
        (link ,(all-the-icons-octicon "link" :face 'all-the-icons-orange :v-adjust 0.01) . " "))))

(use-package! citeproc
  :defer t)

;;; Org-Cite configuration

(map! :after org
      :map org-mode-map
      :localleader
      :desc "Insert citation" "@" #'org-cite-insert)

(use-package! oc
  :after org citar
  :config
  (require 'ox)
  (setq org-cite-global-bibliography
        (let ((paths (or citar-bibliography
                         (bound-and-true-p bibtex-completion-bibliography))))
          ;; Always return bibliography paths as list for org-cite.
          (if (stringp paths) (list paths) paths)))
  ;; setup export processor; default csl/citeproc-el, with biblatex for latex
  (setq org-cite-export-processors
        '((t csl))))

  ;;; Org-cite processors
(use-package! oc-biblatex
  :after oc)

(use-package! oc-csl
  :after oc
  :config
  (setq org-cite-csl-styles-dir "~/Zotero/styles"))

(use-package! oc-natbib
  :after oc)

(use-package! oc-csl-activate
  :after oc
  :config
  (setq org-cite-csl-activate-use-document-style t)
  (defun +org-cite-csl-activate/enable ()
    (interactive)
    (setq org-cite-activate-processor 'csl-activate)
    (add-hook! 'org-mode-hook '((lambda () (cursor-sensor-mode 1)) org-cite-csl-activate-render-all))
    (defadvice! +org-cite-csl-activate-render-all-silent (orig-fn)
      :around #'org-cite-csl-activate-render-all
      (with-silent-modifications (funcall orig-fn)))
    (when (eq major-mode 'org-mode)
      (with-silent-modifications
        (save-excursion
          (goto-char (point-min))
          (org-cite-activate (point-max)))
        (org-cite-csl-activate-render-all)))
    (fmakunbound #'+org-cite-csl-activate/enable)))
#+end_src

ä¹Ÿè®¸å¯ä»¥ç”¨ä¸€ä¸ªå‡½æ•°å°† =org-ref= å¼•ç”¨è½¬æ¢ä¸º =org-cite= ã€‚
#+begin_src emacs-lisp
(after! oc
  (defun org-ref-to-org-cite ()
    "Attempt to convert org-ref citations to org-cite syntax."
    (interactive)
    (let* ((cite-conversions '(("cite" . "//b") ("Cite" . "//bc")
                               ("nocite" . "/n")
                               ("citep" . "") ("citep*" . "//f")
                               ("parencite" . "") ("Parencite" . "//c")
                               ("citeauthor" . "/a/f") ("citeauthor*" . "/a")
                               ("citeyear" . "/na/b")
                               ("Citep" . "//c") ("Citealp" . "//bc")
                               ("Citeauthor" . "/a/cf") ("Citeauthor*" . "/a/c")
                               ("autocite" . "") ("Autocite" . "//c")
                               ("notecite" . "/l/b") ("Notecite" . "/l/bc")
                               ("pnotecite" . "/l") ("Pnotecite" . "/l/bc")))
           (cite-regexp (rx (regexp (regexp-opt (mapcar #'car cite-conversions) t))
                            ":" (group (+ (not (any "\n 	,.)]}")))))))
      (save-excursion
        (goto-char (point-min))
        (while (re-search-forward cite-regexp nil t)
          (message (format "[cite%s:@%s]"
                                 (cdr (assoc (match-string 1) cite-conversions))
                                 (match-string 2)))
          (replace-match (format "[cite%s:@%s]"
                                 (cdr (assoc (match-string 1) cite-conversions))
                                 (match-string 2))))))))
#+end_src
***** cdlatex
~cdlatex~ å¥½ï¼
#+begin_src emacs-lisp
(add-hook 'org-mode-hook 'turn-on-org-cdlatex)
#+end_src

å¯ä»¥ç”¨ =C-c }= å®ç°å¿«é€Ÿæ’å…¥ç¯å¢ƒã€‚ä¸è¿‡æˆ‘æ€»æ˜¯æƒ³åœ¨ä¹‹åç¼–è¾‘å®ƒä»¬ã€‚å°†è¿™å˜ä¸ºé»˜è®¤è¡Œä¸ºã€‚
#+begin_src emacs-lisp
(defadvice! org-edit-latex-emv-after-insert ()
  :after #'org-cdlatex-environment-indent
  (org-edit-latex-environment))
#+end_src

ä¹Ÿè®¸åœ¨ä»¥åå¯ä»¥æ·±å…¥ç ”ç©¶ä¸‹ [[https://scripter.co/splitting-an-org-block-into-two/][æ‹†åˆ† Org å—]]ã€‚åŒæ · [[https://archive.casouri.cat/note/2020/insert-math-symbol-in-emacs/][è¿™]] å¯¹äºç¬¦å·ä¹Ÿä¸é”™ã€‚
***** æ‹¼å†™æ£€æŸ¥
æˆ‘çš„æ‹¼å†™å¾ˆç³Ÿï¼Œè¿˜æ˜¯ç”¨æ£€æŸ¥å§ã€‚
#+begin_src emacs-lisp
(add-hook 'org-mode-hook 'turn-on-flyspell)
#+end_src
***** ~src~ å—çš„ LSP æ”¯æŒ
é»˜è®¤çš„ï¼ŒLSP å¹¶ä¸ä¼šåœ¨ ~src~ ä¸­èµ·ä½œç”¨ã€‚
#+begin_src emacs-lisp
(cl-defmacro lsp-org-babel-enable (lang)
  "Support LANG in org source code block."
  (setq centaur-lsp 'lsp-mode)
  (cl-check-type lang stringp)
  (let* ((edit-pre (intern (format "org-babel-edit-prep:%s" lang)))
         (intern-pre (intern (format "lsp--%s" (symbol-name edit-pre)))))
    `(progn
       (defun ,intern-pre (info)
         (let ((file-name (->> info caddr (alist-get :file))))
           (unless file-name
             (setq file-name (make-temp-file "babel-lsp-")))
           (setq buffer-file-name file-name)
           (lsp-deferred)))
       (put ',intern-pre 'function-documentation
            (format "Enable lsp-mode in the buffer of org source block (%s)."
                    (upcase ,lang)))
       (if (fboundp ',edit-pre)
           (advice-add ',edit-pre :after ',intern-pre)
         (progn
           (defun ,edit-pre (info)
             (,intern-pre info))
           (put ',edit-pre 'function-documentation
                (format "Prepare local buffer environment for org source block (%s)."
                        (upcase ,lang))))))))
(defvar org-babel-lang-list
  '("go" "python" "ipython" "bash" "sh"))
(dolist (lang org-babel-lang-list)
  (eval `(lsp-org-babel-enable ,lang)))
#+end_src
***** æŸ¥çœ‹å¯¼å‡ºæ–‡ä»¶
='localeader v= æ²¡æœ‰é¢„å…ˆè®¾ç½®çš„å¿«æ·é”®ï¼Œæ‰€ä»¥æˆ‘å¯ä»¥å°è¯•å°†å®ƒä¸ LaTeX çš„ç›¸åŒåŠŸèƒ½ä¸€èµ·ä½¿ç”¨ã€‚å°è¯•ç”¨å®ƒæŸ¥çœ‹ä¸€ä¸‹å¯èƒ½çš„å¯¼å‡ºæ–‡ä»¶ã€‚
#+begin_src emacs-lisp
(map! :map org-mode-map
      :localleader
      :desc "View exported file" "v" #'org-view-output-file)

(defun org-view-output-file (&optional org-file-path)
  "Visit buffer open on the first output file (if any) found, using `org-view-output-file-extensions'"
  (interactive)
  (let* ((org-file-path (or org-file-path (buffer-file-name) ""))
         (dir (file-name-directory org-file-path))
         (basename (file-name-base org-file-path))
         (output-file nil))
    (dolist (ext org-view-output-file-extensions)
      (unless output-file
        (when (file-exists-p
               (concat dir basename "." ext))
          (setq output-file (concat dir basename "." ext)))))
    (if output-file
        (if (member (file-name-extension output-file) org-view-external-file-extensions)
            (browse-url-xdg-open output-file)
          (pop-to-buffer (or (find-buffer-visiting output-file)
                             (find-file-noselect output-file))))
      (message "No exported file found"))))

(defvar org-view-output-file-extensions '("pdf" "md" "rst" "txt" "tex" "html")
  "Search for output files with these extensions, in order, viewing the first that matches")
(defvar org-view-external-file-extensions '("html")
  "File formats that should be opened externally.")
#+end_src

**** agenda æ”¯æŒ
agenda æŒºå¥½ç”¨çš„ï¼Œä½†åŠ å¼ºç‰ˆæ›´å¼ºã€‚
#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! org-super-agenda :pin "fb5e2ef277bc811a3b061106c99e4c47b6b86f80")
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! org-super-agenda
  :commands org-super-agenda-mode)
#+end_src

#+begin_src emacs-lisp
(after! org-agenda
  (org-super-agenda-mode))

(setq org-agenda-skip-scheduled-if-done t
      org-agenda-skip-deadline-if-done t
      org-agenda-include-deadlines t
      org-agenda-block-separator nil
      org-agenda-tags-column 100 ;; from testing this seems to be a good value
      org-agenda-compact-blocks t)

(setq org-agenda-custom-commands
      '(("o" "Overview"
         ((agenda "" ((org-agenda-span 'day)
                      (org-super-agenda-groups
                       '((:name "Today"
                          :time-grid t
                          :date today
                          :todo "TODAY"
                          :scheduled today
                          :order 1)))))
          (alltodo "" ((org-agenda-overriding-header "")
                       (org-super-agenda-groups
                        '((:name "Next to do"
                           :todo "NEXT"
                           :order 1)
                          (:name "Important"
                           :tag "Important"
                           :priority "A"
                           :order 6)
                          (:name "Due Today"
                           :deadline today
                           :order 2)
                          (:name "Due Soon"
                           :deadline future
                           :order 8)
                          (:name "Overdue"
                           :deadline past
                           :face error
                           :order 7)
                          (:name "Assignments"
                           :tag "Assignment"
                           :order 10)
                          (:name "Issues"
                           :tag "Issue"
                           :order 12)
                          (:name "Emacs"
                           :tag "Emacs"
                           :order 13)
                          (:name "Projects"
                           :tag "Project"
                           :order 14)
                          (:name "Research"
                           :tag "Research"
                           :order 15)
                          (:name "To read"
                           :tag "Read"
                           :order 30)
                          (:name "Waiting"
                           :todo "WAITING"
                           :order 20)
                          (:name "University"
                           :tag "uni"
                           :order 32)
                          (:name "Trivial"
                           :priority<= "E"
                           :tag ("Trivial" "Unimportant")
                           :todo ("SOMEDAY" )
                           :order 90)
                          (:discard (:tag ("Chore" "Routine" "Daily")))))))))))
#+end_src

**** Capture
å¼€å§‹è®¾ç½® Org-capture æ¨¡æ¿å§ï¼Œè®©å®ƒä»¬çœ‹èµ·æ¥æ›´å®¹æ˜“è®¿é—®ã€‚

#+attr_html: :class invertible :alt My org-capture dialouge.
[[https://tecosaur.com/lfs/emacs-config/screenshots/org-capture.png]]

~doct~ (Declarative Org Capture Templates) ä¼¼ä¹æ˜¯æŒºä¸é”™çš„ org-capture æ–¹
æ¡ˆã€‚
#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! doct
  :recipe (:host github :repo "progfolio/doct")
  :pin "9ed9b8c7f7e2ea2d2fb739d65ae4626a1cf16b9f")
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! doct
  :commands doct)
#+end_src

#+begin_src emacs-lisp :noweb no-export
(after! org-capture
  <<prettify-capture>>

  (defun +doct-icon-declaration-to-icon (declaration)
    "Convert :icon declaration to icon"
    (let ((name (pop declaration))
          (set  (intern (concat "all-the-icons-" (plist-get declaration :set))))
          (face (intern (concat "all-the-icons-" (plist-get declaration :color))))
          (v-adjust (or (plist-get declaration :v-adjust) 0.01)))
      (apply set `(,name :face ,face :v-adjust ,v-adjust))))

  (defun +doct-iconify-capture-templates (groups)
    "Add declaration's :icon to each template group in GROUPS."
    (let ((templates (doct-flatten-lists-in groups)))
      (setq doct-templates (mapcar (lambda (template)
                                     (when-let* ((props (nthcdr (if (= (length template) 4) 2 5) template))
                                                 (spec (plist-get (plist-get props :doct) :icon)))
                                       (setf (nth 1 template) (concat (+doct-icon-declaration-to-icon spec)
                                                                      "\t"
                                                                      (nth 1 template))))
                                     template)
                                   templates))))

  (setq doct-after-conversion-functions '(+doct-iconify-capture-templates))

  (defvar +org-capture-recipies  "~/Desktop/TEC/Organisation/recipies.org")

  (defun set-org-capture-templates ()
    (setq org-capture-templates
          (doct `(("Personal todo" :keys "t"
                   :icon ("checklist" :set "octicon" :color "green")
                   :file +org-capture-todo-file
                   :prepend t
                   :headline "Inbox"
                   :type entry
                   :template ("* TODO %?"
                              "%i %a"))
                  ("Personal note" :keys "n"
                   :icon ("sticky-note-o" :set "faicon" :color "green")
                   :file +org-capture-todo-file
                   :prepend t
                   :headline "Inbox"
                   :type entry
                   :template ("* %?"
                              "%i %a"))
                  ("Email" :keys "e"
                   :icon ("envelope" :set "faicon" :color "blue")
                   :file +org-capture-todo-file
                   :prepend t
                   :headline "Inbox"
                   :type entry
                   :template ("* TODO %^{type|reply to|contact} %\\3 %? :email:"
                              "Send an email %^{urgancy|soon|ASAP|anon|at some point|eventually} to %^{recipiant}"
                              "about %^{topic}"
                              "%U %i %a"))
                  ("Interesting" :keys "i"
                   :icon ("eye" :set "faicon" :color "lcyan")
                   :file +org-capture-todo-file
                   :prepend t
                   :headline "Interesting"
                   :type entry
                   :template ("* [ ] %{desc}%? :%{i-type}:"
                              "%i %a")
                   :children (("Webpage" :keys "w"
                               :icon ("globe" :set "faicon" :color "green")
                               :desc "%(org-cliplink-capture) "
                               :i-type "read:web")
                              ("Article" :keys "a"
                               :icon ("file-text" :set "octicon" :color "yellow")
                               :desc ""
                               :i-type "read:reaserch")
                              ("\tRecipie" :keys "r"
                               :icon ("spoon" :set "faicon" :color "dorange")
                               :file +org-capture-recipies
                               :headline "Unsorted"
                               :template "%(org-chef-get-recipe-from-url)")
                              ("Information" :keys "i"
                               :icon ("info-circle" :set "faicon" :color "blue")
                               :desc ""
                               :i-type "read:info")
                              ("Idea" :keys "I"
                               :icon ("bubble_chart" :set "material" :color "silver")
                               :desc ""
                               :i-type "idea")))
                  ("Tasks" :keys "k"
                   :icon ("inbox" :set "octicon" :color "yellow")
                   :file +org-capture-todo-file
                   :prepend t
                   :headline "Tasks"
                   :type entry
                   :template ("* TODO %? %^G%{extra}"
                              "%i %a")
                   :children (("General Task" :keys "k"
                               :icon ("inbox" :set "octicon" :color "yellow")
                               :extra "")
                              ("Task with deadline" :keys "d"
                               :icon ("timer" :set "material" :color "orange" :v-adjust -0.1)
                               :extra "\nDEADLINE: %^{Deadline:}t")
                              ("Scheduled Task" :keys "s"
                               :icon ("calendar" :set "octicon" :color "orange")
                               :extra "\nSCHEDULED: %^{Start time:}t")))
                  ("Project" :keys "p"
                   :icon ("repo" :set "octicon" :color "silver")
                   :prepend t
                   :type entry
                   :headline "Inbox"
                   :template ("* %{time-or-todo} %?"
                              "%i"
                              "%a")
                   :file ""
                   :custom (:time-or-todo "")
                   :children (("Project-local todo" :keys "t"
                               :icon ("checklist" :set "octicon" :color "green")
                               :time-or-todo "TODO"
                               :file +org-capture-project-todo-file)
                              ("Project-local note" :keys "n"
                               :icon ("sticky-note" :set "faicon" :color "yellow")
                               :time-or-todo "%U"
                               :file +org-capture-project-notes-file)
                              ("Project-local changelog" :keys "c"
                               :icon ("list" :set "faicon" :color "blue")
                               :time-or-todo "%U"
                               :heading "Unreleased"
                               :file +org-capture-project-changelog-file)))
                  ("\tCentralised project templates"
                   :keys "o"
                   :type entry
                   :prepend t
                   :template ("* %{time-or-todo} %?"
                              "%i"
                              "%a")
                   :children (("Project todo"
                               :keys "t"
                               :prepend nil
                               :time-or-todo "TODO"
                               :heading "Tasks"
                               :file +org-capture-central-project-todo-file)
                              ("Project note"
                               :keys "n"
                               :time-or-todo "%U"
                               :heading "Notes"
                               :file +org-capture-central-project-notes-file)
                              ("Project changelog"
                               :keys "c"
                               :time-or-todo "%U"
                               :heading "Unreleased"
                               :file +org-capture-central-project-changelog-file)))))))

  (set-org-capture-templates)
  (unless (display-graphic-p)
    (add-hook 'server-after-make-frame-hook
              (defun org-capture-reinitialise-hook ()
                (when (display-graphic-p)
                  (set-org-capture-templates)
                  (remove-hook 'server-after-make-frame-hook
                               #'org-capture-reinitialise-hook))))))
#+end_src
æ”¹è¿› capture çš„å¯¹è¯å¤–è§‚ï¼Œä¹Ÿä¸é”™ã€‚
#+name: prettify-capture
#+begin_src emacs-lisp :noweb-ref none
(defun org-capture-select-template-prettier (&optional keys)
  "Select a capture template, in a prettier way than default
Lisp programs can force the template by setting KEYS to a string."
  (let ((org-capture-templates
         (or (org-contextualize-keys
              (org-capture-upgrade-templates org-capture-templates)
              org-capture-templates-contexts)
             '(("t" "Task" entry (file+headline "" "Tasks")
                "* TODO %?\n  %u\n  %a")))))
    (if keys
        (or (assoc keys org-capture-templates)
            (error "No capture template referred to by \"%s\" keys" keys))
      (org-mks org-capture-templates
               "Select a capture template\nâ”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”â”"
               "Template key: "
               `(("q" ,(concat (all-the-icons-octicon "stop" :face 'all-the-icons-red :v-adjust 0.01) "\tAbort")))))))
(advice-add 'org-capture-select-template :override #'org-capture-select-template-prettier)

(defun org-mks-pretty (table title &optional prompt specials)
  "Select a member of an alist with multiple keys. Prettified.

TABLE is the alist which should contain entries where the car is a string.
There should be two types of entries.

1. prefix descriptions like (\"a\" \"Description\")
   This indicates that `a' is a prefix key for multi-letter selection, and
   that there are entries following with keys like \"ab\", \"ax\"â€¦

2. Select-able members must have more than two elements, with the first
   being the string of keys that lead to selecting it, and the second a
   short description string of the item.

The command will then make a temporary buffer listing all entries
that can be selected with a single key, and all the single key
prefixes.  When you press the key for a single-letter entry, it is selected.
When you press a prefix key, the commands (and maybe further prefixes)
under this key will be shown and offered for selection.

TITLE will be placed over the selection in the temporary buffer,
PROMPT will be used when prompting for a key.  SPECIALS is an
alist with (\"key\" \"description\") entries.  When one of these
is selected, only the bare key is returned."
  (save-window-excursion
    (let ((inhibit-quit t)
          (buffer (org-switch-to-buffer-other-window "*Org Select*"))
          (prompt (or prompt "Select: "))
          case-fold-search
          current)
      (unwind-protect
          (catch 'exit
            (while t
              (setq-local evil-normal-state-cursor (list nil))
              (erase-buffer)
              (insert title "\n\n")
              (let ((des-keys nil)
                    (allowed-keys '("\C-g"))
                    (tab-alternatives '("\s" "\t" "\r"))
                    (cursor-type nil))
                ;; Populate allowed keys and descriptions keys
                ;; available with CURRENT selector.
                (let ((re (format "\\`%s\\(.\\)\\'"
                                  (if current (regexp-quote current) "")))
                      (prefix (if current (concat current " ") "")))
                  (dolist (entry table)
                    (pcase entry
                      ;; Description.
                      (`(,(and key (pred (string-match re))) ,desc)
                       (let ((k (match-string 1 key)))
                         (push k des-keys)
                         ;; Keys ending in tab, space or RET are equivalent.
                         (if (member k tab-alternatives)
                             (push "\t" allowed-keys)
                           (push k allowed-keys))
                         (insert (propertize prefix 'face 'font-lock-comment-face) (propertize k 'face 'bold) (propertize "â€º" 'face 'font-lock-comment-face) "  " desc "â€¦" "\n")))
                      ;; Usable entry.
                      (`(,(and key (pred (string-match re))) ,desc . ,_)
                       (let ((k (match-string 1 key)))
                         (insert (propertize prefix 'face 'font-lock-comment-face) (propertize k 'face 'bold) "   " desc "\n")
                         (push k allowed-keys)))
                      (_ nil))))
                ;; Insert special entries, if any.
                (when specials
                  (insert "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€\n")
                  (pcase-dolist (`(,key ,description) specials)
                    (insert (format "%s   %s\n" (propertize key 'face '(bold all-the-icons-red)) description))
                    (push key allowed-keys)))
                ;; Display UI and let user select an entry or
                ;; a sub-level prefix.
                (goto-char (point-min))
                (unless (pos-visible-in-window-p (point-max))
                  (org-fit-window-to-buffer))
                (let ((pressed (org--mks-read-key allowed-keys
                                                  prompt
                                                  (not (pos-visible-in-window-p (1- (point-max)))))))
                  (setq current (concat current pressed))
                  (cond
                   ((equal pressed "\C-g") (user-error "Abort"))
                   ;; Selection is a prefix: open a new menu.
                   ((member pressed des-keys))
                   ;; Selection matches an association: return it.
                   ((let ((entry (assoc current table)))
                      (and entry (throw 'exit entry))))
                   ;; Selection matches a special entry: return the
                   ;; selection prefix.
                   ((assoc current specials) (throw 'exit current))
                   (t (error "No entry available")))))))
        (when buffer (kill-buffer buffer))))))
(advice-add 'org-mks :override
#+end_src
[[file:~/.emacs.d/bin/org-capture][org-capture bin]] ç›¸å½“ä¸é”™ï¼Œä½†æˆ‘æ›´å–œæ¬¢æ›´å°çš„æ¡†æ¶ï¼Œä¸”æ²¡æœ‰ modelineã€‚
#+begin_src emacs-lisp
(setf (alist-get 'height +org-capture-frame-parameters) 15)
;; (alist-get 'name +org-capture-frame-parameters) "â– Capture") ;; ATM hardcoded in other places, so changing breaks stuff
(setq +org-capture-fn
      (lambda ()
        (interactive)
        (set-window-parameter nil 'mode-line-format 'none)
        (org-capture)))
#+end_src

**** Roam
***** åŸºç¡€è®¾ç½®
æˆ‘ç°åœ¨å°†å…¶è®¾ç½®åœ¨ =Organisation= ä¸­ï¼Œå°†æ¥ä¼¼ä¹å¾ˆå€¼å¾—ä¸ [[https://nextcloud.com/][Nextcloud]] ç»“åˆèµ·æ¥ã€‚
#+begin_src emacs-lisp :noweb-ref none :tangle yes
(setq org-roam-directory "~/Desktop/TEC/Organisation/Roam/")
#+end_src

ä¹Ÿå°±æ˜¯è¯´ï¼Œå¦‚æœç›®å½•ä¸å­˜åœ¨æˆ‘ä»¬å¹¶ä¸æƒ³ä½¿ç”¨ roamã€‚ç”±äºæˆ‘ä»¬ä¸æƒ³è§¦å‘é”™è¯¯ (åœ¨ roam åˆå§‹
åŒ–æ—¶å°±ä¼šå‘ç”Ÿ)ï¼Œæˆ‘ä»¬å°±ç›´æ¥ä¸åŠ è½½ roamã€‚
#+begin_src emacs-lisp :noweb-ref none :tangle (if (file-exists-p "~/Desktop/TEC/Organisation/Roam/") "no" "packages.el")
(package! org-roam :disable t)
#+end_src
***** modeline æ–‡ä»¶å
è¿™äº›æ•°å­—å®åœ¨å¤ªï¼æ··ï¼ä¹±ï¼äº†ï¼è®©æˆ‘ä»¬ç”¨ä¸ [[*çª—å£æ ‡é¢˜][çª—å£æ ‡é¢˜]] ç±»ä¼¼çš„æ–¹æ³•è°ƒæ•´å®ƒã€‚
#+begin_src emacs-lisp
(defadvice! doom-modeline--buffer-file-name-roam-aware-a (orig-fun)
  :around #'doom-modeline-buffer-file-name ; takes no args
  (if (s-contains-p org-roam-directory (or buffer-file-name ""))
      (replace-regexp-in-string
       "\\(?:^\\|.*/\\)\\([0-9]\\{4\\}\\)\\([0-9]\\{2\\}\\)\\([0-9]\\{2\\}\\)[0-9]*-"
       "ğŸ¢”(\\1-\\2-\\3) "
       (subst-char-in-string ?_ ?  buffer-file-name))
    (funcall orig-fun)))
#+end_src
***** å›¾å½¢é¢„è§ˆ
org-roam å®ƒæœ¬èº«è¿˜æ˜¯å¾ˆä¼˜ç§€çš„ï¼Œè¿˜å¯ä»¥å°†è¿™äº›éå¸¸æ£’çš„ /é¢å¤–/ åŒ…é›†æˆè¿›æ¥ã€‚
#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! org-roam-ui :recipe (:host github :repo "org-roam/org-roam-ui" :files ("*.el" "out")) :pin "309fe3c58c7081de4e2c9c64f7b40ea291926048")
(package! websocket :pin "fda4455333309545c0787a79d73c19ddbeb57980") ; dependency of `org-roam-ui'
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! websocket
  :after org-roam)

(use-package! org-roam-ui
  :after org-roam
  :commands org-roam-ui-open
  :hook (org-roam . org-roam-ui-mode)
  :config
  (require 'org-roam) ; in case autoloaded
  (defun org-roam-ui-open ()
    "Ensure the server is active, then open the roam graph."
    (interactive)
    (unless org-roam-ui-mode (org-roam-ui-mode 1))
    (browse-url-xdg-open (format "http://localhost:%d" org-roam-ui-port))))
#+end_src

**** æ›´æ£’çš„æ ‡é¢˜ ID ç”Ÿæˆ
éå¸¸æ„Ÿè°¢ alphapapa çš„ [[https://github.com/alphapapa/unpackaged.el#export-to-html-with-useful-anchors][unpackaged.el]].

é€šå¸¸æƒ…å†µä¸‹ Org ä¼šä¸ºæ ‡é¢˜ç”Ÿæˆç±»ä¼¼ =#org80fc2a5= çš„ IDâ€¦â€¦ èƒ½ç”¨ï¼Œå°±æ˜¯æœ‰ä¸¤é—®é¢˜
+ å¤æ‚ä¸”æ— ç”¨çš„ä¿¡æ¯ï¼Œæ²¡ä»€ä¹ˆå¥½æ–¹æ³•å»å¼•ç”¨
+ å¯¼å‡ºç›¸åŒçš„æ–‡ç« ï¼Œä½† ID ä¼šæ”¹å˜ã€‚è™½ç„¶ç°åœ¨æ²¡ä»€ä¹ˆç¡¬ç¼–ç éœ€è¦ä¸€éåˆä¸€éçš„ä¿®æ”¹ï¼Œä½†æ˜¯èƒ½
  ä¸å˜æ˜¯æœ€å¥½çš„ã€‚

è¿™ä¸¤ä¸ªé—®é¢˜å¯ä»¥é‡‡ç”¨ç”Ÿæˆ =#language-configuration= è¿™æ ·çš„ ID æ¥è§£å†³ï¼Œå°±è¿™æ ·åšã€‚

éœ€è¦æ³¨æ„ï¼Œalphapapa ä½¿ç”¨çš„ ~url-hexify-string~ ä¼¼ä¹ç»™æˆ‘å¸¦æ¥äº†äº›é—®é¢˜ã€‚åœ¨
~a53899~ ä¸­æ›¿æ¢å®ƒè§£å†³äº†è¿™ä¸ªé—®é¢˜ï¼Œä¸ºäº†æ›´è¿›ä¸€æ­¥ï¼Œæˆ‘åˆ›å»ºäº†ä¸€ä¸ªæ¼‚äº®çš„çŸ­é“¾æ¥å‡½æ•°ï¼Œå°±åƒ
~reftex-label~ çš„ä½é˜¶ç‰ˆæœ¬ã€‚
#+begin_src emacs-lisp
resolved

(defvar org-reference-contraction-max-words 3
  "Maximum number of words in a reference reference.")
(defvar org-reference-contraction-max-length 35
  "Maximum length of resulting reference reference, including joining characters.")
(defvar org-reference-contraction-stripped-words
  '("the" "on" "in" "off" "a" "for" "by" "of" "and" "is" "to")
  "Superfluous words to be removed from a reference.")
(defvar org-reference-contraction-joining-char "-"
  "Character used to join words in the reference reference.")

(defun org-reference-contraction-truncate-words (words)
  "Using `org-reference-contraction-max-length' as the total character 'budget' for the WORDS
and truncate individual words to conform to this budget.

To arrive at a budget that accounts for words undershooting their requisite average length,
the number of characters in the budget freed by short words is distributed among the words
exceeding the average length.  This adjusts the per-word budget to be the maximum feasable for
this particular situation, rather than the universal maximum average.

This budget-adjusted per-word maximum length is given by the mathematical expression below:

max length = \\floor{ \\frac{total length - chars for seperators - \\sum_{word \\leq average length} length(word) }{num(words) > average length} }"
  ;; trucate each word to a max word length determined by
  ;;
  (let* ((total-length-budget (- org-reference-contraction-max-length  ; how many non-separator chars we can use
                                 (1- (length words))))
         (word-length-budget (/ total-length-budget                      ; max length of each word to keep within budget
                                org-reference-contraction-max-words))
         (num-overlong (-count (lambda (word)                            ; how many words exceed that budget
                                 (> (length word) word-length-budget))
                               words))
         (total-short-length (-sum (mapcar (lambda (word)                ; total length of words under that budget
                                             (if (<= (length word) word-length-budget)
                                                 (length word) 0))
                                           words)))
         (max-length (/ (- total-length-budget total-short-length)       ; max(max-length) that we can have to fit within the budget
                        num-overlong)))
    (mapcar (lambda (word)
              (if (<= (length word) max-length)
                  word
                (substring word 0 max-length)))
            words)))

(defun org-reference-contraction (reference-string)
  "Give a contracted form of REFERENCE-STRING that is only contains alphanumeric characters.
Strips 'joining' words present in `org-reference-contraction-stripped-words',
and then limits the result to the first `org-reference-contraction-max-words' words.
If the total length is > `org-reference-contraction-max-length' then individual words are
truncated to fit within the limit using `org-reference-contraction-truncate-words'."
  (let ((reference-words
         (-filter (lambda (word)
                    (not (member word org-reference-contraction-stripped-words)))
                  (split-string
                   (->> reference-string
                        downcase
                        (replace-regexp-in-string "\\[\\[[^]]+\\]\\[\\([^]]+\\)\\]\\]" "\\1") ; get description from org-link
                        (replace-regexp-in-string "[-/ ]+" " ") ; replace seperator-type chars with space
                        puny-encode-string
                        (replace-regexp-in-string "^xn--\\(.*?\\) ?-?\\([a-z0-9]+\\)$" "\\2 \\1") ; rearrange punycode
                        (replace-regexp-in-string "[^A-Za-z0-9 ]" "") ; strip chars which need %-encoding in a uri
                        ) " +"))))
    (when (> (length reference-words)
             org-reference-contraction-max-words)
      (setq reference-words
            (cl-subseq reference-words 0 org-reference-contraction-max-words)))

    (when (> (apply #'+ (1- (length reference-words))
                    (mapcar #'length reference-words))
             org-reference-contraction-max-length)
      (setq reference-words (org-reference-contraction-truncate-words reference-words)))

    (string-join reference-words org-reference-contraction-joining-char)))
#+end_src

è¿™æ˜¯ alphapapa çš„å¾®è°ƒæ¨¡å¼ã€‚
#+begin_src emacs-lisp
(define-minor-mode unpackaged/org-export-html-with-useful-ids-mode
  "Attempt to export Org as HTML with useful link IDs.
Instead of random IDs like \"#orga1b2c3\", use heading titles,
made unique when necessary."
  :global t
  (if unpackaged/org-export-html-with-useful-ids-mode
      (advice-add #'org-export-get-reference :override #'unpackaged/org-export-get-reference)
    (advice-remove #'org-export-get-reference #'unpackaged/org-export-get-reference)))
(unpackaged/org-export-html-with-useful-ids-mode 1) ; ensure enabled, and advice run

(defun unpackaged/org-export-get-reference (datum info)
  "Like `org-export-get-reference', except uses heading titles instead of random numbers."
  (let ((cache (plist-get info :internal-references)))
    (or (car (rassq datum cache))
        (let* ((crossrefs (plist-get info :crossrefs))
               (cells (org-export-search-cells datum))
               ;; Preserve any pre-existing association between
               ;; a search cell and a reference, i.e., when some
               ;; previously published document referenced a location
               ;; within current file (see
               ;; `org-publish-resolve-external-link').
               ;;
               ;; However, there is no guarantee that search cells are
               ;; unique, e.g., there might be duplicate custom ID or
               ;; two headings with the same title in the file.
               ;;
               ;; As a consequence, before re-using any reference to
               ;; an element or object, we check that it doesn't refer
               ;; to a previous element or object.
               (new (or (cl-some
                         (lambda (cell)
                           (let ((stored (cdr (assoc cell crossrefs))))
                             (when stored
                               (let ((old (org-export-format-reference stored)))
                                 (and (not (assoc old cache)) stored)))))
                         cells)
                        (when (org-element-property :raw-value datum)
                          ;; Heading with a title
                          (unpackaged/org-export-new-named-reference datum cache))
                        (when (member (car datum) '(src-block table example fixed-width property-drawer))
                          ;; Nameable elements
                          (unpackaged/org-export-new-named-reference datum cache))
                        ;; NOTE: This probably breaks some Org Export
                        ;; feature, but if it does what I need, fine.
                        (org-export-format-reference
                         (org-export-new-reference cache))))
               (reference-string new))
          ;; Cache contains both data already associated to
          ;; a reference and in-use internal references, so as to make
          ;; unique references.
          (dolist (cell cells) (push (cons cell new) cache))
          ;; Retain a direct association between reference string and
          ;; DATUM since (1) not every object or element can be given
          ;; a search cell (2) it permits quick lookup.
          (push (cons reference-string datum) cache)
          (plist-put info :internal-references cache)
          reference-string))))

(defun unpackaged/org-export-new-named-reference (datum cache)
  "Return new reference for DATUM that is unique in CACHE."
  (cl-macrolet ((inc-suffixf (place)
                             `(progn
                                (string-match (rx bos
                                                  (minimal-match (group (1+ anything)))
                                                  (optional "--" (group (1+ digit)))
                                                  eos)
                                              ,place)
                                ;; HACK: `s1' instead of a gensym.
                                (-let* (((s1 suffix) (list (match-string 1 ,place)
                                                           (match-string 2 ,place)))
                                        (suffix (if suffix
                                                    (string-to-number suffix)
                                                  0)))
                                  (setf ,place (format "%s--%s" s1 (cl-incf suffix)))))))
    (let* ((headline-p (eq (car datum) 'headline))
           (title (if headline-p
                      (org-element-property :raw-value datum)
                    (or (org-element-property :name datum)
                        (concat (org-element-property :raw-value
                                                      (org-element-property :parent
                                                                            (org-element-property :parent datum)))))))
           ;; get ascii-only form of title without needing percent-encoding
           (ref (concat (org-reference-contraction (substring-no-properties title))
                        (unless (or headline-p (org-element-property :name datum))
                          (concat ","
                                  (pcase (car datum)
                                    ('src-block "code")
                                    ('example "example")
                                    ('fixed-width "mono")
                                    ('property-drawer "properties")
                                    (_ (symbol-name (car datum))))
                                  "--1"))))
           (parent (when headline-p (org-element-property :parent datum))))
      (while (--any (equal ref (car it))
                    cache)
        ;; Title not unique: make it so.
        (if parent
            ;; Append ancestor title.
            (setf title (concat (org-element-property :raw-value parent)
                                "--" title)
                  ;; get ascii-only form of title without needing percent-encoding
                  ref (org-reference-contraction (substring-no-properties title))
                  parent (when headline-p (org-element-property :parent parent)))
          ;; No more ancestors: add and increment a number.
          (inc-suffixf ref)))
      ref)))

(add-hook 'org-load-hook #'unpackaged/org-export-html-with-useful-ids-mode)
#+end_src
å¯¹äºå®ƒç°åœ¨å¯ä»¥ä¼ é€’ä¸€ä¸ªå­—ç¬¦ä¸²å’Œæ•°å­—ï¼Œæˆ‘ä»¬é‡å®šä¹‰
src_elisp{(org-export-format-reference)}ã€‚
#+begin_src emacs-lisp
(defadvice! org-export-format-reference-a (reference)
  "Format REFERENCE into a string.

REFERENCE is a either a number or a string representing a reference,
as returned by `org-export-new-reference'."
  :override #'org-export-format-reference
  (if (stringp reference) reference (format "org%07x" reference)))
#+end_src

**** æ›´æ£’çš„ ~org-return~
åœ¨è¯´ä¸€æ¬¡ï¼Œ[[https://github.com/alphapapa/unpackaged.el#org-return-dwim][unpackaged.el]]ï¼
#+begin_src emacs-lisp
(defun unpackaged/org-element-descendant-of (type element)
  "Return non-nil if ELEMENT is a descendant of TYPE.
TYPE should be an element type, like `item' or `paragraph'.
ELEMENT should be a list like that returned by `org-element-context'."
  ;; MAYBE: Use `org-element-lineage'.
  (when-let* ((parent (org-element-property :parent element)))
    (or (eq type (car parent))
        (unpackaged/org-element-descendant-of type parent))))

;;;###autoload
(defun unpackaged/org-return-dwim (&optional default)
  "A helpful replacement for `org-return-indent'.  With prefix, call `org-return-indent'.

On headings, move point to position after entry content.  In
lists, insert a new item or end the list, with checkbox if
appropriate.  In tables, insert a new row or end the table."
  ;; Inspired by John Kitchin: http://kitchingroup.cheme.cmu.edu/blog/2017/04/09/A-better-return-in-org-mode/
  (interactive "P")
  (if default
      (org-return t)
    (cond
     ;; Act depending on context around point.

     ;; NOTE: I prefer RET to not follow links, but by uncommenting this block, links will be
     ;; followed.

     ;; ((eq 'link (car (org-element-context)))
     ;;  ;; Link: Open it.
     ;;  (org-open-at-point-global))

     ((org-at-heading-p)
      ;; Heading: Move to position after entry content.
      ;; NOTE: This is probably the most interesting feature of this function.
      (let ((heading-start (org-entry-beginning-position)))
        (goto-char (org-entry-end-position))
        (cond ((and (org-at-heading-p)
                    (= heading-start (org-entry-beginning-position)))
               ;; Entry ends on its heading; add newline after
               (end-of-line)
               (insert "\n\n"))
              (t
               ;; Entry ends after its heading; back up
               (forward-line -1)
               (end-of-line)
               (when (org-at-heading-p)
                 ;; At the same heading
                 (forward-line)
                 (insert "\n")
                 (forward-line -1))
               (while (not (looking-back "\\(?:[[:blank:]]?\n\\)\\{3\\}" nil))
                 (insert "\n"))
               (forward-line -1)))))

     ((org-at-item-checkbox-p)
      ;; Checkbox: Insert new item with checkbox.
      (org-insert-todo-heading nil))

     ((org-in-item-p)
      ;; Plain list.  Yes, this gets a little complicated...
      (let ((context (org-element-context)))
        (if (or (eq 'plain-list (car context))  ; First item in list
                (and (eq 'item (car context))
                     (not (eq (org-element-property :contents-begin context)
                              (org-element-property :contents-end context))))
                (unpackaged/org-element-descendant-of 'item context))  ; Element in list item, e.g. a link
            ;; Non-empty item: Add new item.
            (org-insert-item)
          ;; Empty item: Close the list.
          ;; TODO: Do this with org functions rather than operating on the text. Can't seem to find the right function.
          (delete-region (line-beginning-position) (line-end-position))
          (insert "\n"))))

     ((when (fboundp 'org-inlinetask-in-task-p)
        (org-inlinetask-in-task-p))
      ;; Inline task: Don't insert a new heading.
      (org-return t))

     ((org-at-table-p)
      (cond ((save-excursion
               (beginning-of-line)
               ;; See `org-table-next-field'.
               (cl-loop with end = (line-end-position)
                        for cell = (org-element-table-cell-parser)
                        always (equal (org-element-property :contents-begin cell)
                                      (org-element-property :contents-end cell))
                        while (re-search-forward "|" end t)))
             ;; Empty row: end the table.
             (delete-region (line-beginning-position) (line-end-position))
             (org-return t))
            (t
             ;; Non-empty row: call `org-return-indent'.
             (org-return t))))
     (t
      ;; All other cases: call `org-return-indent'.
      (org-return t)))))

(map!
 :after evil-org
 :map evil-org-mode-map
 :i [return] #'unpackaged/org-return-dwim)
#+end_src

**** snippet åŠ©æ‰‹
æˆ‘ç»å¸¸è®¾ç½® =srcå—= çš„å¤´ï¼Œæ‰€ä»¥æœ‰ä»¥ä¸‹ç—›ç‚¹
+ ç±»å‹
+ è®°ä½å¯ä»¥æ¥å—ä»€ä¹ˆå€¼
+ å¯¹ç›¸åŒç¼–ç¨‹è¯­è¨€éœ€è¦ä¸€éä¸€éæŒ‡å®š

ç”¨ä¸‰æ­¥è§£å†³è¿™äº›é—®é¢˜
+ ä¸€ä¸ªå°çš„ç‰‡æ®µï¼Œå¹¶å°† src header çš„æ¡ä»¶æ”¾åœ¨ ~(point)~ å¤„
+ åˆ›å»ºä¸€ä¸ªæç¤ºçª—å£æ¥å±•ç¤ºæ¥æ”¶çš„å€¼ä¸é»˜è®¤å€¼
+ ä¸º =srcå—= çš„è¯­è¨€é¢„å¡«å……ä¸Šä¸€æ¬¡è¯¥è¯­è¨€çš„å€¼

å¯¹äºè¿™äº›å‚æ•°ï¼Œæˆ‘ç®€åŒ–ä¸ºäº†è¿™äº›é”®
+ =r= å¯¹åº” =:results=
+ =e= å¯¹åº” =:exports=
+ =v= å¯¹åº” =:eval=
+ =s= å¯¹åº” =:session=
+ =d= å¯¹åº” =:dir=

#+begin_src emacs-lisp
(defun +yas/org-src-header-p ()
  "Determine whether `point' is within a src-block header or header-args."
  (pcase (org-element-type (org-element-context))
    ('src-block (< (point) ; before code part of the src-block
                   (save-excursion (goto-char (org-element-property :begin (org-element-context)))
                                   (forward-line 1)
                                   (point))))
    ('inline-src-block (< (point) ; before code part of the inline-src-block
                          (save-excursion (goto-char (org-element-property :begin (org-element-context)))
                                          (search-forward "]{")
                                          (point))))
    ('keyword (string-match-p "^header-args" (org-element-property :value (org-element-context))))))
#+end_src

ç°åœ¨è®©æˆ‘ä»¬ç¼–å†™ä¸€ä¸ªå¯ä»¥åœ¨ yasnippets ä¸­å¼•ç”¨çš„äº¤äº’å¼æ–¹æ³•ï¼Œç”¨å®ƒæŒ‡å®šå¤´çš„å‚æ•°ã€‚

#+begin_src emacs-lisp
(defun +yas/org-prompt-header-arg (arg question values)
  "Prompt the user to set ARG header property to one of VALUES with QUESTION.
The default value is identified and indicated. If either default is selected,
or no selection is made: nil is returned."
  (let* ((src-block-p (not (looking-back "^#\\+property:[ \t]+header-args:.*" (line-beginning-position))))
         (default
           (or
            (cdr (assoc arg
                        (if src-block-p
                            (nth 2 (org-babel-get-src-block-info t))
                          (org-babel-merge-params
                           org-babel-default-header-args
                           (let ((lang-headers
                                  (intern (concat "org-babel-default-header-args:"
                                                  (+yas/org-src-lang)))))
                             (when (boundp lang-headers) (eval lang-headers t)))))))
            ""))
         default-value)
    (setq values (mapcar
                  (lambda (value)
                    (if (string-match-p (regexp-quote value) default)
                        (setq default-value
                              (concat value " "
                                      (propertize "(default)" 'face 'font-lock-doc-face)))
                      value))
                  values))
    (let ((selection (consult--read values :prompt question :default default-value)))
      (unless (or (string-match-p "(default)$" selection)
                  (string= "" selection))
        selection))))
#+end_src

Finally, we fetch the language information for new source blocks.

Since we're getting this info, we might as well go a step further and also
provide the ability to determine the most popular language in the buffer that
doesn't have any =header-args= set for it (with =#+properties=).

#+begin_src emacs-lisp
(defun +yas/org-src-lang ()
  "Try to find the current language of the src/header at `point'.
Return nil otherwise."
  (let ((context (org-element-context)))
    (pcase (org-element-type context)
      ('src-block (org-element-property :language context))
      ('inline-src-block (org-element-property :language context))
      ('keyword (when (string-match "^header-args:\\([^ ]+\\)" (org-element-property :value context))
                  (match-string 1 (org-element-property :value context)))))))

(defun +yas/org-last-src-lang ()
  "Return the language of the last src-block, if it exists."
  (save-excursion
    (beginning-of-line)
    (when (re-search-backward "^[ \t]*#\\+begin_src" nil t)
      (org-element-property :language (org-element-context)))))

(defun +yas/org-most-common-no-property-lang ()
  "Find the lang with the most source blocks that has no global header-args, else nil."
  (let (src-langs header-langs)
    (save-excursion
      (goto-char (point-min))
      (while (re-search-forward "^[ \t]*#\\+begin_src" nil t)
        (push (+yas/org-src-lang) src-langs))
      (goto-char (point-min))
      (while (re-search-forward "^[ \t]*#\\+property: +header-args" nil t)
        (push (+yas/org-src-lang) header-langs)))

    (setq src-langs
          (mapcar #'car
                  ;; sort alist by frequency (desc.)
                  (sort
                   ;; generate alist with form (value . frequency)
                   (cl-loop for (n . m) in (seq-group-by #'identity src-langs)
                            collect (cons n (length m)))
                   (lambda (a b) (> (cdr a) (cdr b))))))

    (car (cl-set-difference src-langs header-langs :test #'string=))))
#+end_src

**** å°†å¤§å†™è½¬æ¢ä¸ºå°å†™
æ‰€æœ‰äººéƒ½ä¹ æƒ¯ä½¿ç”¨ ~#+CAPITAL~ å…³é”®å­—ï¼Œä¸è¿‡äººä»¬å‘ç° ~#+lowercase~ æ›´å®¹æ˜“é˜…è¯»ã€è§†
è§‰ä¸Šæ›´å¥½ï¼Œå› æ­¤ç°åœ¨åªæœ‰æ‰‹å†Œåœ¨ä½¿ç”¨å¤§å†™ã€‚
#+begin_quote
Org is standardized on lower case. Uppercase is used in the manual as a poor
man's bold, and supported for historical reasons. --- [[https://orgmode.org/list/87tuuw3n15.fsf@nicolasgoaziou.fr][Nicolas Goaziou on the Org ML]]
#+end_quote

ä¸ºäº†é¿å…æœ‰æ—¶ä¸å¾—ä¸æ›´æ–°æ—§æ–‡æ¡£æ—¶å‡ºç°æ–°æ—§é£æ ¼æ··åˆçš„æƒ…å†µï¼Œæˆ‘å†™äº†ä¸€ä¸ªç®€å•åœ°è½¬ç åŠŸèƒ½ã€‚å®ƒå¯
èƒ½ä¼šé—æ¼ä¸€äº›è¾¹ç¼˜æƒ…å†µï¼Œä½†å¤§å¤šæ•°æƒ…å†µä¸‹åº”è¯¥å¯ä»¥å·¥ä½œã€‚

#+begin_src emacs-lisp
(defun org-syntax-convert-keyword-case-to-lower ()
  "Convert all #+KEYWORDS to #+keywords."
  (interactive)
  (save-excursion
    (goto-char (point-min))
    (let ((count 0)
          (case-fold-search nil))
      (while (re-search-forward "^[ \t]*#\\+[A-Z_]+" nil t)
        (unless (s-matches-p "RESULTS" (match-string 0))
          (replace-match (downcase (match-string 0)) t)
          (setq count (1+ count))))
      (message "Replaced %d occurances" count))))
#+end_src

**** å¤–é“¾
***** xkcd
xkcd å¾ˆé…·ï¼Œè®©æˆ‘ä»¬æ›´ç®€ä¾¿ã€æœ‰è¶£çš„æ’å…¥å®ƒä»¬ã€‚æ¯•ç«Ÿå¯ä»¥èŠ‚çœå‡ ç§’é’Ÿï¼ (ä¸€å…±ä¹Ÿå°±è¿™ä¹ˆå¤š)

[[xkcd:1205]]

#+begin_src emacs-lisp
(org-link-set-parameters "xkcd"
                         :image-data-fun #'+org-xkcd-image-fn
                         :follow #'+org-xkcd-open-fn
                         :export #'+org-xkcd-export
                         :complete #'+org-xkcd-complete)

(defun +org-xkcd-open-fn (link)
  (+org-xkcd-image-fn nil link nil))

(defun +org-xkcd-image-fn (protocol link description)
  "Get image data for xkcd num LINK"
  (let* ((xkcd-info (+xkcd-fetch-info (string-to-number link)))
         (img (plist-get xkcd-info :img))
         (alt (plist-get xkcd-info :alt)))
    (message alt)
    (+org-image-file-data-fn protocol (xkcd-download img (string-to-number link)) description)))

(defun +org-xkcd-export (num desc backend _com)
  "Convert xkcd to html/LaTeX form"
  (let* ((xkcd-info (+xkcd-fetch-info (string-to-number num)))
         (img (plist-get xkcd-info :img))
         (alt (plist-get xkcd-info :alt))
         (title (plist-get xkcd-info :title))
         (file (xkcd-download img (string-to-number num))))
    (cond ((org-export-derived-backend-p backend 'html)
           (format "<img class='invertible' src='%s' title=\"%s\" alt='%s'>" img (subst-char-in-string ?\" ?â€œ alt) title))
          ((org-export-derived-backend-p backend 'latex)
           (format "\\begin{figure}[!htb]
  \\centering
  \\includegraphics[scale=0.4]{%s}%s
\\end{figure}" file (if (equal desc (format "xkcd:%s" num)) ""
                      (format "\n  \\caption*{\\label{xkcd:%s} %s}"
                              num
                              (or desc
                                  (format "\\textbf{%s} %s" title alt))))))
          (t (format "https://xkcd.com/%s" num)))))

(defun +org-xkcd-complete (&optional arg)
  "Complete xkcd using `+xkcd-stored-info'"
  (format "xkcd:%d" (+xkcd-select)))
#+end_src
***** Music

***** YouTube
~[[yt:...]]~ é¢„è§ˆä¸é”™ï¼Œä½†å¯¼å‡ºä¸è¡Œï¼Œä¸è¿‡ä½ å¯ä»¥ä¿®å¤å®ƒã€‚
#+begin_src emacs-lisp
(org-link-set-parameters "yt" :export #'+org-export-yt)
(defun +org-export-yt (path desc backend _com)
  (cond ((org-export-derived-backend-p backend 'html)
         (format "<iframe width='440' \
height='335' \
src='https://www.youtube.com/embed/%s' \
frameborder='0' \
allowfullscreen>%s</iframe>" path (or "" desc)))
        ((org-export-derived-backend-p backend 'latex)
         (format "\\href{https://youtu.be/%s}{%s}" path (or desc "youtube")))
        (t (format "https://youtu.be/%s" path))))
#+end_src

**** Fix problematic hooks
å½“ src_elisp{org-mode-hook} å‡½æ•°ä¹‹ä¸€å‡ºé”™æ—¶ï¼Œå®ƒä¼šåœæ­¢ hook æ‰§è¡Œã€‚è¿™æ˜¯ä¸ªå¤§é—®é¢˜
ï¼Œç‰¹åˆ«æ˜¯æœ‰ä¸¤ä¸ª hook ä¼šå‡ºé—®é¢˜ã€‚è®©æˆ‘ä»¬å‡å°‘å‡ºé”™ã€‚

#+begin_src emacs-lisp
(defadvice! shut-up-org-problematic-hooks (orig-fn &rest args)
  :around #'org-fancy-priorities-mode
  :around #'org-superstar-mode
  (ignore-errors (apply orig-fn args)))
#+end_src

**** åŸºäº org-lint çš„ Flycheck
Org ç®€å•ä½†ä¸æ„å‘³ç€ä¸ä¼šå‡ºç°æ ¼å¼é”™è¯¯ã€‚åº†å¹¸å§ï¼Œorg çš„æ ¼å¼é”™è¯¯æ¯”å‹ç¼©çš„ XML
(DOCX/ODT) å°‘å¾—å¤šï¼Œç‰¹åˆ«æ˜¯ ~org-lint~ è¿™ä¸ªå°å·¥å…·ä¸ Org æ†ç»‘åœ¨ä¸€èµ·çš„æƒ…å†µä¸‹ã€‚

[[xkcd:2109]]

ç›®å‰ä¸ºæ­¢ Flycheck ä¸æ”¯æŒ Orgï¼Œå¹¶ä¸”æ²¡æœ‰ä»»ä½•åŒ…åšè¿™ä»¶äº‹ â˜¹ã€‚ç„¶è€Œï¼Œ ~org-lint~ çš„è¿™
ä¸ª issue ç”¨äº† [[https://github.com/flycheck/flycheck/issues/1757#issuecomment-759546940][ä¸€ç‚¹ä»£ç ]] å°±æˆäº†è¿™ä»¶äº‹ã€‚è¿™æ˜¯å‰ªåˆ‡æ¿å‘æ˜çš„ç›®çš„å—ï¼Ÿè¯è™½å¦‚æ­¤ï¼Œé€‰æ‹©è¿™ä»½ä»£
ç ï¼ŒåŒæ‰‹åˆåï¼Œç¥ˆç¥·å§ã€‚

#+begin_src emacs-lisp
(defconst flycheck-org-lint-form
  (flycheck-prepare-emacs-lisp-form
    (require 'org)
    (require 'org-attach)
    (let ((source (car command-line-args-left))
          (process-default-directory default-directory))
      (with-temp-buffer
        (insert-file-contents source 'visit)
        (setq buffer-file-name source)
        (setq default-directory process-default-directory)
        (delay-mode-hooks (org-mode))
        (setq delayed-mode-hooks nil)
        (dolist (err (org-lint))
          (let ((inf (cl-second err)))
            (princ (elt inf 0))
            (princ ": ")
            (princ (elt inf 2))
            (terpri)))))))

(defconst flycheck-org-lint-variables
  '(org-directory
    org-id-locations
    org-id-locations-file
    org-attach-id-dir
    org-attach-use-inheritance
    org-attach-id-to-path-function-list
    org-link-parameters)
  "Variables inherited by the org-lint subprocess.")

(defun flycheck-org-lint-variables-form ()
  (require 'org-attach)  ; Needed to make variables available
  `(progn
     ,@(seq-map (lambda (opt) `(setq-default ,opt ',(symbol-value opt)))
                (seq-filter #'boundp flycheck-org-lint-variables))))

(flycheck-define-checker org-lint
  "Org buffer checker using `org-lint'."
  :command ("emacs" (eval flycheck-emacs-args)
            "--eval" (eval (concat "(add-to-list 'load-path \""
                                   (file-name-directory (locate-library "org"))
                                   "\")"))
            "--eval" (eval (flycheck-sexp-to-string
                            (flycheck-org-lint-variables-form)))
            "--eval" (eval (flycheck-sexp-to-string
                            (flycheck-org-lint-customisations-form)))
            "--eval" (eval flycheck-org-lint-form)
            "--" source)
  :error-patterns
  ((error line-start line ": " (message) line-end))
  :modes org-mode)
#+end_src

äº‹å®è¯æ˜å®ƒå‡ ä¹å¯ä»¥å·¥ä½œã€‚è¿è¡Œè¯¥ç‰‡æ®µåè¿è¡Œ =M-x flycheck-verify-setup=
ä¼šäº§ç”Ÿä»¥ä¸‹ç»“æœï¼š
#+begin_example
The following syntax checkers are not registered:
  - org-lint
Try adding these syntax checkers to `flycheck-checkers'.
#+end_example

è¿™éå¸¸æœ‰ç”¨ï¼Œç°åœ¨å°±é“¾æ¥ ğŸ™‚ã€‚
#+begin_src emacs-lisp
(add-to-list 'flycheck-checkers 'org-lint)
#+end_src

å®ƒç¼ºå°‘è‡ªå®šä¹‰é“¾æ¥ç±»å‹ï¼Œä½†åªéœ€å°† ~org-link-parameters~ æ·»åŠ åˆ°
~flycheck-org-lint-variables~ å³å¯è½»æ¾è§£å†³ã€‚

å‰©ä¸‹ä¸€ä¸ªå°çƒ¦æ¼ï¼Œå®ƒä¼šå°† =#+options= ä½œä¸ºé”™è¯¯æŠ¥å‘Šï¼Œæˆ‘ä»¬éœ€è¦å‘Šè¯‰ ~org-lint~ ä¸éœ€è¦
åŠ è½½æ‰€æœ‰çš„é…ç½®ï¼Œè™½ç„¶ä»£ç é‡å¤äº†ä¸å¥½ï¼Œä½†è‡³å°‘å¹¶ä¸å¤šã€‚

#+name: org-syntax-modifications
#+begin_src emacs-lisp :tangle no
(defun flycheck-org-lint-customisations-form ()
  `(progn
     (require 'ox)
     (cl-pushnew '(:latex-cover-page nil "coverpage" nil)
                 (org-export-backend-options (org-export-get-backend 'latex)))
     (cl-pushnew '(:latex-font-set nil "fontset" nil)
                 (org-export-backend-options (org-export-get-backend 'latex)))))
#+end_src

*** è§†è§‰
åœ¨è¿™é‡Œï¼Œæˆ‘å°è¯•åšä¸¤ä»¶äº‹ï¼šé€šè¿‡å­—ä½“æ›´æ”¹ç­‰æ”¹è¿›å„ç§æ–‡æ¡£çš„æ ·å¼ï¼Œä»¥ä»å½“å‰ä¸»é¢˜è·å–é¢œè‰²ã€‚

[[xkcd:1882]]

**** å­—ä½“å±•ç¤º
ä¸å¤šè¯´ï¼Œ ~+org-pretty-mode~ ã€‚
#+begin_src emacs-lisp
(add-hook 'org-mode-hook #'+org-pretty-mode)
#+end_src

è®©æ ‡é¢˜æ›´å¤§ä¸€äº›
#+begin_src emacs-lisp
(custom-set-faces!
  '(outline-1 :weight extra-bold :height 1.25)
  '(outline-2 :weight bold :height 1.15)
  '(outline-3 :weight bold :height 1.12)
  '(outline-4 :weight semi-bold :height 1.09)
  '(outline-5 :weight semi-bold :height 1.06)
  '(outline-6 :weight semi-bold :height 1.03)
  '(outline-8 :weight semi-bold)
  '(outline-9 :weight semi-bold))
#+end_src

æ–‡æ¡£æ ‡é¢˜ä¹Ÿä¸€æ ·ã€‚
#+begin_src emacs-lisp
(custom-set-faces!
  '(org-document-title :height 1.2))
#+end_src

è®©é”™è¯¯é¢æ¿æœ‰ä¸€ä¸ªæ—¶é—´æœŸé™ä¼¼ä¹æ˜¯åˆç†çš„ã€‚
#+begin_src emacs-lisp
(setq org-agenda-deadline-faces
      '((1.001 . error)
        (1.0 . org-warning)
        (0.5 . org-upcoming-deadline)
        (0.0 . org-upcoming-distant-deadline)))
#+end_src

ç„¶åï¼Œæˆ‘ä»¬å¯ä»¥é€šè¿‡å°†å¼•ç”¨å—è®¾ç½®ä¸º /æ–œä½“/ æ¥ä½¿å®ƒä»¬æ›´åŠ çªå‡ºã€‚
#+begin_src emacs-lisp
(setq org-fontify-quote-and-verse-blocks t)
#+end_src

Org æ–‡ä»¶çœ‹èµ·æ¥å¾ˆæ¼‚äº®ï¼Œå°¤å…¶æ˜¯ä¸€äº›è‡ªå®šä¹‰çš„éƒ¨åˆ†ã€‚ç„¶è€Œè¿™æ˜¯æœ‰ä»£ä»·çš„ï¼Œå­—ä½“é”å®šæ¶ˆè€—æå¤§ã€‚
åœ¨å¤§æ–‡ä»¶ä¸­ï¼Œæ„Ÿè§‰åœ¨ç³–èœœä¸­æ‰“å­—å¹¶ä¸å¥½ç©ï¼Œä½†æœ‰ä¸€ç§æ–¹æ³•å¯ä»¥è®©æˆ‘åœ¨æ‰“å­—æ—¶æ¨è¿Ÿå­—ä½“é”å®šï¼Œä½¿ä½“
éªŒæ›´å…·å“åº”æ€§ã€‚

#+begin_src emacs-lisp
(defun locally-defer-font-lock ()
  "Set jit-lock defer and stealth, when buffer is over a certain size."
  (when (> (buffer-size) 50000)
    (setq-local jit-lock-defer-time 0.05
                jit-lock-stealth-time 1)))

(add-hook 'org-mode-hook #'locally-defer-font-lock)
#+end_src

æ˜¾ç„¶è¿™ä¼šå¼•èµ·ä¸€äº›äººçš„é—®é¢˜ï¼Œä½†é™¤äº†é¢„æœŸçš„å­—ä½“åŒ–è½»å¾®å»¶è¿Ÿä¹‹å¤–ï¼Œæˆ‘æ²¡æœ‰æ³¨æ„åˆ°ä»»ä½•é—®é¢˜ï¼Œæ‰€ä»¥
åœ¨æˆ‘ä¼šå¥½å¥½ä½¿ç”¨çš„ã€‚

**** å†…è”ä»£ç å—çš„å­—ä½“åŒ–
Org ä½¿ç”¨ =#+begin_src= å—åšäº†ä¸€äº›è¶…æ£’çš„äº‹æƒ…ï¼Œæ¯”å¦‚åœ¨åå°ä½¿ç”¨ font-lock ä½œä¸ºè¯­è¨€
çš„ä¸»è¦æ¨¡å¼ï¼Œå¹¶æ‹‰å‡ºå½©è‰²ç»“æœã€‚ç›¸æ¯”ä¹‹ä¸‹ï¼Œå†…è” =src_= å—åœ¨æŸç§ç¨‹åº¦ä¸Šè¢«å¿½ç•¥äº†ã€‚

æˆ‘ä¸æ˜¯ç¬¬ä¸€ä¸ªæœ‰è¿™ç§æ„Ÿè§‰çš„äººï¼Œè°¢å¤©è°¢åœ°ï¼Œå…¶ä»–äººå·²ç» [[https://stackoverflow.com/questions/20309842/how-to-syntax-highlight-for-org-mode-inline-source-code-src-lang/28059832][é€šè¿‡ stackexchange]] æ¥è¡¨è¾¾ä»–ä»¬å¯¹
å†…è” src å­—ä½“åŒ–çš„æ¸´æœ›ã€‚æˆ‘æ‰“ç®—å·å–ä»–ä»¬çš„å·¥ä½œï¼Œä½†ä¸å¹¸çš„æ˜¯ï¼Œä»–ä»¬æ²¡æœ‰ç»™å‡º /çœŸæ­£çš„/ å­—
ä½“åŒ–ä»£ç ï¼Œè€Œåªæ˜¯å°† =org-code= face åº”ç”¨äºå†…å®¹ã€‚

æˆ‘ä»¬å¯ä»¥åšçš„æ›´å¥½ï¼ç”¨ ~org-src-font-lock-fontify-block~ æˆ‘ä»¬å¯ä»¥åº”ç”¨åˆé€‚è¯­è¨€æ”¯
æŒçš„è¯­æ³•é«˜äº®ã€‚ç„¶åï¼Œæ¥ä¸‹æ¥ç”¨ ={{{results(...)}}}= ï¼Œå®ƒèƒ½å°† =org-block= åº”ç”¨åˆ°
åŒ¹é…ä¸­ï¼Œç„¶åé€šè¿‡æ¨¡ä»¿ ~prettify-symbols-mode~ çš„è¡Œä¸ºéšè—å€¼ç¯å¢ƒç»“æ„ã€‚

#+begin_warning
è¿™ç›®å‰ä»…çªå‡ºæ˜¾ç¤ºæ¯è¡Œä¸­çš„ä¸€ä¸ªå†…è” src å—ã€‚æˆ‘ä¸çŸ¥é“å®ƒä¸ºä»€ä¹ˆä¼šåœæ­¢ï¼Œä½†æˆ‘å®æ„¿å®ƒæ²¡æœ‰ã€‚å¦‚
æœæ‚¨çŸ¥é“å‘ç”Ÿäº†ä»€ä¹ˆæˆ–å¦‚ä½•è§£å†³æ­¤é—®é¢˜ï¼Œè¯·ä¸æˆ‘ä»¬è”ç³»ã€‚
#+end_warning

#+begin_src emacs-lisp
(defvar org-prettify-inline-results t
  "Whether to use (ab)use prettify-symbols-mode on {{{results(...)}}}.
Either t or a cons cell of strings which are used as substitutions
for the start and end of inline results, respectively.")

(defvar org-fontify-inline-src-blocks-max-length 200
  "Maximum content length of an inline src block that will be fontified.")

(defun org-fontify-inline-src-blocks (limit)
  "Try to apply `org-fontify-inline-src-blocks-1'."
  (condition-case nil
      (org-fontify-inline-src-blocks-1 limit)
    (error (message "Org mode fontification error in %S at %d"
                    (current-buffer)
                    (line-number-at-pos)))))

(defun org-fontify-inline-src-blocks-1 (limit)
  "Fontify inline src_LANG blocks, from `point' up to LIMIT."
  (let ((case-fold-search t)
        (initial-point (point)))
    (while (re-search-forward "\\_<src_\\([^ \t\n[{]+\\)[{[]?" limit t) ; stolen from `org-element-inline-src-block-parser'
      (let ((beg (match-beginning 0))
            pt
            (lang-beg (match-beginning 1))
            (lang-end (match-end 1)))
        (remove-text-properties beg lang-end '(face nil))
        (font-lock-append-text-property lang-beg lang-end 'face 'org-meta-line)
        (font-lock-append-text-property beg lang-beg 'face 'shadow)
        (font-lock-append-text-property beg lang-end 'face 'org-block)
        (setq pt (goto-char lang-end))
        ;; `org-element--parse-paired-brackets' doesn't take a limit, so to
        ;; prevent it searching the entire rest of the buffer we temporarily
        ;; narrow the active region.
        (save-restriction
          (narrow-to-region beg (min (point-max) limit (+ lang-end org-fontify-inline-src-blocks-max-length)))
          (when (ignore-errors (org-element--parse-paired-brackets ?\[))
            (remove-text-properties pt (point) '(face nil))
            (font-lock-append-text-property pt (point) 'face 'org-block)
            (setq pt (point)))
          (when (ignore-errors (org-element--parse-paired-brackets ?\{))
            (remove-text-properties pt (point) '(face nil))
            (font-lock-append-text-property pt (1+ pt) 'face '(org-block shadow))
            (unless (= (1+ pt) (1- (point)))
              (if org-src-fontify-natively
                  (org-src-font-lock-fontify-block (buffer-substring-no-properties lang-beg lang-end) (1+ pt) (1- (point)))
                (font-lock-append-text-property (1+ pt) (1- (point)) 'face 'org-block)))
            (font-lock-append-text-property (1- (point)) (point) 'face '(org-block shadow))
            (setq pt (point))))
        (when (and org-prettify-inline-results (re-search-forward "\\= {{{results(" limit t))
          (font-lock-append-text-property pt (1+ pt) 'face 'org-block)
          (goto-char pt))))
    (when org-prettify-inline-results
      (goto-char initial-point)
      (org-fontify-inline-src-results limit))))

(defun org-fontify-inline-src-results (limit)
  (while (re-search-forward "{{{results(\\(.+?\\))}}}" limit t)
    (remove-list-of-text-properties (match-beginning 0) (point)
                                    '(composition
                                      prettify-symbols-start
                                      prettify-symbols-end))
    (font-lock-append-text-property (match-beginning 0) (match-end 0) 'face 'org-block)
    (let ((start (match-beginning 0)) (end (match-beginning 1)))
      (with-silent-modifications
        (compose-region start end (if (eq org-prettify-inline-results t) "âŸ¨" (car org-prettify-inline-results)))
        (add-text-properties start end `(prettify-symbols-start ,start prettify-symbols-end ,end))))
    (let ((start (match-end 1)) (end (point)))
      (with-silent-modifications
        (compose-region start end (if (eq org-prettify-inline-results t) "âŸ©" (cdr org-prettify-inline-results)))
        (add-text-properties start end `(prettify-symbols-start ,start prettify-symbols-end ,end))))))

(defun org-fontify-inline-src-blocks-enable ()
  "Add inline src fontification to font-lock in Org.
Must be run as part of `org-font-lock-set-keywords-hook'."
  (setq org-font-lock-extra-keywords
        (append org-font-lock-extra-keywords '((org-fontify-inline-src-blocks)))))

(add-hook 'org-font-lock-set-keywords-hook #'org-fontify-inline-src-blocks-enable)
#+end_src

Doom ä¸»é¢˜é¢å¤–çš„å­—ä½“åŒ–ç›¸æ¯”å¸®åŠ©åˆ™æ›´å¤šçš„æ˜¯é—®é¢˜ã€‚
#+begin_src emacs-lisp
(setq doom-themes-org-fontify-special-tags nil)
#+end_src

**** ç¬¦å·
æ›´æ”¹ç”¨äºæŠ˜å é¡¹ç›®çš„å­—ç¬¦ä¹Ÿå¾ˆå¥½ (é»˜è®¤æƒ…å†µä¸‹ ~â€¦~)ï¼Œæˆ‘è®¤ä¸ºç”¨ ~â–¾~ æ›´é€‚åˆæŒ‡ç¤º ã€ŒæŠ˜å éƒ¨åˆ†
ã€ã€‚å¹¶åœ¨é»˜è®¤çš„å››ä¸ªåˆ—è¡¨ä¸­æ·»åŠ ä¸€ä¸ªé¢å¤–çš„ ~org-bullet~ ã€‚æˆ‘è¿˜æ·»åŠ äº†ä¸€äº›æœ‰è¶£çš„æ›¿ä»£å“
ï¼Œåªæ˜¯æ³¨é‡Šæ‰äº†ã€‚
#+begin_src emacs-lisp
(after! org-superstar
  (setq org-superstar-headline-bullets-list '("â—‰" "â—‹" "âœ¸" "âœ¿" "âœ¤" "âœœ" "â—†" "â–¶")
        org-superstar-prettify-item-bullets t ))

(setq org-ellipsis " â–¾ "
      org-hide-leading-stars t
      org-priority-highest ?A
      org-priority-lowest ?E
      org-priority-faces
      '((?A . 'all-the-icons-red)
        (?B . 'all-the-icons-orange)
        (?C . 'all-the-icons-yellow)
        (?D . 'all-the-icons-green)
        (?E . 'all-the-icons-blue)))
#+end_src

å°† Unicode å­—ç¬¦ç”¨äºå¤é€‰æ¡†å’Œå…¶ä»–å‘½ä»¤ä¹Ÿå¾ˆå¥½ã€‚
#+begin_src emacs-lisp
(appendq! +ligatures-extra-symbols
          `(:checkbox      "â˜"
            :pending       "â—¼"
            :checkedbox    "â˜‘"
            :list_property "âˆ·"
            :em_dash       "â€”"
            :ellipses      "â€¦"
            :arrow_right   "â†’"
            :arrow_left    "â†"
            :title         "ğ™"
            :subtitle      "ğ™©"
            :author        "ğ˜¼"
            :date          "ğ˜¿"
            :property      "â˜¸"
            :options       "âŒ¥"
            :startup       "â»"
            :macro         "ğ“œ"
            :html_head     "ğŸ…·"
            :html          "ğŸ…—"
            :latex_class   "ğŸ„»"
            :latex_header  "ğŸ…»"
            :beamer_header "ğŸ…‘"
            :latex         "ğŸ…›"
            :attr_latex    "ğŸ„›"
            :attr_html     "ğŸ„—"
            :attr_org      "â’ª"
            :begin_quote   "â"
            :end_quote     "â"
            :caption       "â˜°"
            :header        "â€º"
            :results       "ğŸ ¶"
            :begin_export  "â©"
            :end_export    "âª"
            :properties    "âš™"
            :end           "âˆ"
            :priority_a   ,(propertize "âš‘" 'face 'all-the-icons-red)
            :priority_b   ,(propertize "â¬†" 'face 'all-the-icons-orange)
            :priority_c   ,(propertize "â– " 'face 'all-the-icons-yellow)
            :priority_d   ,(propertize "â¬‡" 'face 'all-the-icons-green)
            :priority_e   ,(propertize "â“" 'face 'all-the-icons-blue)))
(set-ligatures! 'org-mode
  :merge t
  :checkbox      "[ ]"
  :pending       "[-]"
  :checkedbox    "[X]"
  :list_property "::"
  :em_dash       "---"
  :ellipsis      "..."
  :arrow_right   "->"
  :arrow_left    "<-"
  :title         "#+title:"
  :subtitle      "#+subtitle:"
  :author        "#+author:"
  :date          "#+date:"
  :property      "#+property:"
  :options       "#+options:"
  :startup       "#+startup:"
  :macro         "#+macro:"
  :html_head     "#+html_head:"
  :html          "#+html:"
  :latex_class   "#+latex_class:"
  :latex_header  "#+latex_header:"
  :beamer_header "#+beamer_header:"
  :latex         "#+latex:"
  :attr_latex    "#+attr_latex:"
  :attr_html     "#+attr_html:"
  :attr_org      "#+attr_org:"
  :begin_quote   "#+begin_quote"
  :end_quote     "#+end_quote"
  :caption       "#+caption:"
  :header        "#+header:"
  :begin_export  "#+begin_export"
  :end_export    "#+end_export"
  :results       "#+RESULTS:"
  :property      ":PROPERTIES:"
  :end           ":END:"
  :priority_a    "[#A]"
  :priority_b    "[#B]"
  :priority_c    "[#C]"
  :priority_d    "[#D]"
  :priority_e    "[#E]")
(plist-put +ligatures-extra-symbols :name "â")
#+end_src

~org-superstar-mode~ å¾ˆæ£’ï¼Œå½“æˆ‘ä»¬è¿™æ ·åšçš„æ—¶å€™ï¼Œæˆ‘ä»¬ä¹Ÿå¯ä»¥è®©æ ‡ç­¾æ›´æ¼‚äº®ã€‚
#+begin_src emacs-lisp :tangle packages.el
;; (package! org-pretty-tags :pin "5c7521651b35ae9a7d3add4a66ae8cc176ae1c76")
#+end_src

#+begin_src emacs-lisp
;; (use-package org-pretty-tags
;; :config
;;  (setq org-pretty-tags-surrogate-strings
;;        `(("uni"        . ,(all-the-icons-faicon   "graduation-cap" :face 'all-the-icons-purple  :v-adjust 0.01))
;;          ("ucc"        . ,(all-the-icons-material "computer"       :face 'all-the-icons-silver  :v-adjust 0.01))
;;          ("assignment" . ,(all-the-icons-material "library_books"  :face 'all-the-icons-orange  :v-adjust 0.01))
;;          ("test"       . ,(all-the-icons-material "timer"          :face 'all-the-icons-red     :v-adjust 0.01))
;;          ("lecture"    . ,(all-the-icons-fileicon "keynote"        :face 'all-the-icons-orange  :v-adjust 0.01))
;;          ("email"      . ,(all-the-icons-faicon   "envelope"       :face 'all-the-icons-blue    :v-adjust 0.01))
;;          ("read"       . ,(all-the-icons-octicon  "book"           :face 'all-the-icons-lblue   :v-adjust 0.01))
;;          ("article"    . ,(all-the-icons-octicon  "file-text"      :face 'all-the-icons-yellow  :v-adjust 0.01))
;;          ("web"        . ,(all-the-icons-faicon   "globe"          :face 'all-the-icons-green   :v-adjust 0.01))
;;          ("info"       . ,(all-the-icons-faicon   "info-circle"    :face 'all-the-icons-blue    :v-adjust 0.01))
;;          ("issue"      . ,(all-the-icons-faicon   "bug"            :face 'all-the-icons-red     :v-adjust 0.01))
;;          ("someday"    . ,(all-the-icons-faicon   "calendar-o"     :face 'all-the-icons-cyan    :v-adjust 0.01))
;;          ("idea"       . ,(all-the-icons-octicon  "light-bulb"     :face 'all-the-icons-yellow  :v-adjust 0.01))
;;          ("emacs"      . ,(all-the-icons-fileicon "emacs"          :face 'all-the-icons-lpurple :v-adjust 0.01))))
;;  (org-pretty-tags-global-mode))
#+end_src

**** LaTeX ç‰‡æ®µ
***** æ›´æ¼‚äº®çš„é«˜äº®
é¦–å…ˆè®©è¿™äº›ç‰‡æ®µçœ‹èµ·æ¥å¾ˆæ£’ã€‚
#+begin_src emacs-lisp
(setq! org-highlight-latex-and-related '(native script entities))
#+end_src

ç„¶è€Œé€šè¿‡ä½¿ç”¨åŸç”Ÿçªå‡ºæ˜¾ç¤ºï¼Œæ·»åŠ äº† =org-block= ï¼Œè¿™çœ‹èµ·æ¥ä¸å¤ªå¥½ --- å°¤å…¶æ˜¯åœ¨é¢„è§ˆ
ç‰‡æ®µæ—¶ã€‚

ç†æƒ³æƒ…å†µä¸‹ ~org-src-font-lock-fontify-block~ ä¸ä¼šæ·»åŠ  =org-block= ï¼Œä½†æˆ‘
ä»¬å¯ä»¥é€šè¿‡æ·»åŠ å¸¦æœ‰ =:inherit default= é¢æ¥é¿å…æ•´ä¸ªåŠŸèƒ½ï¼Œè¿™å°†è¦†ç›–èƒŒæ™¯é¢œè‰²ã€‚

æ£€æŸ¥ ~org-do-latex-and-related~ æ˜¾ç¤º ="latex"= æ˜¯ä¼ é€’çš„è¯­è¨€å‚æ•°ï¼Œå› æ­¤æˆ‘ä»¬
å¯ä»¥å¦‚ä¸Šæ‰€è¿°è¦†ç›–èƒŒæ™¯ã€‚
#+begin_src emacs-lisp
(require 'org-src)
(add-to-list 'org-src-block-faces '("latex" (:inherit default :extend t)))
#+end_src
***** æ›´å¿«çš„æ¸²æŸ“
æ¯”è¯­æ³•é«˜äº®çš„ LaTeX æ›´å¥½çš„æ˜¯ /å‘ˆç°/ LaTeXï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨ =org-fragtog= è‡ªåŠ¨æ‰§
è¡Œæ­¤æ“ä½œã€‚

#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! org-fragtog :pin "680606189d5d28039e6f9301b55ec80517a24005")
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! org-fragtog
  :hook (org-mode . org-fragtog-mode))
#+end_src

***** æ›´å¥½çœ‹çš„æ¸²æŸ“
è‡ªå®šä¹‰ LaTeX ç‰‡æ®µçš„å¤–è§‚å¾ˆèˆ’é€‚ï¼Œè¿™æ ·å®ƒä»¬å°±æ›´é€‚åˆæ–‡æœ¬äº† --- æ¯”å¦‚è¿™ä¸ª
$\sqrt{\beta^2+3}-\sum_{\phi=1}^\infty \frac{x^\phi-1}{\Gamma(a)}$ ã€‚
ä»æ·»åŠ è¡¬çº¿å­—ä½“å¼€å§‹ã€‚æˆ‘è¿˜æƒ³ä½¿ç”¨ =bmc-maths= çš„ä¸€äº›åŠŸèƒ½ï¼Œæ‰€ä»¥æˆ‘ä»¬ä¹Ÿä¼šåŠ è½½å®ƒã€‚

#+begin_src emacs-lisp
(setq org-format-latex-header "\\documentclass{article}
\\usepackage[usenames]{xcolor}

\\usepackage[T1]{fontenc}

\\usepackage{booktabs}

\\pagestyle{empty}             % do not remove
% The settings below are copied from fullpage.sty
\\setlength{\\textwidth}{\\paperwidth}
\\addtolength{\\textwidth}{-3cm}
\\setlength{\\oddsidemargin}{1.5cm}
\\addtolength{\\oddsidemargin}{-2.54cm}
\\setlength{\\evensidemargin}{\\oddsidemargin}
\\setlength{\\textheight}{\\paperheight}
\\addtolength{\\textheight}{-\\headheight}
\\addtolength{\\textheight}{-\\headsep}
\\addtolength{\\textheight}{-\\footskip}
\\addtolength{\\textheight}{-3cm}
\\setlength{\\topmargin}{1.5cm}
\\addtolength{\\topmargin}{-2.54cm}
% my custom stuff
\\usepackage[nofont,plaindd]{bmc-maths}
\\usepackage{arev}
")
#+end_src

æ—¢ç„¶æˆ‘ä»¬å¯ä»¥ï¼Œä¸å¦‚è®©èƒŒæ™¯é¢œè‰²åŒ¹é… =é»˜è®¤= ï¼Œæˆ‘ä»¬è®©å®ƒé€æ˜ã€‚
#+begin_src emacs-lisp
(setq org-format-latex-options
      (plist-put org-format-latex-options :background "Transparent"))
#+end_src
***** æ¸²æŸ“é€Ÿåº¦æµ‹è¯•
æˆ‘ä»¬å¯ä»¥ä» ~dvi~ æˆ– ~pdf~ æ–‡ä»¶è¿›è¡Œæ¸²æŸ“ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬å¯¹ ~latex~ å’Œ ~pdflatex~
è¿›è¡ŒåŸºå‡†æµ‹è¯•ã€‚
| ~latex~ time | ~pdflatex~ time |
|--------------+-----------------|
| 135 \pm 2 ms | 215 \pm 3 ms    |

åœ¨æ¸²æŸ“æ–¹é¢ï¼Œæœ‰ä¸¤ä¸ªæˆ‘æ„Ÿå…´è¶£çš„ ~.dvi~ åˆ°å›¾åƒçš„è½¬æ¢å™¨ï¼š ~dvipng~ å’Œ ~dvisvgm~ ã€‚ç„¶åä½¿ç”¨ ~.pdf~ æˆ‘ä»¬æœ‰ ~pdf2svg~ ã€‚å¯¹äºå†…è”é¢„è§ˆæˆ‘ä»¬å…³å¿ƒé€Ÿåº¦ï¼Œè€Œå¯¹äºå¯¼å‡ºï¼Œæˆ‘
ä»¬å…³å¿ƒæ–‡ä»¶å¤§å°ä¸”æ›´å–œæ¬¢çŸ¢é‡å›¾å½¢ã€‚

ä½¿ç”¨ä¸Šè¿° LaTeX ç¨‹åºå’ŒåŸºå‡†æµ‹è¯•ç»“æœå¦‚ä¸‹ï¼š
| ~dvipng~ time | ~dvisvgm~ time | ~pdf2svg~ time |
|---------------+----------------+----------------|
| 89 \pm 2 ms   | 178 \pm 2 ms   | 12 \pm 2 ms    |

ç°åœ¨è®©æˆ‘ä»¬ç»“åˆèµ·æ¥çœ‹çœ‹ä»€ä¹ˆæ˜¯æœ€å¥½çš„
| Tool chain             | Total time   | Resultant file size |
|------------------------+--------------+---------------------|
| ~latex~ + ~dvipng~     | 226 \pm 2 ms | 7 KiB               |
| ~latex~ + ~dvisvgm~    | 392 \pm 4 ms | 8 KiB               |
| ~pdflatex~ + ~pdf2svg~ | 230 \pm 2 ms | 16 KiB              |

æ‰€ä»¥æˆ‘ä»¬åœ¨ Emacs ä¸­ç”¨ ~dvipng~ æ¥é¢„è§ˆ LaTeX ç‰‡æ®µå§ï¼Œå°† ~dvisvgm~ æ‹¥ç”¨äº
[[LaTeX Rendering]]ã€‚

#+begin_warning
ä¸å¹¸çš„æ˜¯ï¼Œæ®è¯´ SVG å¤§å°ä¼¼ä¹å¾ˆæ¼äººï¼Œæ‰€ä»¥æˆ‘ä»¬ç°åœ¨ä¸è¿™æ ·åšã€‚
#+end_warning

**** ä» [[https://github.com/jkitchin/scimax][scimax]] å·æ¥çš„ (ç°åœ¨æ­£åœ¨åŠå·¥ä½œ)
å¯¹ç‰‡æ®µåšä¸€äº›è°ƒæ•´
#+begin_src emacs-lisp
(defun scimax-org-latex-fragment-justify (justification)
  "Justify the latex fragment at point with JUSTIFICATION.
JUSTIFICATION is a symbol for 'left, 'center or 'right."
  (interactive
   (list (intern-soft
          (completing-read "Justification (left): " '(left center right)
                           nil t nil nil 'left))))
  (let* ((ov (ov-at))
         (beg (ov-beg ov))
         (end (ov-end ov))
         (shift (- beg (line-beginning-position)))
         (img (overlay-get ov 'display))
         (img (and (and img (consp img) (eq (car img) 'image)
                        (image-type-available-p (plist-get (cdr img) :type)))
                   img))
         space-left offset)
    (when (and img
               ;; This means the equation is at the start of the line
               (= beg (line-beginning-position))
               (or
                (string= "" (s-trim (buffer-substring end (line-end-position))))
                (eq 'latex-environment (car (org-element-context)))))
      (setq space-left (- (window-max-chars-per-line) (car (image-size img)))
            offset (floor (cond
                           ((eq justification 'center)
                            (- (/ space-left 2) shift))
                           ((eq justification 'right)
                            (- space-left shift))
                           (t
                            0))))
      (when (>= offset 0)
        (overlay-put ov 'before-string (make-string offset ?\ ))))))

(defun scimax-org-latex-fragment-justify-advice (beg end image imagetype)
  "After advice function to justify fragments."
  (scimax-org-latex-fragment-justify (or (plist-get org-format-latex-options :justify) 'left)))


(defun scimax-toggle-latex-fragment-justification ()
  "Toggle if LaTeX fragment justification options can be used."
  (interactive)
  (if (not (get 'scimax-org-latex-fragment-justify-advice 'enabled))
      (progn
        (advice-add 'org--format-latex-make-overlay :after 'scimax-org-latex-fragment-justify-advice)
        (put 'scimax-org-latex-fragment-justify-advice 'enabled t)
        (message "Latex fragment justification enabled"))
    (advice-remove 'org--format-latex-make-overlay 'scimax-org-latex-fragment-justify-advice)
    (put 'scimax-org-latex-fragment-justify-advice 'enabled nil)
    (message "Latex fragment justification disabled")))
#+end_src

è¿˜æœ‰è¿™äº›ç­‰å¼ç¼–å·
#+begin_src emacs-lisp
;; Numbered equations all have (1) as the number for fragments with vanilla
;; org-mode. This code injects the correct numbers into the previews so they
;; look good.
(defun scimax-org-renumber-environment (orig-func &rest args)
  "A function to inject numbers in LaTeX fragment previews."
  (let ((results '())
        (counter -1)
        (numberp))
    (setq results (cl-loop for (begin . env) in
                           (org-element-map (org-element-parse-buffer) 'latex-environment
                             (lambda (env)
                               (cons
                                (org-element-property :begin env)
                                (org-element-property :value env))))
                           collect
                           (cond
                            ((and (string-match "\\\\begin{equation}" env)
                                  (not (string-match "\\\\tag{" env)))
                             (cl-incf counter)
                             (cons begin counter))
                            ((string-match "\\\\begin{align}" env)
                             (prog2
                                 (cl-incf counter)
                                 (cons begin counter)
                               (with-temp-buffer
                                 (insert env)
                                 (goto-char (point-min))
                                 ;; \\ is used for a new line. Each one leads to a number
                                 (cl-incf counter (count-matches "\\\\$"))
                                 ;; unless there are nonumbers.
                                 (goto-char (point-min))
                                 (cl-decf counter (count-matches "\\nonumber")))))
                            (t
                             (cons begin nil)))))

    (when (setq numberp (cdr (assoc (point) results)))
      (setf (car args)
            (concat
             (format "\\setcounter{equation}{%s}\n" numberp)
             (car args)))))

  (apply orig-func args))


(defun scimax-toggle-latex-equation-numbering ()
  "Toggle whether LaTeX fragments are numbered."
  (interactive)
  (if (not (get 'scimax-org-renumber-environment 'enabled))
      (progn
        (advice-add 'org-create-formula-image :around #'scimax-org-renumber-environment)
        (put 'scimax-org-renumber-environment 'enabled t)
        (message "Latex numbering enabled"))
    (advice-remove 'org-create-formula-image #'scimax-org-renumber-environment)
    (put 'scimax-org-renumber-environment 'enabled nil)
    (message "Latex numbering disabled.")))

(advice-add 'org-create-formula-image :around #'scimax-org-renumber-environment)
(put 'scimax-org-renumber-environment 'enabled t)
#+end_src
**** Org Plot
æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ =org-plot= ä¸­çš„ä¸€äº›å˜é‡æ¥ä½¿ç”¨å½“å‰çš„ doom ä¸»é¢˜é¢œè‰²ã€‚
#+begin_src emacs-lisp
(after! org-plot
  (defun org-plot/generate-theme (_type)
    "Use the current Doom theme colours to generate a GnuPlot preamble."
    (format "
fgt = \"textcolor rgb '%s'\" # foreground text
fgat = \"textcolor rgb '%s'\" # foreground alt text
fgl = \"linecolor rgb '%s'\" # foreground line
fgal = \"linecolor rgb '%s'\" # foreground alt line

# foreground colors
set border lc rgb '%s'
# change text colors of  tics
set xtics @fgt
set ytics @fgt
# change text colors of labels
set title @fgt
set xlabel @fgt
set ylabel @fgt
# change a text color of key
set key @fgt

# line styles
set linetype 1 lw 2 lc rgb '%s' # red
set linetype 2 lw 2 lc rgb '%s' # blue
set linetype 3 lw 2 lc rgb '%s' # green
set linetype 4 lw 2 lc rgb '%s' # magenta
set linetype 5 lw 2 lc rgb '%s' # orange
set linetype 6 lw 2 lc rgb '%s' # yellow
set linetype 7 lw 2 lc rgb '%s' # teal
set linetype 8 lw 2 lc rgb '%s' # violet

# border styles
set tics out nomirror
set border 3

# palette
set palette maxcolors 8
set palette defined ( 0 '%s',\
1 '%s',\
2 '%s',\
3 '%s',\
4 '%s',\
5 '%s',\
6 '%s',\
7 '%s' )
"
            (doom-color 'fg)
            (doom-color 'fg-alt)
            (doom-color 'fg)
            (doom-color 'fg-alt)
            (doom-color 'fg)
            ;; colours
            (doom-color 'red)
            (doom-color 'blue)
            (doom-color 'green)
            (doom-color 'magenta)
            (doom-color 'orange)
            (doom-color 'yellow)
            (doom-color 'teal)
            (doom-color 'violet)
            ;; duplicated
            (doom-color 'red)
            (doom-color 'blue)
            (doom-color 'green)
            (doom-color 'magenta)
            (doom-color 'orange)
            (doom-color 'yellow)
            (doom-color 'teal)
            (doom-color 'violet)
            ))
  (defun org-plot/gnuplot-term-properties (_type)
    (format "background rgb '%s' size 1050,650"
            (doom-color 'bg)))
  (setq org-plot/gnuplot-script-preamble #'org-plot/generate-theme)
  (setq org-plot/gnuplot-term-extra #'org-plot/gnuplot-term-properties))
#+end_src
*** å¯¼å‡º
**** é€šç”¨è®¾ç½®
é»˜è®¤æƒ…å†µä¸‹ï¼ŒOrg ä»…å°†å‰ä¸‰ä¸ªçº§åˆ«çš„æ ‡é¢˜å¯¼å‡ºä¸ºæ ‡é¢˜ã€‚å¾ˆçƒ¦äººï¼Œå› ä¸ºæˆ‘çš„æ–‡æ¡£ç»å¸¸è¶…è¿‡ä¸‰ä¸ªå±‚
æ ‡é¢˜çš„æ·±åº¦ã€‚æˆ‘å…³å¿ƒçš„ä¸¤ç§ä¸»è¦å¯¼å‡ºæ ¼å¼æ˜¯ LaTeX å’Œ HTMLã€‚å½“ä½¿ç”¨ =article= æ—¶ï¼Œ
LaTeX æ ‡é¢˜ä» =\section= ã€ =\subsection= ã€ =\subsubsection= å’Œ
=\paragraph= ã€ =\subgraph= --- /äº”/ ä¸ªçº§åˆ«ã€‚HTML5 æœ‰å…­çº§æ ‡é¢˜ (=<h1>= åˆ°
=<h6>=)ï¼Œä½†ç¬¬ä¸€çº§ Org æ ‡é¢˜è¢«å¯¼å‡ºä¸º =<h2>= å…ƒç´  --- å‰©ä¸‹ /äº”/ ä¸ªå¯ç”¨çº§åˆ«ã€‚

å› æ­¤ï¼Œåœ¨å¯¼å‡ºæ—¶è¯†åˆ«å‰äº”ä¸ªçº§åˆ«çš„ Org æ ‡é¢˜ä¼¼ä¹æ˜¯æœ‰æ„ä¹‰çš„ã€‚
#+begin_src emacs-lisp
(setq org-export-headline-levels 5) ; I like nesting
#+end_src

æˆ‘è¿˜å°†ä½¿ç”¨ =ox-extra= ä¸­çš„ä¸€ä¸ªé¡¹ç›®ï¼Œä»¥ä¾¿æˆ‘å¯ä»¥åœ¨æ ‡é¢˜ä¸­æ·»åŠ  =:ignore:= æ ‡è®°ä»¥ä¿
ç•™å†…å®¹ï¼Œä½†æ ‡é¢˜æœ¬èº«è¢«å¿½ç•¥ (ä¸åƒ =:noexport:= å¿½ç•¥æ ‡é¢˜å’Œå†…å®¹)ã€‚å†™ä½œç»“æ„ä¸­ï¼Œå½“æˆ‘æƒ³
ä½¿ç”¨æ ‡é¢˜æ¥æä¾›æœªå‡ºç°åœ¨æœ€ç»ˆæ–‡æ¡£ä¸­çš„å†…å®¹ï¼Œè¿™å¾ˆæœ‰ç”¨ã€‚

#+begin_src emacs-lisp
(require 'ox-extra)
(ox-extras-activate '(ignore-headlines))
#+end_src

ç”±äºæˆ‘ (å¤§è‡´) è·Ÿè¸ª Org HEADï¼Œå› æ­¤åœ¨åˆ›å»ºè€…å­—ç¬¦ä¸²ä¸­åŒ…å« git ç‰ˆæœ¬æ˜¯æœ‰æ„ä¹‰çš„ã€‚
#+begin_src emacs-lisp
(setq org-export-creator-string
      (format "Emacs %s (Org mode %sâ€“%s)" emacs-version (org-release) (org-git-version)))
#+end_src

**** é¦–å­—æ¯ç¼©ç•¥è¯æ ¼å¼
æˆ‘å–œæ¬¢è‡ªåŠ¨ä½¿ç”¨å¸¦é—´è·çš„å°å‹å¤§å†™å­—æ¯ä½œä¸ºé¦–å­—æ¯ç¼©ç•¥è¯ã€‚å¯¹äºæˆ‘å¸Œæœ›ä¸å—å½±å“çš„å­—ç¬¦ä¸²ï¼Œè®©æˆ‘
ä»¬ä½¿ç”¨ ~;~ ä½œä¸ºé˜²æ­¢è½¬æ¢çš„å‰ç¼€ --- å³ ~;JFK~ (æ­£å¦‚äººä»¬æƒ³è¦çš„ä¸¤ä¸ªå­—æ¯çš„åœ°ç†ä½ç½®å’Œ
åç§°)ã€‚

è¿™å¿…é¡»åœ¨æ¯ç§æ ¼å¼çš„åŸºç¡€ä¸Šå®ç°ï¼Œç›®å‰æ”¯æŒ HTML å’Œ LaTeX å¯¼å‡ºã€‚

#+begin_src emacs-lisp
(defun org-export-filter-text-acronym (text backend _info)
  "Wrap suspected acronyms in acronyms-specific formatting.
Treat sequences of 2+ capital letters (optionally succeeded by \"s\") as an acronym.
Ignore if preceeded by \";\" (for manual prevention) or \"\\\" (for LaTeX commands).

TODO abstract backend implementations."
  (let ((base-backend
         (cond
          ((org-export-derived-backend-p backend 'latex) 'latex)
          ;; Markdown is derived from HTML, but we don't want to format it
          ((org-export-derived-backend-p backend 'md) nil)
          ((org-export-derived-backend-p backend 'html) 'html)))
        (case-fold-search nil))
    (when base-backend
      (replace-regexp-in-string
       "[;\\\\]?\\b[A-Z][A-Z]+s?\\(?:[^A-Za-z]\\|\\b\\)"
       (lambda (all-caps-str)
         (cond ((equal (aref all-caps-str 0) ?\\) all-caps-str)                ; don't format LaTeX commands
               ((equal (aref all-caps-str 0) ?\;) (substring all-caps-str 1))  ; just remove not-acronym indicator char ";"
               (t (let* ((final-char (if (string-match-p "[^A-Za-z]" (substring all-caps-str -1 (length all-caps-str)))
                                         (substring all-caps-str -1 (length all-caps-str))
                                       nil)) ; needed to re-insert the [^A-Za-z] at the end
                         (trailing-s (equal (aref all-caps-str (- (length all-caps-str) (if final-char 2 1))) ?s))
                         (acr (if final-char
                                  (substring all-caps-str 0 (if trailing-s -2 -1))
                                (substring all-caps-str 0 (+ (if trailing-s -1 (length all-caps-str)))))))
                    (pcase base-backend
                      ('latex (concat "\\acr{" (s-downcase acr) "}" (when trailing-s "\\acrs{}") final-char))
                      ('html (concat "<span class='acr'>" acr "</span>" (when trailing-s "<small>s</small>") final-char)))))))
       text t t))))

(add-to-list 'org-export-filter-plain-text-functions
             #'org-export-filter-text-acronym)

;; We won't use `org-export-filter-headline-functions' because it
;; passes (and formats) the entire section contents. That's no good.

(defun org-html-format-headline-acronymised (todo todo-type priority text tags info)
  "Like `org-html-format-headline-default-function', but with acronym formatting."
  (org-html-format-headline-default-function
   todo todo-type priority (org-export-filter-text-acronym text 'html info) tags info))
(setq org-html-format-headline-function #'org-html-format-headline-acronymised)

(defun org-latex-format-headline-acronymised (todo todo-type priority text tags info)
  "Like `org-latex-format-headline-default-function', but with acronym formatting."
  (org-latex-format-headline-default-function
   todo todo-type priority (org-export-filter-text-acronym text 'latex info) tags info))
(setq org-latex-format-headline-function #'org-latex-format-headline-acronymised)
#+end_src
**** å¯¼å‡º Org code

åœ¨æˆ‘ä»¬æ‰€æœ‰çš„ Org é…ç½®ä¸­ï¼Œå½“ä½¿ç”¨ font-lock çš„æ–¹æ³•å¯¼å‡º Org ä»£ç å—æ—¶ï¼Œå¯èƒ½ä¼šäº§ç”Ÿå
åˆ†éš¾çœ‹çš„ç»“æœã€‚ä¸ºäº†è§£å†³è¿™ä¸ªé—®é¢˜ï¼Œå¯ä»¥åœ¨å¯¼å‡ºæ—¶è°ƒæ•´ ~+org-babel-mode-alist~â€‹ã€‚

#+begin_src emacs-lisp
(defun +org-mode--fontlock-only-mode ()
  "Just apply org-mode's font-lock once."
  (let (org-mode-hook
        org-hide-leading-stars
        org-hide-emphasis-markers)
    (org-set-font-lock-defaults)
    (font-lock-ensure))
  (setq-local major-mode #'fundamental-mode))

(defun +org-export-babel-mask-org-config (_backend)
  "Use `+org-mode--fontlock-only-mode' instead of `org-mode'."
  (setq-local org-src-lang-modes
              (append org-src-lang-modes
                      (list (cons "org" #'+org-mode--fontlock-only)))))

(add-hook 'org-export-before-processing-hook #'+org-export-babel-mask-org-config)
#+end_src
*** HTML å¯¼å‡º
:PROPERTIES:
:header-args:emacs-lisp: :noweb-ref ox-html-conf
:END:

æˆ‘æƒ³è°ƒæ•´ä¸€å¤§å †ä¸œè¥¿ã€‚è™½ç„¶æˆ‘ä¸€ç›´éƒ½å¸Œæœ›è¿›è¡Œè°ƒæ•´ï¼Œä½†æœ‰æ—¶æˆ‘å¯èƒ½æƒ³ä½¿ç”¨é»˜è®¤çš„é…ç½®æ¥æµ‹è¯•ç»“æœå¦‚
ä½•ã€‚è€ƒè™‘åˆ°è¿™ä¸€ç‚¹ï¼Œå…¨å±€æ¬¡è¦æ¨¡å¼ä¼¼ä¹æ˜¯æœ€é€‚åˆä½¿ç”¨çš„æ¶æ„ã€‚

#+begin_src emacs-lisp
(define-minor-mode org-fancy-html-export-mode
  "Toggle my fabulous org export tweaks. While this mode itself does a little bit,
the vast majority of the change in behaviour comes from switch statements in:
 - `org-html-template-fancier'
 - `org-html--build-meta-info-extended'
 - `org-html-src-block-collapsable'
 - `org-html-block-collapsable'
 - `org-html-table-wrapped'
 - `org-html--format-toc-headline-colapseable'
 - `org-html--toc-text-stripped-leaves'
 - `org-export-html-headline-anchor'"
  :global t
  :init-value t
  (if org-fancy-html-export-mode
      (setq org-html-style-default org-html-style-fancy
            org-html-meta-tags #'org-html-meta-tags-fancy
            org-html-checkbox-type 'html-span)
    (setq org-html-style-default org-html-style-plain
          org-html-meta-tags #'org-html-meta-tags-default
          org-html-checkbox-type 'html)))
#+end_src

åœ¨å¾ˆå¤šæƒ…å†µä¸‹ï¼Œæˆ‘æƒ³ä¿®æ”¹ =ox-html= ä¸­å®šä¹‰çš„å˜é‡ï¼Œæ‰€ä»¥æˆ‘ä»¬å°†æŠŠæœ¬èŠ‚çš„å†…å®¹åŒ…è£…åœ¨
src_elisp{(after! ox-html ...)} å—ä¸­ã€‚
#+begin_src emacs-lisp :noweb no-export :noweb-ref org-conf
(after! ox-html
  <<ox-html-conf>>
)
#+end_src

**** é¢å¤–çš„å¤´å†…å®¹
æˆ‘ä»¬æƒ³åœ¨ä¸»ä½“çš„å¼€å¤´æ·»åŠ æ›´å¤šä¿¡æ¯ã€‚ä¸å¹¸çš„æ˜¯ï¼Œä¼¼ä¹æ²¡æœ‰ä»»ä½•å¥½çš„å˜é‡æˆ– hookï¼Œæ‰€ä»¥æˆ‘ä»¬é‡å†™
ç›¸å…³çš„å‡½æ•°ã€‚

è¿™æ ·åšæ˜¯ä¸ºäº†è®©æˆ‘å¯ä»¥åœ¨é¡µçœ‰ä¸­æ·»åŠ æ—¥æœŸå’Œä½œè€…ï¼Œå®ç°ä»… CSS çš„äº®/æš—ä¸»é¢˜åˆ‡æ¢ï¼Œä»¥åŠä¸€äº›
[[https://ogp.me/][Open Graph]] å…ƒæ•°æ®ã€‚

#+begin_src emacs-lisp
(defadvice! org-html-template-fancier (orig-fn contents info)
  "Return complete document string after HTML conversion.
CONTENTS is the transcoded contents string.  INFO is a plist
holding export options. Adds a few extra things to the body
compared to the default implementation."
  :around #'org-html-template
  (if (or (not org-fancy-html-export-mode) (bound-and-true-p org-msg-export-in-progress))
      (funcall orig-fn contents info)
    (concat
     (when (and (not (org-html-html5-p info)) (org-html-xhtml-p info))
       (let* ((xml-declaration (plist-get info :html-xml-declaration))
              (decl (or (and (stringp xml-declaration) xml-declaration)
                        (cdr (assoc (plist-get info :html-extension)
                                    xml-declaration))
                        (cdr (assoc "html" xml-declaration))
                        "")))
         (when (not (or (not decl) (string= "" decl)))
           (format "%s\n"
                   (format decl
                           (or (and org-html-coding-system
                                    (fboundp 'coding-system-get)
                                    (coding-system-get org-html-coding-system 'mime-charset))
                               "utf-8"))))))
     (org-html-doctype info)
     "\n"
     (concat "<html"
             (cond ((org-html-xhtml-p info)
                    (format
                     " xmlns=\"http://www.w3.org/1999/xhtml\" lang=\"%s\" xml:lang=\"%s\""
                     (plist-get info :language) (plist-get info :language)))
                   ((org-html-html5-p info)
                    (format " lang=\"%s\"" (plist-get info :language))))
             ">\n")
     "<head>\n"
     (org-html--build-meta-info info)
     (org-html--build-head info)
     (org-html--build-mathjax-config info)
     "</head>\n"
     "<body>\n<input type='checkbox' id='theme-switch'><div id='page'><label id='switch-label' for='theme-switch'></label>"
     (let ((link-up (org-trim (plist-get info :html-link-up)))
           (link-home (org-trim (plist-get info :html-link-home))))
       (unless (and (string= link-up "") (string= link-home ""))
         (format (plist-get info :html-home/up-format)
                 (or link-up link-home)
                 (or link-home link-up))))
     ;; Preamble.
     (org-html--build-pre/postamble 'preamble info)
     ;; Document contents.
     (let ((div (assq 'content (plist-get info :html-divs))))
       (format "<%s id=\"%s\">\n" (nth 1 div) (nth 2 div)))
     ;; Document title.
     (when (plist-get info :with-title)
       (let ((title (and (plist-get info :with-title)
                         (plist-get info :title)))
             (subtitle (plist-get info :subtitle))
             (html5-fancy (org-html--html5-fancy-p info)))
         (when title
           (format
            (if html5-fancy
                "<header class=\"page-header\">%s\n<h1 class=\"title\">%s</h1>\n%s</header>"
              "<h1 class=\"title\">%s%s</h1>\n")
            (if (or (plist-get info :with-date)
                    (plist-get info :with-author))
                (concat "<div class=\"page-meta\">"
                        (when (plist-get info :with-date)
                          (org-export-data (plist-get info :date) info))
                        (when (and (plist-get info :with-date) (plist-get info :with-author)) ", ")
                        (when (plist-get info :with-author)
                          (org-export-data (plist-get info :author) info))
                        "</div>\n")
              "")
            (org-export-data title info)
            (if subtitle
                (format
                 (if html5-fancy
                     "<p class=\"subtitle\" role=\"doc-subtitle\">%s</p>\n"
                   (concat "\n" (org-html-close-tag "br" nil info) "\n"
                           "<span class=\"subtitle\">%s</span>\n"))
                 (org-export-data subtitle info))
              "")))))
     contents
     (format "</%s>\n" (nth 1 (assq 'content (plist-get info :html-divs))))
     ;; Postamble.
     (org-html--build-pre/postamble 'postamble info)
     ;; Possibly use the Klipse library live code blocks.
     (when (plist-get info :html-klipsify-src)
       (concat "<script>" (plist-get info :html-klipse-selection-script)
               "</script><script src=\""
               org-html-klipse-js
               "\"></script><link rel=\"stylesheet\" type=\"text/css\" href=\""
               org-html-klipse-css "\"/>"))
     ;; Closing document.
     "</div>\n</body>\n</html>")))
#+end_src

æˆ‘è®¤ä¸ºå¦‚æœ "ç›®å½•" å°†æ‚¨å¸¦å›é¡µé¢é¡¶éƒ¨ä¼šå¾ˆå¥½ã€‚å¥½å§ï¼Œæ—¢ç„¶æˆ‘ä»¬å·²ç»åšäº†è¿™ä¹ˆå¤šå»ºè®®...
#+begin_src emacs-lisp
(defadvice! org-html-toc-linked (depth info &optional scope)
  "Build a table of contents.

Just like `org-html-toc', except the header is a link to \"#\".

DEPTH is an integer specifying the depth of the table.  INFO is
a plist used as a communication channel.  Optional argument SCOPE
is an element defining the scope of the table.  Return the table
of contents as a string, or nil if it is empty."
  :override #'org-html-toc
  (let ((toc-entries
         (mapcar (lambda (headline)
                   (cons (org-html--format-toc-headline headline info)
                         (org-export-get-relative-level headline info)))
                 (org-export-collect-headlines info depth scope))))
    (when toc-entries
      (let ((toc (concat "<div id=\"text-table-of-contents\">"
                         (org-html--toc-text toc-entries)
                         "</div>\n")))
        (if scope toc
          (let ((outer-tag (if (org-html--html5-fancy-p info)
                               "nav"
                             "div")))
            (concat (format "<%s id=\"table-of-contents\">\n" outer-tag)
                    (let ((top-level (plist-get info :html-toplevel-hlevel)))
                      (format "<h%d><a href=\"#\" style=\"color:inherit; text-decoration: none;\">%s</a></h%d>\n"
                              top-level
                              (org-html--translate "Table of Contents" info)
                              top-level))
                    toc
                    (format "</%s>\n" outer-tag))))))))
#+end_src

æœ€åï¼Œè®©æˆ‘ä»¬å †ç§¯ä¸€äº›å…ƒæ•°æ®ã€‚è¿™äº›åµŒå…¥å°±å¿«å®Œæˆäº†ã€‚
#+begin_src emacs-lisp
(defvar org-html-meta-tags-opengraph-image
  '(:image "https://tecosaur.com/resources/org/nib.png"
    :type "image/png"
    :width "200"
    :height "200"
    :alt "Green fountain pen nib")
  "Plist of og:image:PROP properties and their value, for use in `org-html-meta-tags-fancy'.")

(defun org-html-meta-tags-fancy (info)
  "Use the INFO plist to construct the meta tags, as described in `org-html-meta-tags'."
  (let ((title (org-html-plain-text
                (org-element-interpret-data (plist-get info :title)) info))
        (author (and (plist-get info :with-author)
                     (let ((auth (plist-get info :author)))
                       ;; Return raw Org syntax.
                       (and auth (org-html-plain-text
                                  (org-element-interpret-data auth) info))))))
    (append
     (list
      (when (org-string-nw-p author)
        (list "name" "author" author))
      (when (org-string-nw-p (plist-get info :description))
        (list "name" "description"
              (plist-get info :description)))
      '("name" "generator" "org mode")
      '("name" "theme-color" "#77aa99")
      '("property" "og:type" "article")
      (list "property" "og:title" title)
      (let ((subtitle (org-export-data (plist-get info :subtitle) info)))
        (when (org-string-nw-p subtitle)
          (list "property" "og:description" subtitle))))
     (when org-html-meta-tags-opengraph-image
       (list (list "property" "og:image" (plist-get org-html-meta-tags-opengraph-image :image))
             (list "property" "og:image:type" (plist-get org-html-meta-tags-opengraph-image :type))
             (list "property" "og:image:width" (plist-get org-html-meta-tags-opengraph-image :width))
             (list "property" "og:image:height" (plist-get org-html-meta-tags-opengraph-image :height))
             (list "property" "og:image:alt" (plist-get org-html-meta-tags-opengraph-image :alt))))
     (list
      (when (org-string-nw-p author)
        (list "property" "og:article:author:first_name" (car (s-split-up-to " " author 2))))
      (when (and (org-string-nw-p author) (s-contains-p " " author))
        (list "property" "og:article:author:last_name" (cadr (s-split-up-to " " author 2))))
      (list "property" "og:article:published_time"
            (format-time-string
             "%FT%T%z"
             (or
              (when-let ((date-str (cadar (org-collect-keywords '("DATE")))))
                (unless (string= date-str (format-time-string "%F"))
                  (ignore-errors (encode-time (org-parse-time-string date-str)))))
              (if buffer-file-name
                  (file-attribute-modification-time (file-attributes buffer-file-name))
                (current-time)))))
      (when buffer-file-name
        (list "property" "og:article:modified_time"
              (format-time-string "%FT%T%z" (file-attribute-modification-time (file-attributes buffer-file-name)))))))))

(unless (functionp #'org-html-meta-tags-default)
  (defalias 'org-html-meta-tags-default #'ignore))
(setq org-html-meta-tags #'org-html-meta-tags-fancy)
#+end_src
**** è‡ªå®šä¹‰ CSS/JS
é»˜è®¤çš„ org HTML å¯¼å‡ºæ˜¯...å¥½å§ï¼Œä½†æˆ‘ä»¬çœŸçš„å¯ä»¥è®©å®ƒå˜å¾—å¥½çœ‹èµ·æ¥ã€‚[[https://lepisma.xyz][lepisma.xyz]] çš„é£
æ ¼éå¸¸å¥½ï¼Œè€Œä¸”ä¹Ÿæœ‰ from å’Œ org å¯¼å‡ºï¼å¯ä»¥è¯´æˆ‘çˆ±ä¸Šäº†å®ƒï¼Œå¹¶åšäº†ä¸€ç‚¹æˆ‘è‡ªå·±çš„è°ƒæ•´ã€‚

#+begin_src html :tangle misc/org-export-header.html :comments no
<link rel="icon" href="https://tecosaur.com/resources/org/nib.ico" type="image/ico" />

<link rel="preload" as="font" crossorigin="anonymous" type="font/woff2" href="https://tecosaur.com/resources/org/etbookot-roman-webfont.woff2">
<link rel="preload" as="font" crossorigin="anonymous" type="font/woff2" href="https://tecosaur.com/resources/org/etbookot-italic-webfont.woff2">
<link rel="preload" as="font" crossorigin="anonymous" type="font/woff2" href="https://tecosaur.com/resources/org/Merriweather-TextRegular.woff2">
<link rel="preload" as="font" crossorigin="anonymous" type="font/woff2" href="https://tecosaur.com/resources/org/Merriweather-TextItalic.woff2">
<link rel="preload" as="font" crossorigin="anonymous" type="font/woff2" href="https://tecosaur.com/resources/org/Merriweather-TextBold.woff2">
#+end_src

#+begin_src emacs-lisp
(setq org-html-style-plain org-html-style-default
      org-html-htmlize-output-type 'css
      org-html-doctype "html5"
      org-html-html5-fancy t)

(defun org-html-reload-fancy-style ()
  (interactive)
  (setq org-html-style-fancy
        (concat (f-read-text (expand-file-name "misc/org-export-header.html" doom-private-dir))
                "<script>\n"
                (f-read-text (expand-file-name "misc/org-css/main.js" doom-private-dir))
                "</script>\n<style>\n"
                (f-read-text (expand-file-name "misc/org-css/main.min.css" doom-private-dir))
                "</style>"))
  (when org-fancy-html-export-mode
    (setq org-html-style-default org-html-style-fancy)))
(org-html-reload-fancy-style)
#+end_src
**** å¯æŠ˜å çš„ src å’Œç¤ºä¾‹å—
é€šè¿‡å°† ~<pre>~ å…ƒç´ åŒ…è£…åœ¨ ~<details>~ å—ä¸­ï¼Œæˆ‘ä»¬å¯ä»¥å¾—åˆ°æ²¡æœ‰ CSS çš„å¯æŠ˜å å—ï¼Œ
å°½ç®¡æˆ‘ä»¬ä¼šç¨å¾®æŠ˜è…¾ä¸€ä¸‹ï¼Œè®©è¿™çœ‹èµ·æ¥æœ‰ç‚¹æ¼‚äº®ã€‚

ç”±äºé»˜è®¤æƒ…å†µä¸‹å¯¹æŸäº›ä»£ç å—å¯ç”¨å¯æŠ˜å æ€§ä¼¼ä¹å¾ˆæœ‰ç”¨ï¼Œå› æ­¤å¦‚æœæ‚¨å¯ä»¥ä½¿ç”¨
=#+attr_html: :collapsed t= è®¾ç½®å®ƒä¼šæ›´å¥½ã€‚

å¦‚æœæœ‰ä¸€ä¸ªç›¸åº”çš„å…¨å±€/ä¼šè¯æœ¬åœ°æ–¹å¼æ¥è®¾ç½®å®ƒä¼šå¾ˆå¥½ï¼Œä½†æˆ‘è¿˜æ²¡èƒ½è®©å®ƒæ­£å¸¸å·¥ä½œã€‚

#+begin_src emacs-lisp
(defvar org-html-export-collapsed nil)
(eval '(cl-pushnew '(:collapsed "COLLAPSED" "collapsed" org-html-export-collapsed t)
                   (org-export-backend-options (org-export-get-backend 'html))))
(add-to-list 'org-default-properties "EXPORT_COLLAPSED")
#+end_src

æˆ‘ä»¬å¯ä»¥è¿›ä¸€æ­¥ä¿®æ”¹ src å—ï¼Œå¹¶åœ¨ src å—çš„ä¸€ä¾§æ·»åŠ ä¸€ä¸ªå—ï¼Œå…¶ä¸­åŒ…å«å¼•ç”¨å½“å‰å—çš„é”šç‚¹å’Œ
å¤åˆ¶å—å†…å®¹çš„æŒ‰é’®ã€‚

#+name: Src blocks
#+begin_src emacs-lisp
(defadvice! org-html-src-block-collapsable (orig-fn src-block contents info)
  "Wrap the usual <pre> block in a <details>"
  :around #'org-html-src-block
  (if (or (not org-fancy-html-export-mode) (bound-and-true-p org-msg-export-in-progress))
      (funcall orig-fn src-block contents info)
    (let* ((properties (cadr src-block))
           (lang (mode-name-to-lang-name
                  (plist-get properties :language)))
           (name (plist-get properties :name))
           (ref (org-export-get-reference src-block info))
           (collapsed-p (member (or (org-export-read-attribute :attr_html src-block :collapsed)
                                    (plist-get info :collapsed))
                                '("y" "yes" "t" t "true" "all"))))
      (format
       "<details id='%s' class='code'%s><summary%s>%s</summary>
<div class='gutter'>
<a href='#%s'>#</a>
<button title='Copy to clipboard' onclick='copyPreToClipbord(this)'>â˜</button>\
</div>
%s
</details>"
       ref
       (if collapsed-p "" " open")
       (if name " class='named'" "")
       (concat
        (when name (concat "<span class=\"name\">" name "</span>"))
        "<span class=\"lang\">" lang "</span>")
       ref
       (if name
           (replace-regexp-in-string (format "<pre\\( class=\"[^\"]+\"\\)? id=\"%s\">" ref) "<pre\\1>"
                                     (funcall orig-fn src-block contents info))
         (funcall orig-fn src-block contents info))))))

(defun mode-name-to-lang-name (mode)
  (or (cadr (assoc mode
                   '(("asymptote" "Asymptote")
                     ("awk" "Awk")
                     ("C" "C")
                     ("clojure" "Clojure")
                     ("css" "CSS")
                     ("D" "D")
                     ("ditaa" "ditaa")
                     ("dot" "Graphviz")
                     ("calc" "Emacs Calc")
                     ("emacs-lisp" "Emacs Lisp")
                     ("fortran" "Fortran")
                     ("gnuplot" "gnuplot")
                     ("haskell" "Haskell")
                     ("hledger" "hledger")
                     ("java" "Java")
                     ("js" "Javascript")
                     ("latex" "LaTeX")
                     ("ledger" "Ledger")
                     ("lisp" "Lisp")
                     ("lilypond" "Lilypond")
                     ("lua" "Lua")
                     ("matlab" "MATLAB")
                     ("mscgen" "Mscgen")
                     ("ocaml" "Objective Caml")
                     ("octave" "Octave")
                     ("org" "Org mode")
                     ("oz" "OZ")
                     ("plantuml" "Plantuml")
                     ("processing" "Processing.js")
                     ("python" "Python")
                     ("R" "R")
                     ("ruby" "Ruby")
                     ("sass" "Sass")
                     ("scheme" "Scheme")
                     ("screen" "Gnu Screen")
                     ("sed" "Sed")
                     ("sh" "shell")
                     ("sql" "SQL")
                     ("sqlite" "SQLite")
                     ("forth" "Forth")
                     ("io" "IO")
                     ("J" "J")
                     ("makefile" "Makefile")
                     ("maxima" "Maxima")
                     ("perl" "Perl")
                     ("picolisp" "Pico Lisp")
                     ("scala" "Scala")
                     ("shell" "Shell Script")
                     ("ebnf2ps" "ebfn2ps")
                     ("cpp" "C++")
                     ("abc" "ABC")
                     ("coq" "Coq")
                     ("groovy" "Groovy")
                     ("bash" "bash")
                     ("csh" "csh")
                     ("ash" "ash")
                     ("dash" "dash")
                     ("ksh" "ksh")
                     ("mksh" "mksh")
                     ("posh" "posh")
                     ("ada" "Ada")
                     ("asm" "Assembler")
                     ("caml" "Caml")
                     ("delphi" "Delphi")
                     ("html" "HTML")
                     ("idl" "IDL")
                     ("mercury" "Mercury")
                     ("metapost" "MetaPost")
                     ("modula-2" "Modula-2")
                     ("pascal" "Pascal")
                     ("ps" "PostScript")
                     ("prolog" "Prolog")
                     ("simula" "Simula")
                     ("tcl" "tcl")
                     ("tex" "LaTeX")
                     ("plain-tex" "TeX")
                     ("verilog" "Verilog")
                     ("vhdl" "VHDL")
                     ("xml" "XML")
                     ("nxml" "XML")
                     ("conf" "Configuration File"))))
      mode))
#+end_src

#+name: Example, fixed width, and property blocks
#+begin_src emacs-lisp
(defun org-html-block-collapsable (orig-fn block contents info)
  "Wrap the usual block in a <details>"
  (if (or (not org-fancy-html-export-mode) (bound-and-true-p org-msg-export-in-progress))
      (funcall orig-fn block contents info)
    (let ((ref (org-export-get-reference block info))
          (type (pcase (car block)
                  ('property-drawer "Properties")))
          (collapsed-default (pcase (car block)
                               ('property-drawer t)
                               (_ nil)))
          (collapsed-value (org-export-read-attribute :attr_html block :collapsed))
          (collapsed-p (or (member (org-export-read-attribute :attr_html block :collapsed)
                                   '("y" "yes" "t" t "true"))
                           (member (plist-get info :collapsed) '("all")))))
      (format
       "<details id='%s' class='code'%s>
<summary%s>%s</summary>
<div class='gutter'>\
<a href='#%s'>#</a>
<button title='Copy to clipboard' onclick='copyPreToClipbord(this)'>â˜</button>\
</div>
%s\n
</details>"
       ref
       (if (or collapsed-p collapsed-default) "" " open")
       (if type " class='named'" "")
       (if type (format "<span class='type'>%s</span>" type) "")
       ref
       (funcall orig-fn block contents info)))))

(advice-add 'org-html-example-block   :around #'org-html-block-collapsable)
(advice-add 'org-html-fixed-width     :around #'org-html-block-collapsable)
(advice-add 'org-html-property-drawer :around #'org-html-block-collapsable)
#+end_src

**** åŒ…å« HTML åŒ–çš„é¢å¤–å­—ä½“é”
Org ä½¿ç”¨ [[https://github.com/hniksic/emacs-htmlize][htmlize.el]] å¯¼å‡ºå¸¦æœ‰è¯­æ³•é«˜äº®çš„ç¼“å†²åŒºã€‚

åœ¨å¤§å¤šæ•°æƒ…å†µä¸‹è¿™äº›æ ¼å¼éå¸¸æ£’ã€‚ä¸éœ€è¦åŠ è½½æä¾›å­—ä½“é”å®šçš„æ¬¡è¦æ¨¡å¼ï¼Œå› æ­¤ä¸ä¼šå½±å“ç»“æœã€‚

é€šè¿‡åœ¨ ~htmlize-before-hook~ ä¸­å¯ç”¨è¿™äº›æ¨¡å¼ï¼Œæˆ‘ä»¬å¯ä»¥çº æ­£è¿™ç§è¡Œä¸ºã€‚
#+begin_src emacs-lisp
(autoload #'highlight-numbers--turn-on "highlight-numbers")
(add-hook 'htmlize-before-hook #'highlight-numbers--turn-on)
#+end_src

**** å¤„ç†è¡¨æº¢å‡º
ä¸ºäº†é€‚åº”å®½è¡¨ --- å°¤å…¶æ˜¯åœ¨ç§»åŠ¨è®¾å¤‡ä¸Š --- æˆ‘ä»¬æƒ³è¦è®¾ç½®æœ€å¤§å®½åº¦å’Œæ»šåŠ¨æº¢å‡ºã€‚ä¸å¹¸çš„æ˜¯ï¼Œ
è¿™ä¸èƒ½ç›´æ¥åº”ç”¨äº ~è¡¨æ ¼~ å…ƒç´ ï¼Œæ‰€ä»¥æˆ‘ä»¬å¿…é¡»å°†å®ƒåŒ…è£…åœ¨ä¸€ä¸ª ~div~ ä¸­ã€‚

å½“æˆ‘ä»¬è¿™æ ·åšæ—¶ï¼Œæˆ‘ä»¬å¯ä»¥åƒæˆ‘ä»¬å¯¹ src å—æ‰€åšçš„é‚£æ ·ï¼Œè®¾ç½®ä¸€ä¸ªé“¾æ¥å—ï¼Œå¹¶æ˜¾ç¤º
~#+name~ (å¦‚æœæœ‰çš„è¯)ã€‚
#+begin_src emacs-lisp
(defadvice! org-html-table-wrapped (orig-fn table contents info)
  "Wrap the usual <table> in a <div>"
  :around #'org-html-table
  (if (or (not org-fancy-html-export-mode) (bound-and-true-p org-msg-export-in-progress))
      (funcall orig-fn table contents info)
    (let* ((name (plist-get (cadr table) :name))
           (ref (org-export-get-reference table info)))
      (format "<div id='%s' class='table'>
<div class='gutter'><a href='#%s'>#</a></div>
<div class='tabular'>
%s
</div>\
</div>"
              ref ref
              (if name
                  (replace-regexp-in-string (format "<table id=\"%s\"" ref) "<table"
                                            (funcall orig-fn table contents info))
                (funcall orig-fn table contents info))))))
#+end_src

**** å¯å±•å¼€çš„ç›®å½•æ ‘
TOC ä½œä¸ºå¯æŠ˜å æ ‘æ›´æ˜“äºå¯¼èˆªã€‚ä¸å¹¸çš„æ˜¯ï¼Œæˆ‘ä»¬ä¸èƒ½å•ç‹¬ä½¿ç”¨ CSS æ¥å®ç°è¿™ä¸€ç‚¹ã€‚å€¼å¾—åº†å¹¸
çš„æ˜¯ï¼Œæˆ‘ä»¬å¯ä»¥é€šè¿‡è°ƒæ•´ TOC ç”Ÿæˆä»£ç æ¥ä¸ºæ¯ä¸ªé¡¹ç›®ä½¿ç”¨ä¸€ä¸ª ~æ ‡ç­¾~ ï¼Œä»¥åŠä¸€ä¸ªéšè—çš„
~å¤é€‰æ¡†~ æ¥è·Ÿè¸ªçŠ¶æ€ï¼Œä»è€Œé¿å…ä½¿ç”¨ JSã€‚

è¦æ·»åŠ å®ƒï¼Œæˆ‘ä»¬éœ€è¦åœ¨ [[file:~/.emacs.d/.local/straight/repos/org-mode/lisp/ox-html.el::(format "<a href=\"#%s\">%s</a>"][org-html--format-toc-headline]] ä¸­æ›´æ”¹ä¸€è¡Œã€‚

å› ä¸ºæˆ‘ä»¬å®é™…ä¸Šå¯ä»¥é€šè¿‡åœ¨å‡½æ•°å‘¨å›´æ·»åŠ å»ºè®®æ¥å®ç°æ‰€éœ€çš„æ•ˆæœï¼Œè€Œæ— éœ€è¦†ç›–å®ƒ --- è®©æˆ‘ä»¬è¿™
æ ·åšæ¥å‡å°‘è¿™ä¸ªé…ç½®çš„é”™è¯¯è¡¨é¢ã€‚
#+begin_src emacs-lisp
(defadvice! org-html--format-toc-headline-colapseable (orig-fn headline info)
  "Add a label and checkbox to `org-html--format-toc-headline's usual output,
to allow the TOC to be a collapseable tree."
  :around #'org-html--format-toc-headline
  (if (or (not org-fancy-html-export-mode) (bound-and-true-p org-msg-export-in-progress))
      (funcall orig-fn headline info)
    (let ((id (or (org-element-property :CUSTOM_ID headline)
                  (org-export-get-reference headline info))))
      (format "<input type='checkbox' id='toc--%s'/><label for='toc--%s'>%s</label>"
              id id (funcall orig-fn headline info)))))
#+end_src

ç°åœ¨ï¼Œå¶å­ (æ²¡æœ‰å­æ ‡é¢˜çš„æ ‡é¢˜) ä¸åº”è¯¥æœ‰ ~æ ‡ç­¾é¡¹~ ã€‚å®ç°è¿™ä¸€ç‚¹çš„æ˜æ˜¾æ–¹æ³•æ˜¯åœ¨
~org-html--format-toc-headline-colapseable~ ä¸­åŒ…å«ä¸€äº› (å¦‚æœæ²¡æœ‰å­é¡¹) é€»è¾‘
ã€‚ä¸å¹¸çš„æ˜¯ï¼Œæˆ‘çš„ elisp æ— æ³•ä» org æä¾›çš„å¤§é‡ä¿¡æ¯ä¸­æå–å­æ ‡é¢˜çš„æ•°é‡ã€‚
#+begin_src emacs-lisp
(defadvice! org-html--toc-text-stripped-leaves (orig-fn toc-entries)
  "Remove label"
  :around #'org-html--toc-text
  (if (or (not org-fancy-html-export-mode) (bound-and-true-p org-msg-export-in-progress))
      (funcall orig-fn toc-entries)
    (replace-regexp-in-string "<input [^>]+><label [^>]+>\\(.+?\\)</label></li>" "\\1</li>"
                              (funcall orig-fn toc-entries))))
#+end_src

**** ä½¿ verbatim ä¸ code ä¸åŒ
è®©æˆ‘ä»¬ä¸º =varbatim= å’Œ ~code~ æ·»åŠ ä¸€äº›å·®å¼‚ã€‚

æˆ‘ä»¬å¯ä»¥å°† ~code~ ä¸“é—¨ç”¨äºä»£ç ç‰‡æ®µå’Œå‘½ä»¤ï¼Œä¾‹å¦‚ï¼šåœ¨ Emacs çš„æ‰¹å¤„ç†æ¨¡å¼ä¸­è°ƒç”¨
src_elisp{(message "Hello")} ä¼šåƒ ~echo~ ä¸€æ ·æ‰“å°åˆ°æ ‡å‡†è¾“å‡ºã€‚ å¯ä»¥å¯¹
=verbatim= ä½¿ç”¨å„ç§ 'å…¶ä»–ç­‰å®½'ï¼Œä¾‹å¦‚é”®ç›˜å¿«æ·é”®ï¼š =C-c C-c= æˆ– =C-g= å¯èƒ½æ˜¯
Emacs ä¸­æœ€æœ‰ç”¨çš„å¿«æ·é”®ï¼Œæˆ–æ–‡ä»¶åï¼šæˆ‘å°†æˆ‘çš„é…ç½®ä¿å­˜åœ¨ =~/.config/doom/= ä¸­ï¼Œå…¶ä»–
æƒ…å†µåˆ™ä½¿ç”¨æ­£å¸¸æ ·å¼ã€‚

ç„¶åï¼Œå¯¹è¿™ä¸¤ç§æƒ…å†µè¿›è¡Œä¸åŒçš„æ ·å¼è®¾ç½®æœ‰åŠ©äºæé«˜æ–‡æ¡£çš„æ¸…æ™°åº¦ã€‚

#+begin_src emacs-lisp
(setq org-html-text-markup-alist
      '((bold . "<b>%s</b>")
        (code . "<code>%s</code>")
        (italic . "<i>%s</i>")
        (strike-through . "<del>%s</del>")
        (underline . "<span class=\"underline\">%s</span>")
        (verbatim . "<kbd>%s</kbd>")))
#+end_src

**** æ”¹å˜å¤é€‰æ¡†ç±»å‹
æˆ‘ä»¬ä¹Ÿæƒ³ä½¿ç”¨ HTML å¤é€‰æ¡†ï¼Œä½†æ˜¯æˆ‘ä»¬æƒ³æ¯”é»˜è®¤çš„æ›´æ¼‚äº®
#+begin_src emacs-lisp
(appendq! org-html-checkbox-types
          '((html-span
             (on . "<span class='checkbox'></span>")
             (off . "<span class='checkbox'></span>")
             (trans . "<span class='checkbox'></span>"))))
(setq org-html-checkbox-type 'html-span)
#+end_src
- [ ] I'm yet to do this
- [-] Work in progress
- [X] This is done

**** é¢å¤–çš„ç‰¹æ®Šå­—ç¬¦ä¸²
~org-html-special-string-regexps~ å˜é‡å®šä¹‰äº†ä»¥ä¸‹å†…å®¹çš„æ›¿æ¢ï¼š
+ =\-=, shy è¿å­—ç¬¦
+ =---=, å¢å¼ºçš„ç ´æŠ˜å·
+ =--=, ç ´æŠ˜å·
+ =...=, (æ°´å¹³çš„) çœç•¥å·

ä½†æˆ‘è®¤ä¸ºå¦‚æœè¿˜å¯ä»¥æ›¿æ¢å·¦/å³ç®­å¤´ (=->= å’Œ =<-=) é‚£å°±æ›´å¥½äº†ã€‚ è¿™æ˜¯ä¸€ä¸ª
~defconst~ ï¼Œä½†æ­£å¦‚ä½ å¯ä»¥ä»è¿™ä¸ªé…ç½®ä¸­çš„å¤§é‡å»ºè®®ä¸­çœ‹å‡ºçš„é‚£æ ·ï¼Œæˆ‘å¹¶æ²¡æœ‰æ”¾å¼ƒæˆ‘ä¸ 'åº”
è¯¥' åšçš„äº‹æƒ…ã€‚

å”¯ä¸€çš„å°éº»çƒ¦æ˜¯åœ¨è¿™ä¸ªé˜¶æ®µçš„è¾“å‡ºå¤„ç†ä¹‹å‰ =<= å’Œ =>= è¢«è½¬æ¢ä¸º =&lt;= å’Œ =&gt;= ã€‚
#+begin_src emacs-lisp
(pushnew! org-html-special-string-regexps
          '("-&gt;" . "&#8594;")
          '("&lt;-" . "&#8592;"))
#+end_src

**** æ ‡é¢˜é”šç‚¹
ä¸€ä¸ª GitHub é£æ ¼çš„æ ‡é¢˜é“¾æ¥
#+begin_src emacs-lisp
(defun org-export-html-headline-anchor (text backend info)
  (when (and (org-export-derived-backend-p backend 'html)
             (not (org-export-derived-backend-p backend 're-reveal))
             org-fancy-html-export-mode)
    (unless (bound-and-true-p org-msg-export-in-progress)
      (replace-regexp-in-string
       "<h\\([0-9]\\) id=\"\\([a-z0-9-]+\\)\">\\(.*[^ ]\\)<\\/h[0-9]>" ; this is quite restrictive, but due to `org-reference-contraction' I can do this
       "<h\\1 id=\"\\2\">\\3<a aria-hidden=\"true\" href=\"#\\2\">#</a> </h\\1>"
       text))))

(add-to-list 'org-export-filter-headline-functions
             'org-export-html-headline-anchor)
#+end_src

**** é“¾æ¥é¢„è§ˆ
æœ‰æ—¶è®©é“¾æ¥ç‰¹åˆ«çªå‡ºä¼šå¾ˆå¥½ï¼Œæˆ‘è®¤ä¸ºåƒ Twitter é‚£æ ·çš„åµŒå…¥ / é¢„è§ˆä¼šå¾ˆå¥½ã€‚

æˆ‘ä»¬å¯ä»¥é€šè¿‡æ·»åŠ ä¸€ä¸ªä¸ =https= --- =Https= ç•¥æœ‰ä¸åŒçš„æ–°é“¾æ¥ç±»å‹æ¥è½»æ¾åšåˆ°è¿™ç‚¹ã€‚

#+begin_src emacs-lisp
(org-link-set-parameters "Https"
                         :follow (lambda (url arg) (browse-url (concat "https:" url) arg))
                         :export #'org-url-fancy-export)
#+end_src

ç„¶åï¼Œå¦‚æœæˆ‘ä»¬å¯ä»¥é€šè¿‡å‡½æ•° ~org-url-unfurl-metadata~ ä¸ºæ­¤ç±»é“¾æ¥è·å–
src_elisp{(:title "..." :description "..." :image "...")} å½¢å¼çš„ plist
ï¼Œæˆ‘ä»¬å°±å¯ä»¥è¿›è¡Œç²¾ç¾çš„å¯¼å‡ºã€‚

#+begin_src emacs-lisp
(defun org-url-fancy-export (url _desc backend)
  (let ((metadata (org-url-unfurl-metadata (concat "https:" url))))
    (cond
     ((org-export-derived-backend-p backend 'html)
      (concat
       "<div class=\"link-preview\">"
       (format "<a href=\"%s\">" (concat "https:" url))
       (when (plist-get metadata :image)
         (format "<img src=\"%s\"/>" (plist-get metadata :image)))
       "<small>"
       (replace-regexp-in-string "//\\(?:www\\.\\)?\\([^/]+\\)/?.*" "\\1" url)
       "</small><p>"
       (when (plist-get metadata :title)
         (concat "<b>" (org-html-encode-plain-text (plist-get metadata :title)) "</b></br>"))
       (when (plist-get metadata :description)
         (org-html-encode-plain-text (plist-get metadata :description)))
       "</p></a></div>"))
     (t url))))
#+end_src

ç°åœ¨æˆ‘ä»¬åªéœ€è¦å®ç°å…ƒæ•°æ®æå–åŠŸèƒ½ã€‚
#+begin_src emacs-lisp
(setq org-url-unfurl-metadata--cache nil)
(defun org-url-unfurl-metadata (url)
  (cdr (or (assoc url org-url-unfurl-metadata--cache)
           (car (push
                 (cons
                  url
                  (let* ((head-data
                          (-filter #'listp
                                   (cdaddr
                                    (with-current-buffer (progn (message "Fetching metadata from %s" url)
                                                                (url-retrieve-synchronously url t t 5))
                                      (goto-char (point-min))
                                      (delete-region (point-min) (- (search-forward "<head") 6))
                                      (delete-region (search-forward "</head>") (point-max))
                                      (goto-char (point-min))
                                      (while (re-search-forward "<script[^\u2800]+?</script>" nil t)
                                        (replace-match ""))
                                      (goto-char (point-min))
                                      (while (re-search-forward "<style[^\u2800]+?</style>" nil t)
                                        (replace-match ""))
                                      (libxml-parse-html-region (point-min) (point-max))))))
                         (meta (delq nil
                                     (mapcar
                                      (lambda (tag)
                                        (when (eq 'meta (car tag))
                                          (cons (or (cdr (assoc 'name (cadr tag)))
                                                    (cdr (assoc 'property (cadr tag))))
                                                (cdr (assoc 'content (cadr tag))))))
                                      head-data))))
                    (let ((title (or (cdr (assoc "og:title" meta))
                                     (cdr (assoc "twitter:title" meta))
                                     (nth 2 (assq 'title head-data))))
                          (description (or (cdr (assoc "og:description" meta))
                                           (cdr (assoc "twitter:description" meta))
                                           (cdr (assoc "description" meta))))
                          (image (or (cdr (assoc "og:image" meta))
                                     (cdr (assoc "twitter:image" meta)))))
                      (when image
                        (setq image (replace-regexp-in-string
                                     "^/" (concat "https://" (replace-regexp-in-string "//\\([^/]+\\)/?.*" "\\1" url) "/")
                                     (replace-regexp-in-string
                                      "^//" "https://"
                                      image))))
                      (list :title title :description description :image image))))
                 org-url-unfurl-metadata--cache)))))
#+end_src

**** LaTeX Rendering
***** é¢„æ¸²æŸ“
æˆ‘è®¤ä¸º ~dvisvgm~ æ˜¯ä¸€ä¸ªç›¸å½“å¼•äººæ³¨ç›®çš„é€‰æ‹©ã€‚ç„¶è€Œç›®å‰è¿™å¹¶ä¸æ˜¯å¾ˆå¥½åœ°æ‰©å±•ã€‚
#+begin_src emacs-lisp
;; (setq-default org-html-with-latex `dvisvgm)
#+end_src
***** MathJax
å¦‚æœä½¿ç”¨ MathJaxï¼Œæˆ‘ä»¬å¸Œæœ›ä½¿ç”¨ç‰ˆæœ¬ 3 è€Œä¸æ˜¯é»˜è®¤çš„ç‰ˆæœ¬ 2ã€‚é€šè¿‡[[https://www.intmath.com/cg5/katex-mathjax-comparison.php][å¯¹æ¯”]]æˆ‘ä»¬å‘ç°å®ƒä¼¼ä¹
å¿«äº† 5 å€ï¼Œè€Œä¸”å®ƒä½¿ç”¨å•ä¸ªæ–‡ä»¶è€Œä¸æ˜¯å¤šä¸ªæ–‡ä»¶ï¼Œå°±æ˜¯æœ‰ç‚¹å¤§è€Œå·²ã€‚å€¼å¾—åº†å¹¸çš„æ˜¯ï¼Œè¿™å¯ä»¥
é€šè¿‡æ·»åŠ  ~async~ å±æ€§æ¥å»¶è¿ŸåŠ è½½æ¥ç¼“è§£ã€‚
#+begin_src emacs-lisp
(setq org-html-mathjax-options
      '((path "https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-svg.js" )
        (scale "1")
        (autonumber "ams")
        (multlinewidth "85%")
        (tagindent ".8em")
        (tagside "right")))

(setq org-html-mathjax-template
      "<script>
MathJax = {
  chtml: {
    scale: %SCALE
  },
  svg: {
    scale: %SCALE,
    fontCache: \"global\"
  },
  tex: {
    tags: \"%AUTONUMBER\",
    multlineWidth: \"%MULTLINEWIDTH\",
    tagSide: \"%TAGSIDE\",
    tagIndent: \"%TAGINDENT\"
  }
};
</script>
<script id=\"MathJax-script\" async
        src=\"%PATH\"></script>")
#+end_src

*** LaTeX å¯¼å‡º
**** ç¼–è¯‘
é»˜è®¤æƒ…å†µä¸‹ï¼ŒOrg ä½¿ç”¨ ~pdflatex~ \times 3 + ~bibtex~ ã€‚ è¿™åœ¨ç°ä»£ä¸–ç•Œæ ¹æœ¬è¡Œä¸
é€šã€‚ ~latexmk~ + ~biber~ (è‡ªåŠ¨ä¸ ~latexmk~ ä¸€èµ·ä½¿ç”¨) æ˜¯ä¸€ä¸ªç®€å•çš„ä¼˜è¶Šç»„åˆã€‚

#+begin_src emacs-lisp
;; org-latex-compilers = ("pdflatex" "xelatex" "lualatex"), which are the possible values for %latex
(setq org-latex-pdf-process '("latexmk -f -pdf -%latex -shell-escape -interaction=nonstopmode -output-directory=%o %f"))
#+end_src

è™½ç„¶ ~org-latex-pdf-process~ ç¡®å®æ”¯æŒä¸€ä¸ªå‡½æ•°ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨å®ƒæ¥ä»£æ›¿ï¼Œä½†è¿™å°†ä¸å†
ä½¿ç”¨æ—¥å¿—ç¼“å†²åŒº --- è¿™æœ‰ç‚¹ç›²ç›®ï¼Œä½ ç»™å®ƒä¸€ä¸ªæ–‡ä»¶åå¹¶æœŸæœ›å®ƒåšå®ƒçš„äº‹æƒ…ã€‚

~org-latex-compilers~ çš„é»˜è®¤å€¼ä»¥æ³¨é‡Šå½¢å¼ç»™å‡ºï¼Œä»¥æŸ¥çœ‹
~org-latex-pdf-process~ å¦‚ä½•ä¸å®ƒä»¬ä¸€èµ·å·¥ä½œã€‚

è™½ç„¶ä¸Šé¢çš„ ~-%latex~ æœ‰ç‚¹ hacky (~-pdflatex~ æœŸæœ›è¢«èµ‹äºˆä¸€ä¸ªå€¼)ï¼Œä½†å®ƒå…è®¸æˆ‘ä»¬ ä¿æŒ ~org-latex-compilers~ ä¸å˜ã€‚å¦‚æœæˆ‘æ‰“å¼€ä¸€ä¸ªä½¿ç”¨ =#+LATEX_COMPILER= çš„
org æ–‡ä»¶ï¼Œè¿™å¾ˆå¥½ï¼Œå®ƒåº”è¯¥ä»ç„¶å¯ä»¥å·¥ä½œã€‚

**** è¶…æ£’çš„å¤é€‰æ¡†
æˆ‘ä»¬å¯ä»¥å‡è®¾åœ¨åºè¨€éƒ¨åˆ†ï¼Œå®šä¹‰äº†ä¸‹é¢çš„å„ç§è‡ªå®šä¹‰ =\checkbox...= å‘½ä»¤ã€‚

#+begin_src emacs-lisp
(defun +org-export-latex-fancy-item-checkboxes (text backend info)
  (when (org-export-derived-backend-p backend 'latex)
    (replace-regexp-in-string
     "\\\\item\\[{$\\\\\\(\\w+\\)$}\\]"
     (lambda (fullmatch)
       (concat "\\\\item[" (pcase (substring fullmatch 9 -3) ; content of capture group
                             ("square"   "\\\\checkboxUnchecked")
                             ("boxminus" "\\\\checkboxTransitive")
                             ("boxtimes" "\\\\checkboxChecked")
                             (_ (substring fullmatch 9 -3))) "]"))
     text)))

(add-to-list 'org-export-filter-item-functions
             '+org-export-latex-fancy-item-checkboxes)
#+end_src

**** ç±»æ¨¡æ¿
æˆ‘çœŸçš„å¾ˆå–œæ¬¢ KOMA æ†ç»‘åŒ…ã€‚å®ƒæä¾›äº†ä¸€ç»„æœºåˆ¶æ¥è°ƒæ•´æ–‡æ¡£æ ·å¼ï¼Œè¿™äº›æœºåˆ¶æ—¢æ˜“äºä½¿ç”¨ï¼Œåˆé
å¸¸å…¨é¢ã€‚ä¾‹å¦‚ï¼Œæˆ‘æ›´å–œæ¬¢é¡µè¾¹ç©ºç™½å¤„çš„èŠ‚å·ï¼Œè¿™å¯ä»¥ç”¨ä¸‹é¢çš„ LaTeX æ¥å®Œæˆã€‚
#+name: latex-hanging-secnum
#+begin_src LaTeX
\\renewcommand\\sectionformat{\\llap{\\thesection\\autodot\\enskip}}
\\renewcommand\\subsectionformat{\\llap{\\thesubsection\\autodot\\enskip}}
\\renewcommand\\subsubsectionformat{\\llap{\\thesubsubsection\\autodot\\enskip}}
#+end_src

è¿˜æœ‰è¶…å¤§çš„ =\chapter= ã€‚
#+name: latex-big-chapter
#+begin_src LaTeX
\\RedeclareSectionCommand[afterindent=false, beforeskip=0pt, afterskip=0pt, innerskip=0pt]{chapter}
\\setkomafont{chapter}{\\normalfont\\Huge}
\\renewcommand*{\\chapterheadstartvskip}{\\vspace*{0\\baselineskip}}
\\renewcommand*{\\chapterheadendvskip}{\\vspace*{0\\baselineskip}}
\\renewcommand*{\\chapterformat}{%
  \\fontsize{60}{30}\\selectfont\\rlap{\\hspace{6pt}\\thechapter}}
\\renewcommand*\\chapterlinesformat[3]{%
  \\parbox[b]{\\dimexpr\\textwidth-0.5em\\relax}{%
    \\raggedleft{{\\large\\scshape\\bfseries\\chapapp}\\vspace{-0.5ex}\\par\\Huge#3}}%
    \\hfill\\makebox[0pt][l]{#2}}
#+end_src

ç°åœ¨è®©æˆ‘ä»¬åœ¨ Org LaTeX ç±»ä¸­æ·»åŠ ä¸€äº› KOMAã€‚

#+begin_src emacs-lisp :noweb no-export
(after! ox-latex
  (let* ((article-sections '(("\\section{%s}" . "\\section*{%s}")
                             ("\\subsection{%s}" . "\\subsection*{%s}")
                             ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                             ("\\paragraph{%s}" . "\\paragraph*{%s}")
                             ("\\subparagraph{%s}" . "\\subparagraph*{%s}")))
         (book-sections (append '(("\\chapter{%s}" . "\\chapter*{%s}"))
                                article-sections))
         (hanging-secnum-preamble "
<<latex-hanging-secnum>>
")
         (big-chap-preamble "
<<latex-big-chapter>>
"))
    (setcdr (assoc "article" org-latex-classes)
            `(,(concat "\\documentclass{scrartcl}" hanging-secnum-preamble)
              ,@article-sections))
    (add-to-list 'org-latex-classes
                 `("report" ,(concat "\\documentclass{scrartcl}" hanging-secnum-preamble)
                   ,@article-sections))
    (add-to-list 'org-latex-classes
                 `("book" ,(concat "\\documentclass[twoside=false]{scrbook}"
                                   big-chap-preamble hanging-secnum-preamble)
                   ,@book-sections))
    (add-to-list 'org-latex-classes
                 `("blank" "[NO-DEFAULT-PACKAGES]\n[NO-PACKAGES]\n[EXTRA]"
                   ,@article-sections))
    (add-to-list 'org-latex-classes
                 `("bmc-article" "\\documentclass[article,code,maths]{bmc}\n[NO-DEFAULT-PACKAGES]\n[NO-PACKAGES]\n[EXTRA]"
                   ,@article-sections))
    (add-to-list 'org-latex-classes
                 `("bmc" "\\documentclass[code,maths]{bmc}\n[NO-DEFAULT-PACKAGES]\n[NO-PACKAGES]\n[EXTRA]"
                   ,@book-sections))))

(setq org-latex-tables-booktabs t
      org-latex-hyperref-template "
<<latex-fancy-hyperref>>
"
      org-latex-reference-command "\\cref{%s}")
#+end_src

ç„¶è€Œ =hyperref= è®¾ç½®éœ€è¦å•ç‹¬å¤„ç†ã€‚
#+name: latex-fancy-hyperref
#+begin_src LaTeX
\\providecolor{url}{HTML}{0077bb}
\\providecolor{link}{HTML}{882255}
\\providecolor{cite}{HTML}{999933}
\\hypersetup{
  pdfauthor={%a},
  pdftitle={%t},
  pdfkeywords={%k},
  pdfsubject={%d},
  pdfcreator={%c},
  pdflang={%L},
  breaklinks=true,
  colorlinks=true,
  linkcolor=link,
  urlcolor=url,
  citecolor=cite\n}
\\urlstyle{same}
#+end_src

**** æ›´æ™ºèƒ½çš„åºè¨€
***** ä½¿ç”¨ç¤ºä¾‹
æˆ‘ä»¬ç»å¸¸å¸Œæœ›åœ¨æ–‡æ¡£åºè¨€ä¸­åŒ…å«ç‰¹å®šçš„ LaTeX ç‰‡æ®µã€‚ æ¯æ¬¡éƒ½å¿…é¡»ç å‡º / è®°ä½å®ƒä»¬æ˜¯ä¸€ç§
ç—›è‹¦ã€‚æˆ‘å‡ ä¹æ€»æ˜¯æƒ³åŒ…å«ä¸‹é¢çš„ä»£ç ç‰‡æ®µã€‚

#+name: org-latex-embed-files-preamble
#+begin_src LaTeX
\\usepackage[main,include]{embedall}
\\IfFileExists{./\\jobname.org}{\\embedfile[desc=The original file]{\\jobname.org}}{}
#+end_src

æˆ‘ä»¬å¯ä»¥åœ¨æ¯ä¸ª ~org-latex-classes~ ä¸­åŒ…å«æˆ‘ä»¬å¯èƒ½éœ€è¦çš„æ¯ä¸ªåŒ…ï¼Œä½†è¿™éå¸¸ä½æ•ˆï¼Œ
æˆ‘ä¸æƒ³è€ƒè™‘ç»´æŠ¤å®ƒã€‚

ç›¸åï¼Œæˆ‘ä»¬å¯ä»¥é€šè¿‡æ‹†åˆ†æˆ‘ä»¬æƒ³è¦çš„åŠŸèƒ½æ¥æä¾›ä¸€äº›ç»†ç²’åº¦ï¼Œç„¶åé€šè¿‡å®ç°ä¸€ä¸ªç³»ç»Ÿæ¥è‡ªåŠ¨
æ£€æµ‹éœ€è¦å“ªäº›åŠŸèƒ½å¹¶ç”Ÿæˆæä¾›è¿™äº›åŠŸèƒ½çš„å‰å¯¼ç ï¼Œä»è€Œå°†ä½“éªŒæå‡åˆ°ä¸€ä¸ªå…¨æ–°çš„æ°´å¹³ã€‚
***** æ¡ä»¶å†…å®¹
è®©æˆ‘ä»¬è€ƒè™‘åœ¨ç‰¹å®šæƒ…å†µä¸‹æˆ‘ä»¬æƒ³è¦çš„å†…å®¹ã€‚

Caption å¯ä»¥åšä¸€äº›è°ƒæ•´ï¼Œæ¯”å¦‚è¿™æ ·
+ æ‚¨å¯ä»¥è½»æ¾æ‹¥æœ‰å¤šä¸ª Caption
+ æŒ‡å‘å›¾çš„é“¾æ¥å°†æ‚¨å¸¦åˆ°å›¾çš„é¡¶éƒ¨ (è€Œä¸æ˜¯åº•éƒ¨)
+ Caption æ ‡ç­¾å¯ä»¥ç¨å¾®å¼ºè°ƒä¸€ç‚¹
+ åªæœ‰å½“è·¨è¶Šå¤šè¡Œæ—¶ï¼Œå¤šè¡Œ Caption åº”è¯¥å‘å³ä¸€ç‚¹

#+name: org-latex-caption-preamble
#+begin_src LaTeX
\\usepackage{subcaption}
\\usepackage[hypcap=true]{caption}
\\setkomafont{caption}{\\sffamily\\small}
\\setkomafont{captionlabel}{\\upshape\\bfseries}
\\captionsetup{justification=raggedright,singlelinecheck=true}
\\usepackage{capt-of} % required by Org
#+end_src

é»˜è®¤çš„å¤é€‰æ¡†å¤ªä¸‘äº†ï¼Œç”¨ä¸€äº›å¥½çœ‹çš„æ›¿ä»£å“å§ã€‚
#+name: org-latex-checkbox-preamble
#+begin_src LaTeX
\\newcommand{\\checkboxUnchecked}{$\\square$}
\\newcommand{\\checkboxTransitive}{\\rlap{\\raisebox{-0.1ex}{\\hspace{0.35ex}\\Large\\textbf -}}$\\square$}
\\newcommand{\\checkboxChecked}{\\rlap{\\raisebox{0.2ex}{\\hspace{0.35ex}\\scriptsize \\ding{52}}}$\\square$}
#+end_src

"æ¶ˆæ¯å—" æ˜¯ä¸€ä¸ªä¸é”™çš„æƒ³æ³•ï¼Œå°±åƒ info/warning/error/successã€‚LaTeX ä¸­çš„å®å¯
ä»¥åˆ›å»ºå®ƒä»¬ã€‚

#+name: org-latex-box-preamble
#+begin_src LaTeX
\\ExplSyntaxOn
\\NewCoffin\\Content
\\NewCoffin\\SideRule
\\NewDocumentCommand{\\defsimplebox}{O{\\ding{117}} O{0.36em} m m m}{%
  % #1 ding, #2 ding offset, #3 name, #4 colour, #5 default label
  \\definecolor{#3}{HTML}{#4}
  \\NewDocumentEnvironment{#3}{ O{#5} }
  {
    \\vcoffin_set:Nnw \\Content { \\linewidth }
    \\noindent \\ignorespaces
    \\par\\vspace{-0.7\\baselineskip}%
    \\textcolor{#3}{#1}~\\textcolor{#3}{\\textbf{##1}}%
    \\vspace{-0.8\\baselineskip}
    \\begin{addmargin}[1em]{1em}
    }
    {
    \\end{addmargin}
    \\vspace{-0.5\\baselineskip}
    \\vcoffin_set_end:
    \\SetHorizontalCoffin\\SideRule{\\color{#3}\\rule{1pt}{\\CoffinTotalHeight\\Content}}
    \\JoinCoffins*\\Content[l,t]\\SideRule[l,t](#2,-0.7em)
    \\noindent\\TypesetCoffin\\Content
    \\vspace*{\\CoffinTotalHeight\\Content}\\bigskip
    \\vspace{-2\\baselineskip}
  }
}
\\ExplSyntaxOff
#+end_src

æœ€åï¼Œæˆ‘ä»¬ä¼šå°†è¿™äº›å†…å®¹ä¼ é€’åˆ°ä¸€äº›å…¨å±€å˜é‡ä¸­ï¼Œä»¥ä¾¿äºè®¿é—®ã€‚

#+begin_src emacs-lisp :noweb no-export
(defvar org-latex-embed-files-preamble "
<<org-latex-embed-files-preamble>>
"
  "Preamble that embeds files within the pdf.")

(defvar org-latex-caption-preamble "
<<org-latex-caption-preamble>>
"
  "Preamble that improves captions.")

(defvar org-latex-checkbox-preamble "
<<org-latex-checkbox-preamble>>
"
  "Preamble that improves checkboxes.")

(defvar org-latex-box-preamble "
<<org-latex-box-preamble>>
"
  "Preamble that provides a macro for custom boxes.")
#+end_src

åœ¨ "é€šç”¨åºè¨€" ä¸­ï¼Œæˆ‘ä»¬å·²ç»åµŒå…¥äº†æº .org æ–‡ä»¶ï¼Œä½†æœ€å¥½åµŒå…¥æ‰€æœ‰ tangle æ–‡ä»¶ã€‚é€šè¿‡
å°†æ¯ä¸ª tangle æ–‡ä»¶æ˜ å°„åˆ°åµŒå…¥æ–‡ä»¶ (å¦‚æœå­˜åœ¨) çš„è¡¨å•ï¼Œè¿™å¾ˆå®¹æ˜“å®ç°ã€‚è€ƒè™‘åˆ°æˆ‘ä»¬å·²
ç»èµ°åˆ°äº†è¿™ä¸€æ­¥ï¼Œä¸ºä»€ä¹ˆä¸æ·»åŠ ä¸€ä¸ªä¸“ç”¨çš„ =#+emded= å…³é”®å­—ï¼Œè¿™æ ·æˆ‘ä»¬å°±å¯ä»¥åµŒå…¥ä»»ä½•
æˆ‘ä»¬æƒ³è¦çš„ä¸œè¥¿ã€‚

#+begin_src emacs-lisp
(defun org-latex-embed-extra-files ()
  "Return a string that uses embedfile to embed all tangled files."
  (mapconcat
   (lambda (file-desc)
     (format "\\IfFileExists{%1$s}{\\embedfile[desc=%2$s]{%1$s}}{}"
             (thread-last (car file-desc)
               (replace-regexp-in-string "\\\\" "\\\\\\\\")
               (replace-regexp-in-string "~" "\\\\string~"))
             (cdr file-desc)))
   (append
    (mapcar (lambda (f-block)
              (let ((file-lang (cons (or (car f-block) (caddr (cadr f-block))) (caadr f-block))))
                (cons (car file-lang) (format "Tangled %s file" (cdr file-lang)))))
            (org-babel-tangle-collect-blocks)) ; all files being tangled to
    (let (extra-files)
      (save-excursion
        (goto-char (point-min))
        (while (re-search-forward "^[ \t]*#\\+embed:" nil t)
          (let* ((file-desc (split-string (org-element-property :value (org-element-at-point)) " :desc\\(?:ription\\)? ")))
            (push (cons (car file-desc) (or (cdr file-desc) "Extra file")) extra-files))))
      (nreverse extra-files)))
   "\n"))
#+end_src

ç°åœ¨æ‰€æœ‰ tangle æ–‡ä»¶éƒ½å°†è¢«åµŒå…¥ï¼Œæˆ‘ä»¬å¯ä»¥åƒè¿™æ ·åµŒå…¥ä»»æ„æ–‡ä»¶ï¼š
#+begin_src org
,#+embed: some-file :description flavour text about the file
#+end_src

è¿™ç›®å‰ä¸ä¼šå®Œæˆæˆ–ç±»ä¼¼çš„äº‹æƒ…ï¼Œå› ä¸ºæˆ‘ä»¬è¿˜æ²¡æœ‰å‘Šè¯‰ Org å®ƒæ˜¯ä¸€ä¸ªå…³é”®å­—ã€‚å®ƒä¹Ÿæ˜¯ LaTeX
ç‰¹æœ‰çš„ï¼Œæ‰€ä»¥ä¹Ÿè®¸åº”è¯¥å°†å®ƒæ›´æ”¹ä¸º =#+latex_embed= æˆ–ç±»ä¼¼çš„ä¸œè¥¿ã€‚
***** å†…å®¹-ç‰¹å¾-åºè¨€å…³è”
æœ€åˆï¼Œè¿™ä¸ªæƒ³æ³•æ˜¯é€šè¿‡ä¸€ä¸ª alist å®ç°çš„ï¼Œè¯¥ alist å°†æœç´¢å½“å‰ Org æ–‡ä»¶ä»¥æŒ‡ç¤ºéœ€è¦
æŸäº›åŠŸèƒ½çš„ç»“æ„ï¼Œå¹¶åœ¨æä¾›è¯¥åŠŸèƒ½çš„åºè¨€ä¸­æ’å…¥ä¸€ä¸ª LaTeX ç‰‡æ®µã€‚å½“æ£€æµ‹åˆ°çš„ç‰¹å¾ä¸æ”¯æŒ
è¿™äº›ç‰¹å¾æ‰€éœ€çš„ LaTeX ä»£ç ä¹‹é—´å­˜åœ¨åŒå°„æ—¶ï¼Œè¿™ä¸€åˆ‡éƒ½å¾ˆå¥½ï¼Œä½†åœ¨è®¸å¤šæƒ…å†µä¸‹ï¼Œè¿™ç§å…³ç³»
ä¸æ˜¯å•å°„çš„ã€‚

ä¸ºäº†æ›´å¥½åœ°æ¨¡æ‹Ÿå®é™…æƒ…å†µï¼Œæˆ‘åœ¨è¿™ä¸ªè¿‡ç¨‹ä¸­æ·»åŠ äº†ä¸€ä¸ªé¢å¤–çš„å±‚ï¼Œå…¶ä¸­æ¯ä¸ªæ£€æµ‹åˆ°çš„ç‰¹å¾éƒ½
ä¼šç»™å‡ºä¸€ä¸ªæ‰€éœ€çš„ ã€Œç‰¹å¾æ ‡å¿—ã€åˆ—è¡¨ã€‚åªéœ€åˆå¹¶åŠŸèƒ½æ ‡å¿—åˆ—è¡¨ï¼Œæˆ‘ä»¬ä¸å†éœ€è¦æ³¨å…¥æ€§æ¥é¿å…
LaTeX é‡å¤ã€‚ç„¶åé¢å¤–çš„å±‚åœ¨ç‰¹å¾æ ‡å¿—å’Œå¯ç”¨äºå®ç°è¯¥ç‰¹å¾çš„è§„èŒƒä¹‹é—´å½¢æˆåŒå°„ã€‚

è¯¥æ¨¡å‹è¿˜æä¾›äº†è®¸å¤šä¸é”™çš„æ¬¡è¦å¥½å¤„ï¼Œä¾‹å¦‚åŠŸèƒ½ä¾èµ–çš„ç®€å•å®ç°ã€‚

#+begin_src dot :file misc/org-latex-clever-preamble.svg :exports none
digraph {
    graph [bgcolor="transparent"];
    node  [shape="underline" penwidth="2" width="1.3" style="rounded,filled" fillcolor="#efefef" color="#c9c9c9" fontcolor="#000000" fontname="overpass"];
    edge  [color="#aaaaaa" penwidth="1.2"]
    rankdir=LR

    node[group=a,color="#2ec27e"]
    "file:*.svg"
    "file:*.jpeg"
    "file:*.png"
    "#+caption"
    "xkcd:*"
    node[group=b,color="#f5c211"]
    "svg"
    "image"
    "caption"
    node[group=c,color="#813d9c"]
    "(TeX) svg"
    "(TeX) graphicx"
    "(TeX) caption"

    "file:*.svg" -> "svg" -> "(TeX) svg"
    "file:*.jpeg" -> "image" -> "(TeX) graphicx"
    "file:*.png" -> "image"
    "(TeX) svg":s -> "(TeX) graphicx":n [constraint=false]
    "#+caption" -> "caption" -> "(TeX) caption"
    "xkcd:*" -> "image"
    "xkcd:*" -> "caption"
}
#+end_src

#+caption: Org åŠŸèƒ½ã€åŠŸèƒ½æ ‡å¿—å’Œ LaTeX ç‰‡æ®µä¹‹é—´çš„å…³è”
#+attr_html: :class invertible :alt DAG showing how Org features flow through to LaTeX :style max-width:min(24em,100%)
#+attr_latex: :width 0.6\linewidth
[[file:misc/org-latex-clever-preamble.svg]]

é¦–å…ˆï¼Œæˆ‘ä»¬å°†å®ç°è¯¥æ¨¡å‹çš„ç‰¹å¾æ£€æµ‹ç»„ä»¶ã€‚æˆ‘å¸Œæœ›å®ƒèƒ½å¤Ÿä½¿ç”¨å°½å¯èƒ½å¤šçš„çŠ¶æ€ä¿¡æ¯ï¼Œå› æ­¤åŠŸ
èƒ½æµ‹è¯•åº”è¯¥éå¸¸é€šç”¨ã€‚

#+begin_src emacs-lisp
(defvar org-latex-embed-files t
  "Embed the source .org, .tex, and any tangled files.")
(defvar org-latex-use-microtype t
  "Use the microtype pakage.")
(defvar org-latex-italic-quotes t
  "Make \"quote\" environments italic.")
(defvar org-latex-par-sep t
  "Vertically seperate paragraphs, and remove indentation.")

(defvar org-latex-conditional-features
  '(("\\[\\[\\(?:file\\|https?\\):\\(?:[^]]\\|\\\\\\]\\)+?\\.\\(?:eps\\|pdf\\|png\\|jpeg\\|jpg\\|jbig2\\)\\]\\]\\|\\\\includegraphics[\\[{]" . image)
    ("\\[\\[\\(?:file\\|https?\\):\\(?:[^]]+?\\|\\\\\\]\\)\\.svg\\]\\]\\|\\\\includesvg[\\[{]" . svg)
    ("\\\\(\\|\\\\\\[\\|\\\\begin{\\(?:math\\|displaymath\\|equation\\|align\\|flalign\\|multiline\\|gather\\)[a-z]*\\*?}" . maths)
    ("^[ \t]*|" . table)
    ("cref:\\|\\cref{\\|\\[\\[[^\\]+\n?[^\\]\\]\\]" . cleveref)
    ("[;\\\\]?\\b[A-Z][A-Z]+s?[^A-Za-z]" . acronym)
    ("\\+[^ ].*[^ ]\\+\\|_[^ ].*[^ ]_\\|\\\\uu?line\\|\\\\uwave\\|\\\\sout\\|\\\\xout\\|\\\\dashuline\\|\\dotuline\\|\\markoverwith" . underline)
    (":float wrap" . float-wrap)
    (":float sideways" . rotate)
    ("^[ \t]*#\\+caption:\\|\\\\caption" . caption)
    ("\\[\\[xkcd:" . (image caption))
    (org-latex-use-microtype . microtype)
    ((and org-latex-italic-quotes "^[ \t]*#\\+begin_quote\\|\\\\begin{quote}") . italic-quotes)
    (org-latex-par-sep . par-sep)
    ((org-latex-embed-extra-files) . embed-files)
    ((and org-latex-embed-files "^[ \t]*#\\+begin_src\\|^[ \t]*#\\+BEGIN_SRC") . embed-tangled)
    ("^[ \t]*\\(?:[-+*]\\|[0-9]+[.)]\\|[A-Za-z]+[.)]\\) \\[[ -X]\\]" . checkbox)
    ("^[ \t]*#\\+begin_warning\\|\\\\begin{warning}" . box-warning)
    ("^[ \t]*#\\+begin_info\\|\\\\begin{info}"       . box-info)
    ("^[ \t]*#\\+begin_notes\\|\\\\begin{notes}"     . box-notes)
    ("^[ \t]*#\\+begin_success\\|\\\\begin{success}" . box-success)
    ("^[ \t]*#\\+begin_error\\|\\\\begin{error}"     . box-error))
  "Org feature tests and associated LaTeX feature flags.

Alist where the car is a test for the presense of the feature,
and the cdr is either a single feature symbol or list of feature symbols.

When a string, it is used as a regex search in the buffer.
The feature is registered as present when there is a match.

The car can also be a
- symbol, the value of which is fetched
- function, which is called with info as an argument
- list, which is `eval'uated

If the symbol, function, or list produces a string: that is used as a regex
search in the buffer. Otherwise any non-nil return value will indicate the
existance of the feature.")
#+end_src

ç„¶åæˆ‘ä»¬æä¾›ä¸€ç§æ–¹æ³•æ¥ç”Ÿæˆæä¾›è¿™äº›åŠŸèƒ½çš„åºè¨€ã€‚é™¤äº†
~org-latex-conditional-features~ ä¸­å‘½åçš„ç‰¹æ€§ä¹‹å¤–ï¼Œæˆ‘ä»¬è¿˜å°†åˆ›å»ºå…ƒç‰¹æ€§ï¼Œè¿™äº›
ç‰¹æ€§å¯èƒ½æ˜¯å…¶ä»–ç‰¹æ€§æ‰€éœ€è¦çš„ (=:requires=)ï¼Œæˆ–è€…é»˜è®¤æƒ…å†µä¸‹æ˜¯å¯ç”¨çš„
(=:eager t=)ã€‚ä¸ºäº†è¿›ä¸€æ­¥æ§åˆ¶ï¼Œæˆ‘åªèƒ½åœ¨æŸäº›å…¶ä»–åŠŸèƒ½å¤„äºå¯ç”¨çŠ¶æ€ (=:when=) å¹¶è¢«
å…¶ä»–åŠŸèƒ½å±è”½ (=:prevents=) æ—¶ä½¿ç”¨æŸäº›åŠŸèƒ½ã€‚æˆ‘å°†ä½¿ç”¨ä»¥ =.= å¼€å¤´çš„å…ƒåŠŸèƒ½çš„çº¦å®š
ï¼Œä»¥åŠä»¥ =!= å¼€å¤´çš„ =:eager= åŠŸèƒ½ï¼Œä½¿å®ƒä»¬çš„æ€§è´¨æ›´æ˜æ˜¾ã€‚

LaTeX ä¸­çš„å¦ä¸€ä¸ªè€ƒè™‘å› ç´ æ˜¯åŠ è½½é¡ºåºï¼Œè¿™åœ¨æŸäº›æƒ…å†µä¸‹å¾ˆé‡è¦ã€‚é™¤æ­¤ä¹‹å¤–ï¼Œæœ‰æŸç§åˆç†çš„
æ’åºæ˜¯å¾ˆå¥½çš„ã€‚ä¸ºæ­¤æˆ‘å°†ä»‹ç»ä¸€ä¸ª =:order= å…³é”®å­—ã€‚ä½¿ç”¨å®ƒå°†æŒ‰å¦‚ä¸‹æ–¹å¼æ’åˆ—ç‰‡æ®µã€‚

+ =-2= åµŒå…¥æ–‡ä»¶è®¾ç½®
+ =-1= é¢å¤–çš„æ–‡ä»¶åµŒå…¥
+ =0= æ’ç‰ˆ
  - =0= å­—ä½“æœ¬èº«
  - =0.1= æ’ç‰ˆè°ƒæ•´ (=microtype=)
  - =0.2= æ•°å­¦è®¾ç½®
  - =0.3= æ•°å­¦å­—ä½“
  - =0.4= é¢å¤–çš„æ–‡å­—æ•´å½¢ (~\acr~)
  - =0.5-0.9= å…¶ä»–æ–‡æœ¬ä¿®æ”¹ï¼Œå°è¯•å°†è¾ƒçŸ­çš„ç‰‡æ®µæ”¾åœ¨é¦–ä½
+ =1= (/default/)
+ =2= è¡¨å’Œå›¾
+ =3= å…¶ä»–çŸ­å†…å®¹
+ =4= å„ç§ boxes

#+begin_src emacs-lisp
(defvar org-latex-feature-implementations
  '((image         :snippet "\\usepackage{graphicx}" :order 2)
    (svg           :snippet "\\usepackage[inkscapelatex=false]{svg}" :order 2)
    (maths         :snippet "\\usepackage[nofont]{bmc-maths}" :order 0.2)
    (table         :snippet "\\usepackage{longtable}\n\\usepackage{booktabs}" :order 2)
    (cleveref      :snippet "\\usepackage[capitalize]{cleveref}" :order 1) ; after bmc-maths
    (underline     :snippet "\\usepackage[normalem]{ulem}" :order 0.5)
    (float-wrap    :snippet "\\usepackage{wrapfig}" :order 2)
    (rotate        :snippet "\\usepackage{rotating}" :order 2)
    (caption       :snippet org-latex-caption-preamble :order 2.1)
    (microtype     :snippet "\\usepackage[activate={true,nocompatibility},final,tracking=true,kerning=true,spacing=true,factor=2000]{microtype}\n" :order 0.1)
    (embed-files   :snippet org-latex-embed-files-preamble :order -2)
    (embed-tangled :requires embed-files :snippet (concat (org-latex-embed-extra-files) "\n") :order -1)
    (acronym       :snippet "\\newcommand{\\acr}[1]{\\protect\\textls*[110]{\\scshape #1}}\n\\newcommand{\\acrs}{\\protect\\scalebox{.91}[.84]{\\hspace{0.15ex}s}}" :order 0.4)
    (italic-quotes :snippet "\\renewcommand{\\quote}{\\list{}{\\rightmargin\\leftmargin}\\item\\relax\\em}\n" :order 0.5)
    (par-sep       :snippet "\\setlength{\\parskip}{\\baselineskip}\n\\setlength{\\parindent}{0pt}\n" :order 0.5)
    (.pifont       :snippet "\\usepackage{pifont}")
    (.xcoffins     :snippet "\\usepackage{xcoffins}")
    (checkbox      :requires .pifont :order 3
                   :snippet (concat (unless (memq 'maths features)
                                      "\\usepackage{amssymb} % provides \\square")
                                    org-latex-checkbox-preamble))
    (.fancy-box    :requires (.pifont .xcoffins) :snippet org-latex-box-preamble :order 3.9)
    (box-warning   :requires .fancy-box :snippet "\\defsimplebox{warning}{e66100}{Warning}" :order 4)
    (box-info      :requires .fancy-box :snippet "\\defsimplebox{info}{3584e4}{Information}" :order 4)
    (box-notes     :requires .fancy-box :snippet "\\defsimplebox{notes}{26a269}{Notes}" :order 4)
    (box-success   :requires .fancy-box :snippet "\\defsimplebox{success}{26a269}{\\vspace{-\\baselineskip}}" :order 4)
    (box-error     :requires .fancy-box :snippet "\\defsimplebox{error}{c01c28}{Important}" :order 4))
  "LaTeX features and details required to implement them.

List where the car is the feature symbol, and the rest forms a plist with the
following keys:
- :snippet, which may be either
  - a string which should be included in the preamble
  - a symbol, the value of which is included in the preamble
  - a function, which is evaluated with the list of feature flags as its
    single argument. The result of which is included in the preamble
  - a list, which is passed to `eval', with a list of feature flags available
    as \"features\"

- :requires, a feature or list of features that must be available
- :when, a feature or list of features that when all available should cause this
    to be automatically enabled.
- :prevents, a feature or list of features that should be masked
- :order, for when ordering is important. Lower values appear first.
    The default is 0.
- :eager, when non-nil the feature will be eagerly loaded, i.e. without being detected.")
#+end_src
***** ç‰¹å¾ç¡®å®š
ç°åœ¨æˆ‘ä»¬å·²ç»å®šä¹‰äº† ~org-latex-conditional-features~ ï¼Œæˆ‘ä»¬éœ€è¦ä½¿ç”¨å®ƒæ¥æå–
åœ¨ Org ç¼“å†²åŒºä¸­æ‰¾åˆ°çš„ç‰¹å¾åˆ—è¡¨ã€‚

#+begin_src emacs-lisp
(defun org-latex-detect-features (&optional buffer info)
  "List features from `org-latex-conditional-features' detected in BUFFER."
  (let ((case-fold-search nil))
    (with-current-buffer (or buffer (current-buffer))
      (delete-dups
       (mapcan (lambda (construct-feature)
                 (when (let ((out (pcase (car construct-feature)
                                    ((pred stringp) (car construct-feature))
                                    ((pred functionp) (funcall (car construct-feature) info))
                                    ((pred listp) (eval (car construct-feature)))
                                    ((pred symbolp) (symbol-value (car construct-feature)))
                                    (_ (user-error "org-latex-conditional-features key %s unable to be used" (car construct-feature))))))
                         (if (stringp out)
                             (save-excursion
                               (goto-char (point-min))
                               (re-search-forward out nil t))
                           out))
                   (if (listp (cdr construct-feature)) (cdr construct-feature) (list (cdr construct-feature)))))
               org-latex-conditional-features)))))
#+end_src

***** åºè¨€ç”Ÿæˆ
ä¸€æ—¦ç¡®å®šäº†æ‰€éœ€åŠŸèƒ½çš„åˆ—è¡¨ï¼Œæˆ‘ä»¬å¸Œæœ›ä½¿ç”¨ ~org-latex-feature-implementations~
æ¥ç”Ÿæˆ LaTeXï¼Œè¯¥ LaTeX åº”è¯¥æ’å…¥åˆ°åºè¨€ä¸­ä»¥æä¾›è¿™äº›åŠŸèƒ½ã€‚

é¦–å…ˆï¼Œæˆ‘ä»¬è¦åœ¨ ~org-latex-feature-implementations~ ä¸­å¤„ç†æˆ‘ä»¬çš„å…³é”®å­—ï¼Œä»¥
ç”Ÿæˆæ‰©å±•çš„åŠŸèƒ½åˆ—è¡¨ã€‚æˆ‘ä»¬å°†é€šè¿‡æ‰§è¡Œä»¥ä¸‹æ­¥éª¤æ¥åšåˆ°è¿™ä¸€ç‚¹ã€‚

+ æ¯ä¸ªåˆ—å‡ºçš„åŠŸèƒ½çš„ä¾èµ–é¡¹éƒ½æ·»åŠ åˆ°åŠŸèƒ½åˆ—è¡¨ (src_elisp{:requires}) ä¸­ã€‚
+ æ¯ä¸ªç‰¹æ€§çš„ src_elisp{:when} æ¡ä»¶ï¼Œä»¥åŠå¸¦æœ‰ src_elisp{:eager t} çš„å¯ç”¨ç‰¹
  æ€§ï¼Œéƒ½ä¼šè¢«è¯„ä¼°ï¼Œå¹¶ç›¸åº”åœ°æ·»åŠ  / åˆ é™¤
+ src_elisp{:prevents} å€¼ä¸­å­˜åœ¨çš„ä»»ä½•ç‰¹æ€§éƒ½å°†è¢«åˆ é™¤
+ åŠŸèƒ½åˆ—è¡¨æ¸…é™¤é‡å¤é¡¹
+ ç‰¹å¾åˆ—è¡¨æŒ‰ src_elisp{:order} (å‡åº) æ’åº

#+begin_src emacs-lisp
(defun org-latex-expand-features (features)
  "For each feature in FEATURES process :requires, :when, and :prevents keywords and sort according to :order."
  (dolist (feature features)
    (unless (assoc feature org-latex-feature-implementations)
      (message "Feature %s not provided in org-latex-feature-implementations, ignoring." feature)
      (setq features (remove feature features))))
  (setq current features)
  (while current
    (when-let ((requirements (plist-get (cdr (assq (car current) org-latex-feature-implementations)) :requires)))
      (setcdr current (if (listp requirements)
                          (append requirements (cdr current))
                        (cons requirements (cdr current)))))
    (setq current (cdr current)))
  (dolist (potential-feature
           (append features (delq nil (mapcar (lambda (feat)
                                                (when (plist-get (cdr feat) :eager)
                                                  (car feat)))
                                              org-latex-feature-implementations))))
    (when-let ((prerequisites (plist-get (cdr (assoc potential-feature org-latex-feature-implementations)) :when)))
      (setf features (if (if (listp prerequisites)
                             (cl-every (lambda (preq) (memq preq features)) prerequisites)
                           (memq prerequisites features))
                         (append (list potential-feature) features)
                       (delq potential-feature features)))))
  (dolist (feature features)
    (when-let ((prevents (plist-get (cdr (assoc feature org-latex-feature-implementations)) :prevents)))
      (setf features (cl-set-difference features (if (listp prevents) prevents (list prevents))))))
  (sort (delete-dups features)
        (lambda (feat1 feat2)
          (if (< (or (plist-get (cdr (assoc feat1 org-latex-feature-implementations)) :order) 1)
                 (or (plist-get (cdr (assoc feat2 org-latex-feature-implementations)) :order) 1))
              t nil))))
#+end_src

ç°åœ¨æˆ‘ä»¬æœ‰ä¸€ä¸ªå¾ˆå¥½çš„æœ€ç»ˆè¦ä½¿ç”¨çš„ç‰¹æ€§åˆ—è¡¨ï¼Œå¯ä»¥æå–å®ƒä»¬çš„ç‰‡æ®µå¹¶å°†ç»“æœè¿æ¥åœ¨ä¸€èµ·ã€‚

#+begin_src emacs-lisp
(defun org-latex-generate-features-preamble (features)
  "Generate the LaTeX preamble content required to provide FEATURES.
This is done according to `org-latex-feature-implementations'"
  (let ((expanded-features (org-latex-expand-features features)))
    (concat
     (format "\n%% features: %s\n" expanded-features)
     (mapconcat (lambda (feature)
                  (when-let ((snippet (plist-get (cdr (assoc feature org-latex-feature-implementations)) :snippet)))
                    (concat
                     (pcase snippet
                       ((pred stringp) snippet)
                       ((pred functionp) (funcall snippet features))
                       ((pred listp) (eval `(let ((features ',features)) (,@snippet))))
                       ((pred symbolp) (symbol-value snippet))
                       (_ (user-error "org-latex-feature-implementations :snippet value %s unable to be used" snippet)))
                     "\n")))
                expanded-features
                "")
     "% end features\n")))
#+end_src

ç„¶åéœ€è¦å»ºè®® Org å®é™…ä½¿ç”¨è¿™ä¸ªç”Ÿæˆçš„å‰å¯¼å†…å®¹ã€‚
#+begin_src emacs-lisp
(defvar info--tmp nil)

(defadvice! org-latex-save-info (info &optional t_ s_)
  :before #'org-latex-make-preamble
  (setq info--tmp info))

(defadvice! org-splice-latex-header-and-generated-preamble-a (orig-fn tpl def-pkg pkg snippets-p &optional extra)
  "Dynamically insert preamble content based on `org-latex-conditional-preambles'."
  :around #'org-splice-latex-header
  (let ((header (funcall orig-fn tpl def-pkg pkg snippets-p extra)))
    (if snippets-p header
      (concat header
              (org-latex-generate-features-preamble (org-latex-detect-features nil info--tmp))
              "\n"))))
#+end_src

æˆ‘å¯¹ ~info--tmp~ çš„ä½¿ç”¨æœ‰ç‚¹è€å¥—ã€‚ å½“æˆ‘å°è¯•å°†å…¶ä¸Šæ¸¸åŒ–æ—¶ï¼Œè¿™åº”è¯¥ä¼šå˜å¾—æ›´åŠ æ¸…æ™°ï¼Œ
å› ä¸ºæˆ‘å¯ä»¥é€šè¿‡ç›´æ¥ä¿®æ”¹ ~org-latex-make-preamble~ æ¥ä¼ é€’ä¿¡æ¯ã€‚
***** å‡å°‘é»˜è®¤åŒ…
ç”±äºä¸Šæ–‡æ·»åŠ ï¼Œæˆ‘ä»¬å¯ä»¥ä» ~org-latex-default-packages-alist~ ä¸­åˆ é™¤ä¸€äº›åŒ…ã€‚

é»˜è®¤å€¼ä¸­ä¹Ÿæœ‰ä¸€äº›è¿‡æ—¶çš„æ¡ç›®ï¼Œå…·ä½“æ¥è¯´
+ =grffile= çš„åŠŸèƒ½å†…ç½®äºå½“å‰ç‰ˆæœ¬çš„ =graphicsx=
+ =textcomp= çš„åŠŸèƒ½å·²ç»åŒ…å«åœ¨ LaTeX çš„æ ¸å¿ƒä¸­ä¸€æ®µæ—¶é—´äº†

#+begin_src emacs-lisp
(setq org-latex-default-packages-alist
      '(("AUTO" "inputenc" t ("pdflatex"))
        ("T1" "fontenc" t ("pdflatex"))
        ("" "xcolor" nil) ; Generally useful
        ("" "hyperref" nil)))
#+end_src

**** å­—ä½“é›†åˆ
ä½¿ç”¨å¥½ç”¨çš„æ¡ä»¶åºè¨€ï¼Œæˆ‘å°†å®šä¹‰ä¸€äº›å¯ç”¨äº LaTeX å¯¼å‡ºçš„å­—ä½“é›†åˆã€‚è°çŸ¥é“å‘¢ï¼Œ
ä¹Ÿè®¸æˆ‘ä¼šåœ¨æŸä¸ªæ—¶å€™å°†å®ƒä¸å…¶ä»–å¯¼å‡ºæ ¼å¼ä¸€èµ·ä½¿ç”¨ã€‚

é¦–å…ˆï¼Œæˆ‘å°†åˆ›å»ºä¸€ä¸ªé»˜è®¤çŠ¶æ€å˜é‡å¹¶å°†å­—ä½“é›†æ³¨å†Œä¸º =#+options= çš„ä¸€éƒ¨åˆ†ã€‚

#+begin_src emacs-lisp
(defvar org-latex-default-fontset 'alegreya
  "Fontset from `org-latex-fontsets' to use by default.
As cm (computer modern) is TeX's default, that causes nothing
to be added to the document.

If \"nil\" no custom fonts will ever be used.")

(eval '(cl-pushnew '(:latex-font-set nil "fontset" org-latex-default-fontset)
                   (org-export-backend-options (org-export-get-backend 'latex))))
#+end_src

ç„¶åéœ€è¦ä¸€ä¸ªå‡½æ•°æ¥ç”Ÿæˆåº”ç”¨å­—ä½“é›†çš„ LaTeX ç‰‡æ®µã€‚å¦‚æœå¯ä»¥ä¸ºå•ä¸ªæ ·å¼å®Œæˆæ­¤æ“ä½œå¹¶ä½¿ç”¨
ä¸åŒæ ·å¼ä½œä¸ºä¸»è¦æ–‡æ¡£å­—ä½“ï¼Œé‚£å°±å¤ªå¥½äº†ã€‚å¦‚æœå­—ä½“é›†çš„å„ä¸ªå­—ä½“åˆ†åˆ«å®šä¹‰ä¸º
src_elisp{:serif}ã€ src_elisp{:sans}ã€ src_elisp{:mono} å’Œ
src_elisp{:maths}ã€‚ æˆ‘å¯ä»¥ä½¿ç”¨å®ƒä»¬ä¸ºå®Œæ•´å­—ä½“é›†çš„å­é›†ç”Ÿæˆ LaTeXã€‚ç„¶åï¼Œå¦‚æœæˆ‘ä¸
è®©ä»»ä½•å­—ä½“é›†åç§°ä¸­åŒ…å« =-= ï¼Œæˆ‘å¯ä»¥ä½¿ç”¨ =-sans= å’Œ =-mono= ä½œä¸ºæŒ‡å®šè¦ä½¿ç”¨çš„æ–‡æ¡£
å­—ä½“çš„åç¼€ã€‚


#+begin_src emacs-lisp
(defun org-latex-fontset-entry ()
  "Get the fontset spec of the current file.
Has format \"name\" or \"name-style\" where 'name' is one of
the cars in `org-latex-fontsets'."
  (let ((fontset-spec
         (symbol-name
          (or (car (delq nil
                         (mapcar
                          (lambda (opt-line)
                            (plist-get (org-export--parse-option-keyword opt-line 'latex)
                                       :latex-font-set))
                          (cdar (org-collect-keywords '("OPTIONS"))))))
              org-latex-default-fontset))))
    (cons (intern (car (split-string fontset-spec "-")))
          (when (cadr (split-string fontset-spec "-"))
            (intern (concat ":" (cadr (split-string fontset-spec "-"))))))))

(defun org-latex-fontset (&rest desired-styles)
  "Generate a LaTeX preamble snippet which applies the current fontset for DESIRED-STYLES."
  (let* ((fontset-spec (org-latex-fontset-entry))
         (fontset (alist-get (car fontset-spec) org-latex-fontsets)))
    (if fontset
        (concat
         (mapconcat
          (lambda (style)
            (when (plist-get fontset style)
              (concat (plist-get fontset style) "\n")))
          desired-styles
          "")
         (when (memq (cdr fontset-spec) desired-styles)
           (pcase (cdr fontset-spec)
             (:serif "\\renewcommand{\\familydefault}{\\rmdefault}\n")
             (:sans "\\renewcommand{\\familydefault}{\\sfdefault}\n")
             (:mono "\\renewcommand{\\familydefault}{\\ttdefault}\n"))))
      (error "Font-set %s is not provided in org-latex-fontsets" (car fontset-spec)))))
#+end_src

ç°åœ¨æ‰€æœ‰çš„åŠŸèƒ½éƒ½å·²ç»å®ç°äº†ï¼Œæˆ‘ä»¬åº”è¯¥æŠŠå®ƒæŒ‚åˆ°æˆ‘ä»¬çš„åºè¨€ç”Ÿæˆä¸­ã€‚

#+begin_src emacs-lisp
(add-to-list 'org-latex-conditional-features '(org-latex-default-fontset . custom-font) t)
(add-to-list 'org-latex-feature-implementations '(custom-font :snippet (org-latex-fontset :serif :sans :mono) :order 0) t)
(add-to-list 'org-latex-feature-implementations '(.custom-maths-font :eager t :when (custom-font maths) :snippet (org-latex-fontset :maths) :order 0.3) t)
#+end_src

æœ€åï¼Œæˆ‘ä»¬åªéœ€è¦æ·»åŠ ä¸€äº›å­—ä½“ã€‚

#+begin_src emacs-lisp
(defvar org-latex-fontsets
  '((cm nil) ; computer modern
    (## nil) ; no font set
    (alegreya
     :serif "\\usepackage[osf]{Alegreya}"
     :sans "\\usepackage{AlegreyaSans}"
     :mono "\\usepackage[scale=0.88]{sourcecodepro}"
     :maths "\\usepackage[varbb]{newpxmath}")
    (biolinum
     :serif "\\usepackage[osf]{libertineRoman}"
     :sans "\\usepackage[sfdefault,osf]{biolinum}"
     :mono "\\usepackage[scale=0.88]{sourcecodepro}"
     :maths "\\usepackage[libertine,varvw]{newtxmath}")
    (fira
     :sans "\\usepackage[sfdefault,scale=0.85]{FiraSans}"
     :mono "\\usepackage[scale=0.80]{FiraMono}"
     :maths "\\usepackage{newtxsf} % change to firamath in future?")
    (kp
     :serif "\\usepackage{kpfonts}")
    (newpx
     :serif "\\usepackage{newpxtext}"
     :sans "\\usepackage{gillius}"
     :mono "\\usepackage[scale=0.9]{sourcecodepro}"
     :maths "\\usepackage[varbb]{newpxmath}")
    (noto
     :serif "\\usepackage[osf]{noto-serif}"
     :sans "\\usepackage[osf]{noto-sans}"
     :mono "\\usepackage[scale=0.96]{noto-mono}"
     :maths "\\usepackage{notomath}")
    (plex
     :serif "\\usepackage{plex-serif}"
     :sans "\\usepackage{plex-sans}"
     :mono "\\usepackage[scale=0.95]{plex-mono}"
     :maths "\\usepackage{newtxmath}") ; may be plex-based in future
    (source
     :serif "\\usepackage[osf,semibold]{sourceserifpro}"
     :sans "\\usepackage[osf,semibold]{sourcesanspro}"
     :mono "\\usepackage[scale=0.92]{sourcecodepro}"
     :maths "\\usepackage{newtxmath}") ; may be sourceserifpro-based in future
    (times
     :serif "\\usepackage{newtxtext}"
     :maths "\\usepackage{newtxmath}"))
  "Alist of fontset specifications.
Each car is the name of the fontset (which cannot include \"-\").

Each cdr is a plist with (optional) keys :serif, :sans, :mono, and :maths.
A key's value is a LaTeX snippet which loads such a font.")
#+end_src

å½“æˆ‘ä»¬ä½¿ç”¨ Alegreya æ—¶ï¼Œæˆ‘ä»¬å¯ä»¥å¯¹ =è¡¨æ ¼= åº”ç”¨ä¸€ä¸ªå¯çˆ±çš„å°è°ƒæ•´ï¼Œå®ƒ (æœ¬åœ°) æ”¹å˜äº†
ç”¨äºè¡¬é‡Œå›ºå®šå®½åº¦çš„å›¾è¡¨ã€‚

#+begin_src emacs-lisp
(add-to-list 'org-latex-conditional-features '((string= (car (org-latex-fontset-entry)) "alegreya") . alegreya-typeface))
(add-to-list 'org-latex-feature-implementations '(alegreya-typeface) t)
(add-to-list 'org-latex-feature-implementations'(.alegreya-tabular-figures :eager t :when (alegreya-typeface table) :order 0.5 :snippet "
\\makeatletter
% tabular lining figures in tables
\\renewcommand{\\tabular}{\\AlegreyaTLF\\let\\@halignto\\@empty\\@tabular}
\\makeatother\n") t)
#+end_src

ç”±äº Alegreya çš„æŒ‡æ ‡ï¼Œ\LaTeX ç¬¦å·çœ‹èµ·æ¥ä¸å¤ªæ­£ç¡®ã€‚æˆ‘ä»¬å¯ä»¥é€šè¿‡ä½¿ç”¨å¾®å¦™çš„å­—è·è°ƒæ•´
æ¥é‡æ–°å®šä¹‰å®ƒæ¥çº æ­£è¿™ä¸ªé—®é¢˜ã€‚

#+begin_src emacs-lisp
(add-to-list 'org-latex-conditional-features '("LaTeX" . latex-symbol))
(add-to-list 'org-latex-feature-implementations '(latex-symbol :when alegreya-typeface :order 0.5 :snippet "
\\makeatletter
% Kerning around the A needs adjusting
\\DeclareRobustCommand{\\LaTeX}{L\\kern-.24em%
        {\\sbox\\z@ T%
         \\vbox to\\ht\\z@{\\hbox{\\check@mathfonts
                              \\fontsize\\sf@size\\z@
                              \\math@fontsfalse\\selectfont
                              A}%
                        \\vss}%
        }%
        \\kern-.10em%
        \\TeX}
\\makeatother\n") t)
#+end_src

**** å°é¢
è¦åˆ¶ä½œæ¼‚äº®çš„å°é¢ï¼Œæƒ³åˆ°çš„ä¸€ä¸ªç®€å•æ–¹æ³•å°±æ˜¯é‡æ–°å®šä¹‰ =\maketitle= ã€‚ä¸ºäº†ç²¾ç¡®æ§åˆ¶å®šä½
ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ =tikz= åŒ…ï¼Œç„¶åæ·»åŠ  Tikz åº“ =calc= å’Œ =shape.geometric= æ¥ä¸ºèƒŒ
æ™¯åšä¸€äº›æ¼‚äº®çš„è£…é¥°ã€‚

æˆ‘å°†é¦–å…ˆä¸ºåºè¨€è®¾ç½®å¿…è¦çš„è¡¥å……ã€‚ è¿™å°†å®Œæˆä»¥ä¸‹ä»»åŠ¡ï¼š
+ åŠ è½½æ‰€éœ€çš„åŒ…
+ é‡æ–°å®šä¹‰ =\maketitle=
+ ç”¨ Tikz ç”»ä¸€ä¸ª =Org= å›¾æ ‡ï¼Œç”¨åœ¨å°é¢ä¸Š (è¿™æ˜¯ä¸€ä¸ªå°å½©è›‹)
+ é€šè¿‡é‡æ–°å®šä¹‰ =\tableofcontents= åœ¨ç›®å½•ä¹‹åå¼€å§‹ä¸€ä¸ªæ–°é¡µé¢

#+name: latex-cover-page
#+begin_src LaTeX
\\usepackage{tikz}
\\usetikzlibrary{shapes.geometric}
\\usetikzlibrary{calc}

\\newsavebox\\orgicon
\\begin{lrbox}{\\orgicon}
  \\begin{tikzpicture}[y=0.80pt, x=0.80pt, inner sep=0pt, outer sep=0pt]
    \\path[fill=black!6] (16.15,24.00) .. controls (15.58,24.00) and (13.99,20.69) .. (12.77,18.06)arc(215.55:180.20:2.19) .. controls (12.33,19.91) and (11.27,19.09) .. (11.43,18.05) .. controls (11.36,18.09) and (10.17,17.83) .. (10.17,17.82) .. controls (9.94,18.75) and (9.37,19.44) .. (9.02,18.39) .. controls (8.32,16.72) and (8.14,15.40) .. (9.13,13.80) .. controls (8.22,9.74) and (2.18,7.75) .. (2.81,4.47) .. controls (2.99,4.47) and (4.45,0.99) .. (9.15,2.41) .. controls (14.71,3.99) and (17.77,0.30) .. (18.13,0.04) .. controls (18.65,-0.49) and (16.78,4.61) .. (12.83,6.90) .. controls (10.49,8.18) and (11.96,10.38) .. (12.12,11.15) .. controls (12.12,11.15) and (14.00,9.84) .. (15.36,11.85) .. controls (16.58,11.53) and (17.40,12.07) .. (18.46,11.69) .. controls (19.10,11.41) and (21.79,11.58) .. (20.79,13.08) .. controls (20.79,13.08) and (21.71,13.90) .. (21.80,13.99) .. controls (21.97,14.75) and (21.59,14.91) .. (21.47,15.12) .. controls (21.44,15.60) and (21.04,15.79) .. (20.55,15.44) .. controls (19.45,15.64) and (18.36,15.55) .. (17.83,15.59) .. controls (16.65,15.76) and (15.67,16.38) .. (15.67,16.38) .. controls (15.40,17.19) and (14.82,17.01) .. (14.09,17.32) .. controls (14.70,18.69) and (14.76,19.32) .. (15.50,21.32) .. controls (15.76,22.37) and (16.54,24.00) .. (16.15,24.00) -- cycle(7.83,16.74) .. controls (6.83,15.71) and (5.72,15.70) .. (4.05,15.42) .. controls (2.75,15.19) and (0.39,12.97) .. (0.02,10.68) .. controls (-0.02,10.07) and (-0.06,8.50) .. (0.45,7.18) .. controls (0.94,6.05) and (1.27,5.45) .. (2.29,4.85) .. controls (1.41,8.02) and (7.59,10.18) .. (8.55,13.80) -- (8.55,13.80) .. controls (7.73,15.00) and (7.80,15.64) .. (7.83,16.74) -- cycle;
  \\end{tikzpicture}
\\end{lrbox}

\\makeatletter
\\g@addto@macro\\tableofcontents{\\clearpage}
\\renewcommand\\maketitle{
  \\thispagestyle{empty}
  \\hyphenpenalty=10000 % hyphens look bad in titles
  \\renewcommand{\\baselinestretch}{1.1}
  \\let\\oldtoday\\today
  \\renewcommand{\\today}{\\LARGE\\number\\year\\\\\\large%
    \\ifcase \\month \\or Jan\\or Feb\\or Mar\\or Apr\\or May \\or Jun\\or Jul\\or Aug\\or Sep\\or Oct\\or Nov\\or Dec\\fi
    ~\\number\\day}
  \\begin{tikzpicture}[remember picture,overlay]
    %% Background Polygons %%
    \\foreach \\i in {2.5,...,22} % bottom left
    {\\node[rounded corners,black!3.5,draw,regular polygon,regular polygon sides=6, minimum size=\\i cm,ultra thick] at ($(current page.west)+(2.5,-4.2)$) {} ;}
    \\foreach \\i in {0.5,...,22} % top left
    {\\node[rounded corners,black!5,draw,regular polygon,regular polygon sides=6, minimum size=\\i cm,ultra thick] at ($(current page.north west)+(2.5,2)$) {} ;}
    \\node[rounded corners,fill=black!4,regular polygon,regular polygon sides=6, minimum size=5.5 cm,ultra thick] at ($(current page.north west)+(2.5,2)$) {};
    \\foreach \\i in {0.5,...,24} % top right
    {\\node[rounded corners,black!2,draw,regular polygon,regular polygon sides=6, minimum size=\\i cm,ultra thick] at ($(current page.north east)+(0,-8.5)$) {} ;}
    \\node[fill=black!3,rounded corners,regular polygon,regular polygon sides=6, minimum size=2.5 cm,ultra thick] at ($(current page.north east)+(0,-8.5)$) {};
    \\foreach \\i in {21,...,3} % bottom right
    {\\node[black!3,rounded corners,draw,regular polygon,regular polygon sides=6, minimum size=\\i cm,ultra thick] at ($(current page.south east)+(-1.5,0.75)$) {} ;}
    \\node[fill=black!3,rounded corners,regular polygon,regular polygon sides=6, minimum size=2 cm,ultra thick] at ($(current page.south east)+(-1.5,0.75)$) {};
    \\node[align=center, scale=1.4] at ($(current page.south east)+(-1.5,0.75)$) {\\usebox\\orgicon};
    %% Text %%
    \\node[left, align=right, black, text width=0.8\\paperwidth, minimum height=3cm, rounded corners,font=\\Huge\\bfseries] at ($(current page.north east)+(-2,-8.5)$)
    {\\@title};
    \\node[left, align=right, black, text width=0.8\\paperwidth, minimum height=2cm, rounded corners, font=\\Large] at ($(current page.north east)+(-2,-11.8)$)
    {\\scshape \\@author};
    \\renewcommand{\\baselinestretch}{0.75}
    \\node[align=center,rounded corners,fill=black!3,text=black,regular polygon,regular polygon sides=6, minimum size=2.5 cm,inner sep=0, font=\\Large\\bfseries ] at ($(current page.west)+(2.5,-4.2)$)
    {\\@date};
  \\end{tikzpicture}
  \\let\\today\\oldtoday
  \\clearpage}
\\makeatother
#+end_src

ç°åœ¨æˆ‘ä»¬æœ‰äº†ä¸€ä¸ªä¸é”™çš„å°é¢é¡µï¼Œæˆ‘ä»¬åªéœ€è¦ä¸æ—¶ä½¿ç”¨å®ƒã€‚å°†æ­¤æ·»åŠ åˆ° =#+options= æ„Ÿè§‰æœ€
åˆé€‚ã€‚è®©å°é¢é€‰é¡¹æ¥å— auto ä½œä¸ºå€¼ï¼Œç„¶åæ ¹æ®å­—æ•°å†³å®šæ˜¯å¦åº”ä½¿ç”¨å°é¢ --- æˆ‘å°†æŠŠå®ƒä½œä¸º
å…¨å±€é»˜è®¤å€¼ã€‚ç„¶åæˆ‘ä»¬åªæƒ³æ’å…¥ä¸€ä¸ª LaTeX ç‰‡æ®µåœ¨å°é¢ä¸­æ¥è°ƒæ•´æ ‡é¢˜æ ¼å¼ã€‚
#+begin_src emacs-lisp :noweb no-export
(defvar org-latex-cover-page 'auto
  "When t, use a cover page by default.
When auto, use a cover page when the document's wordcount exceeds
`org-latex-cover-page-wordcount-threshold'.

Set with #+option: coverpage:{yes,auto,no} in org buffers.")
(defvar org-latex-cover-page-wordcount-threshold 5000
  "Document word count at which a cover page will be used automatically.
This condition is applied when cover page option is set to auto.")
(defvar org-latex-subtitle-coverpage-format "\\\\\\bigskip\n\\LARGE\\mdseries\\itshape\\color{black!80} %s\\par"
  "Variant of `org-latex-subtitle-format' to use with the cover page.")
(defvar org-latex-cover-page-maketitle "
<<latex-cover-page>>
"
  "LaTeX snippet for the preamble that sets \\maketitle to produce a cover page.")

(eval '(cl-pushnew '(:latex-cover-page nil "coverpage" org-latex-cover-page)
                   (org-export-backend-options (org-export-get-backend 'latex))))

(defun org-latex-cover-page-p ()
  "Whether a cover page should be used when exporting this Org file."
  (pcase (or (car
              (delq nil
                    (mapcar
                     (lambda (opt-line)
                       (plist-get (org-export--parse-option-keyword opt-line 'latex) :latex-cover-page))
                     (cdar (org-collect-keywords '("OPTIONS"))))))
             org-latex-cover-page)
    ((or 't 'yes) t)
    ('auto (when (> (count-words (point-min) (point-max)) org-latex-cover-page-wordcount-threshold) t))
    (_ nil)))

(defadvice! org-latex-set-coverpage-subtitle-format-a (contents info)
  "Set the subtitle format when a cover page is being used."
  :before #'org-latex-template
  (when (org-latex-cover-page-p)
    (setf info (plist-put info :latex-subtitle-format org-latex-subtitle-coverpage-format))))

(add-to-list 'org-latex-feature-implementations '(cover-page :snippet org-latex-cover-page-maketitle :order 9) t)
(add-to-list 'org-latex-conditional-features '((org-latex-cover-page-p) . cover-page) t)
#+end_src
**** ç²¾ç®€åˆ—è¡¨
é»˜è®¤æƒ…å†µä¸‹ LaTeX éå¸¸å¥½ï¼Œä½†æ˜¯é»˜è®¤æƒ…å†µä¸‹å®ƒåœ¨åˆ—è¡¨é¡¹ä¹‹é—´æ”¾ç½®ç©ºé—´æ—¶çœŸçš„å¾ˆæ…·æ…¨ã€‚æˆ‘ä¸èƒ½
æ¥å—ã€‚

å€¼å¾—åº†å¹¸çš„æ˜¯ï¼Œè¿™å¾ˆå®¹æ˜“é€šè¿‡ä¸€ä¸ªå°ç‰‡æ®µæ¥çº æ­£ï¼š
#+name: latex-condense-lists
#+begin_src LaTeX
\\newcommand{\\setuplistspacing}{\\setlength{\\itemsep}{-0.5ex}\\setlength{\\parskip}{1.5ex}\\setlength{\\parsep}{0pt}}
\\let\\olditem\\itemize\\renewcommand{\\itemize}{\\olditem\\setuplistspacing}
\\let\\oldenum\\enumerate\\renewcommand{\\enumerate}{\\oldenum\\setuplistspacing}
\\let\\olddesc\\description\\renewcommand{\\description}{\\olddesc\\setuplistspacing}
#+end_src

ç„¶åæˆ‘ä»¬å¯ä»¥ç”¨æˆ‘ä»¬çš„åºè¨€æŠŠå®ƒ hook è¿›æ¥ã€‚

#+begin_src emacs-lisp :noweb no-export
(defvar org-latex-condense-lists t
  "Reduce the space between list items.")
(defvar org-latex-condensed-lists "
<<latex-condense-lists>>
")

(add-to-list 'org-latex-conditional-features '((and org-latex-condense-lists "^[ \t]*[-+]\\|^[ \t]*[1Aa][.)] ") . condensed-lists) t)
(add-to-list 'org-latex-feature-implementations '(condensed-lists :snippet org-latex-condensed-lists :order 0.7) t)
#+end_src

**** æ¼‚äº®çš„ä»£ç å—
æˆ‘ä»¬å¯ä»¥åªä½¿ç”¨ =minted= è¿›è¡Œè¯­æ³•é«˜äº® --- ä½†æ˜¯ï¼Œæˆ‘ä»¬å¯ä»¥åšå¾—æ›´å¥½ï¼
=engrave-faces= åŒ…è®©æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ Emacs çš„å­—ä½“é”å®šæ¥çªå‡ºæ˜¾ç¤ºè¯­æ³•ï¼Œå¹¶å°†å…¶å¯¼å‡ºä¸º
LaTeX å‘½ä»¤ã€‚

#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! engrave-faces :recipe (:local-repo "lisp/engrave-faces"))
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! engrave-faces-latex
  :after ox-latex)
#+end_src

æˆ‘ä»¬å°†ä¿®æ”¹åˆ—è¡¨çš„ç”Ÿæˆæ–¹å¼ï¼Œä»¥ä½¿ä½¿ç”¨å®ƒå˜å¾—å¦‚æ­¤ç®€å•ï¼š
#+begin_src emacs-lisp
(setq org-latex-listings 'engraved) ; NOTE non-standard value
#+end_src

ç”±äº ~org-latex-conditional-features~ å’Œä¸€äº›å¤åˆ¶ç²˜è´´ä»¥åŠ
~org-latex-scr-block~ ä¸­çš„ =minted= æ¡ç›®ï¼Œæˆ‘ä»¬å¯ä»¥è½»æ¾åœ°å°†å…¶æ·»åŠ ä¸ºå¯è¯†åˆ«çš„
~org-latex-listings~ å€¼ã€‚

#+begin_src emacs-lisp :noweb no-export
(defadvice! org-latex-src-block-engraved (orig-fn src-block contents info)
  "Like `org-latex-src-block', but supporting an engraved backend"
  :around #'org-latex-src-block
  (if (eq 'engraved (plist-get info :latex-listings))
      (org-latex-scr-block--engraved src-block contents info)
    (funcall orig-fn src-block contents info)))

(defadvice! org-latex-inline-src-block-engraved (orig-fn inline-src-block contents info)
  "Like `org-latex-inline-src-block', but supporting an engraved backend"
  :around #'org-latex-inline-src-block
  (if (eq 'engraved (plist-get info :latex-listings))
      (org-latex-inline-scr-block--engraved inline-src-block contents info)
    (funcall orig-fn src-block contents info)))

(defvar-local org-export-has-code-p nil)

(defadvice! org-export-expect-no-code (&rest _)
  :before #'org-export-as
  (setq org-export-has-code-p nil))

(defadvice! org-export-register-code (&rest _)
  :after #'org-latex-src-block-engraved
  :after #'org-latex-inline-src-block-engraved
  (setq org-export-has-code-p t))

(setq org-latex-engraved-code-preamble "
<<org-latex-engraved-code-preamble>>
")

(add-to-list 'org-latex-conditional-features '((and org-export-has-code-p "^[ \t]*#\\+begin_src\\|^[ \t]*#\\+BEGIN_SRC\\|src_[A-Za-z]") . engraved-code) t)
(add-to-list 'org-latex-conditional-features '("^[ \t]*#\\+begin_example\\|^[ \t]*#\\+BEGIN_EXAMPLE" . engraved-code-setup) t)
(add-to-list 'org-latex-feature-implementations '(engraved-code :requires engraved-code-setup :snippet (engrave-faces-latex-gen-preamble) :order 99) t)
(add-to-list 'org-latex-feature-implementations '(engraved-code-setup :snippet org-latex-engraved-code-preamble :order 98) t)

(defun org-latex-scr-block--engraved (src-block contents info)
  (let* ((lang (org-element-property :language src-block))
         (attributes (org-export-read-attribute :attr_latex src-block))
         (float (plist-get attributes :float))
         (num-start (org-export-get-loc src-block info))
         (retain-labels (org-element-property :retain-labels src-block))
         (caption (org-element-property :caption src-block))
         (caption-above-p (org-latex--caption-above-p src-block info))
         (caption-str (org-latex--caption/label-string src-block info))
         (placement (or (org-unbracket-string "[" "]" (plist-get attributes :placement))
                        (plist-get info :latex-default-figure-position)))
         (float-env
          (cond
           ((string= "multicolumn" float)
            (format "\\begin{listing*}[%s]\n%s%%s\n%s\\end{listing*}"
                    placement
                    (if caption-above-p caption-str "")
                    (if caption-above-p "" caption-str)))
           (caption
            (format "\\begin{listing}[%s]\n%s%%s\n%s\\end{listing}"
                    placement
                    (if caption-above-p caption-str "")
                    (if caption-above-p "" caption-str)))
           ((string= "t" float)
            (concat (format "\\begin{listing}[%s]\n"
                            placement)
                    "%s\n\\end{listing}"))
           (t "%s")))
         (options (plist-get info :latex-minted-options))
         (content-buffer
          (with-temp-buffer
            (insert
             (let* ((code-info (org-export-unravel-code src-block))
                    (max-width
                     (apply 'max
                            (mapcar 'length
                                    (org-split-string (car code-info)
                                                      "\n")))))
               (org-export-format-code
                (car code-info)
                (lambda (loc _num ref)
                  (concat
                   loc
                   (when ref
                     ;; Ensure references are flushed to the right,
                     ;; separated with 6 spaces from the widest line
                     ;; of code.
                     (concat (make-string (+ (- max-width (length loc)) 6)
                                          ?\s)
                             (format "(%s)" ref)))))
                nil (and retain-labels (cdr code-info)))))
            (funcall (org-src-get-lang-mode lang))
            (engrave-faces-latex-buffer)))
         (content
          (with-current-buffer content-buffer
            (buffer-string)))
         (body
          (format
           "\\begin{Code}\n\\begin{Verbatim}[%s]\n%s\\end{Verbatim}\n\\end{Code}"
           ;; Options.
           (concat
            (org-latex--make-option-string
             (if (or (not num-start) (assoc "linenos" options))
                 options
               (append
                `(("linenos")
                  ("firstnumber" ,(number-to-string (1+ num-start))))
                options)))
            (let ((local-options (plist-get attributes :options)))
              (and local-options (concat "," local-options))))
           content)))
    (kill-buffer content-buffer)
    ;; Return value.
    (format float-env body)))

(defun org-latex-inline-scr-block--engraved (inline-src-block _contents info)
  (let ((options (org-latex--make-option-string
                  (plist-get info :latex-minted-options)))
        code-buffer code)
    (setq code-buffer
          (with-temp-buffer
            (insert (org-element-property :value inline-src-block))
            (funcall (org-src-get-lang-mode
                      (org-element-property :language inline-src-block)))
            (engrave-faces-latex-buffer)))
    (setq code (with-current-buffer code-buffer
                 (buffer-string)))
    (kill-buffer code-buffer)
    (format "\\Verb%s{%s}"
            (if (string= options "") ""
              (format "[%s]" options))
            code)))
#+end_src

æ¯å½“ä½¿ç”¨å®ƒæ—¶ï¼Œä¸ºäº†è®©å®ƒå®é™…å·¥ä½œ (å¹¶ä¸”çœ‹èµ·æ¥æ›´å¥½ä¸€ç‚¹) ï¼Œæˆ‘ä»¬åœ¨åºè¨€ä¸­æ·»åŠ ä¸€ç‚¹ï¼š

#+name: org-latex-engraved-code-preamble
#+begin_src LaTeX
\\usepackage{fvextra}
\\fvset{
  commandchars=\\\\\\{\\},
  highlightcolor=white!95!black!80!blue,
  breaklines=true,
  breaksymbol=\\color{white!60!black}\\tiny\\ensuremath{\\hookrightarrow}}
\\renewcommand\\theFancyVerbLine{\\footnotesize\\color{black!40!white}\\arabic{FancyVerbLine}}

\\definecolor{codebackground}{HTML}{f7f7f7}
\\definecolor{codeborder}{HTML}{f0f0f0}
\\providecolor{EFD}{HTML}{28292e}

% TODO have code boxes keep line vertical alignment
\\usepackage[breakable,xparse]{tcolorbox}
\\DeclareTColorBox[]{Code}{o}%
{colback=codebackground, colframe=codeborder,
  fontupper=\\footnotesize,
  colupper=EFD,
  IfNoValueTF={#1}%
  {boxsep=2pt, arc=2.5pt, outer arc=2.5pt,
    boxrule=0.5pt, left=2pt}%
  {boxsep=2.5pt, arc=0pt, outer arc=0pt,
    boxrule=0pt, leftrule=1.5pt, left=0.5pt},
  right=2pt, top=1pt, bottom=0.5pt,
  breakable}
#+end_src

å¯¹æ­¤çš„ä¸€ä¸ªå°çƒ¦æ¼æ˜¯å¾®å‹å­—ä½“å’Œ =verbatim= ç¯å¢ƒä¹‹é—´çš„äº¤äº’ã€‚çªå‡ºåœ¨è¿™é‡Œæ˜¯ä¸å¯å–çš„ã€‚å€¼
å¾—åº†å¹¸çš„æ˜¯ï¼Œæˆ‘ä»¬å¯ä»¥ä¿®è¡¥ =verbatim= ç¯å¢ƒä»¥åœ¨æœ¬åœ°å…³é—­çªå‡ºæ˜¾ç¤ºã€‚
#+begin_src emacs-lisp
(add-to-list 'org-latex-feature-implementations
             '(.no-protrusion-in-code :snippet "\\let\\oldcode\\Code\\renewcommand{\\Code}{\\microtypesetup{protrusion=false}\\oldcode}"
                                      :when (microtype engraved-code-setup)
                                      :eager t
                                      :order 98.5) t)
#+end_src

åœ¨æŸäº›æ—¶å€™ï¼Œè®©ç›’å­é¢œè‰²æ˜“äºå®šåˆ¶ä¼šå¾ˆå¥½ã€‚ç›®å‰ï¼Œä½¿ç”¨
src_elisp{(setq engrave-faces-preset-styles (engrave-faces-generate-preset))}
æ›´æ”¹è¯­æ³•çªå‡ºæ˜¾ç¤ºé¢œè‰²ç›¸å½“å®¹æ˜“ï¼Œä½†ä¹Ÿè®¸éœ€è¦åˆ‡æ¢ï¼Œå®ƒæŒ‡å®šæ˜¯å¦ä½¿ç”¨é»˜è®¤å€¼ã€å½“å‰ä¸»é¢˜ã€ æˆ–ä»»
ä½•å‘½åçš„ä¸»é¢˜å¯èƒ½æ˜¯ä¸€ä¸ªå¥½ä¸»æ„ã€‚è¿˜åº”è¯¥å¯ä»¥åŠ¨æ€è®¾ç½®åŒ¹é…çš„æ¡†èƒŒæ™¯ã€‚å‘½åä¸»é¢˜å¯ä»¥é€šè¿‡åœ¨ç¼“å­˜
ç›®å½•ä¸­æŸ¥æ‰¾å…·æœ‰ç‰¹å®šåç§°çš„æ ·å¼å®šä¹‰æ¥å·¥ä½œï¼Œç„¶ååˆ‡æ¢åˆ°è¯¥ä¸»é¢˜å¹¶ç”Ÿæˆ (å¹¶ä¿å­˜) æ ·å¼å®šä¹‰
(å¦‚æœå®ƒä¸å­˜åœ¨)ã€‚

ç°åœ¨è®©æˆ‘ä»¬ä¸ºç¤ºä¾‹å—è®¾ç½®ç±»ä¼¼çš„æ ·å¼ã€‚
#+begin_src emacs-lisp
(defadvice! org-latex-example-block-engraved (orig-fn example-block contents info)
  "Like `org-latex-example-block', but supporting an engraved backend"
  :around #'org-latex-example-block
  (let ((output-block (funcall orig-fn example-block contents info)))
    (if (eq 'engraved (plist-get info :latex-listings))
        (format "\\begin{Code}[alt]\n%s\n\\end{Code}" output-block)
      output-block)))
#+end_src

é™¤äº†éå¸¸å‡ºè‰²çš„è§†è§‰è¾“å‡ºä¹‹å¤–ï¼Œè¿™å¯¹äºä»£ç ç¹é‡çš„æ–‡æ¡£ (å¦‚æ­¤é…ç½®) çš„ç¼–è¯‘é€Ÿåº¦ä¹Ÿåº”è¯¥æ›´å¿«ã€‚

ç”¨è¿™ä¸ªæ–‡æ¡£åšä¸€ä¸ªå°åŸºå‡†æµ‹è¯•ï¼Œæˆ‘å‘ç°ç¡®å®æ˜¯è¿™æ ·ã€‚
| LaTeX è¯­æ³•é«˜äº®åç«¯ | ç¼–è¯‘æ—¶é—´ | è¶…å‡ºåŸºå‡†æ—¶é—´ | è¶…å‡ºç‡ |
|--------------------+----------+--------------+--------|
| verbatim           | 12 s     | 0            |    0.0 |
| lstlistings        | 15 s     | 3 s          |    0.2 |
| Engrave            | 34 s     | 22 s         |    1.8 |
| Pygments (Minted)  | 184 s    | 172 s        |   14.3 |
#+TBLFM: $3=$2-@2$2::$4=$3 / @2$2;%.1f

=verbatim= (æ— è¯­æ³•é«˜äº®) ç»“æœè§†ä¸ºåŸºçº¿ï¼›è¿™ä¸ªåˆæ­¥æµ‹è¯•è¡¨æ˜ï¼Œ =engrave-faces= æ¯”
pygments å¿«å…«å€å·¦å³ï¼Œåªéœ€è¦ =verbatim= (æ— è¯­æ³•é«˜äº®) çš„ 3 å€æ—¶é—´ã€‚
**** Julia ä»£ç å—
Julia ä»£ç å¯¹ unicode æœ‰æå¥½çš„æ”¯æŒï¼ç¼ºç‚¹æ˜¯ =pdflatex= ä¸ unicode ç¬¦å·ä¸€èµ·ä½¿ç”¨
ä»ç„¶å¾ˆç—›è‹¦ã€‚è§£å†³æ–¹æ¡ˆ --- =lualatex= ã€‚ç°åœ¨æˆ‘ä»¬åªéœ€è¦è®©å®ƒè‡ªåŠ¨åŒ–

#+begin_src emacs-lisp
(defadvice! org-latex-pick-compiler (_contents info)
  :before #'org-latex-template
  (when (and org-export-has-code-p (memq 'julia-code (org-latex-detect-features)))
    (setf info (plist-put
                (if (member #'+org-latex-replace-non-ascii-chars (plist-get info :filter-final-output))
                    (plist-put info :filter-final-output
                               (delq #'+org-latex-replace-non-ascii-chars (plist-get info :filter-final-output)))
                  info)
                :latex-compiler "lualatex"))))
#+end_src

ç„¶åå¿…é¡»ä½¿ç”¨æ”¯æŒ unicode çš„å­—ä½“ã€‚JuliaMono æ˜¯æ˜¾è€Œæ˜“è§çš„é€‰æ‹©ï¼Œæˆ‘ä»¬å¯ä»¥å°†å®ƒä¸
=fontspec= åŒ…ä¸€èµ·ä½¿ç”¨ã€‚å°†å…¶è®¾ç½®ä¸ºå¤‡ç”¨å­—ä½“å¯èƒ½ä¼šå¾ˆå¥½ (å¦‚æœè¿™æ ·åšå¹¶ä¸ç—›è‹¦)ã€‚

#+name: julia-mono-fontspec
#+begin_src LaTeX
\\usepackage{fontspec}
\\newfontfamily\\JuliaMono{JuliaMono-Regular.ttf}[Path=/usr/share/fonts/truetype/, Extension=.ttf]
\\newfontface\\JuliaMonoRegular{JuliaMono-Regular}
\\setmonofont{JuliaMonoRegular}[Contextuals=Alternate, Scale=MatchLowercase]
#+end_src

ç°åœ¨å‰©ä¸‹çš„å°±æ˜¯å°†å…¶ hook è¿›åºè¨€ã€‚

#+begin_src emacs-lisp :noweb no-export
(setq org-latex-julia-mono-fontspec "
<<julia-mono-fontspec>>
")

(add-to-list 'org-latex-feature-implementations '(julia-code :snippet org-latex-julia-mono-fontspec :order 0) t)
(add-to-list 'org-latex-conditional-features '((and org-export-has-code-p "^[ \t]*#\\+begin_src julia\\|^[ \t]*#\\+BEGIN_SRC julia\\|src_julia") . julia-code) t)

(add-to-list 'org-latex-feature-implementations '(.microtype-lualatex :eager t :when (microtype julia-code) :prevents microtype :order 0.1 :snippet "\\usepackage[activate={true,nocompatibility},final,tracking=true,factor=2000]{microtype}\n"))
(add-to-list 'org-latex-feature-implementations '(.custom-font-no-mono :eager t :prevents custom-font :order 0 :snippet (org-latex-fontset :serif :sans)) t)
#+end_src

**** Emojis
åœ¨ä½¿ç”¨çš„åœ°æ–¹å®é™…åŒ…å«è¡¨æƒ…ç¬¦å·ä¼šå¾ˆå¥½ã€‚å¤šäºäº† =emojify= ï¼Œæˆ‘ä»¬æœ‰äº†ä¸€ä¸ªè¡¨æƒ…ç¬¦å·å›¾åƒæ–‡
ä»¶å¤¹ç”¨äºä½¿ç”¨ ğŸ™‚ã€‚

é¦–å…ˆï¼Œæˆ‘ä»¬æƒ³è¦æ£€æµ‹è¡¨æƒ…ç¬¦å·ä½•æ—¶å®é™…å­˜åœ¨ã€‚æˆ‘ä»¬å¯ä»¥å°è¯•ä½¿ç”¨ =[?-?]= æ­£åˆ™è¡¨è¾¾å¼ç»„çš„é›†
åˆæ£€æŸ¥ unicode èŒƒå›´ï¼Œä½† Emojis å®é™…ä¸Šåˆ†å¸ƒåœ¨ç›¸å½“å¤šçš„åœ°æ–¹ï¼Œå› æ­¤è¿™ä¸æ˜¯å¾ˆç®€å•ã€‚ç›¸å
ï¼Œæˆ‘å¯ä»¥è¿­ä»£å½»åº•çš„é ASCII å­—ç¬¦å¹¶æ£€æŸ¥æ˜¯å¦æœ‰ä»»ä½•æ–‡æœ¬å±æ€§ =emojified= ã€‚

#+begin_src emacs-lisp
(defun emojify-emoji-in-buffer-p ()
  "Determine if any emojis are present in the current buffer, using `emojify-mode'."
  (unless emojify-mode
    (emojify-mode 1)
    (emojify-display-emojis-in-region (point-min) (point-max)))
  (let (emoji-found end)
    (save-excursion
      (goto-char (point-min))
      (while (not (or emoji-found end))
        (if-let ((pos (re-search-forward "[^[:ascii:]]" nil t)))
            (when (get-text-property (1- pos) 'emojified)
              (setq emoji-found t))
          (setq end t))))
    emoji-found))
#+end_src

ä¸€æ—¦æˆ‘ä»¬çŸ¥é“å­˜åœ¨è¡¨æƒ…ç¬¦å·ï¼Œæˆ‘ä»¬å°±å¯ä»¥åœ¨ç¼“å†²åŒºä¸­æ·»åŠ ä¸€äº›å‰å¯¼ç ä»¥ä½¿æ’å…¥æ›´å®¹æ˜“ã€‚

#+begin_src emacs-lisp
(defun org-latex-emoji-setup ()
  (format "\\newcommand\\emoji[1]{\\raisebox{-0.3ex}{\\includegraphics[height=1.8ex]{%s/#1}}}" (emojify-image-dir)))

(add-to-list 'org-latex-conditional-features '((emojify-emoji-in-buffer-p) . emoji) t)
(add-to-list 'org-latex-feature-implementations '(emoji :requires image :snippet (org-latex-emoji-setup) :order 3 ))
#+end_src

å†æ¬¡ä½¿ç”¨ =emojify= ï¼Œæˆ‘ä»¬å¯ä»¥ç›¸å½“è½»æ¾åœ°ä¸ºæˆ‘ä»¬çš„è¡¨æƒ…ç¬¦å·ç”Ÿæˆ LaTeX å‘½ä»¤ã€‚
#+begin_src emacs-lisp
(defun emojify-latexify-emoji-in-buffer ()
  (unless emojify-mode
    (emojify-mode 1)
    (emojify-display-emojis-in-region (point-min) (point-max)))
  (let (end)
    (save-excursion
      (goto-char (point-min))
      (while (not end)
        (if-let ((pos (re-search-forward "[^[:ascii:]]\\{1,2\\}" nil t)))
            (when-let ((char (get-text-property (1- pos) 'emojify-text))
                       (emoji (emojify-get-emoji char)))
              (replace-match (format "\\\\emoji{%s}" (file-name-sans-extension (ht-get emoji "image")))))
          (setq end t))))))
#+end_src

ç°åœ¨æˆ‘ä»¬åªéœ€è¦å°†è¿™ä¸ªæ–¹ä¾¿çš„å‡½æ•° hook åˆ° Org çš„å¯¼å‡ºä¸­ã€‚æˆ‘ä»¬ä¸èƒ½ä½¿ç”¨æ ‡å‡†çš„å­—ç¬¦ä¸²æ›¿æ¢
ï¼Œå› ä¸ºæˆ‘ä»¬ä¾èµ–äº src_elisp{(emojify-mode)} æŒ‡å®šçš„ç¼“å†²åŒºä¿®æ”¹ã€‚

ç”±äºæˆ‘è¿˜æ²¡æœ‰å®ç°åœ¨ src_elisp{(org-latex-generate-features-preamble)} ä¹‹å¤–
å…±äº«ç‰¹å¾æ£€æµ‹ä¿¡æ¯çš„å¥½æ–¹æ³•ï¼Œæˆ‘ä»¬å°†åœ¨å°è¯• LaTeXify è¡¨æƒ…ç¬¦å·ä¹‹å‰ä½¿ç”¨ç›¸åŒçš„æ£€æŸ¥ï¼Œå¹¶å¸Œæœ›
ä¸ä¼šå‘ç”Ÿä»»ä½•å¥‡æ€ªçš„äº‹æƒ…ã€‚

#+begin_src emacs-lisp
(defun +org-latex-convert-emojis (text backend _info)
  (when (org-export-derived-backend-p backend 'latex)
    (with-temp-buffer
      (insert text)
      (when (emojify-emoji-in-buffer-p)
        (emojify-latexify-emoji-in-buffer)
        (buffer-string)))))

(add-to-list 'org-export-filter-final-output-functions #'+org-latex-convert-emojis)
#+end_src

è¿™å·¥ä½œå¾—ç›¸å½“å¥½ï¼Œæˆ‘ä»¬å¯ä»¥æ‰§è¡Œä¸€ç‚¹ç‚¹ QOL å‡çº§ã€‚ =emojify= ä¸‹è½½ ~72x72~ ç‰ˆæœ¬çš„
Twemojiï¼Œä½†ä¹Ÿä¼šç”Ÿæˆ SVG ç‰ˆæœ¬ã€‚æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ inkscape å°†å®ƒä»¬è½¬æ¢ä¸º PDFï¼Œè¿™å¯èƒ½æ˜¯æœ€é€‚åˆçš„æƒ…å†µã€‚

é¦–å…ˆï¼Œå€¼å¾—æ£€æŸ¥ =.pdf= å›¾å½¢æ–‡ä»¶æ˜¯å¦ä¼šä¼˜å…ˆäº =.png= æ–‡ä»¶ã€‚å¦‚æœæ˜¯è¿™æ ·ï¼Œé‚£å°†æ˜¯ç†æƒ³çš„
ï¼Œå› ä¸ºåœ¨è·å–å’Œè½¬æ¢æ–‡ä»¶ä¹‹åä¸éœ€è¦é¢å¤–çš„åŠªåŠ›ã€‚

#+begin_src shell :tangle no :exports both :results output verbatim :wrap example
texdef -t pdflatex -p graphicx Gin@extensions
#+end_src

#+RESULTS:
#+begin_example
\Gin@extensions:
macro:->.pdf,.png,.jpg,.mps,.jpeg,.jbig2,.jb2,.PDF,.PNG,.JPG,.JPEG,.JBIG2,.JB2,.eps
#+end_example

æå¥½çš„ï¼æˆ‘ä»¬å¯ä»¥çœ‹åˆ° =.pdf= å®é™…ä¸Šåœ¨ä¼˜å…ˆçº§åˆ—è¡¨ä¸­æ’åœ¨ç¬¬ä¸€ä½ã€‚æ‰€ä»¥ç°åœ¨æˆ‘ä»¬åªéœ€è¦è·å–
å’Œè½¬æ¢è¿™äº› SVG --- ç†æƒ³æƒ…å†µä¸‹ä½¿ç”¨ä¸€ä¸ªæ–¹ä¾¿çš„å‘½ä»¤æ¥ä¸ºæˆ‘ä»¬åšè¿™ä»¶äº‹ã€‚

#+begin_src emacs-lisp
(defun org-latex-emoji-install-vector-graphics ()
  "Dowload, convert, and install vector emojis for use with LaTeX."
  (interactive)
  (let ((dir (org-latex-emoji-install-vector-graphics--download)))
    (org-latex-emoji-install-vector-graphics--convert dir)
    (org-latex-emoji-install-vector-graphics--install dir))
  (message "Vector emojis installed."))

(defun org-latex-emoji-install-vector-graphics--download ()
  (message "Locating latest emojis...")
  (let* ((twemoji-url (substring (shell-command-to-string "echo \"https://github.com$(curl -sL https://github.com/twitter/twemoji/releases/latest | grep '.zip\"' | cut -d '\"' -f 2)\"") 0 -1))
         (twemoji-version (replace-regexp-in-string "^.*tags/v\\(.*\\)\\.zip" "\\1" twemoji-url))
         (twemoji-dest-folder (make-temp-file "twemoji-" t)))
    (message "Downloading Twemoji v%s" twemoji-version)
    (let ((default-directory twemoji-dest-folder))
      (call-process "curl" nil nil nil "-L" twemoji-url "--output" "twemoji.zip")
      (message "Unzipping")
      (call-process "unzip" nil nil nil "twemoji.zip")
      (concat twemoji-dest-folder "/twemoji-" twemoji-version "/assets/svg"))))

(defun org-latex-emoji-install-vector-graphics--convert (dir)
  (let ((default-directory dir))
    (if (executable-find "cairosvg") ; cairo's PDFs are ~10% smaller
        (let* ((images (directory-files dir nil ".*.svg"))
               (num-images (length images))
               (index 0)
               (max-threads (1- (string-to-number (shell-command-to-string "nproc"))))
               (threads 0))
          (while (< index num-images)
            (setf threads (1+ threads))
            (message "Converting emoji %d/%d (%s)" (1+ index) num-images (nth index images))
            (make-process :name "cairosvg"
                          :command (list "cairosvg" (nth index images) "-o" (concat (file-name-sans-extension (nth index images)) ".pdf"))
                          :sentinel (lambda (proc msg)
                                      (when (memq (process-status proc) '(exit signal))
                                        (setf threads (1- threads)))))
            (setq index (1+ index))
            (while (> threads max-threads)
              (sleep-for 0.01)))
          (while (> threads 0)
            (sleep-for 0.01))
          (message "Finished conversion!")))
    (shell-command "inkscape --batch-process --export-type='pdf' *.svg")))

(defun org-latex-emoji-install-vector-graphics--install (dir)
  (message "Installing vector emojis into emoji directory")
  (let ((images (directory-files dir t ".*.pdf"))
        (emoji-dir (concat (emojify-image-dir) "/")))
    (mapcar
     (lambda (image)
       (rename-file image emoji-dir t))
     images)))
#+end_src

**** ç§»é™¤é ascii å­—ç¬¦
ä½¿ç”¨ ~pdflatex~ æ—¶ï¼Œå‡ ä¹é ascii å­—ç¬¦ä¸€èˆ¬éƒ½æœ‰é—®é¢˜ï¼Œä¸ä¼šå‡ºç°åœ¨ pdf ä¸­ã€‚æœ€å¥½çœ‹åˆ°
æœ‰ä¸€äº›å­—ç¬¦æ²¡æœ‰æ˜¾ç¤ºï¼Œè€Œä¸æ˜¯ä»€ä¹ˆéƒ½æ²¡æœ‰ã€‚

æˆ‘ä»¬æ£€æŸ¥æ¯ä¸ªé ascii å­—ç¬¦ï¼Œä»¥ç¡®ä¿å®ƒä¸æ˜¯ä½¿ç”¨ =utf8= é€‰é¡¹åŠ è½½æ—¶ç”± =inputenc= åŒ…
ç¼–ç çš„å­—ç¬¦ã€‚æœ€åï¼Œæˆ‘ä»¬çœ‹çœ‹æˆ‘ä»¬æ˜¯å¦æœ‰è‡ªå·±çš„ LaTeX è½¬æ¢å¯ä»¥åº”ç”¨ï¼Œå¦‚æœæ²¡æœ‰ï¼Œæˆ‘ä»¬ç”¨
=Â¿= æ›¿æ¢é ascii å­—ç¬¦ã€‚

å¦ä»¥ç¡®ä¿æˆ‘ä»¬åªåˆ é™¤æ— æ³•æ˜¾ç¤ºçš„å­—ç¬¦ï¼Œæˆ‘ä»¬æ£€æŸ¥
=/usr/share/texmf/tex/latex/base/utf8enc.dfu= ã€‚

æˆ‘ä»¬åªéœ€è¦ç¡®ä¿å°†å…¶é™„åŠ åˆ°è¿‡æ»¤å™¨å‡½æ•°åˆ—è¡¨ä¸­ï¼Œå› ä¸ºæˆ‘ä»¬æƒ³è®©è¡¨æƒ…ç¬¦å·å¤„ç†é¦–å…ˆå‘ç”Ÿã€‚

#+begin_src emacs-lisp
(defvar +org-pdflatex-inputenc-encoded-chars
  "[[:ascii:]\u00A0-\u01F0\u0218-\u021BÈ²È³È·Ë†Ë‡ËœË˜Ë™Ë›Ë\u0400-\u04FFá¸‚á¸ƒáº\u200C\u2010-\u201Eâ€ â€¡â€¢â€¦â€°â€±â€¹â€ºâ€»â€½â„ââ’â‚¡â‚¤â‚¦â‚©â‚«â‚¬â‚±â„ƒâ„–â„—â„â„ â„¢â„¦â„§â„®â†â†‘â†’â†“âŒ©âŒªâ¢â£â—¦â—¯â™ªâŸ¨âŸ©á¸ á¸¡\uFB00-\uFB06]")

(defun +org-latex-replace-non-ascii-chars (text backend info)
  "Replace non-ascii chars with \\char\"XYZ forms."
  (when (and (org-export-derived-backend-p backend 'latex)
             (string= (plist-get info :latex-compiler) "pdflatex"))
    (let (case-replace)
      (replace-regexp-in-string "[^[:ascii:]]"
                                (lambda (nonascii)
                                  (if (string-match-p +org-pdflatex-inputenc-encoded-chars nonascii) nonascii
                                    (or (cdr (assoc nonascii +org-latex-non-ascii-char-substitutions)) "Â¿")))
                                text))))

(add-to-list 'org-export-filter-final-output-functions #'+org-latex-replace-non-ascii-chars t)
#+end_src

ç°åœ¨ï¼Œæœ‰äº›ç¬¦å·æ²¡æœ‰åŒ…å«åœ¨ =inputenc= ä¸­ï¼Œä½†æ— è®ºå¦‚ä½•æˆ‘ä»¬åº”è¯¥èƒ½å¤Ÿå¤„ç†ã€‚æˆ‘ä»¬ä¸ºä»–ä»¬å®š
ä¹‰äº†ä¸€å¼  LaTeX ç¿»è¯‘è¡¨ã€‚

#+name: latex-non-ascii-char-substitutions
| Character | LaTeX           |
|-----------+-----------------|
| É‘         | \(\alpha\)      |
| Î²         | \(\beta\)       |
| Î³         | \(\gamma\)      |
| Î´         | \(\delta\)      |
| Îµ         | \(\epsilon\)    |
| Ïµ         | \(\varepsilon\) |
| Î¶         | \(\zeta\)       |
| Î·         | \(\eta\)        |
| Î¸         | \(\theta\)      |
| Ï‘         | \(\vartheta\)   |
| Î¹         | \(\iota\)       |
| Îº         | \(\kappa\)      |
| Î»         | \(\lambda\)     |
| Î¼         | \(\mu\)         |
| Î½         | \(\nu\)         |
| Î¾         | \(\xi\)         |
| Ï€         | \(\pi\)         |
| Ï–         | \(\varpi\)      |
| Ï         | \(\rho\)        |
| Ï±         | \(\varrho\)     |
| Ïƒ         | \(\sigma\)      |
| Ï‚         | \(\varsigma\)   |
| Ï„         | \(\tau\)        |
| Ï…         | \(\upsilon\)    |
| Ï•         | \(\phi\)        |
| Ï†         | \(\varphi\)     |
| Ïˆ         | \(\psi\)        |
| Ï‰         | \(\omega\)      |
| Î“         | \(\Gamma\)      |
| Î”         | \(\Delta\)      |
| Î˜         | \(\Theta\)      |
| Î›         | \(\Lambda\)     |
| Î         | \(\Xi\)         |
| Î          | \(\Pi\)         |
| Î£         | \(\Sigma\)      |
| Î¥         | \(\Upsilon\)    |
| Î¦         | \(\Phi\)        |
| Î¨         | \(\Psi\)        |
| Î©         | \(\Omega\)      |
| ×         | \(\aleph\)      |
| ×‘         | \(\beth\)       |
| ×“         | \(\daleth\)     |
| ×’         | \(\gimel\)      |

#+name: gen-latex-non-ascii-char-substitutions
#+begin_src emacs-lisp :noweb-ref none :var latex-non-ascii-char-substitutions=latex-non-ascii-char-substitutions
(replace-regexp-in-string
 " '((" "\n   '(("
 (replace-regexp-in-string
  ") (" ")\n     ("
  (prin1-to-string
   `(defvar +org-latex-non-ascii-char-substitutions
      ',(mapcar
         (lambda (entry)
           (cons (car entry) (replace-regexp-in-string "\\\\" "\\\\\\\\" (cadr entry))))
         latex-non-ascii-char-substitutions)))))
#+end_src

#+begin_src emacs-lisp :noweb no-export
<<gen-latex-non-ascii-char-substitutions()>>
#+end_src

**** ç¼©å†™åçš„æ™®é€šç©ºæ ¼
åœ¨ LaTeX ä¸­ï¼Œå•è¯å’Œå¥å­ç©ºé—´é€šå¸¸å…·æœ‰ä¸åŒçš„å®½åº¦ã€‚å½“ä½¿ç”¨ç¼©å†™æ—¶ï¼Œè¿™å¯èƒ½æ˜¯ä¸€ä¸ªé—®é¢˜ï¼Œ
i.e. e.g. etc. et al.. ã€‚è¿™å¯ä»¥é€šè¿‡ä½¿ç”¨ src_LaTeX{\ } å¼ºåˆ¶ä¸€ä¸ªæ­£å¸¸ç©ºæ ¼æ¥çº æ­£
ã€‚åœ¨å¯¼å‡ºOrgæ–‡æ¡£çš„æ—¶å€™ï¼Œæˆ‘ä»¬å¯ä»¥æ·»åŠ ä¸€ä¸ªè¿‡æ»¤å™¨æ¥æ£€æŸ¥å¸¸è§çš„ç¼©å†™ï¼Œè®©ç©ºæ ¼æ­£å¸¸ã€‚

#+begin_src emacs-lisp
(defvar +org-latex-abbreviations
  '(;; Latin
    "cf." "e.g." "etc." "et al." "i.e." "v." "vs." "viz." "n.b."
    ;; Corperate
    "inc." "govt." "ltd." "pty." "dept."
    ;; Temporal
    "est." "c."
    ;; Honorifics
    "Prof." "Dr." "Mr." "Mrs." "Ms." "Miss." "Sr." "Jr."
    ;; Components of a work
    "ed." "vol." "sec." "chap." "pt." "pp." "op." "no."
    ;; Common usage
    "approx." "misc." "min." "max.")
  "A list of abbreviations that should be spaced correctly when exporting to LaTeX.")

(defun +org-latex-correct-latin-abbreviation-spaces (text backend _info)
  "Normalise spaces after Latin abbreviations."
  (when (org-export-derived-backend-p backend 'latex)
    (replace-regexp-in-string (rx (group (or line-start space)
                                         (regexp (regexp-opt-group +org-latex-abbreviations)))
                                  (or line-end space))
                              "\\1\\\\ "
                              text)))

(add-to-list 'org-export-filter-paragraph-functions #'+org-latex-correct-latin-abbreviation-spaces t)
#+end_src

**** é¢å¤–çš„ç‰¹æ®Šå­—ç¬¦ä¸²
LaTeX å·²ç»å°† =---= å’Œ =--= è¯†åˆ«ä¸º em/en-dashesï¼Œ =\-= ä½œä¸ºè¿å­—ç¬¦ï¼Œå¹¶ä¸”å°†
=...= è½¬æ¢ä¸º =\ldots{}= è¢«ç¡¬ç¼–ç åœ¨ ~org-latex-plain-text~ ä¸­ (ä¸åƒ
~org-html-plain-text~)ã€‚

æˆ‘ä¹Ÿå¾ˆæƒ³é‡å®šä¹‰ =->= å’Œ =<-= ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬æå‡ºä¸€äº›å»ºè®®ã€‚

#+begin_src emacs-lisp
(defvar org-latex-extra-special-string-regexps
  '(("->" . "\\\\textrightarrow{}")
    ("<-" . "\\\\textleftarrow{}")))

(defun org-latex-convert-extra-special-strings (string)
  "Convert special characters in STRING to LaTeX."
  (dolist (a org-latex-extra-special-string-regexps string)
    (let ((re (car a))
          (rpl (cdr a)))
      (setq string (replace-regexp-in-string re rpl string t)))))

(defadvice! org-latex-plain-text-extra-special-a (orig-fn text info)
  "Make `org-latex-plain-text' handle some extra special strings."
  :around #'org-latex-plain-text
  (let ((output (funcall orig-fn text info)))
    (when (plist-get info :with-special-strings)
      (setq output (org-latex-convert-extra-special-strings output)))
    output))
#+end_src

**** æ”¯æŒæ¥è‡ª URL çš„å›¾åƒ
æ‚¨å¯ä»¥è½»æ¾é“¾æ¥åˆ°è¿œç¨‹å›¾åƒï¼Œå¹¶ä¸”å®ƒä»¬å¯ä»¥å¾ˆå¥½åœ°ä¸åŸºäº HTML çš„å¯¼å‡ºé…åˆä½¿ç”¨ã€‚ä½†æ˜¯ï¼Œ
LaTeX åªèƒ½åŒ…å«æœ¬åœ°æ–‡ä»¶ï¼Œå› æ­¤ =org-latex-link= çš„å½“å‰è¡Œä¸ºåªæ˜¯å°† URL æ’å…¥åˆ°å›¾åƒ
ä¸­ã€‚

é€šè¿‡å°†å›¾åƒä¸‹è½½åˆ°å¯é¢„æµ‹çš„ä½ç½®å¹¶ä½¿ç”¨å®ƒï¼Œæˆ‘ä»¬å¯ä»¥åšå¾—æ›´å¥½ã€‚é€šè¿‡ä½¿æ–‡ä»¶åå¯é¢„æµ‹è€Œä¸ä»…ä»…æ˜¯
ä¸€ä¸ªä¸´æ—¶æ–‡ä»¶ï¼Œè¿™å¯ä»¥æä¾›ä¸€ç§ç¼“å­˜æœºåˆ¶ã€‚
#+begin_src emacs-lisp
(defadvice! +org-latex-link (orig-fn link desc info)
  "Acts as `org-latex-link', but supports remote images."
  :around #'org-latex-link
  (setq o-link link
        o-desc desc
        o-info info)
  (if (and (member (plist-get (cadr link) :type) '("http" "https"))
           (member (file-name-extension (plist-get (cadr link) :path))
                   '("png" "jpg" "jpeg" "pdf" "svg")))
      (org-latex-link--remote link desc info)
    (funcall orig-fn link desc info)))

(defun org-latex-link--remote (link _desc info)
  (let* ((url (plist-get (cadr link) :raw-link))
         (ext (file-name-extension url))
         (target (format "%s%s.%s"
                         (temporary-file-directory)
                         (replace-regexp-in-string "[./]" "-"
                                                   (file-name-sans-extension (substring (plist-get (cadr link) :path) 2)))
                         ext)))
    (unless (file-exists-p target)
      (url-copy-file url target))
    (setcdr link (--> (cadr link)
                   (plist-put it :type "file")
                   (plist-put it :path target)
                   (plist-put it :raw-link (concat "file:" target))
                   (list it)))
    (concat "% fetched from " url "\n"
            (org-latex--inline-image link info))))
#+end_src

**** å˜è‰²é¾™ --- åˆååŒ¹é…ä¸»é¢˜
ä¸€æ—¦æˆ‘æœ‰äº†è®©ç”Ÿæˆçš„ LaTeX æ–‡æ¡£çš„å¤–è§‚ä¸å½“å‰çš„ Emacs ä¸»é¢˜ç›¸åŒ¹é…çš„æƒ³æ³•ï¼Œæˆ‘å°±æ¬£å–œè‹¥ç‹‚
ã€‚ç»“æœæ˜¯ç±» ~å˜è‰²é¾™~ ï¼Œæˆ‘åœ¨åŒ… =ox-chameleon= ä¸­å®ç°äº†å®ƒã€‚

#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! ox-chameleon :recipe (:local-repo "lisp/ox-chameleon"))
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! ox-chameleon
  :after ox)
#+end_src

**** ä½¿ verbatim ä¸ code ä¸åŒ
æ—¢ç„¶å·²ç»åœ¨ä¸Šé¢åšäº†è¿™ä¹ˆå¤šçš„åŠªåŠ›ï¼Œæˆ‘ä»¬é€šè¿‡è®© =verbatim= ä½¿ç”¨ ~verb~ è€Œä¸æ˜¯
~protectedtexttt~ (é»˜è®¤) æ¥å……åˆ†åˆ©ç”¨å®ƒã€‚

è¿™æä¾›äº†ä¸ [[*ä½¿ verbatim ä¸ code ä¸åŒ][HTML å¯¼å‡ºéƒ¨åˆ†]] ä¸­æåˆ°çš„ç›¸åŒçš„ä¼˜ç‚¹ã€‚

#+begin_src emacs-lisp
(setq org-latex-text-markup-alist
      '((bold . "\\textbf{%s}")
        (code . protectedtexttt)
        (italic . "\\emph{%s}")
        (strike-through . "\\sout{%s}")
        (underline . "\\uline{%s}")
        (verbatim . verb)))
#+end_src

**** æ£€æŸ¥æ‰€éœ€çš„åŒ…
å¯¹äºæˆ‘å¦‚ä½•è®¾ç½® Org çš„ LaTeX å¯¼å‡ºï¼Œéœ€è¦ä»¥ä¸‹åŒ…ï¼š
#+name: org-latex-required-packages-list
| Package    | Description                                           |
|------------+-------------------------------------------------------|
| adjustbox  | Adjust general LaTeX material in like includegraphics |
| amsmath    | A near-essential maths package                        |
| booktabs   | Nice horizontal lines in tables                       |
| cancel     | Cancel terms in equations                             |
| capt-of    | Captions outside floats                               |
| caption    | Finer control over captions                           |
| cleveref   | Easy cross-referencing                                |
| embedall   | Embed files in the document                           |
| float      | Floating environments                                 |
| fontenc    | Font encodings                                        |
| fvextra    | Enhanced verbatim environments                        |
| graphicx   | An extended graphics package                          |
| hanging    | Used by oc-csl                                        |
| hyperref   | Links                                                 |
| inputenc   | Input file encodings                                  |
| longtable  | Multi-page tables                                     |
| mathalpha  | Set extended math alphabet fonts                      |
| mathtools  | Typesetting tools for maths                           |
| microtype  | Microtypography                                       |
| pdfx       | Create pdf/a- and pdf/x- compatible documents         |
| pifont     | A collection of symbols                               |
| preview    | Needed for AUCTeX and ob-latex                        |
| scrbase    | KOMA classes and more                                 |
| siunitx    | Proper unit support                                   |
| soul       | Strikethrough and underline, flexibly                 |
| subcaption | Form subfigures and subcaptions                       |
| svg        | Insert SVG images                                     |
| tikz       | Generally handy, as a dependancy and for graphics     |
| tcolorbox  | Nice boxes for code                                   |
| textcomp   | Font encodings                                        |
| xcolor     | Colours                                               |
| xparse     | Extended command/env definition forms                 |
| xcoffins   | Manipulate coffins (boxes) for typesetting            |

ç„¶åä¾èµ–çš„å„ç§å­—ä½“é›†ï¼š
#+name: org-latex-font-packages-list
+ Alegreya
+ arev
+ biolinum
+ FiraMono
+ FiraSans
+ fourier
+ gillius
+ kpfonts
+ libertine
+ newpxmath
+ newpxtext
+ newtxmath
+ newtxtext
+ newtxsf
+ noto
+ plex-mono
+ plex-sans
+ plex-serif
+ sourcecodepro
+ sourcesanspro
+ sourceserifpro

æˆ‘ä»¬å¯ä»¥ç¼–å†™ä¸€ä¸ªå‡½æ•°ï¼Œä½¿ç”¨ =kpsewhich= æ£€æŸ¥è¿™äº›åŒ…ä¸­çš„æ¯ä¸€ä¸ªï¼Œç„¶åå¦‚æœå…¶ä¸­ä»»ä½•ä¸€ä¸ª
ä¸¢å¤±ï¼Œæˆ‘ä»¬å°†åœ¨ç”Ÿæˆçš„é…ç½®ä¸­æ³¨å…¥ä¸€äº›å»ºè®®ï¼Œè¯¥é…ç½®ä¼šè·å–ä¸¢å¤±åŒ…çš„åˆ—è¡¨å¹¶åœ¨æ¯æ¬¡å¯¼å‡ºæ—¶åœ¨
PDF æ ¼å¼ä¸­è­¦å‘Šæˆ‘ä»¬ã€‚

#+name: org-missing-latex-packages
#+begin_src emacs-lisp :noweb-ref none :var org-latex-required-packages-list=org-latex-required-packages-list[,0] :var org-latex-font-packages-list=org-latex-font-packages-list
(setq org-required-latex-packages (append org-latex-required-packages-list
                                          (mapcar #'car org-latex-font-packages-list)))

(defun check-for-latex-packages (packages)
  (delq nil (mapcar (lambda (package)
                      (unless
                          (= 0 (shell-command (format "kpsewhich %s.sty" package)))
                        package))
                    packages)))

(if-let ((missing-pkgs (check-for-latex-packages org-required-latex-packages)))
    (concat
     (pp-to-string `(setq org-required-latex-packages ',org-required-latex-packages))
     (message ";; Detected missing LaTeX packages: %s\n" (mapconcat #'identity missing-pkgs ", "))
     (pp-to-string
      '(defun check-for-latex-packages (packages)
         (delq nil (mapcar (lambda (package)
                             (unless
                                 (= 0 (shell-command (format "kpsewhich %s.sty" package)))
                               package))
                           packages))))
     (pp-to-string
      '(defun +org-warn-about-missing-latex-packages (&rest _)
         (message "Checking for missing LaTeX packages...")
         (sleep-for 0.4)
         (if-let (missing-pkgs (check-for-latex-packages org-required-latex-packages))
             (message "%s You are missing the following LaTeX packages: %s."
                      (propertize "Warning!" 'face '(bold warning))
                      (mapconcat (lambda (pkg) (propertize pkg 'face 'font-lock-variable-name-face))
                                 missing-pkgs
                                 ", "))
           (message "%s You have all the required LaTeX packages. Run %s to make this message go away."
                    (propertize "Success!" 'face '(bold success))
                    (propertize "doom sync" 'face 'font-lock-keyword-face))
           (advice-remove 'org-latex-export-to-pdf #'+org-warn-about-missing-latex-packages))
         (sleep-for 1)))
     (pp-to-string
      '(advice-add 'org-latex-export-to-pdf :before #'+org-warn-about-missing-latex-packages)))
  ";; No missing LaTeX packags detected")
#+end_src

#+begin_src emacs-lisp :noweb no-export
<<org-missing-latex-packages()>>
#+end_src

*** å¹»ç¯ç‰‡å¯¼å‡º
å¦‚æœèƒ½ä½¿ç”¨ä¸åŒçš„ä¸»é¢˜å°±å¾ˆå¥½äº†
It's nice to use a different theme
#+begin_src emacs-lisp
(setq org-beamer-theme "[progressbar=foot]metropolis")
#+end_src

#+begin_src emacs-lisp
(defun org-beamer-p (info)
  (eq 'beamer (and (plist-get info :back-end) (org-export-backend-name (plist-get info :back-end)))))

(add-to-list 'org-latex-conditional-features '(org-beamer-p . beamer) t)
(add-to-list 'org-latex-feature-implementations '(beamer :requires .missing-koma :prevents (italic-quotes condensed-lists)) t)
(add-to-list 'org-latex-feature-implementations '(.missing-koma :snippet "\\usepackage{scrextend}" :order 2) t)
#+end_src

è€Œä¸”æˆ‘è®¤ä¸ºå°†æ¼”ç¤ºæ–‡ç¨¿åˆ†æˆå‡ ä¸ªéƒ¨åˆ†æ˜¯å¾ˆè‡ªç„¶çš„ï¼Œä¾‹å¦‚ç®€ä»‹ã€æ¦‚è¿°â€¦â€¦ æ‰€ä»¥è®©æˆ‘ä»¬å°†æ ‡é¢˜çº§åˆ«è®¾ç½®
ä¸ºä» ~1~ åˆ° ~2~ çš„å¸§ã€‚
#+begin_src emacs-lisp
(setq org-beamer-frame-level 2)
#+end_src

*** Reveal å¯¼å‡º
é»˜è®¤æƒ…å†µä¸‹æ˜¾ç¤ºç›¸å½“ä¸é”™ï¼Œæˆ‘è®¤ä¸ºåªéœ€è¦è°ƒæ•´ä¸€ç‚¹ç‚¹ã€‚

#+begin_src emacs-lisp
(setq org-re-reveal-theme "white"
      org-re-reveal-transition "slide"
      org-re-reveal-plugins '(markdown notes math search zoom))
#+end_src

*** ASCII å¯¼å‡º

å½“ UTF-8 å­˜åœ¨æ—¶ï¼Œä¸ºä»€ä¹ˆè¦æ»¡è¶³äº ASCIIï¼Ÿ
#+begin_src emacs-lisp
(setq org-ascii-charset 'utf-8)
#+end_src

ASCII å¯¼å‡ºé€šå¸¸ä¸é”™ï¼Œæˆ‘è®¤ä¸ºä»æ”¹è¿›ä¸­æ”¶ç›Šçš„ä¸»è¦æ–¹é¢æ˜¯ LaTeX ç‰‡æ®µçš„å¤–è§‚ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿
ç”¨ä¸€ä¸ªå¾ˆå¥½çš„ç¨‹åºæ¥åˆ›å»ºæ›´å¥½çš„ unicode è¡¨ç¤ºã€‚å®ƒå«åš ~latex2text~ ï¼Œå¹¶[[https://repology.org/project/python:pylatexenc/versions][æ²¡æœ‰çœŸæ­£çš„
æ‰“æˆ elisp åŒ…]]ï¼Œå®ƒæ˜¯ =pylatexenc= çš„ä¸€éƒ¨åˆ†ã€‚å› æ­¤å°†ä½¿ç”¨ =pip= å®‰è£…å®ƒã€‚

#+begin_src shell :tangle (if (executable-find "latex2text") "no" "setup.sh")
sudo python3 -m pip install pylatexenc
#+end_src

å®‰è£…åï¼Œæˆ‘ä»¬å¯ä»¥è¦†ç›– src_elisp{(org-ascii-latex-fragment)} å’Œ
src_elisp{(org-ascii-latex-environment)} å‡½æ•°ï¼Œå®ƒä»¬éå¸¸æ–¹ä¾¿
--- åªéœ€æå–å†…å®¹å¹¶ç¼©è¿›ã€‚æˆ‘ä»¬åªä¼šåœ¨è®¾ç½® =utf-8= æ—¶åšä¸€äº›ä¸åŒçš„äº‹æƒ…ã€‚

#+begin_src emacs-lisp :noweb-ref none :tangle (if (executable-find "latex2text") "yes" "no")
(after! ox-ascii
  (defvar org-ascii-convert-latex t
    "Use latex2text to convert LaTeX elements to unicode.")

  (defadvice! org-ascii-latex-environment-unicode-a (latex-environment _contents info)
    "Transcode a LATEX-ENVIRONMENT element from Org to ASCII, converting to unicode.
CONTENTS is nil.  INFO is a plist holding contextual
information."
    :override #'org-ascii-latex-environment
    (when (plist-get info :with-latex)
      (org-ascii--justify-element
       (org-remove-indentation
        (let* ((latex (org-element-property :value latex-environment))
               (unicode (and (eq (plist-get info :ascii-charset) 'utf-8)
                             org-ascii-convert-latex
                             (doom-call-process "latex2text" "-q" "--code" latex))))
          (if (= (car unicode) 0) ; utf-8 set, and sucessfully ran latex2text
              (cdr unicode) latex)))
       latex-environment info)))

  (defadvice! org-ascii-latex-fragment-unicode-a (latex-fragment _contents info)
    "Transcode a LATEX-FRAGMENT object from Org to ASCII, converting to unicode.
CONTENTS is nil.  INFO is a plist holding contextual
information."
    :override #'org-ascii-latex-fragment
    (when (plist-get info :with-latex)
      (let* ((latex (org-element-property :value latex-fragment))
             (unicode (and (eq (plist-get info :ascii-charset) 'utf-8)
                           org-ascii-convert-latex
                             (doom-call-process "latex2text" "-q" "--code" latex))))
        (if (and unicode (= (car unicode) 0)) ; utf-8 set, and sucessfully ran latex2text
            (cdr unicode) latex)))))
#+end_src

*** Markdown å¯¼å‡º
**** GFM
ç”±äº /[[https://github.com/commonmark/commonmark-spec/wiki/markdown-flavors][Markdown å®ç°çš„å¤šæ ·æ€§]]/ ï¼Œå®é™…ä¸Šå¹¶æ²¡æœ‰æ ‡å‡†çš„è¡¨æ ¼è§„èŒƒâ€¦â€¦æˆ–æ ‡å‡†çš„ä»»ä½•ä¸œè¥¿ã€‚å› 
ä¸º ~org-md~ è¡¨ç°çš„ä¸ä¼—ä¸åŒï¼Œå®ƒåªæ˜¯å¯¹æ‰€æœ‰è¿™äº›éæ ‡å‡†åŒ–å…ƒç´  (å¾ˆå¤š) ä½¿ç”¨ HTMLã€‚å› æ­¤
ox-gfm å¯ä»¥æ–¹ä¾¿åœ°å¯¼å‡ºå…·æœ‰ GitHub æ‹¥æœ‰çš„æ‰€æœ‰åŠŸèƒ½çš„ Markdownã€‚

#+begin_src emacs-lisp :noweb-ref none :tangle packages.el
(package! ox-gfm :pin "99f93011b069e02b37c9660b8fcb45dab086a07f")
#+end_src

#+begin_src emacs-lisp :noweb-ref none :tangle yes
(use-package! ox-gfm
  :after ox)
#+end_src

**** å­—ç¬¦æ›¿æ¢
å½“æˆ‘æƒ³åœ¨æŸå¤„ç²˜è´´å¯¼å‡ºçš„ Markdown æ—¶ (ä¾‹å¦‚åœ¨ä½¿ç”¨ [[Emacs Everywhere][Emacs Everywhere]] æ—¶)ï¼Œæœ€å¥½ä½¿ç”¨
unicode å­—ç¬¦ =---= è€Œä¸æ˜¯ =&#x2014;= ã€‚

ä¸ºæ­¤ï¼Œæˆ‘ä»¬åªéœ€è¦åœ¨æœ¬åœ°é‡æ–°ç»‘å®šæä¾›çš„è¿™äº› alistã€‚
#+begin_src emacs-lisp
(defadvice! org-md-plain-text-unicode-a (orig-fn text info)
  "Locally rebind `org-html-special-string-regexps'"
  :around #'org-md-plain-text
  (let ((org-html-special-string-regexps
         '(("\\\\-" . "-")
           ("---\\([^-]\\|$\\)" . "â€”\\1")
           ("--\\([^-]\\|$\\)" . "â€“\\1")
           ("\\.\\.\\." . "â€¦")
           ("->" . "â†’")
           ("<-" . "â†"))))
    (funcall orig-fn text (plist-put info :with-smart-quotes nil))))
#+end_src

å°†æ¥ï¼Œæˆ‘å¯èƒ½æƒ³ä»…åœ¨ä½¿ç”¨ =ox-gfm= æ—¶å¯ç”¨æ£€æŸ¥ =ä¿¡æ¯= ã€‚

å¦ä¸€ä¸ªå€¼å¾—è€ƒè™‘çš„æ˜¯ LaTeX æ ¼å¼ã€‚ä¼¼ä¹å¤§å¤šæ•° Markdown è§£æå™¨éƒ½ä¸“æ³¨äº TeX æ ·å¼çš„è¯­æ³•
 (=$= å’Œ =$$=)ã€‚ä¸å¹¸çš„æ˜¯ï¼Œä¸ºäº†è·å¾—è‰¯å¥½çš„æ¸²æŸ“ï¼Œæœ€å¥½å…¼å®¹å®ƒä»¬ã€‚

=ox-md= æ²¡æœ‰ä¸ºæ­¤æä¾›è½¬ç å™¨ï¼Œå› æ­¤å¿…é¡»è‡ªå·±åˆ›å»ºå¹¶å°†å®ƒä»¬æ¨é€åˆ° =md= è½¬ç å™¨åˆ—è¡¨ä¸­ã€‚

#+begin_src emacs-lisp
(after! ox-md
  (defun org-md-latex-fragment (latex-fragment _contents info)
    "Transcode a LATEX-FRAGMENT object from Org to Markdown."
    (let ((frag (org-element-property :value latex-fragment)))
      (cond
       ((string-match-p "^\\\\(" frag)
        (concat "$" (substring frag 2 -2) "$"))
       ((string-match-p "^\\\\\\[" frag)
        (concat "$$" (substring frag 2 -2) "$$"))
       (t (message "unrecognised fragment: %s" frag)
          frag))))

  (defun org-md-latex-environment (latex-environment contents info)
    "Transcode a LATEX-ENVIRONMENT object from Org to Markdown."
    (concat "$$\n"
            (org-html-latex-environment latex-environment contents info)
            "$$\n"))

  (defun org-utf8-entity (entity _contents _info)
    "Transcode an ENTITY object from Org to utf-8.
CONTENTS are the definition itself.  INFO is a plist holding
contextual information."
    (org-element-property :utf-8 entity))

  ;; We can't let this be immediately parsed and evaluated,
  ;; because eager macro-expansion tries to call as-of-yet
  ;; undefined functions.
  ;; NOTE in the near future this shouldn't be required
  (eval
   '(dolist (extra-transcoder
             '((latex-fragment . org-md-latex-fragment)
               (latex-environment . org-md-latex-environment)
               (entity . org-utf8-entity)))
      (unless (member extra-transcoder (org-export-backend-transcoders
                                        (org-export-get-backend 'md)))
        (push extra-transcoder (org-export-backend-transcoders
                                (org-export-get-backend 'md)))))))
#+end_src

*** Babel
Doom æ‡’åŠ è½½ babel è¯­è¨€ã€‚å®ƒè¿˜å¼•å…¥äº† [[https://github.com/astahlman/ob-async][ob-async]]ï¼Œè¿™å¤ªæ£’äº†ï¼Œä½†å¦‚æœé»˜è®¤ä½¿ç”¨å®ƒä¼šæ›´å¥½ã€‚

=ob-async= æœ‰ä¸¤ä¸ªæ³¨æ„äº‹é¡¹ï¼š
1. ä¸æ”¯æŒ =:session=
  + å½“è®¾ç½®äº† =:session= æ˜¯æˆ‘ä»¬ä¸ä¼šä½¿ç”¨ =:async=
2. å¢åŠ äº†æ‰§è¡Œçš„å›ºå®šå»¶è¿Ÿ
  + è¿™åœ¨è®¸å¤šæƒ…å†µä¸‹æ˜¯ä¸å¯å–çš„ï¼Œä¾‹å¦‚ =emacs-lisp= ä»£ç é€šå¸¸ä¸éœ€è¦å®ƒ
  + å› æ­¤ï¼Œæˆ‘è¿˜å¼•å…¥äº†ä¸€ä¸ªå¼‚æ­¥è¯­è¨€é»‘åå•æ¥æ§åˆ¶å®ƒä½•æ—¶è‡ªåŠ¨å¯ç”¨

ç”±äºæ‰€éœ€è¡Œä¸ºçš„ç»†å¾®å·®åˆ«ï¼Œæˆ‘å»ºè®® ~org-babel-get-src-block-info~ æ™ºèƒ½åœ°æ·»åŠ  =:async=
ï¼Œè€Œä¸æ˜¯ä»…ä»…å°† =:async= æ·»åŠ åˆ° ~org-babel-default-header-args~ ã€‚ä½œä¸ºä¸€ä¸ª escape
hatchï¼Œå®ƒè¿˜å°† =:sync= è¯†åˆ«ä¸ºä¸åº”æ·»åŠ  =:async= çš„æŒ‡ç¤ºã€‚

æˆ‘æœ€åˆç¡®å®ä¸ºé™¤ =emacs-lisp= å’Œ =LaTeX= ä¹‹å¤–çš„æ‰€æœ‰å†…å®¹å¯ç”¨äº†æ­¤åŠŸèƒ½ (å­˜åœ¨ä¸€äº›å¥‡æ€ª
çš„é—®é¢˜)ï¼Œä½†è¿™ä¸ºæ¯ä¸ª src å—è¯„ä¼°å¢åŠ äº†å¤§çº¦ 3 ç§’çš„ "å¯åŠ¨" æˆæœ¬ï¼Œè¿™æœ‰ç‚¹ç—›è‹¦ã€‚ç”±äº
=:async= å¯ä»¥é€šè¿‡ =#+properties= è½»æ¾æ·»åŠ ï¼Œå› æ­¤æˆ‘å°†æ­¤è¡Œä¸ºä»é»‘åå•è½¬å˜ä¸ºç™½åå•ã€‚

#+begin_src emacs-lisp
(add-transient-hook! #'org-babel-execute-src-block
  (require 'ob-async))

(defvar org-babel-auto-async-languages '()
  "Babel languages which should be executed asyncronously by default.")

(defadvice! org-babel-get-src-block-info-eager-async-a (orig-fn &optional light datum)
  "Eagarly add an :async parameter to the src information, unless it seems problematic.
This only acts o languages in `org-babel-auto-async-languages'.
Not added when either:
+ session is not \"none\"
+ :sync is set"
  :around #'org-babel-get-src-block-info
  (let ((result (funcall orig-fn light datum)))
    (when (and (string= "none" (cdr (assoc :session (caddr result))))
               (member (car result) org-babel-auto-async-languages)
               (not (assoc :async (caddr result))) ; don't duplicate
               (not (assoc :sync (caddr result))))
      (push '(:async) (caddr result)))
    result))
#+end_src

*** ESS
æˆ‘ä»¬ä¸å¸Œæœ› ~R~ è¯„ä¼°æŒ‚èµ·ç¼–è¾‘å™¨ï¼Œå› æ­¤
#+begin_src emacs-lisp
(setq ess-eval-visibly 'nowait)
#+end_src

è¯­æ³•é«˜äº®å¾ˆå¥½ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬å¼€å¯è¿™äº›è®¾ç½®
#+begin_src emacs-lisp
(setq ess-R-font-lock-keywords
      '((ess-R-fl-keyword:keywords . t)
        (ess-R-fl-keyword:constants . t)
        (ess-R-fl-keyword:modifiers . t)
        (ess-R-fl-keyword:fun-defs . t)
        (ess-R-fl-keyword:assign-ops . t)
        (ess-R-fl-keyword:%op% . t)
        (ess-fl-keyword:fun-calls . t)
        (ess-fl-keyword:numbers . t)
        (ess-fl-keyword:operators . t)
        (ess-fl-keyword:delimiters . t)
        (ess-fl-keyword:= . t)
        (ess-R-fl-keyword:F&T . t)))
#+end_src

æœ€åï¼Œå¦‚æœä½¿ç”¨ =JAGS= æœ€å¥½èƒ½å¤Ÿä½¿ç”¨ =jags= ä½œä¸ºè¯­è¨€æ ‡è¯†ç¬¦ï¼Œè€Œä¸æ˜¯ =ess-jags= ã€‚
#+begin_src emacs-lisp
(add-to-list '+org-babel-mode-alist '(jags . ess-jags))
#+end_src

** LaTeX
[[xkcd:1301]]
*** éšæƒ³éšç”¨
- ä»å‰ªè´´æ¿ç²˜è´´å›¾åƒ
  + ç¡®å®š ~graphicspath~ ä¸­çš„ç¬¬ä¸€ä¸ªæ–‡ä»¶å¤¹ (å¦‚æœé€‚ç”¨)
  + è¯¢é—®æ–‡ä»¶å
  + ä½¿ç”¨ ~xclip~ å°†æ–‡ä»¶ä¿å­˜åˆ°å›¾å½¢æ–‡ä»¶å¤¹æˆ–å½“å‰ç›®å½• (ä»¥é€‚ç”¨è€…ä¸ºå‡†)
  #+begin_src shell :eval no :tangle no
command -v xclip >/dev/null 2>&1 || { echo >&1 "no xclip"; exit 1; }

if
    xclip -selection clipboard -target image/png -o >/dev/null 2>&1
then
    xclip -selection clipboard -target image/png -o >$1 2>/dev/null
    echo $1
else
    echo "no image"
fi
  #+end_src
  + æ’å…¥å›¾å½¢ï¼Œå¡«å……è¯¦ç»†ä¿¡æ¯ (æ–‡ä»¶åä½œä¸ºå˜é‡å¯èƒ½æ¿€æ´» =yasnippet=?)

*** ç¼–è¯‘
#+begin_src emacs-lisp
(setq TeX-save-query nil
      TeX-show-compilation t
      TeX-command-extra-options "-shell-escape")
(after! latex
  (add-to-list 'TeX-command-list '("XeLaTeX" "%`xelatex%(mode)%' %t" TeX-run-TeX nil t)))
#+end_src

å¯¹äºæŸ¥çœ‹ PDFï¼Œæˆ‘æ›´å–œæ¬¢ pdf-tools æŸ¥çœ‹å™¨ã€‚è™½ç„¶ auctex è¯•å›¾å¾ˆå¥½åœ°è¯†åˆ«æˆ‘å®‰è£…çš„ä¸€äº›
PDF æŸ¥çœ‹åº”ç”¨ç¨‹åºï¼Œä½†æˆ‘ä¸æƒ³è®©å®ƒé»˜è®¤ä½¿ç”¨å®ƒä»¬ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬é‡æ–°æ’åºé¦–é€‰é¡¹ã€‚
#+begin_src emacs-lisp
(setq +latex-viewers '(pdf-tools evince zathura okular skim sumatrapdf))
#+end_src

*** Snippet helpers
ä¸ºäº†åœ¨æ–°æ–‡ä»¶æ¨¡æ¿ä¸­ä½¿ç”¨ï¼Œè®©æˆ‘ä»¬åˆ—å‡ºä¸€ä¸ªæˆ‘ä»¬å¯èƒ½æƒ³è¦ä½¿ç”¨çš„æ¼‚äº®åºè¨€ã€‚
#+name: latex-nice-preamble
#+begin_src latex :tangle no
\\usepackage[pdfa,unicode=true,hidelinks]{hyperref}

\\usepackage[dvipsnames,svgnames,table,hyperref]{xcolor}
\\renewcommand{\\UrlFont}{\\ttfamily\\small}

\\usepackage[a-2b]{pdfx} % why not be archival

\\usepackage[T1]{fontenc}
\\usepackage[osf]{newpxtext}  % Palatino
\\usepackage{gillius}
\\usepackage[scale=0.9]{sourcecodepro}

\\usepackage[varbb]{newpxmath}
\\usepackage{mathtools}
\\usepackage{amssymb}

\\usepackage[activate={true,nocompatibility},final,tracking=true,kerning=true,spacing=true,factor=2000]{microtype}
% microtype makes text look nicer

\\usepackage{graphicx} % include graphics

\\usepackage{booktabs} % nice table rules
#+end_src
ç„¶åè®©æˆ‘ä»¬å°†å†…å®¹ç»‘å®šåˆ°ä¸€ä¸ªå‡½æ•°ï¼Œå¹¶å®šä¹‰ä¸€äº›ä¸é”™çš„è¾…åŠ©å‡½æ•°ã€‚
#+begin_src emacs-lisp :noweb no-export
(setq tec/yas-latex-template-preamble "
<<latex-nice-preamble>>
")

(defun tec/yas-latex-get-class-choice ()
  "Prompt user for LaTeX class choice"
  (setq tec/yas-latex-class-choice (completing-read "Select document class: " '("article" "scrartcl" "bmc"))))

(defun tec/yas-latex-preamble-if ()
  "Based on class choice prompt for insertion of default preamble"
  (if (equal tec/yas-latex-class-choice "bmc") 'nil
    (eq (read-char-choice "Include default preamble? [Type y/n]" '(?y ?n)) ?y)))
#+end_src

**** åˆ†éš”ç¬¦
#+begin_src emacs-lisp
(after! tex
  (defvar tec/tex-last-delim-char nil
    "Last open delim expanded in a tex document")
  (defvar tec/tex-delim-dot-second t
    "When the `tec/tex-last-delim-char' is . a second character (this) is prompted for")
  (defun tec/get-open-delim-char ()
    "Exclusivly read next char to tec/tex-last-delim-char"
    (setq tec/tex-delim-dot-second nil)
    (setq tec/tex-last-delim-char (read-char-exclusive "Opening deliminator, recognises: 9 ( [ { < | ."))
    (when (eql ?. tec/tex-last-delim-char)
      (setq tec/tex-delim-dot-second (read-char-exclusive "Other deliminator, recognises: 0 9 (  ) [ ] { } < > |"))))
  (defun tec/tex-open-delim-from-char (&optional open-char)
    "Find the associated opening delim as string"
    (unless open-char (setq open-char (if (eql ?. tec/tex-last-delim-char)
                                          tec/tex-delim-dot-second
                                        tec/tex-last-delim-char)))
    (pcase open-char
      (?\( "(")
      (?9  "(")
      (?\[ "[")
      (?\{ "\\{")
      (?<  "<")
      (?|  (if tec/tex-delim-dot-second "." "|"))
      (_   ".")))
  (defun tec/tex-close-delim-from-char (&optional open-char)
    "Find the associated closing delim as string"
    (if tec/tex-delim-dot-second
        (pcase tec/tex-delim-dot-second
          (?\) ")")
          (?0  ")")
          (?\] "]")
          (?\} "\\}")
          (?\> ">")
          (?|  "|")
          (_   "."))
      (pcase (or open-char tec/tex-last-delim-char)
        (?\( ")")
        (?9  ")")
        (?\[ "]")
        (?\{ "\\}")
        (?<  ">")
        (?\) ")")
        (?0  ")")
        (?\] "]")
        (?\} "\\}")
        (?\> ">")
        (?|  "|")
        (_   "."))))
  (defun tec/tex-next-char-smart-close-delim (&optional open-char)
    (and (bound-and-true-p smartparens-mode)
         (eql (char-after) (pcase (or open-char tec/tex-last-delim-char)
                             (?\( ?\))
                             (?\[ ?\])
                             (?{ ?})
                             (?< ?>)))))
  (defun tec/tex-delim-yas-expand (&optional open-char)
    (yas-expand-snippet (yas-lookup-snippet "_deliminators" 'latex-mode) (point) (+ (point) (if (tec/tex-next-char-smart-close-delim open-char) 2 1)))))
#+end_src

*** ç¼–è¾‘å™¨è§†è§‰æ•ˆæœ
è®©æˆ‘ä»¬å¢å¼ºä¸€ä¸‹ ~TeX-fold-math~
#+begin_src emacs-lisp
(after! latex
  (setcar (assoc "â‹†" LaTeX-fold-math-spec-list) "â˜…")) ;; make \star bigger

(setq TeX-fold-math-spec-list
      `(;; missing/better symbols
        ("â‰¤" ("le"))
        ("â‰¥" ("ge"))
        ("â‰ " ("ne"))
        ;; convenience shorts -- these don't work nicely ATM
        ;; ("â€¹" ("left"))
        ;; ("â€º" ("right"))
        ;; private macros
        ("â„" ("RR"))
        ("â„•" ("NN"))
        ("â„¤" ("ZZ"))
        ("â„š" ("QQ"))
        ("â„‚" ("CC"))
        ("â„™" ("PP"))
        ("â„" ("HH"))
        ("ğ”¼" ("EE"))
        ("ğ‘‘" ("dd"))
        ;; known commands
        ("" ("phantom"))
        (,(lambda (num den) (if (and (TeX-string-single-token-p num) (TeX-string-single-token-p den))
                                (concat num "ï¼" den)
                              (concat "âª" num "ï¼" den "â«"))) ("frac"))
        (,(lambda (arg) (concat "âˆš" (TeX-fold-parenthesize-as-necessary arg))) ("sqrt"))
        (,(lambda (arg) (concat "â­¡" (TeX-fold-parenthesize-as-necessary arg))) ("vec"))
        ("â€˜{1}â€™" ("text"))
        ;; private commands
        ("|{1}|" ("abs"))
        ("â€–{1}â€–" ("norm"))
        ("âŒŠ{1}âŒ‹" ("floor"))
        ("âŒˆ{1}âŒ‰" ("ceil"))
        ("âŒŠ{1}âŒ‰" ("round"))
        ("ğ‘‘{1}/ğ‘‘{2}" ("dv"))
        ("âˆ‚{1}/âˆ‚{2}" ("pdv"))
        ;; fancification
        ("{1}" ("mathrm"))
        (,(lambda (word) (string-offset-roman-chars 119743 word)) ("mathbf"))
        (,(lambda (word) (string-offset-roman-chars 119951 word)) ("mathcal"))
        (,(lambda (word) (string-offset-roman-chars 120003 word)) ("mathfrak"))
        (,(lambda (word) (string-offset-roman-chars 120055 word)) ("mathbb"))
        (,(lambda (word) (string-offset-roman-chars 120159 word)) ("mathsf"))
        (,(lambda (word) (string-offset-roman-chars 120367 word)) ("mathtt"))
        )
      LaTeX-fold-macro-spec-list
      '(
        ;; as the defaults
        ("[f]" ("footnote" "marginpar"))
        ("[c]" ("cite"))
        ("[l]" ("label"))
        ("[r]" ("ref" "pageref" "eqref"))
        ("[i]" ("index" "glossary"))
        ("..." ("dots"))
        ("{1}" ("emph" "textit" "textsl" "textmd" "textrm" "textsf" "texttt"
                "textbf" "textsc" "textup"))
        ;; tweaked defaults
        ("Â©" ("copyright"))
        ("Â®" ("textregistered"))
        ("â„¢"  ("texttrademark"))
        ("[1]:||â–º" ("item"))
        ("â¡â¡â€†{1}" ("part" "part*"))
        ("â¡â€†{1}" ("chapter" "chapter*"))
        ("Â§â€†{1}" ("section" "section*"))
        ("Â§Â§â€†{1}" ("subsection" "subsection*"))
        ("Â§Â§Â§â€†{1}" ("subsubsection" "subsubsection*"))
        ("Â¶â€†{1}" ("paragraph" "paragraph*"))
        ("Â¶Â¶â€†{1}" ("subparagraph" "subparagraph*"))
        ;; extra
        ("â¬–â€†{1}" ("begin"))
        ("â¬—â€†{1}" ("end"))
        ))

(defun string-offset-roman-chars (offset word)
  "Shift the codepoint of each character in WORD by OFFSET with an extra -6 shift if the letter is lowercase"
  (apply 'string
         (mapcar (lambda (c)
                   (string-offset-apply-roman-char-exceptions
                    (+ (if (>= c 97) (- c 6) c) offset)))
                 word)))

(defvar string-offset-roman-char-exceptions
  '(;; lowercase serif
    (119892 .  8462) ; â„
    ;; lowercase caligraphic
    (119994 . 8495) ; â„¯
    (119996 . 8458) ; â„Š
    (120004 . 8500) ; â„´
    ;; caligraphic
    (119965 . 8492) ; â„¬
    (119968 . 8496) ; â„°
    (119969 . 8497) ; â„±
    (119971 . 8459) ; â„‹
    (119972 . 8464) ; â„
    (119975 . 8466) ; â„’
    (119976 . 8499) ; â„³
    (119981 . 8475) ; â„›
    ;; fraktur
    (120070 . 8493) ; â„­
    (120075 . 8460) ; â„Œ
    (120076 . 8465) ; â„‘
    (120085 . 8476) ; â„œ
    (120092 . 8488) ; â„¨
    ;; blackboard
    (120122 . 8450) ; â„‚
    (120127 . 8461) ; â„
    (120133 . 8469) ; â„•
    (120135 . 8473) ; â„™
    (120136 . 8474) ; â„š
    (120137 . 8477) ; â„
    (120145 . 8484) ; â„¤
    )
  "An alist of deceptive codepoints, and then where the glyph actually resides.")

(defun string-offset-apply-roman-char-exceptions (char)
  "Sometimes the codepoint doesn't contain the char you expect.
Such special cases should be remapped to another value, as given in `string-offset-roman-char-exceptions'."
  (if (assoc char string-offset-roman-char-exceptions)
      (cdr (assoc char string-offset-roman-char-exceptions))
    char))

(defun TeX-fold-parenthesize-as-necessary (tokens &optional suppress-left suppress-right)
  "Add âª â« parenthesis as if multiple LaTeX tokens appear to be present"
  (if (TeX-string-single-token-p tokens) tokens
    (concat (if suppress-left "" "âª")
            tokens
            (if suppress-right "" "â«"))))

(defun TeX-string-single-token-p (teststring)
  "Return t if TESTSTRING appears to be a single token, nil otherwise"
  (if (string-match-p "^\\\\?\\w+$" teststring) t nil))
#+end_src

ç»‘å®šä¸€äº›æ¨¡å¼ä¸‹çš„å¿«æ·é”®ï¼Œç”¨èµ·æ¥æ›´è½»æ¾
#+begin_src emacs-lisp
(after! tex
  (map!
   :map LaTeX-mode-map
   :ei [C-return] #'LaTeX-insert-item)
  (setq TeX-electric-math '("\\(" . "")))
#+end_src

æ•°å­¦å®šç•Œç¬¦å¯ä»¥ä¸ç”¨å¼ºè°ƒ
#+begin_src emacs-lisp
;; Making \( \) less visible
(defface unimportant-latex-face
  '((t :inherit font-lock-comment-face :weight extra-light))
  "Face used to make \\(\\), \\[\\] less visible."
  :group 'LaTeX-math)

(font-lock-add-keywords
 'latex-mode
 `(("\\\\[]()[]" 0 'unimportant-latex-face prepend))
 'end)

;; (font-lock-add-keywords
;;  'latex-mode
;;  '(("\\\\[[:word:]]+" 0 'font-lock-keyword-face prepend))
;;  'end)
#+end_src

å¹¶ä¸ºé¢„è§ˆå¯ç”¨ shell è½¬ä¹‰
#+begin_src emacs-lisp
(setq preview-LaTeX-command '("%`%l \"\\nonstopmode\\nofiles\
\\PassOptionsToPackage{" ("," . preview-required-option-list) "}{preview}\
\\AtBeginDocument{\\ifx\\ifPreview\\undefined"
preview-default-preamble "\\fi}\"%' \"\\detokenize{\" %t \"}\""))
#+end_src

*** æ•°å­¦è¾“å…¥
**** CDLaTeX
é»˜è®¤æƒ…å†µä¸‹ï¼Œç¬¦å·å’Œä¿®æ”¹éå¸¸å¥½ï¼Œä½†å¯ä»¥åšäº›è¡¥å……ã€‚è®©æˆ‘ä»¬å°†å‰ç¼€æ›´æ”¹ä¸ºç±»ä¼¼å¾ˆå°‘ä½¿ç”¨ä½†
æ›´æ–¹ä¾¿çš„é”®ï¼Œä¾‹å¦‚ =;= ã€‚
#+begin_src emacs-lisp
(after! cdlatex
  (setq cdlatex-env-alist
        '(("bmatrix" "\\begin{bmatrix}\n?\n\\end{bmatrix}" nil)
          ("equation*" "\\begin{equation*}\n?\n\\end{equation*}" nil)))
  (setq ;; cdlatex-math-symbol-prefix ?\; ;; doesn't work at the moment :(
   cdlatex-math-symbol-alist
   '( ;; adding missing functions to 3rd level symbols
     (?_    ("\\downarrow"  ""           "\\inf"))
     (?2    ("^2"           "\\sqrt{?}"     ""     ))
     (?3    ("^3"           "\\sqrt[3]{?}"  ""     ))
     (?^    ("\\uparrow"    ""           "\\sup"))
     (?k    ("\\kappa"      ""           "\\ker"))
     (?m    ("\\mu"         ""           "\\lim"))
     (?c    (""             "\\circ"     "\\cos"))
     (?d    ("\\delta"      "\\partial"  "\\dim"))
     (?D    ("\\Delta"      "\\nabla"    "\\deg"))
     ;; no idea why \Phi isnt on 'F' in first place, \phi is on 'f'.
     (?F    ("\\Phi"))
     ;; now just convenience
     (?.    ("\\cdot" "\\dots"))
     (?:    ("\\vdots" "\\ddots"))
     (?*    ("\\times" "\\star" "\\ast")))
   cdlatex-math-modify-alist
   '( ;; my own stuff
     (?B    "\\mathbb"        nil          t    nil  nil)
     (?a    "\\abs"           nil          t    nil  nil))))
#+end_src
**** LAAS
è¿™åˆ©ç”¨ =aas= (/è‡ªåŠ¨æ¿€æ´»ç‰‡æ®µ/) è¿›è¡Œç±»ä¼¼ CDLaTeX çš„ç¬¦å·è¾“å…¥ã€‚

#+begin_src emacs-lisp :tangle packages.el
(package! laas :recipe (:local-repo "lisp/LaTeX-auto-activating-snippets"))
#+end_src

#+begin_src emacs-lisp
(use-package! laas
  :hook (LaTeX-mode . laas-mode)
  :config
  (defun laas-tex-fold-maybe ()
    (unless (equal "/" aas-transient-snippet-key)
      (+latex-fold-last-macro-a)))
  (add-hook 'aas-post-snippet-expand-hook #'laas-tex-fold-maybe))
#+end_src

*** SyncTeX
#+begin_src emacs-lisp
(after! tex
  (add-to-list 'TeX-view-program-list '("Evince" "evince %o"))
  (add-to-list 'TeX-view-program-selection '(output-pdf "Evince")))
#+end_src

*** Fixes
Emacs28
#+begin_src emacs-lisp
(when EMACS28+
  (add-hook 'latex-mode-hook #'TeX-latex-mode))
#+end_src

** Python
ç”±äºæˆ‘ä½¿ç”¨çš„æ˜¯ =mypyls= ï¼Œæ­£å¦‚ [[file:~/.emacs.d/modules/lang/python/README.org::*Language Server Protocol Support][:lang python LSP support]] ä¸­æ‰€å»ºè®®çš„ï¼Œæˆ‘å°†è°ƒæ•´
=mypyls= çš„ä¼˜å…ˆçº§
#+begin_src emacs-lisp
(after! lsp-python-ms
  (set-lsp-priority! 'mspyls 1))
#+end_src

** PDF
*** MuPDF
è™½ç„¶ =pdf-tools= å¾ˆå¥½ï¼Œä½†åŸºäº =mupdf= çš„è§£å†³æ–¹æ¡ˆæ›´å¥½ã€‚

#+begin_src emacs-lisp :tangle no
(package! paper :recipe (:host github :repo "ymarco/paper-mode"
                         :files ("*.el" ".so")
                         :pre-build ("make")))
#+end_src

#+begin_src emacs-lisp :tangle yes
;; (use-package paper
;;   ;; :mode ("\\.pdf\\'"  . paper-mode)
;;   ;; :mode ("\\.epub\\'"  . paper-mode)
;;   :config
;;   (require 'evil-collection-paper)
;;   (evil-collection-paper-setup))
#+end_src

*** ç»ˆç«¯é¢„è§ˆ
æœ‰æ—¶æˆ‘åœ¨ç»ˆç«¯ä¸­ï¼Œä½†æˆ‘ä»ç„¶æƒ³æŸ¥çœ‹å†…å®¹ã€‚æ­¤å¤–ï¼Œæœ‰æ—¶æˆ‘æƒ³å¯¹æ–‡æœ¬å†…å®¹é‡‡å–ä¸€äº›æ“ä½œï¼Œå› æ­¤æƒ³
è¦çº¯æ–‡æœ¬ç‰ˆæœ¬ã€‚å¤šäºäº† src_shell{pdftotext} å¯ä»¥ç”¨å®ƒæ¥è¿›è¡Œæ–¹ä¾¿çš„è½¬æ¢ã€‚æˆ‘å·²ç»å°†å®ƒé›†
æˆåˆ°äº† =pdftotext.el= è¿™ä¸ªåŒ…ä¸­ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! pdftotext :recipe (:local-repo "lisp/pdftotext"))
#+end_src

è¾“å‡ºå¯ä»¥ç¨å¾®å¥½ä¸€ç‚¹ï¼Œæ²¡æœ‰æ‹¼å†™é”™è¯¯ï¼Œå¹¶ä¸”é¡µé¢æè¦æ›´æ¼‚äº® (é»˜è®¤ä¸º =^L=)ã€‚

#+begin_src emacs-lisp
#+end_src

è¿™å¾ˆå¥½ï¼Œç°åœ¨æˆ‘ä»¬åªéœ€è¦å°†å®ƒä¸ =.pdf= æ–‡ä»¶å…³è”ï¼Œå¹¶ç¡®ä¿ä¸ä¼˜å…ˆä½¿ç”¨ =pdf-tools= ã€‚

æœ€åï¼Œåªè¦ Emacs æ˜¯éå›¾å½¢çš„ (å³ TUI)ï¼Œæˆ‘ä»¬å¸Œæœ›é»˜è®¤ä½¿ç”¨å®ƒã€‚
#+begin_src emacs-lisp :tangle (if (executable-find "pdftotext") "yes" "no")
(use-package! pdftotext
  :init
  (unless (display-graphic-p)
    (add-to-list 'auto-mode-alist '("\\.[pP][dD][fF]\\'" . pdftotext-mode))
    (add-to-list 'magic-mode-alist '("%PDF" . pdftotext-mode)))
  :config
  (unless (display-graphic-p) (after! pdf-tools (pdftotext-install)))
  ;; For prettyness
  (add-hook 'pdftotext-mode-hook #'spell-fu-mode-disable)
  (add-hook 'pdftotext-mode-hook (lambda () (page-break-lines-mode 1)))
  ;; I have no idea why this is needed
  (map! :map pdftotext-mode-map
        "<mouse-4>" (cmd! (scroll-down mouse-wheel-scroll-amount-horizontal))
        "<mouse-5>" (cmd! (scroll-up mouse-wheel-scroll-amount-horizontal))))
#+end_src

** R
*** ç¼–è¾‘å™¨è§†è§‰ä½“éªŒ
#+begin_src emacs-lisp
(after! ess-r-mode
  (appendq! +ligatures-extra-symbols
            '(:assign "âŸµ"
              :multiply "Ã—"))
  (set-ligatures! 'ess-r-mode
    ;; Functional
    :def "function"
    ;; Types
    :null "NULL"
    :true "TRUE"
    :false "FALSE"
    :int "int"
    :floar "float"
    :bool "bool"
    ;; Flow
    :not "!"
    :and "&&" :or "||"
    :for "for"
    :in "%in%"
    :return "return"
    ;; Other
    :assign "<-"
    :multiply "%*%"))
#+end_src

** Julia
å¦‚ [[https://github.com/non-Jedi/lsp-julia/issues/35][lsp-julia#35]] ä¸­æ‰€è¿°ï¼Œ =lsp-mode= ä¼¼ä¹å‘ Julia æœåŠ¡å™¨æä¾›äº†æ— æ•ˆå“åº”ã€‚
ä¼ªä¿®å¤è‡³å°‘ç›¸å½“ç®€å•ã€‚
#+begin_src emacs-lisp
(after! julia-mode
  (add-hook 'julia-mode-hook #'rainbow-delimiters-mode-enable)
  (add-hook! 'julia-mode-hook
    (setq-local lsp-enable-folding t
                lsp-folding-range-limit 100)))
#+end_src

** Graphviz
Graphviz æ˜¯ä¸€ç§åŸºäºçº¯æ–‡æœ¬ .dot / .gv æ–‡ä»¶å¯è§†åŒ–ç®€å•å›¾å½¢çš„å¥½æ–¹æ³•ã€‚
#+begin_src emacs-lisp :tangle packages.el
(package! graphviz-dot-mode :pin "a1b7d66f5c20404a1e59c2ee5e841022622535b8")
#+end_src

#+begin_src emacs-lisp
(use-package! graphviz-dot-mode
  :commands graphviz-dot-mode
  :mode ("\\.dot\\'" "\\.gz\\'")
  :init
  (after! org
    (setcdr (assoc "dot" org-src-lang-modes)
            'graphviz-dot)))

(use-package! company-graphviz-dot
  :after graphviz-dot-mode)
#+end_src

** Markdown
å¤§å¤šæ•°æƒ…å†µä¸‹ï¼Œå½“æˆ‘ç¼–å†™ Markdown æ—¶ï¼Œå®ƒä¼šè¿›å…¥ä¸€äº›åº”ç”¨ç¨‹åº / ç½‘ç«™ï¼Œå®ƒä»¬ä¼šè¿›è¡Œè‡ªå·±
çš„æ¢è¡Œï¼Œå› æ­¤æˆ‘ä»¬ /åª/ æƒ³ä½¿ç”¨å¯è§†æ¢è¡Œã€‚è€Œä¸æ˜¯ç¡¬æ€§çš„ã€‚
Most of the time when I write markdown, it's going into some app/website which
will do it's own line wrapping, hence we /only/ want to use visual line wrapping. No hard stuff.
#+begin_src emacs-lisp
(add-hook! (gfm-mode markdown-mode) #'visual-line-mode #'turn-off-auto-fill)
#+end_src

ç”±äº Markdown é€šå¸¸è¢«è§†ä¸ºæ¸²æŸ“çš„ HTMLï¼Œè®©æˆ‘ä»¬å°è¯•åœ¨æŸç§ç¨‹åº¦ä¸Šæ¨¡ä»¿æ ·å¼æˆ– Markdown
æ¸²æŸ“å™¨ã€‚

å¤§å¤šæ•° Markdown æ¸²æŸ“ä¼¼ä¹ä½¿å‰ä¸‰ä¸ªæ ‡é¢˜çº§åˆ«å¤§äºæ™®é€šæ–‡æœ¬ï¼Œå‰ä¸¤ä¸ªæ›´å¤§ã€‚ç„¶åç¬¬å››å±‚å¾€å¾€
ä¸æ­£æ–‡ç›¸åŒï¼Œè€Œç¬¬äº”å’Œç¬¬å…­å±‚ (è¶Šæ¥è¶Šå°)ï¼Œç¬¬å…­å±‚å˜ç°ã€‚ç¬¬å…­å…³å¤ªå°äº†ï¼Œæˆ‘æŠŠå°è¯•å°†å…¶å†
æé«˜ä¸€ä¸ªæ¡£æ¬¡ã€‚
#+begin_src emacs-lisp
(custom-set-faces!
  '(markdown-header-face-1 :height 1.25 :weight extra-bold :inherit markdown-header-face)
  '(markdown-header-face-2 :height 1.15 :weight bold       :inherit markdown-header-face)
  '(markdown-header-face-3 :height 1.08 :weight bold       :inherit markdown-header-face)
  '(markdown-header-face-4 :height 1.00 :weight bold       :inherit markdown-header-face)
  '(markdown-header-face-5 :height 0.90 :weight bold       :inherit markdown-header-face)
  '(markdown-header-face-6 :height 0.75 :weight extra-bold :inherit markdown-header-face))
#+end_src

** Beancount
[[https://plaintextaccounting.org/][çº¯æ–‡æœ¬è®°è´¦]]æœ‰è®¸å¤šç›¸å½“å¼•äººæ³¨ç›®çš„ä¼˜åŠ¿ï¼Œ[[https://www.ledger-cli.org/][åˆ†ç±»å¸]]æ˜¯æœ€æ˜æ˜¾çš„ä¾‹å­ã€‚ç„¶è€Œ [[https://github.com/beancount/beancount][beancount]]ï¼Œè¿™ä¸ªæƒ³
æ³•çš„æ›´æ–°å®ç°æ˜¯è´¦æœ¬å…¼å®¹çš„ (è¿™æ„å‘³ç€å¦‚æœæˆ‘æ”¹å˜ä¸»æ„ï¼Œæˆ‘å¯ä»¥å¾ˆå®¹æ˜“åœ°åˆ‡æ¢) å¹¶ä¸”æœ‰ä¸€ä¸ª
åä¸½çš„å‰ç«¯ --- [[https://beancount.github.io/fava/][fava]]ã€‚

å½“ç„¶ï¼Œè¿™æ˜¯ Emacs çš„ä¸€éƒ¨åˆ†ã€‚

#+begin_src emacs-lisp :tangle packages.el
(package! beancount :recipe (:host github :repo "beancount/beancount-mode")
  :pin "ea8257881b7e276e8d170d724e3b2e179f25cb77")
#+end_src

#+begin_src emacs-lisp
(use-package! beancount
  :mode ("\\.beancount\\'" . beancount-mode)
  :init
  (after! all-the-icons
    (add-to-list 'all-the-icons-icon-alist
                 '("\\.beancount\\'" all-the-icons-material "attach_money" :face all-the-icons-lblue))
    (add-to-list 'all-the-icons-mode-icon-alist
                 '(beancount-mode all-the-icons-material "attach_money" :face all-the-icons-lblue)))
  :config
  (setq beancount-electric-currency t)
  (defun beancount-bal ()
    "Run bean-report bal."
    (interactive)
    (let ((compilation-read-command nil))
      (beancount--run "bean-report"
                      (file-relative-name buffer-file-name) "bal")))
  (map! :map beancount-mode-map
        :n "TAB" #'beancount-align-to-previous-number
        :i "RET" (cmd! (newline-and-indent) (beancount-align-to-previous-number))))
#+end_src
